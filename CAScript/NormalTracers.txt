Class DCY_TracerPlayer : DCY_FastProjectile
{
  sound whiz;
  property WhizSound : whiz;
  bool explosive;
  property IsExplosive : explosive;
  string spark;
  property SparkFX : spark;
  
  Default
  {
  +RANDOMIZE;
  +FORCEXYBILLBOARD;
  +NOEXTREMEDEATH;
  +NOTIMEFREEZE
  +BLOODSPLATTER;
  +THRUGHOST;
  +NOSPLASHALERT;
  -DONTSPLASH;
  +MISSILE;
  +BRIGHT;
  DCY_TracerPlayer.WhizSound "Rec";
  DCY_TracerPlayer.IsExplosive false;
  DCY_TracerPlayer.SparkFX "DCY_BulletPuffSpark";
  DamageFunction 0;
  Radius 2;
  Height 2;
  Speed 300;
  Mass 2;
  RenderStyle "Add";
  Decal "BulletChip";
  DamageType "Player";
  Scale .08;
  Alpha 0.9;
  }
  
  void A_CheckWhiz()
  {
	if (!CheckRange(128))
	{
		let curCamera = players [consolePlayer].camera;
		if(CheckIfCloser(curCamera,128)) A_StartSound(whiz, 20, CHANF_DEFAULT|CHANF_OVERLAP, attenuation: 0.65);
	}
  }
  
  void A_Sparks()
  {
	int zposs;
	float ang1;
	float ang2;
	float angles;
	
	for (int i = 3; i > 0; i--)
	{
		if (pos.z <= floorz)
		{
			A_SpawnItemEx(spark, 0, 0, 1, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 60);
			A_SpawnItemEx(spark, 0, 0, 1, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 28);
		}
		else if (pos.z >= ceilingz - height)
		{
			A_SpawnItemEx(spark, 0, 0, -1, frandom(4.0, 14.5), 0, frandom(-3.0, -6.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 60);
			A_SpawnItemEx(spark, 0, 0, -1, frandom(4.0, 14.5), 0, frandom(-3.0, -6.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 28);
		}
		else
		{
			A_SpawnItemEx(spark, 0, 0, 0, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, -3.0), frandom(-20.0, 20.0)+180, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 60);
			A_SpawnItemEx(spark, 0, 0, 0, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, -3.0), frandom(-20.0, 20.0)+180, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 28);
		}
	}
  }
  
  States
  {
  Spawn:
    TRAC A 1 A_CheckWhiz();
    Loop;
  Death:
	TNT1 A 1
	{
		if (explosive)
		{
			A_StartSound("TracerExplode", CHAN_BODY);
			A_Explode(25, 50, XF_NOTMISSILE, 0);
			
			for (int i = 2; i > 0; i--)
				A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION);
		}
		else
		{
			A_StartSound("DCYBullet/Hit", CHAN_BODY, volume: 0.5);
			A_SpawnItemEx("DCY_BulletPuffGlow", flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
			A_Sparks();
		}
	}
    Stop;
  XDeath:
    TNT1 A 0 { if (explosive) SetStateLabel("Death"); }
    Stop;
  }
}

Class DCY_PistolTracer : DCY_TracerPlayer { Default { DamageFunction (random(1, 5) * 3); } }
Class DCY_RifleTracer : DCY_TracerPlayer { Default { DamageFunction (random(2, 8) * 3); } }
Class DCY_SSGTracer : DCY_TracerPlayer { Default { DamageFunction (7 * random[FireSG2](1, 3)); } }
Class DCY_DeathbringerTracer : DCY_TracerPlayer { Default { DamageFunction (random(1, 10) * 5); } }
Class DCY_DeagleTracer : DCY_TracerPlayer { Default { DamageFunction (random(1, 5) * 10); } }

Class DCY_ExplosiveTracer : DCY_TracerPlayer { Default { DCY_TracerPlayer.IsExplosive true; } }
Class DCY_ExplosivePistolTracer : DCY_PistolTracer { Default { DCY_TracerPlayer.IsExplosive true; } }
Class DCY_ExplosiveRifleTracer : DCY_RifleTracer { Default { DCY_TracerPlayer.IsExplosive true; } }
Class DCY_ExplosiveSSGTracer : DCY_SSGTracer { Default { DCY_TracerPlayer.IsExplosive true; } }
Class DCY_ExplosiveDeathbringerTracer : DCY_DeathbringerTracer { Default { DCY_TracerPlayer.IsExplosive true; } }
Class DCY_ExplosiveDeagleTracer : DCY_DeagleTracer { Default { DCY_TracerPlayer.IsExplosive true; } }

Class DCY_EnemyTracer : DCY_TracerPlayer
{
  Default
  {
  Speed 55;
  DamageFunction 3;
  +RANDOMIZE;
  DamageType "Bullet";
  Decal "BulletChip";
  -NOTIMEFREEZE
  }
  States
  {
  Spawn:
    TRAC A 1 NoDelay { A_CheckWhiz(); ThrustThingZ(0,random(-8, 8),0,1); }
    Goto Idle;
  Idle:
    TRAC A 1 A_CheckWhiz();
    Loop;
  }
}

Class DCY_ZombieTracer : DCY_EnemyTracer { Default { DamageFunction (random(1, 5) * random(2, 3)); } }
Class DCY_Zombie2Tracer : DCY_EnemyTracer { Default { DamageFunction (random(1, 5)); } }
Class DCY_AracgunnerBullet : DCY_EnemyTracer { Default { DamageFunction (random(1, 4) * 3); } }
Class DCY_AracgunnerBullet2 : DCY_EnemyTracer { Default { DamageFunction (random(1, 5) * 3); } }
Class DCY_EnemyDeagleTracer : DCY_EnemyTracer { Default { Scale .105; Speed 70; DamageFunction (random(2, 3) * 15); } }

Class DCY_AlacritousGuyBullet : DCY_EnemyTracer
{
  States
  {
  Death:
	TNT1 AA 0 A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION);
	TNT1 A 1
	{
		A_StartSound("TracerExplode");
		A_Explode(15, 40, XF_NOTMISSILE, 0);
	}
	Stop;
  XDeath:
	TNT1 A 0;
	Goto Death;
  }
}

Class DCY_BlueZombieTracer : DCY_ZombieTracer
{
  Default
  {
	DCY_TracerPlayer.SparkFX "DCY_BlueBulletPuffSpark";
	DamageType "Electric";
  }
  
  States
  {
  Spawn:
	TRAC E 1 A_CheckWhiz();
	Loop;
  Death:
	TNT1 A 2
	{
		A_StartSound("DCYBullet/Hit", CHAN_BODY, volume: 0.5);
		A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_ElecDeathBase", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		A_Sparks();
	}
	Stop;
  XDeath:
	TNT1 A 2 A_SpawnItemEx("DCY_ElecDeathBase", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	Stop;
  }
} 

Class DCY_MechaZombiePlasma : DCY_EnemyTracer
{
  Default
  {
  Speed 58;
  FastSpeed 68.5;
  Damage 4;
  Radius 5;
  Height 5;
  Scale .15;
  Alpha 0.95;
  RenderStyle "Add";
  +BRIGHT;
  +NOEXTREMEDEATH;
  SeeSound "DSPLASM4";
  DeathSound "";
  DCY_TracerPlayer.SparkFX "DCY_AndroidPuffSpark";
  }
  States
  {
  Spawn:
    TRAC E 1;
	Loop;
  Death:
	TNT1 A 2 A_Sparks();
	Stop;
  }
}

Class DCY_MechaZombiePlasma2 : DCY_MechaZombiePlasma { Default { DamageFunction 5; Speed 140; +NEVERFAST; } }

Class DCY_JadeLaser : DCY_MechaZombiePlasma
{
  Default
  {
  SeeSound "Rifle/Laser";
  DeathSound "Rifle/LaserImpact";
  Speed 90;
  XScale .05;
  YScale .225;
  DamageFunction 1;
  DamageType "Player";
  +NEVERFAST;
  +BLOODLESSIMPACT;
  +DONTSPLASH;
  +RIPPER;
  DCY_TracerPlayer.SparkFX "DCY_GreenPuffSpark";
  }
  States
  {
  Spawn:
    TRAC D 1 A_Explode(3, 32);
	Loop;
  Death:
    TNT1 A 0;
	TNT1 A 1
	{
		A_Sparks();
		A_SpawnItemEx("DCY_GreenRing", flags: SXF_NOCHECKPOSITION);
	}
	TNT1 A 2;
	Stop;
  }
}

Class DCY_JadeLaserEnemy : DCY_JadeLaser { Default { DamageType "Normal"; } }

Class DCY_ExplosiveJadeLaser : DCY_JadeLaser
{
  Default
  {
  SeeSound "Rifle/LaserExplosive";
  DeathSound "Rifle/LaserExplode";
  Translation "0:255=%[0.00,0.00,0.00]:[1.21,2.00,0.42]";
  Speed 85;
  XScale .085;
  YScale .205;
  }
  
  States
  {
  Spawn:
    TRAC D 1 A_Explode(5, 32);
	Loop;
  Death:
    TNT1 A 0;
	TNT1 A 1
	{
		A_Sparks();
		A_SpawnItemEx("DCY_GreenRing", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		A_Explode(35, 80, 0);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	TNT1 A 2;
	Stop;
  }
}

Class DCY_AbyssalAndroidPlasma : DCY_MechaZombiePlasma
{
  Default
  {
  SeeSound "Android/Laser";
  Damage 3;
  DCY_TracerPlayer.SparkFX "DCY_RedAndroidPuffSpark";
  }
  
  States
  {
  Spawn:
    TRAC F 1;
	Loop;
  Death:
	TNT1 A 2 A_Sparks();
	Stop;
  }
}

Class DCY_PhasicRazerLaser : DCY_MechaZombiePlasma
{
  Default
  {
  DamageFunction (2);
  Speed 85;
  +NEVERFAST;
  +RIPPER;
  -RANDOMIZE;
  SeeSound "";
  MissileType "";
  DCY_TracerPlayer.SparkFX "DCY_PhasicAndroidPuffSpark";
  }
  States
  {
  Spawn:
	TRAC C 1;
	Loop;
  Death:
	TNT1 A 1 A_Sparks();
	Stop;
  }
}

Class DCY_KaiserMechaZombiePlasma : DCY_MechaZombiePlasma { Default { SeeSound "KaiserDrone/Laser"; Damage 2; } }

Class DCY_AbyssalAndroidPlasma2 : DCY_AbyssalAndroidPlasma { Default { Species "BIONIC"; +THRUSPECIES; +MTHRUSPECIES; DamageFunction 4; } }

Class DCY_UltracerestMechaZombiePlasma : DCY_MechaZombiePlasma { Default { SeeSound "Android/Laser"; } }

Class DCY_OverdemoniacBeam : DCY_EnemyTracer
{
  Default
  {
  DamageFunction 8;
  Speed 70;
  Scale .4;
  Height 20;
  Radius 20;
  +NEVERFAST
  +BRIGHT
  +RIPPER
  +EXTREMEDEATH
  SeeSound "BLASER";
  DeathSound "BLASERX";
  MissileType "DCY_OverdemoniacBeamTrail";
  MissileHeight 6;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.75,0.75]";
  DCY_FastProjectile.CSX true;
  DCY_FastProjectile.CSY true;
  DCY_FastProjectile.CT true;
  DCY_FastProjectile.CV true;
  DCY_FastProjectile.CR true;
  }
  
  int timer;
  
  States
  {
  Spawn:
	TRAC E 1
	{
		if (timer < 40)
		{
			A_ScaleVelocity(1.0065);
			timer++;
		}
		
		if (!(level.time % 4)) A_SpawnItemEx("DCY_OverdemoniacRing", xvel: vel.x, yvel: vel.y, zvel: vel.z, flags: SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	TNT1 A 50
	{
		A_QuakeEx(4, 4, 4, 40, 0, 600, "none", QF_SCALEDOWN|QF_RELATIVE, 0, 0, 0, 0, 978);
		A_SpawnItemEx("DCY_WretchedTeleport", flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_BionicRing", flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_OverdemoniacExplosion", angle: frandom(0, 360), flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_Explode(50, 79, XF_NOTMISSILE);
	}
	Stop;
  }
}

Class DCY_OverdemoniacBeamTrail : DCY_ProjectileBaseTrail
{
  Default
  {
  Scale .4;
  Alpha 0.135;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,0.00,0.00]";
  }
  
  States
  {
  Spawn:
	TRAC A 0 NoDelay
	{
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_ChangeVelocity(vel.x / 100, vel.y / 100, vel.z / 100, CVF_REPLACE);
		A_FaceMovementDirection();
	}
	Goto Looping;
  Looping:
	TRAC A 1 A_FadeOut(0.01);
	Wait;
  }
}

Class DCY_OverdemoniacRing : DCY_OverdemoniacBeamTrail
{
  Default
  {
  Scale .5;
  Alpha 1.0;
  Translation "0:255=%[0.00,0.00,0.00]:[1.00,0.00,0.00]";
  +FLATSPRITE
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	SPE1 A 0 NoDelay
	{
		A_ChangeVelocity(vel.x / 100, vel.y / 100, vel.z / 100, CVF_REPLACE);
		A_FaceMovementDirection();
		A_SetPitch(Pitch + 90);
	}
	SPE1 A 1
	{
		A_SetScale(Scale.X + 0.1);
		A_SetRoll(Roll + 5);
		A_FadeOut(0.05);
	}
	Wait;
  }
}

Class DCY_UmbraLaser : DCY_JadeLaserEnemy
{
  Default
  {
  DamageFunction 1;
  +RIPPER
  +BLOODLESSIMPACT
  Translation "0:255=%[0.00,0.00,0.00]:[1.34,0.67,2.00]";
  Speed 70.5;
  DCY_TracerPlayer.SparkFX "DCY_PhasicAndroidPuffSpark";
  }
  States
  {
  Spawn:
    TRAC G 1 A_Explode(1, 32);
	Loop;
  Death:
    TNT1 A 0;
	TNT1 A 1
	{
		A_Sparks();
		A_SpawnItemEx("DCY_GreenRing", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	TNT1 A 2;
	Stop;
  }
}


Class DCY_ArbiterTracer : DCY_ZombieTracer
{
  Default
  {
  DamageFunction (5);
  DamageType "Regulus";
  Scale .1125;
  DCY_TracerPlayer.SparkFX "DCY_BlueBulletPuffSpark";
  }
  States
  {
  Spawn:
	TRAC E 1 A_CheckWhiz();
	Loop;
  Death:
	TNT1 A 2
	{
		A_StartSound("DCYBullet/Hit", CHAN_BODY, volume: 0.5);
		A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
		A_Sparks();
	}
	Stop;
  }
}

Class DCY_TripartiteTracer : DCY_ArbiterTracer
{
  Default { DCY_TracerPlayer.SparkFX "DCY_BulletPuffSpark"; }
  States
  {
  Spawn:
	TRAC A 1 A_CheckWhiz();
	Loop;
  Death:
	TNT1 A 1
	{
		A_StartSound("DCYBullet/Hit", CHAN_BODY, volume: 0.5);
		A_SpawnItemEx("DCY_BulletPuffGlow", flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
		A_Sparks();
	}
    Stop;
  }
}

Class DCY_ChrysalisLaser : DCY_ZombieTracer
{
  Default
  {
  Speed 50.666;
  +NEVERFAST
  DamageFunction 2;
  DamageType "Arcane";
  SeeSound "Chrysalis/Laser";
  DeathSound "Chrysalis/LaserXplode";
  Scale 1.5;
  DCY_TracerPlayer.SparkFX "DCY_AndroidPuffSpark";
  }
  States
  {
  Spawn:
	TRAC E 1;
	Loop;
  Death:
	TNT1 A 1 A_Sparks();
	Stop;
  }
}

Class DCY_VandalLaser : DCY_ZombieTracer
{
  Default
  {
  DamageFunction (2);
  +RIPPER;
  +BLOODLESSIMPACT;
  Speed 70.5;
  Scale .15;
  SeeSound "Vandal/Laser";
  DeathSound "";
  DCY_TracerPlayer.SparkFX "DCY_PhasicAndroidPuffSpark";
  }
  States
  {
  Spawn:
	TRAC G 1;
	Loop;
  Death:
	TNT1 A 1 A_Sparks();
	TNT1 A 2 A_Die();
	Stop;
  }
}

Class DCY_DronePlasma : DCY_AbyssalAndroidPlasma { Default { SeeSound "LASER001"; Damage 3; Speed 40; } }

Class DCY_EspionPlasma : DCY_MechaZombiePlasma
{
  Default
  {
  Radius 7;
  Height 7;
  Scale .3;
  SeeSound "Pistol/LaserChargedShot";
  DeathSound "B3LLFIRE";
  Translation "0:255=%[0.00,0.00,0.00]:[0.76,1.10,1.82]";
  +PIERCEARMOR
  DCY_TracerPlayer.SparkFX "DCY_AExplosionSpark";
  }
  States
  {
  Spawn:
    TRAC E 1 A_SpawnItemEx("DCY_FluorescentSparkle", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Loop;
  Death:
	TNT1 A 1
	{
		A_Sparks();
		for (int i = 3; i > 0; i--) A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_CWFXDeathTrail2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_QuakeEx(4, 4, 3, 30, 0, 400, "", QF_SCALEDOWN|QF_3D);
		A_Explode(128, 140, 0, 0);
	}
	TNT1 A 40;
	Stop;
  }
}

Class DCY_TransfixerPlasma : DCY_EspionPlasma { Default { -PIERCEARMOR Translation "0:255=%[0.00,0.00,0.00]:[1.39,0.94,1.93]"; } }

Class DCY_EspionEmissionTracer : DCY_MechaZombiePlasma
{
  Default
  {
  Damage 5;
  SeeSound "RPLSFIR";
  DeathSound "RPLSPIM";
  Translation "0:255=%[0.00,0.00,0.00]:[0.76,1.10,1.82]";
  Speed 130;
  +NEVERFAST
  +RANDOMIZE
  Scale .3275;
  DCY_TracerPlayer.SparkFX "DCY_RedAndroidPuffSpark";
  }
  
  States
  {
  Spawn:
	TRAC E 1;
	Loop;
  Death:
  XDeath:
	TNT1 A 1;
	TNT1 A 1
	{
		A_Explode(25, 200, XF_NOTMISSILE, 1);
		A_QuakeEx(2, 2, 2, 20, 0, 100, flags: QF_SCALEDOWN);
		A_SpawnItemEx("DCY_EmissionSphere", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		for (int i = 2; i > 0; i--)
			A_Sparks();
	}
	Stop;
  }
}

Class DCY_NephelomaTracer : DCY_EnemyTracer
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.22,0.80,2.00]";
  }
  
  States
  {
  Spawn:
	TRAC E 1 A_CheckWhiz();
	Loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	TNT1 A 1
	{
		A_StartSound("CYBRSMSH");
		A_Explode(15, 40, XF_NOTMISSILE, 0);
	}
	Stop;
  XDeath:
	TNT1 A 0;
	Goto Death;
  }
}