Class DCY_UltrapissSoulSphere : DCY_Powerup
{
  Default
  {
  Tag "Orb of Luminescence";
  Inventory.PickupSound "SoulSphere/PickedUp";
  DCY_Powerup.EffectColor1 "2f6699";
  DCY_Powerup.AmbientNoise "SoulSphere/IdleNoise";
  Inventory.PickupMessage "\c[l7]Cyan luminescence entangles you with comfort.\c- (200+ Health)";
  }
  
  override void Tick()
  {
	super.Tick();
	
	if (DCY_EffectDistance(900))
	{
		A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 80, 70, 0, frandom(20, -20), frandom(-20, 20), frandom(-20, 20), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.5, 1.5), accelz: 0.025, startalphaf: 0.15, fadestepf: 0.0025, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(3, 6), frandom(0, 360), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(-20, 20), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.05, 0.2), startalphaf: 0.4, fadestepf: 0, sizestep: -0.15);
	}
  }
  
  States
  {
  Spawn:
	S8_L ABCD 4 Bright;
	Loop;
  }
  
  override bool Use(bool pickup)
  {
	Owner.GiveBody(100, 200);
	Owner.A_SetBlend("bdfffd", 0.5, 20, "Cyan");
	
	for (int i = 0; i < 360; i += 12)
	{
		Owner.A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(12, 16), i, frandom(20, -20), frandom(-20, 20), frandom(-20, 20) + 36, frandom(-0.1, 0.1) + 3, frandom(-0.1, 0.1), frandom(0.5, 1.5), accelx: -0.1, accelz: 0.025, startalphaf: 0.5, fadestepf: 0.05, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		Owner.A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 100, 70, i, frandom(20, -20), frandom(-20, 20), frandom(-20, 20) + 36, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.4, fadestepf: 0.015, sizestep: 0.05, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
	}
	
	for (int i = 5; i > 0; i--)
		Owner.A_SpawnItemEx("DCY_SoulSphereRing", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), 0, 0, 0, frandom(0.0, 360.0), SXF_NOCHECKPOSITION|SXF_SETMASTER, 10);
		
	return true;
  }  
}

Class DCY_UltrapissMegaSphere : DCY_Powerup
{
  Default
  {
  Tag "Hope?";
  Inventory.PickupSound "Doom4/Megasphere";
  Inventory.PickupMessage "\c[Yellow]Stepping through, you strangely start to feel much more better now.\c- (200+ Health and Armor)";
  DCY_Powerup.AmbientNoise "MegaSphere/IdleNoise";
  DCY_Powerup.EffectColor1 "857759";
  DCY_Powerup.Pulse true;
  DCY_Powerup.LightRadius 70;
  }
  
  void A_HopeEffect()
  {
	if (DCY_EffectDistance(900))
	{
		A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 80, 70, 0, frandom(20, -20), frandom(-20, 20), frandom(-20, 20), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.5, 1.5), accelz: 0.025, startalphaf: 0.25, fadestepf: 0.005, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(3, 6), frandom(0, 360), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(-20, 20), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.05, 0.2), startalphaf: 0.4, fadestepf: 0, sizestep: -0.15);
			
		if ((level.time % 24) == 0)
			A_SpawnItemEx("DCY_HopeLight", flags: SXF_NOCHECKPOSITION);
	}
  }
  
  override void Tick()
  {
	super.tick();
	A_HopeEffect();
  }
  
  States
  {
  Spawn:
	M3_G ABCD 5;
	Loop;
  }
  
  override bool Use(bool pickup)
  {
	Owner.GiveBody(200, 200);
	Owner.GiveInventory("BlueArmorforMegasphere", 1);
	Owner.A_SetBlend("ffe8b8", 1, 25, "ffac00");
	
	for (int i = 0; i < 360; i += 12)
	{
		Owner.A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(12, 16), i, frandom(20, -20), frandom(-20, 20), frandom(-20, 20) + 36, frandom(-0.1, 0.1) + 3, frandom(-0.1, 0.1), frandom(0.5, 1.5), accelx: -0.1, accelz: 0.025, startalphaf: 0.5, fadestepf: 0.05, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		Owner.A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 100, 70, i, frandom(20, -20), frandom(-20, 20), frandom(-20, 20) + 36, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.4, fadestepf: 0.015, sizestep: 0.05, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
	}
	
	for (int i = 5; i > 0; i--)
		Owner.A_SpawnItemEx("DCY_MegaSphereRing", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), 0, 0, 0, frandom(0.0, 360.0), SXF_NOCHECKPOSITION|SXF_SETMASTER, 10);
	
	return true;
  }
}

Class DCY_HopeLight : Actor
{
  Default
  {
  RenderStyle "Add";
  Scale 1.35;
  Alpha 0.0;
  +NOGRAVITY
  +FLOORHUGGER
  +BRIGHT
  +DONTSPLASH
  DistanceCheck "cl_effectdistance";
  }
  
  States
  {
  Spawn:
	H0P3 AAAAA 1
	{
		Scale.X += 0.015;
		Scale.Y -= 0.015;
		alpha += 0.2;
	}
	H0P3 A 1
	{
		Scale.X += 0.015;
		Scale.Y -= 0.015;
		alpha -= 0.01;
	}
	Wait;
  }
}

Class DCY_UncoSphere : CustomInventory
{
  Default
  {
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  +INVENTORY.BIGPOWERUP
  +INVENTORY.FANCYPICKUPSOUND
  +FLOATBOB
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.64,0.64,1.29]";
  Inventory.PickupMessage "\c[Purple]The lavender essence seeps through your soul.\c- (250+ Health and Armor)";
  Inventory.PickupSound "Souffle";
  Inventory.PickupFlash "DCY_UncoRingThing";
  }
  
  States
  {
  Spawn:
    M771 AABBCCDDEEFFGGHH 1 Bright A_SpawnItemEx("DCY_UncoAura", random(-1, 1), random(-1, 1), random(26, 29), random(-1, 1), random(-1, 1), random(-1, 1), 0, SXF_NOCHECKPOSITION);
	Loop;
  Pickup:
	PDFS A 0
	{
		A_GiveInventory("DCY_UncoShield");
		A_GiveInventory("DCY_UncoHealth");
	}
	Stop;
  }
}

Class DCY_UncoHealth : Health
{
  Default
  {
  +INVENTORY.AUTOACTIVATE;
  +INVENTORY.ALWAYSPICKUP;
  -COUNTITEM;
  Scale 1.3;
  DamageFactor "Normal", 0.75;
  Inventory.Amount 250;
  Inventory.MaxAmount 400;
  Inventory.Icon "PDFS[0";
  Inventory.PickupMessage "";
  Inventory.PickupSound "";
  }
  
  States
  {
  Spawn:
    PDFS ABCD 2 Bright;
    Loop;
  }
}

Class DCY_UncoShield : BasicArmorPickup
{
  Default
  {
  Inventory.PickupMessage "";
  +INVENTORY.QUIET;
  +INVENTORY.AUTOACTIVATE;
  +INVENTORY.ALWAYSPICKUP;
  DamageFactor "Normal", 0.75;
  Inventory.Icon "PDFS[0";
  Armor.SavePercent 250;
  Armor.SaveAmount 400;
  }
  
  States
  {
  Spawn:
    PDFS ABCD 2 Bright;
    Loop;
  }
}

Class DCY_UltrapissInvulnerability : InvulnerabilitySphere Replaces InvulnerabilitySphere
{
  Default
  {
  Powerup.Color "00 80 80", 0.135;
  Inventory.PickupSound "InvulnerabilitySphere/PickedUp";
  Inventory.PickupMessage "\c[Green]You are indestructable! How is this possible?\c-";
  RenderStyle "Add";
  Alpha 1.0;
  Powerup.Type "DCY_PowerInvulCA";
  +FLOATBOB
  }
  
  States
  {
  Spawn:
	PINV AAABBBCCCDDD 1 Bright A_SpawnItemEx("DCY_InvulSphereAura", random(-1, 1), random(-1, 1), random(19, 23), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION);
	Loop;
  Pickup:
	PINV A 0 A_SetBlend("White", 0.82, 23, "Green");
	Stop;
  }
}

Class DCY_PowerInvulCA : PowerInvulnerable
{
  Default
  {
  Powerup.Duration -30;
  }
  
  Mixin CYPowerup;
  
  override void DoEffect()
  {
	Super.DoEffect();
	time = EffectTics;
	PowerTimer("InvulnerabilitySphere/Warning", "", "66d988", "66d988", true);
  }
}

Class DCY_RadiationSymbol : RadSuit
{
  Default
  {
  +FLOATBOB
  +BRIGHT
  +VISIBILITYPULSE
  RenderStyle "Add";
  Alpha 1.0;
  Inventory.PickupSound "RadiationSphere/PickedUp";
  Inventory.PickupMessage "\c[Green]Radiation Protection\c-";
  }
  
  States
  {
  Spawn:
	BIOS A 1 Bright A_SpawnItemEx("DCY_RadSphereAura", random(-1, 1), random(-1, 1), random(24, 28), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION);
	Loop;
  Pickup:
	SOUL A 0 A_SetBlend("Green", 0.6, 30, "Yellow");
	Stop;
  }
}

Class DCY_Crosscut : CustomInventory
{
  Default
  {
  Inventory.Amount 1;
  Inventory.MaxAmount 3;
  Inventory.InterHubAmount 3;
  +FLOATBOB
  +INVENTORY.INVBAR
  +INVENTORY.HUBPOWER
  Inventory.PickupMessage "\c[x1]Soon you get to drain your enemies' lives just by hurting them. Good.\c-";
  Inventory.PickupSound "LevelUpMaybe";
  Inventory.UseSound "Soin 2";
  RenderStyle "Add";
  Scale 1.25;
  Tag "Crosscut - Portable Drain for 60 seconds.";
  Inventory.Icon "CRCTE0";
  }
  
  States
  {
  Spawn:
	CRCT AABBCCDDCCBBAA 1 Bright A_SpawnItemEx("DCY_CrosscutAura", random(-1, 1), random(-1, 1), random(24, 28), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION);
	Loop;
  Use:
	TNT1 A 32
	{
		A_SetBlend("Yellow", 0.75, 35, "Orange");
		A_GiveInventory("DCY_CrosscutPower", 1);
	}
	TNT1 A 1;
	Stop;
  }
}

Class DCY_CrosscutPower : PowerDrain
{ 
  Mixin CYPowerup;
  
  Default
  {
  Powerup.Strength 0.5;
  Powerup.Duration -60;
  }
  
  override void DoEffect()
  {
	Super.DoEffect();
	time = EffectTics;
	PowerTimer("CROSWARN", "CROSEND", "ffa200", "ffa200", true);
  }
}

Class DCY_RazeSpherule : CustomInventory
{ 
  Default
  {
  Inventory.PickupMessage "\c[Purple]Suddently you feel fantasy RPG buffs.\c- (4x Damage)";
  Inventory.PickupSound "Raze/Pickup";
  +INVENTORY.ALWAYSPICKUP
  +VISIBILITYPULSE
  Inventory.PickupFlash "DCY_UncoRingThing";
  RenderStyle "Add";
  }
  
  States
  {
  Spawn:
	RAZ_ AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("DCY_UncoAura", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(29.0, 31.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 3.0), random(0, 360), SXF_NOCHECKPOSITION);
    RAZ_ A 1 Bright
	{
		A_SpawnItemEx("DCY_UncoAura", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(29.0, 31.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(2.0, 3.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_RazeSpheruleCircle");
		A_SpawnItemEx("DCY_RazeSpheruleCircle2");
	}
    Loop;
  Pickup:
	TNT1 A 1
	{
		A_QuakeEx(2, 2, 2, 40, 0, 300, "none", QF_SCALEDOWN, 0, 0, 0, 600);
		A_SetBlend("White", 0.85, 30, "Purple");
		A_SetMugshotState("Grin");
		A_GiveInventory("DCY_QuadDamagePower", 1);
	}
    Stop;
  }
}

Class DCY_RazeSpheruleCircle : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  Translation "0:255=%[0.00,0.00,0.00]:[1.31,0.00,2.00]";
  +FLATSPRITE;
  +FLOORHUGGER;
  +BRIGHT;
  RenderStyle "Add";
  Alpha 0.001;
  Scale 0.25;
  }
  
  States
  {
  Spawn:
	SPE2 A 0 NoDelay
	{
		A_ChangeVelocity(0, 0, -200000000, CVF_REPLACE);
		A_SetRoll(frandom(0.00, 360.00));
		A_SetTranslation("0:255=%[0.00,0.00,0.00]:[1.31,0.00,2.00]");
	}
	SPE2 ABCDE 1 A_FadeIn(0.2, 0);
	SPE2 FGHIJKLMNOPQRSTUVWXYZ 1;
	SP22 ABCDEFGHIJKL 1;
	Stop;
  }
} 

Class DCY_RazeSpheruleCircle2 : DCY_RazeSpheruleCircle
{
  Default
  {
  Scale 0.4;
  }
  
  States
  {
  Spawn:
	SPE2 A 0 NoDelay
	{
		A_ChangeVelocity(0, 0, -200000000, CVF_REPLACE);
		A_SetRoll(frandom(0.00, 360.00));
		A_SetTranslation("0:255=%[0.00,0.00,0.00]:[1.31,0.00,2.00]");
	}
	SPE2 ABCDE 1 A_FadeIn(0.1, 0);
	SPE2 FGHIJKLMNOPQRSTUVWXYZ 1;
	SP22 ABCDEFGHIJKL 1;
	Stop;
  }
}

Class DCY_QuadDamagePower : PowerDamage
{
  Mixin CYPowerup;
  
  Default
  {
  DamageFactor "Normal", 4;
  Powerup.Color "50 00 50", 0.4;
  }
  
  override void DoEffect()
  {
	Super.DoEffect();
	time = EffectTics;
	PowerTimer("Raze/Warning", "Raze/End", "7300ff", "7300ff", true);
  }
}

Class DCY_Rehabiliation : DCY_Powerup
{
  Default
  {
  Inventory.PickupMessage "\c[n2]You undergo through a feeling of rehabiliation by embracing what's near you.\c- (150+ Health)";
  Inventory.PickupSound "D4REGEN1";
  }
  
  States
  {
  Spawn:
    LFSP AABBCCDDCCBB 1 Bright A_SpawnItemEx("DCY_RehabiliationAura", random(-1, 1), random(-1, 1), random(19, 23), random(-1, 1), random(-1, 1), random(-1, 1), 0, SXF_NOCHECKPOSITION);
    Loop;
  }
  
  override bool Use(bool pickup)
  {
	Owner.GiveBody(150, 300);
	Owner.A_SetBlend("Yellow", 0.5, 20);
	return true;
  }
}

Class DCY_Supremacy : DCY_Powerup
{
  Default
  {
  -FLOATBOB
  Scale 1.1;
  Tag "Elite's Blessing";
  DCY_Powerup.EffectColor1 "99a865";
  DCY_Powerup.AmbientNoise "Supremacy/IdleNoise";
  Inventory.PickupSound "StronkSphere/PickedUp";
  Inventory.PickupMessage "\c[i0]All of a sudden you feel like you can do it!\c- (160+ Health)";
  }
  
  bool rising;
  
  override void Tick()
  {
	super.Tick();
	
	alpha = frandom(0.6, 1.0);
	
	if (DCY_EffectDistance(900))
	{
		A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 80, 120, 0, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) + 20, frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.05 * alpha, fadestepf: 0.0015, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		
		A_SpawnItemEx("DCY_SupremacyLine", frandom(-30, 30), frandom(-30, 30), frandom(0, 15), zvel: frandom(0.5, 1.5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, failchance: 70);
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(effectcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(6, 9), frandom(0, 360), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) + 20, frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.4 * alpha, fadestepf: 0, sizestep: -0.15);
	}
  }
  
  States
  {
  Spawn:
	SJRM AB 7 Bright;
	Loop;
  Pickup:
	SJRM AAAAAAABBBBBBB 7
	{
		rising = true;
		
		A_FadeOut(0.03);
		vel.z += 0.05;
	}
	Wait;
  }
  
  override bool Use(bool pickup)
  {
	Owner.GiveBody(150, 700);
	Owner.A_SetBlend("White", 1, 40, effectcolor);
	Owner.A_SpawnItemEx("DCY_SupremacyPickupFlash", flags: SXF_SETMASTER);
	A_SpawnItemEx("DCY_SupremacyRise", flags: SXF_NOCHECKPOSITION);
	return true;
  }
}

Class DCY_SupremacyRise : Actor
{
  Mixin DCY_Effects;
  
  override void Tick()
  {
	super.Tick();
	
	if (DCY_EffectDistance(900))
	{
		A_SpawnParticleEx(0x99a865, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 80, 120, 0, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) + 20, frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.05, fadestepf: 0.0015, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		
		A_SpawnItemEx("DCY_SupremacyLine", frandom(-30, 30), frandom(-30, 30), frandom(0, 15), zvel: frandom(0.5, 1.5), flags: SXF_NOCHECKPOSITION, failchance: 70);
		A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA);
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(0x99a865, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(6, 9), frandom(0, 360), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) + 20, frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.4, fadestepf: 0, sizestep: -0.15);
	}
  }
  
  Default
  {
	+NOGRAVITY
	+THRUACTORS
	-SOLID
	RenderStyle "Add";
	Alpha 0.8;
  }
  
  States
  {
  Spawn:
	SJRM AAAAAAABBBBBBB 1
	{
		A_FadeOut(0.01);
		vel.z += 0.05;
	}
	Wait;
  }
}

Class DCY_SupremacyLine : DCY_VLine { Default { Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.35,0.65]"; } }

Class DCY_SupremacyPickupFlash : Actor
{
  override void Tick()
  {
	super.Tick();
	
	if (master)
		Warp(master, 0, 0, 0, master.angle, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
  }
  
  int timer;
  
  Default
  {
  Radius 0;
  Height 0;
  -SOLID;
  +THRUACTORS
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (timer > 60)
			Destroy();
		else
			timer++;
		
		A_SpawnParticleEx(0x99a865, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 80, 120, 0, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10) + 20, frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.05, fadestepf: 0.0015, sizestep: -0.5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
		
		A_SpawnItemEx("DCY_SupremacyLine", frandom(-30, 30), frandom(-30, 30), frandom(0, 15), zvel: frandom(0.75, 2), flags: SXF_NOCHECKPOSITION, failchance: 90);
	}
	Loop;
  }
}


Class DCY_Prospect : CustomInventory
{
  Default
  {
  Inventory.Amount 1;
  Inventory.MaxAmount 2;
  Inventory.InterHubAmount 2;
  +FLOATBOB
  +INVENTORY.INVBAR
  +INVENTORY.HUBPOWER
  Inventory.PickupMessage "\c[d8]Shield yourself for your own good.\c-";
  Inventory.PickupSound "SPELL10";
  Inventory.UseSound "SPELL16";
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.50]:[2.00,1.01,1.01]";
  Scale 1.1;
  Tag "Prospect - Invulnerability for 7 seconds. But damages enemies instead.";
  Inventory.Icon "_407E0";
  }
  
  States
  {
  Spawn:
	_407 AAABBBCCCDDD 1 Bright A_SpawnItemEx("DCY_ProspectAura", random(-1, 1), random(-1, 1), random(24, 28), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION);
	Loop;
  Use:
	TNT1 A 32
	{
		A_SpawnItemEx("DCY_ProspectRingThing", 0, 0, 30, 0, 0, 0, angle, SXF_NOCHECKPOSITION);
		A_QuakeEx(2, 2, 2, 22, 0, 200, "none", QF_SCALEDOWN);
		A_SetBlend("CB B9 EE", 0.75, 25, "Red");
		A_GiveInventory("DCY_SoundPower", 1);
		A_GiveInventory("DCY_ProspectPower", 1);
		A_GiveInventory("DCY_ReflectivePower", 1);
	}
	TNT1 A 1;
	Stop;
  }
}

Class DCY_ProspectPower : PowerInvulnerable { Default { Powerup.Duration -7; Powerup.Color "65 10 90", 0.075; } }
Class DCY_SoundPower : PowerProtection { Default { Powerup.Duration -7; ActiveSound "SPELL04"; +PIERCEARMOR; DeathSound "SPELL02"; DamageFactor "Normal", 1.0; -DONTBLAST -DONTMORPH -DONTRIP -DONTSQUASH -NOPAIN -NORADIUSDMG -NOTELEOTHER } }
Class DCY_ReflectivePower : PowerReflection { Default { Powerup.Duration -7; } }