Class DCY_Grenades : DCY_Weapon
{
  Default
  {
	Tag "Intervention Y0";
	Weapon.SelectionOrder 2800;
	Weapon.SlotNumber 5;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.EXPLOSIVE;
	Weapon.AmmoGive 2;
	Weapon.AmmoUse 1;
	Weapon.AmmoType "RocketAmmo";
	Inventory.PickupSound "dcy/rocketpickup";
	Inventory.PickupMessage "You got the Intervention! A great weapon when your explosives cannot reach in certain areas. It's time to make a lot of crimes in demonland.";
  }
  
  action void A_CyberaugumentedGrenade()
  {
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_StartSound("Grenade/Launch", CHAN_WEAPON, 0, 0.85, 0.85);
	A_FireProjectile(countInv("DCY_PhotocoagulatorPower") ? "DCY_Napalm" : "DCY_Grenade", 0, 0, pitch: -7.5);
	A_QuakeEx(1, 1, 1, 25, 0, 150, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 295);
	A_WeaponOffset(frandom(-4.5, 4.5), frandom(0, 4.35), WOF_ADD|WOF_INTERPOLATE);
	A_GunFlash();
  }

  States
  {
  Spawn:
	G3RN Z -1;
	Stop;
  Select:
	GNN_ WXY 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	GNN_ YXW 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	GNN_ A 1 A_WeaponReady();
	Loop;
  Fire:
	GNN_ A 2;
	GNN_ B 2 Bright;
	GNN_ C 2 Bright A_CyberaugumentedGrenade();
	GNN_ D 2 Bright;
	GNN_ EFGHI 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	GNN_ J 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	GNN_ J 0 A_Refire();
	Goto Ready;
  Flash:
	TNT1 A 3 Bright A_Light(1);
    TNT1 B 2 Bright;
    TNT1 CD 3 Bright A_Light(2);
    Goto LightDone;
  }
}