Class DCY_UnderlineShotgun : DCY_Weapon
{
  Default
  {
	Tag "Silver Blaster SB-7";
	Inventory.PickupMessage "You got the Silver Blaster! A simple assault/automatic shotgun.";
	Obituary "$o was blown away into some meters by $k's Silver Blaster.";
	Weapon.AmmoType "Shell";
	Weapon.AmmoGive 15;
	Weapon.AmmoUse 1;
	Weapon.SlotNumber 3;
	Decal "BulletChip";
	+FLOORCLIP;
	+DONTGIB;
	Weapon.Kickback 65;
	Inventory.PickupSound "Underline/Pickup";
	Weapon.SelectionOrder 1100;
  }
  
  action void A_UnderlineFire()
  {
    A_StartSound("Underline/Fire", CHAN_WEAPON);
    A_QuakeEx(1, 1, 1, 20, 0, 100, "", QF_RELATIVE|QF_SCALEDOWN, falloff: 200);
    invoker.DepleteAmmo(invoker.bAltFire, true);
    A_GunFlash();
	A_FireBullets(5, 3, 7, 8, "DCY_Effect", FBF_NORANDOM, 8192, "DCY_TracerPlayer", 0);
    A_WeaponOffset(frandom(-2.0, 2.0), frandom(-2.0, 2.0), WOF_ADD|WOF_INTERPOLATE);
  }
  
  States
  {
  Spawn:
    S0HT Z -1; //The spawn sprite is not here yet.
    Stop;
  Ready:
	S0HT A 1 A_WeaponReady();
	Loop;
  Select:
	S0HT WXY 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	S0HT YXW 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower;
	Wait;
  Fire:
    S0HT A 1 Bright;
	S0HT B 1 Bright A_UnderlineFire();
	S0HT CD 1 Bright;
	S0HT E 1 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	S0HT FGHA 1;
	S0HT A 0 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	S0HT H 0 A_Refire();
	Goto Ready;
  }
}