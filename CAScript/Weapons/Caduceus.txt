Class DCY_NeonicWand : DCY_Weapon 
{
  Default
  {
	Tag "Caduceus";
	Obituary "%o couldn't even surpass %k's Caduceus azure power.";
	Weapon.AmmoType "DCY_UltramodernEnergy";
	Weapon.AmmoUse 1;
	Weapon.AmmoGive 40;
	+WEAPON.EXPLOSIVE;
	DamageType "Neonic";
	Weapon.SlotNumber 6;
	Weapon.SelectionOrder 2000;
	Inventory.Icon "NNW3Z0";
	Inventory.PickupSound "dcy/plasmariflepickup";
	Inventory.PickupMessage "You hands hold the presence of the \c[l5]Caduceus.\cL Not your general plasma rifle.";
	Radius 23;
	Height 53;
  }
  
  action void A_NeonLaser()
  {
	A_GunFlash();
	A_StartSound("Lazer/Fire", CHAN_WEAPON);
	A_RailAttack(random(50, 100), 0, 0, flags: RGF_FULLBRIGHT|RGF_SILENT, pufftype: "DCY_NeonicRing", sparsity: 30, spawnclass: "DCY_NeonicWandTrail");
	invoker.DepleteAmmo(invoker.bAltFire, true);
  }

  action void A_NeonSphere()
  {
	A_StartSound("NeonicBall/Fire", CHAN_WEAPON);
	A_FireProjectile("DCY_NeonicBall", 0, 0);
	A_GunFlash("Flash2");
	invoker.DepleteAmmo(invoker.bAltFire, true, 4);
  }
  
  int weaponmode;
  
  States
  {
  Spawn:
    NNW3 Z -1;
	Stop;
  Select:
	C_AD XYZ 1 Bright A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	C_AD ZYX 1 Bright;
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
    C_AD A 5 Bright A_WeaponReady(WRF_ALLOWRELOAD);
	Loop;
  Reload: //AKA. Switch Modez
	TNT1 A 0
	{
		string s = (!invoker.weaponmode) ? "Neonic Spherule" : "Plasmatic Rays";
		A_StartSound("NeonicWand/Switch", CHAN_WEAPON);
		invoker.weaponmode = !invoker.weaponmode;
		console.printf("Changing to:\c[l5] %s", s);
	}
    C_AD AAAAA 2 Bright A_WeaponOffset(frandom(-2.0, 2.0), frandom(-2.0, 2.0) + 32, WOF_INTERPOLATE);
	C_AD A 2 Bright A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	Goto Ready;
  Fire:
	C_AD BCD 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH|WRF_NOBOB);
	Goto Hold;
  Hold:
    C_AD E 1 Bright
	{
		if (invoker.weaponmode) A_NeonSphere();
		else A_NeonLaser();
	}
	C_AD FGHI 1 Bright;
	C_AD I 0 Bright
	{
		if (!invoker.weaponmode) A_Refire("Hold");
		A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
	}
	C_AD JKLAA 2 Bright {
		tics = (invoker.weaponmode) ? 3 : 2;
	}
	Goto Ready;
  Flash:
	TNT1 A 2 A_Light1();
	TNT1 A 1 A_Light0();
	Goto LightDone;
  Flash2:
	TNT1 A 5 A_Light1();
	TNT1 A 4 A_Light0();
	Goto LightDone;
  }
  
  override Inventory CreateCopy(Actor other)
  {
	DCY_IncreaseLevel(2);
	return Super.CreateCopy(other);
  }
}

Class DCY_NeonicBall : FastProjectile
{
  Default
  {
	Damage 15;
	Radius 24;
	Height 24;
	Speed 60;
	DamageType "Player";
	RenderStyle "Add";
	Alpha 1.0;
	Scale 1.0;
	+BRIGHT;
	+NOEXTREMEDEATH;
	+FORCERADIUSDMG;
	Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
	SeeSound "NeonicBall/Fire";
	DeathSound "NeonicBall/Death";
	MissileType "DCY_NeonicTrail";
	MissileHeight 6;
  }
  
  void A_NeonDestruct()
  {
	for (int i = 12; i > 0; i--)
		A_SpawnParticle(0xaaddff, SPF_FULLBRIGHT, random(40, 55), 10, frandom(0, 360), frandom(-7.5, 7.5), frandom(-7.5, 7.5), frandom(-7.5, 7.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(0.5, 11.4), accelz: -0.85, fadestepf: 0, sizestep: -0.425);
	
	for (int i = 2; i > 0; i--)
		A_SpawnItemEx("DCY_NeonExplode", flags: SXF_NOCHECKPOSITION);
	
	A_Explode(256, 128, XF_NOTMISSILE);
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_SpawnParticle(0xaaddff, SPF_FULLBRIGHT, random(40, 55), 10, frandom(0, 360), frandom(-17.5, 17.5), frandom(-17.5, 17.5), frandom(-17.5, 17.5), fadestepf: 0, sizestep: -0.425);
		A_Weave(1, 1, 0.5, 0.5);
	}
	Loop;
  Death:
	TNT1 A 2 A_NeonDestruct();
	Stop;
  }
}

Class DCY_NeonicTrail : Actor
{
  Default
  {
	Radius 0;
	Height 0;
	Alpha 0.5;
	Scale 1.2;
	Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
	RenderStyle "Add";
	+NOINTERACTION;
	+NOCLIP;
	+ROLLSPRITE;
	+BRIGHT;
  }
  
  States
  {
  Spawn:
    M_TR C 1 NoDelay A_SetRoll(frandom(0, 360));
	M_TR CCDDDEEEFFFGGGH 1
	{
		A_SetScale(Scale.X - 0.175);
		A_FadeOut(0.05);
	}
	Stop;
  }
}

Class DCY_NeonExplode : Actor
{
  Default
  {
  Scale 1.275;
  Alpha 1.0;
  RenderStyle "Add";
  +BRIGHT
  +ROLLSPRITE
  +NOINTERACTION
  -SOLID
  Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
  }
  
  States
  {
  Spawn:
    KABE A 1 NoDelay A_SetRoll(frandom(0, 360));
	KABE BCDEFGHIJKLMNOPQRSTUVW 1;
	Stop;
  }
}