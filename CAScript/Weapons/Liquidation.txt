Class DCY_TheBFG10000 : DCY_Weapon
{
  Default
  {
	Weapon.Selectionorder 2500;
	Weapon.AmmoUse1 1;
	Weapon.AmmoGive 40;
	Weapon.AmmoType1 "Cell";
	Weapon.Kickback 100;
	Weapon.SlotNumber 7;
	Height 20;
	Inventory.Pickupmessage "Aquired the Liquidation! It says what it's written on the tin. It liquidates everywhing whenever you feel like it.";
	Weapon.UpSound "BFG10000Proto/UP";
	Inventory.PickupSound "BFG10000Proto/UP";
	Tag "Aurum-powered 'Liquidation' Laser Blaster 777";
	Obituary "All of %o's remains was blasted by %k's Liquidation.";
	+WEAPON.BFG;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.NOAUTOAIM;
	//Decal BFGLightning;
  }
  
  float lower;
  
  action void A_LiquidationShake(double shake1, double shake2, int shake3)
  {
	invoker.lower += 1;	
	A_QuakeEx(shake3, shake3, shake3, 10, 0, 200, "", QF_SCALEDOWN);
	A_OverlayScale(-4, 1 + (invoker.lower / 100), 1 + (invoker.lower / 100));
	A_OverlayOffset(-4, frandom(-shake1, shake1) - (invoker.lower / 2), (invoker.lower / 2) + frandom(-shake2, shake2), WOF_INTERPOLATE);
	
	return;
  }
  
  action void A_Liquidation()
  {
	A_RailAttack(100, 0, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, pufftype: "DCY_BFG10kLaserPuff", sparsity: 64, spawnclass: "DCY_BFG10kLASERTrail");
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_GunFlash();
  }
  
  action void A_ResetOverlay()
  {
	A_OverlayOffset(-4, 0, 0);
	A_OverlayScale(-4, 1, 1);
	A_Overlay(-4, "RemoveOverlay");
	invoker.lower = 0;
  }
  
  States
  {
  Spawn:
    BFG2 Z -1;
    Loop;
  Ready:
    L1QU A 1/* Bright*/
	{
		A_StartSound("BFG10000Proto/Idle", CHAN_5, CHANF_LOOPING);
		A_WeaponReady();
	}
    Loop;
  Deselect:
	L1QU A 1
	{
		A_ResetOverlay();
		A_Lower(26);
		A_StopSound(CHAN_5);
		A_StopSound(CHAN_WEAPON);
		A_StopSound(CHAN_7);
	}
	L1QU A 1 A_Lower(26);
	Wait;
  Select:
	L1QU A 1 A_Raise(26);
    Loop;
  Flash:
    TNT1 A 1/* Bright*/ A_Light1();
    TNT1 A 2/* Bright*/ A_Light2();
    Goto LightDone;
  ChargeOverlay:
	L1QU AAAABBBB 1/* Bright*/ A_LiquidationShake(1.5, 1.5, 1);
	L1QU CCCCDDDD 1/* Bright*/ A_LiquidationShake(3.15, 3.15, 2);
	L1QU EEEEFFFGGGHHH 1/* Bright*/ A_LiquidationShake(5.75, 5.75, 3);
	Stop;
  FiringOverlay:
	L1QU I 1/* Bright*/
	{
		A_OverlayOffset(-4, frandom(-3.0, 3.0) - (invoker.lower / 1.5), frandom(32.0, 37.0), WOF_INTERPOLATE);
		A_OverlayScale(-4, 1 + (invoker.lower / 100), 1 + (invoker.lower / 100), WOF_INTERPOLATE);
		A_QuakeEx(2, 2, 2, 15, 0, 300, "", QF_RELATIVE|QF_SCALEDOWN, rollintensity: 1.0, rollwave: 0.5);
	}
	Loop;
  RemoveOverlay:
	TNT1 A 1;
	Stop;
  CooldownOverlay:
	L1QU IJ 2/* Bright*/ { invoker.lower = -3; A_OverlayOffset(-4, 0, 44, WOF_INTERPOLATE); }
	L1QU KKKKKKKKKK 1/* Bright*/ { invoker.lower += 0.1; A_OverlayOffset(-4, 0, invoker.lower, WOF_INTERPOLATE|WOF_ADD); }
	L1QU LLLMMMNNNOOOPPPQQQRRR 1/* Bright*/ { invoker.lower += 0.1; A_OverlayOffset(-4, 0, invoker.lower, WOF_INTERPOLATE|WOF_ADD); }
	L1QU A 5/* Bright*/ A_OverlayOffset(-4, 0, 0, WOF_INTERPOLATE);
	Stop;
  Fire:
	L1QU A 2/* Bright*/ A_StopSound(CHAN_5);
	TNT1 A 27/* Bright*/
	{
		A_StartSound("BFG10000Proto/Charge", CHAN_WEAPON, 0, 0.85);
		A_Overlay(-4, "ChargeOverlay");
	}
	TNT1 A 0/* Bright*/
	{
		A_QuakeEx(5, 5, 5, 12, 0, 300, "", QF_RELATIVE|QF_SCALEDOWN, rollintensity: 2.0, rollwave: 2.0);
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_StopSound(CHAN_WEAPON);
		A_Overlay(-4, "FiringOverlay");
		A_StartSound("BFG10kLaser/Fire", CHAN_6, CHANF_DEFAULT, 0.75);
		A_SetBlend("White", 0.85, 20, "Yellow");
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1/* Bright*/ A_Liquidation();
	Goto Hold;
  Hold:
	TNT1 A 1/* Bright*/
	{
		A_Liquidation();
		A_StartSound("BFG10kLaser/Hold", CHAN_7, CHANF_LOOPING, 0.75);
	}
	TNT1 A 0 A_Refire("Hold");
	TNT1 A 1/* Bright*/;
	Goto CooldownSupreme;
  CooldownSupreme:
	TNT1 I 0 A_Overlay(-4, "CooldownOverlay");
	TNT1 A 10
	{
		A_StopSound(CHAN_WEAPON);
		A_StopSound(CHAN_7);
		A_StartSound("BFG10kLaser/Stop", CHAN_6, CHANF_DEFAULT, 0.75);
	}
	TNT1 A 29/* Bright*/ A_WeaponOffset(0, 32);
	TNT1 A 0 A_ResetOverlay();
	Goto Ready;
  }
}

Class DCY_SmallBFG10kBoom : BulletPuff
{
  Default
  {
	  Radius 0;
	  Height 0;
	  Scale 0.76;
	  Alpha 1.0;
	  VSpeed 0;
	  +ALWAYSPUFF;
	  RenderStyle "Add";
	  +ROLLSPRITE;
	  +FLATSPRITE;
	  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.49,0.72]";
	  +BRIGHT;
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	
	A_SetRoll(Roll+random(0, 360));
	A_SetAngle(Angle+random(0, 360));
	A_SetPitch(Pitch+random(0, 360));
		
	for (int i = 3; i > 0; i--)
	{
		A_SpawnItemEx("DCY_BFG10kLaserPuffSpark", 0, 0, 0, frandom(10.0, 20.5), 0, frandom(2.0, 13.0)+frandom(5.0, -5.0), frandom(-360.0, 360.0)+frandom(-180.0, 180.0), SXF_NOCHECKPOSITION, 28);
		A_SpawnItemEx("DCY_BFG10kLaserPuffSpark", 0, 0, 0, frandom(10.0, 20.5), 0, frandom(2.0, 13.0)+frandom(5.0, -5.0), frandom(-360.0, 360.0)+frandom(-180.0, 180.0), SXF_NOCHECKPOSITION, 60);
	}
		
	A_Explode(random(5, 10), 100, XF_NOTMISSILE);
  }
  
  States
  {
  Spawn:
	SUPH AACEGIKMOPQR 1;
	Stop;
  }
}