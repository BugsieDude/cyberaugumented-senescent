Class DCY_SuperShotty : DCY_Weapon
{
  Default
  {
	Tag "Stevens 430k Model Dual Barrel Shotgun";
	Inventory.PickupSound "dcy/shotgunpickup";
	Inventory.PickupMessage "You got the Dual Barrel shotgun! A certified classic. Time to kick ass.";
	Weapon.SelectionOrder 400;
	Weapon.AmmoUse 2;
	Weapon.AmmoGive 8;
	Weapon.AmmoType "Shell";
	Weapon.SelectionOrder 600;
  }
  
  action void A_SuperShotgunFire()
  {
	A_GunFlash();
	
	invoker.DepleteAmmo(invoker.bAltFire, true);
	
	for (int i = -4; i < 5; i += 8)
		A_FireBullets(11, 7, 10, 11, "DCY_Effect", FBF_NORANDOM, 8192, "DCY_TracerPlayer", 0, spawnofs_xy: i);
	
	A_QuakeEx(1, 1, 1, 30, 0, 90, "none", QF_SCALEDOWN, falloff: 180);
	A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
  }
  
  States
  {
  Spawn:
	_S2G Z -1;
	Stop;
  Select:
	_S2G ABC 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	_S2G CBA 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	_SSG A 1 A_WeaponReady();
	Loop;
  Fire:
	_SSG A 0;
	_SSG A 1
	{
		A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		A_StartSound("DCYSuperShotgun/Fire", CHAN_WEAPON);
	}
	_SSG B 1 Bright A_SuperShotgunFire();
	_SSG CDE 1 Bright;
	_SSG FGH 1;
	_SSG I 0 A_JumpIfNoAmmo("Ready");
	_SSG IJKLM 2;
	_SSG N 2 A_StartSound("DCYSuperShotgun/Reload", CHAN_WEAPON, CHANF_OVERLAP, 0.5);
	_SSG OPQRSTUVWXYZ 2;
	_S2G ABC 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_S2G C 0 A_Refire();
	Goto Ready;
  Flash:
	TNT1 A 2 A_Light1();
	TNT1 A 2 A_Light2();
	Goto LightDone;
  }
}