//Eternal's Burst Pistol
Class DCY_BurstPistol : Weapon
{
  Default
  {
	  Tag "Burst Pistol";
	  Inventory.PickupSound "dcy/riflepickup";
	  Weapon.SelectionOrder 1900;
	  Weapon.BobStyle "Alpha";
	  Weapon.BobRangeX 0.45;
	  Weapon.BobRangeY 0.25;
	  Weapon.BobSpeed 1.45;
	  Weapon.AmmoUse1 1;
	  Weapon.AmmoGive1 50;
	  Weapon.AmmoUse2 1;
	  Weapon.AmmoGive2 50;
	  Weapon.SlotNumber 2;
	  Weapon.AmmoType2 "Clip";
	  Weapon.AmmoType1 "Clip";
	  Obituary "$OB_MPPISTOL";
	  Inventory.PickupMessage "You got back your... Burst Pistol? Thanks, console.";
	  +WEAPON.WIMPY_WEAPON;
	  +WEAPON.NOAUTOFIRE;
  }
  
  action void A_PistolFire()
  {
	A_StartSound("BurstPistol/Fire", CHAN_WEAPON);
	A_GunFlash();
	invoker.DepleteAmmo(invoker.bAltFire, true);
	
	if (CheckInventory("DCY_PhotocoagulatorPower", 1, AAPTR_DEFAULT))
	{
		A_FireProjectile("DCY_ExplosivePistolTracer", frandom(-0.85, 0.85), 0, 0, frandom(-0.85, 0.85));
	}
	else
	{
		A_FireProjectile("DCY_PistolTracer", frandom(-0.85, 0.85), 0, 0, frandom(-0.85, 0.85));
	}
	
	A_WeaponOffset(frandom(-1.5, 1.5), frandom(-1.5, 1.5), WOF_ADD|WOF_INTERPOLATE);
  }
  
  action void A_PistolBurstFire()
  {
	A_GunFlash();
	invoker.DepleteAmmo(invoker.bAltFire, true);
	
	if (CheckInventory("DCY_PhotocoagulatorPower", 1, AAPTR_DEFAULT))
	{
		A_FireProjectile("DCY_ExplosivePistolTracer", frandom(-0.85, 0.85), 0, 0, frandom(-0.85, 0.85));
	}
	else
	{
		A_FireProjectile("DCY_PistolTracer", frandom(-0.85, 0.85), 0, 0, frandom(-0.85, 0.85));
	}
	
	A_WeaponOffset(frandom(-2.25, 2.25), frandom(-2.25, 2.25), WOF_ADD|WOF_INTERPOLATE);
  }
	
  States
  {
  Select:
	PES_ ABC 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	PES_ CBA 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	PIS_ A 1 A_WeaponReady();
	Loop;
  NoAmmo:
	PIS_ A 1;
	Goto Ready;
  Fire:
	PIS_ A 0;
	PIS_ B 1 A_PistolFire();
	PIS_ CD 1 Bright;
	PIS_ E 1 Bright
	{
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_WeaponReady(WRF_NOBOB);
	}
	PIS_ FGHI 1 A_WeaponReady(WRF_NOBOB);
	Goto Ready;
  AltFire:
	PIS_ A 0;
	PIS_ A 1;
	PIS_ B 1;
	PIS_ C 1
	{
		A_PistolBurstFire();
		A_StartSound("BurstPistol/Burst", CHAN_WEAPON);
	}
	PIS_ D 1 Bright A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	PIS_ C 1 A_PistolBurstFire();
	PIS_ D 1 Bright A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	PIS_ C 1 A_PistolBurstFire();
	PIS_ D 1 Bright A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	PIS_ E 1 Bright;
	PIS_ FGHIAAAA 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	Goto Ready;
  Flash:
	TNT1 A 5 Bright A_Light1;
    Goto LightDone;
  }
}

//The Deagle
Class DCY_Razorwind : Weapon Replaces Pistol
{
  Default
  {
	Tag "Razorwind - ''Unknowingly Mechanized Deagle''";
	Weapon.SelectionOrder 2000;
	Weapon.BobStyle "Alpha";
	Weapon.BobRangeX 0.45;
	Weapon.BobRangeY 0.25;
	Weapon.BobSpeed 1.45;
	Weapon.AmmoUse1 1;
	Weapon.AmmoGive1 50;
	Weapon.AmmoUse2 1;
	Weapon.AmmoGive2 50;
	Weapon.SlotNumber 2;
	Weapon.AmmoType1 "DCY_UltramodernEnergy";
	Obituary "%o messed up with the deagle man.";
	Inventory.PickupSound "WH1PPKUP";
	Inventory.PickupMessage "You got the Razorwind! Laser Pistol or Plasmatic Deagle of some sorts?";
	+WEAPON.WIMPY_WEAPON;
	+WEAPON.NOAUTOFIRE;
  }
  
  action void A_Deagle()
  {
	for (int i = 0; i < 2; i++)
		A_StartSound("WH1PSWNG", CHAN_WEAPON + i);
	
	A_GunFlash();
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_FireProjectile("DCY_RazorwindLaser", frandom(-0.85, 0.85), 0, 0, frandom(-0.85, 0.85));
	A_WeaponOffset(frandom(-2.5, 2.5), frandom(-2.5, 2.5), WOF_ADD|WOF_INTERPOLATE);
  }
  
  override void Tick()
  {
	super.tick();
	
	if (!tracer)
		A_StopSound(20);
  }
  
  States
  {
  Spawn:
	DGL_ Y -1;
	Stop;
  Select:
	AFX_ WXY 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	AFX_ YXW 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	AFX_ A 1 A_WeaponReady();
	Loop;
  Fire:
	AFX_ BCD 1;
	AFX_ H 1 Bright A_Deagle();
	AFX_ I 1 A_WeaponReady(WRF_NOBOB);
	AFX_ K 2
	{
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_WeaponReady(WRF_NOBOB);
	}
	AFX_ L 2 A_WeaponReady(WRF_NOBOB);
	Goto Ready;
  Flash:
	TNT1 A 5 Bright A_Light1();
    Goto LightDone;
  }
}

Class DCY_RazorwindLaser : FastProjectile
{
  Default
  {
  +BRIGHT
  DamageFunction (random(1, 5) * 10);
  DamageType "Neonic";
  Speed 120;
  RenderStyle "Add";
  Scale .2;
  MissileHeight 6;
  MissileType "DCY_RazorwindTrail";
  }
  
  static const string ParticleColors[] = { "ffffff", "bffbff", "80d7ff", "87abff" };
  
  States
  {
  Spawn:
	TRAC E 1
	{
		for (int i = 3; i > 0; i--)
			A_SpawnParticle(ParticleColors[random(0, ParticleColors.Size() - 1)], SPF_FULLBRIGHT|SPF_RELATIVE, random(9, 16), random(9, 13), frandom(0, 360), frandom(-25.0, 25.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.1, sizestep: -1.25);
	}
	Loop;
  XDeath:
	TNT1 A 0
	{ 
		for (int i = 0; i < 2; i++)
		{
			target.A_StopSound(CHAN_WEAPON + i);
			target.A_StartSound("WH1PHURT", CHAN_WEAPON + i);
		}
	}
	Goto Death;
  Death:
	TNT1 A 1
	{
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_AndroidPuffSpark", 0, 0, 0, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, -3.0), frandom(-20.0, 20.0)+180, SXF_NOCHECKPOSITION, 60);
			A_SpawnItemEx("DCY_AndroidPuffSpark", 0, 0, 0, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(3.0, -3.0), frandom(-20.0, 20.0)+180, SXF_NOCHECKPOSITION, 28);
		}
	}
	TNT1 A 0;
	Stop;
  }
}

Class DCY_RazorwindSmallLaser : DCY_RazorwindLaser
{
  Default
  {
  DamageFunction (random(1, 2) * 2);
  Speed 200;
  Scale .1;
  MissileType "";
  }
  
  States
  {
  XDeath:
	TNT1 A 0 A_StartSound("WH1PHURT", CHAN_WEAPON);
	Goto Death;
  Death:
	TNT1 A 1
	{
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_AndroidPuffSpark", 0, 0, 0, frandom(4.0, 14.5), 0, Pitch+frandom(2.0, 8.0)+frandom(1.0, -1.0), frandom(-20.0, 20.0)+180, SXF_NOCHECKPOSITION, 60);
	}
	TNT1 A 2;
	Stop;
  }
}

Class DCY_RazorwindTrail : Actor
{
  Default
  {
  RenderStyle "Add";
  Scale 0.475;
  +ROLLSPRITE
  +BRIGHT
  +NOGRAVITY
  +DONTSPLASH
  Translation "0:255=%[0.00,0.00,0.00]:[0.20,0.39,0.74]";
  }
  
  States
  {
  Spawn:
	SUE2 I 0 NoDelay
	{
		A_ChangeVelocity(frandom(-3, 3), frandom(-3, 3), frandom(-3, 3));
		A_SetRoll(frandom(0, 360));
	}
	SUE2 I 1
	{
		A_FadeOut(0.2, 0);
		A_Stop();
	}
	SUE2 IIII 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_RazorwindTrailS : DCY_RazorwindTrail { Default { Alpha 0.5; Scale 0.23; } }

Class DCY_Cyberrifle : Weapon
{
  Default
  {
	Tag "8.12x50mm ''Warbringer''-52 MK4 Automatic Justifier DMR";
	Weapon.SlotNumber 4;
	Weapon.BobStyle "Alpha";
	Weapon.BobRangeX 0.45;
	Weapon.BobRangeY 0.25;
	Weapon.BobSpeed 1.45;
	Weapon.AmmoUse1 1;
	Weapon.AmmoGive1 20;
	Weapon.SlotNumber 2;
	Weapon.AmmoType2 "Clip";
	Weapon.AmmoType1 "Clip";
	Inventory.PickupSound "dcy/riflepickup";
	Obituary "%o got holes in %h body by %k.";
	Inventory.PickupMessage "The Warbringer! It's a mauve rifle with some resemblance of the Heavy Cannon Rifle.";
	+WEAPON.WIMPY_WEAPON;
	//+WEAPON.NOAUTOFIRE;
  }
  
  action void A_RifleF()
  {
	A_StartSound("Rifle/Fire", CHAN_WEAPON, volume: 0.75);
	A_GunFlash();
	invoker.DepleteAmmo(invoker.bAltFire, true);
	if (CheckInventory("DCY_PhotocoagulatorPower", 1, AAPTR_DEFAULT))
	{
		A_FireProjectile("DCY_ExplosiveRifleTracer", frandom(-0.35, 0.35), 0, 0, frandom(-0.35, 0.35));
	}
	else
	{
		A_FireProjectile("DCY_RifleTracer", frandom(-0.35, 0.35), 0, 0, frandom(-0.35, 0.35));
	}
	
	A_WeaponOffset(frandom(-1.5, 1.5), frandom(-1.5, 1.5), WOF_ADD|WOF_INTERPOLATE);
  }
  
  States
  {
  Spawn:
	RIF_ Z -1;
	Stop;
  Select:
	REE_ ABC 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	REE_ CBA 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	RFL_ A 1 A_WeaponReady();
	Loop;
  NoAmmo:
	RFL_ A 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Fire:
	RFL_ B 1 Bright; 
	RFL_ C 1 Bright A_RifleF();
	RFL_ D 1 Bright;
	RFL_ E 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	RFL_ F 1
	{
		A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	}
	RFL_ G 1
	{
		A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
		A_Refire("Fire");
	}
	RFL_ H 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	Goto Ready;
  Flash:
	TNT1 A 3 Bright A_Light1();
	TNT1 A 3 Bright A_Light2();
    Goto LightDone;
  }
}