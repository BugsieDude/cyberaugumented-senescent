Class DCY_ChainsawGoBZZZZZZZZZZZZZZZZ : DCY_Weapon
{
  Default
  {
	  Inventory.PickupMessage "You got the Power Saw! ...You're going to cut the bushes right? Right?";
	  Weapon.UpSound "Chainsaw/Draw";
	  Weapon.ReadySound "";
	  Weapon.SelectionOrder 2200;
	  Weapon.Kickback 0;
	  Obituary "%o got sliced and diced by $k's power saw.";
	  Tag "Power Saw";
	  -WEAPON.WIMPY_WEAPON;
	  +WEAPON.MELEEWEAPON;
  }
  
  action void A_ChainsawIdle()
  {
	A_WeaponOffset(frandom(-1, 1), frandom(31.0, 33.0));
	A_WeaponReady();
	A_StartSound("Chainsaw/Ready", CHAN_6, CHANF_LOOPING, 0.75);
  }
  
  action void A_Chainsaw()
  {
	A_WeaponOffset(frandom(-2.3, 2.3), frandom(32.0, 36.3));
	A_StartSound("Chainsaw/Loop", CHAN_6, CHANF_LOOPING, 0.75);
	A_QuakeEx(1, 1, 1, 20, 0, 100, "none", QF_SCALEDOWN);
  }
  
  States
  {
  Spawn:
	CHSS AB 8;
	Loop;
  Select:
	SAW_ KLM 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	SAW_ M 1
	{
		A_StopSound(CHAN_WEAPON);
		A_StopSound(CHAN_6);
	}
	SAW_ LK 1
	{
		A_StopSound(CHAN_6);
		A_WeaponOffset(0, 32);
	}
	TNT1 A 0 A_Lower;
	Wait;
  Ready:
	SAW_ AB 1 A_ChainsawIdle();
	Loop;
  Fire:
	SAW_ C 1 A_StartSound("Chainsaw/Start", CHAN_6, volume: 0.75);
	SAW_ DE 1;
	Goto Hold;
  Hold:
	SAW_ F 1
	{
		A_Saw("", "Chainsaw/Blood", random(1, 2), "DCY_ChainsawPuff", SF_NORANDOMPUFFZ);
		A_Chainsaw();
	}
	SAW_ G 1 A_Chainsaw();
	SAW_ G 0 A_Refire();
	SAW_ H 1 A_StopSound(CHAN_6);
	SAW_ H 1 A_StartSound("Chainsaw/Stop", CHAN_WEAPON);
	SAW_ IJ 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	Goto Ready;
  }
}

Class DCY_ArgentChainsaw : DCY_ChainsawGoBZZZZZZZZZZZZZZZZ //Um.
{
  Default
  {
	Inventory.PickupMessage "What the fuck is this???";
	Tag "UAC Prototype-42B Argent Chainsaw";
	Weapon.ReadySound "";
	Weapon.UpSound "CrucibleChainsaw/Up";
	Inventory.PickupSound "CrucibleChainsaw/Up";
	Obituary "%o was never seen again.";
	+EXTREMEDEATH;
	DamageType "Crucible";
  }
  
  action void A_CrucibleSaw()
  {
	A_WeaponOffset(frandom(-3.6, 3.6), frandom(32.5, 38.3));
	A_Saw("", "Chainsaw/Blood", random(10, 40), "DCY_CrucibleChainsawPuff", SF_NORANDOMPUFFZ);
	A_StartSound("CrucibleChainsaw/Loop", CHAN_6, CHANF_LOOPING, 0.75);
	A_QuakeEx(3, 3, 3, 20, 0, 100, "none", QF_SCALEDOWN);
  }
	
  States
  {
  Spawn:
	CRUH YZ 1 Bright;
	Loop;
  Select:
	CRUH ABCD 1 Bright A_Raise(12);
	Loop;
  Deselect:
	CRUH A 1 Bright
	{
		A_StopSound(CHAN_WEAPON);
		A_StopSound(CHAN_6);
		A_Lower(12);
	}
	CRUH BCDA 1 Bright
	{
		A_StopSound(CHAN_6);
		A_Lower(12);
	}
	Goto Deselect+1;
  Ready:
	CRUH ABCD 1 Bright
	{
		A_WeaponOffset(frandom(-1.5, 1.5), frandom(32.5, 34.5));
		A_WeaponReady();
		A_StartSound("CrucibleChainsaw/Ready", CHAN_6, CHANF_LOOPING, 0.75);
	}
	Loop;
  Fire:
	CRUH C 1 Bright A_StartSound("CrucibleChainsaw/Start", CHAN_6, volume: 0.75);
	CRUH DE 1 Bright;
	Goto Hold;
  Hold:
	CRUH EF 1 Bright A_CrucibleSaw();
	CRUH F 0 A_Refire("Hold");
	CRUH B 1 Bright A_StopSound(CHAN_6);
	CRUH A 1 Bright A_StartSound("CrucibleChainsaw/Stop", CHAN_WEAPON, volume: 0.75);
	Goto Ready;
  }
  
  override Inventory CreateCopy(Actor other)
  {
	DCY_IncreaseLevel(19);
	return Super.CreateCopy(other);
  }
}