Class DCY_SiriusCrisis : DCY_Weapon
{
  Default
  {
	Tag "Sirius Crisis Roscoe";
	Inventory.PickupMessage "Your hands handle the great \c[Sapphire]Sirius Crisis Roscoe\cL.";
	Obituary "%o cannot even handle the power of the $k's \c[Sapphire]Sirius Crisis Roscoe\cL.";
	Height 20;
	Weapon.BobSpeed 1.0;
	Weapon.AmmoType "DCY_UltramodernEnergy";
	Weapon.AmmoGive 45;
	Weapon.AmmoUse 45;
	Weapon.AmmoUse2 400;
	Weapon.Kickback 150;
	Weapon.SlotNumber 7;
	Weapon.UpSound "SiriusBFG/Up";
	Inventory.PickupSound "SiriusBFG/Pickup";
	+WEAPON.BFG;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.NOAUTOAIM;
	Weapon.SelectionOrder 2000;
  }
  
  action void A_SiriusBlast()
  {
	A_SetBlend("Cyan", 0.5, 20, "Blue");
	A_GunFlash();
	A_StartSound("SiriusBFG/Fire", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.5);
	A_FireProjectile("DCY_SiriusBFGBall", 0, 0);
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_QuakeEx(4, 4, 4, 40, 0, 600, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(2, 2, 2, 50, 0, 600, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(1, 1, 1, 60, 0, 600, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(1, 1, 1, 55, 0, 600, "", QF_SCALEDOWN, rollintensity: frandom(0.35, 0.45), rollwave: frandom(-0.65, 0.65));
  }
  
  action void A_SiriusLaser()
  {
	A_SetBlend("Cyan", 0.5, 10 + (invoker.chargemeter / 5), "Blue");
	A_GunFlash();
	
	if (invoker.chargemeter > 0)
		A_FireProjectile("DCY_SiriusLaser", 0, 0);
	
	invoker.DepleteAmmo(invoker.bAltFire, true);
  }
  
  float shakemeter;
  float chargemeter;
  
  States
  {
  Spawn:
    BFG8 A -1;
	Stop;
  Select:
    SRB0 A 1 Bright A_Raise(20);
	Loop;
  Deselect:
    SRB0 A 1 Bright
	{
		A_StopSound(CHAN_5);
		A_Lower(20);
	}
	Loop;
  Ready:
    SRB0 ABCB 1 Bright
	{
		A_StartSound("BFG10000Proto/Idle", CHAN_5, CHANF_LOOPING, 0.65);
		A_WeaponReady();
	}
	Loop;
  Fire:
	SRB0 A 10 Bright { A_StopSound(CHAN_5); invoker.chargemeter = 0; A_StartSound("MECHGRAB", CHAN_6); }
	Goto FHold;
  FHold:
	SRB0 C 1 Bright
	{
		A_WeaponOffset(frandom(-1.0, 1.0) * (invoker.chargemeter / 15), 32 + (frandom(-1.0, 1.0) * (invoker.chargemeter / 15)), WOF_INTERPOLATE);
		A_RailAttack(false, 0, 0, "", "Cyan", RGF_FULLBRIGHT|RGF_SILENT|RGF_NOPIERCING|RGF_NORANDOMPUFFZ, frandom(-5, 5), duration: 20, sparsity: 5, driftspeed: frandom(-2, 2));
		A_RailAttack(false, 0, 0, flags: RGF_FULLBRIGHT|RGF_SILENT|RGF_NOPIERCING|RGF_NORANDOMPUFFZ, sparsity: 20, spawnclass: "DCY_SiriusTrack");
		A_StartSound("DIVLOOP", CHAN_5, CHANF_LOOPING);
		
		if (invoker.chargemeter < 20)
			invoker.chargemeter += 0.1;
	}
	SRB0 A 0
	{
		if (invoker.chargemeter < 20)
			A_Refire("FHold");
	}
	Goto HoldEnd;
  HoldEnd:
	SRB0 E 0 { A_StartSound("Eradicator/Laser", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.5, 1.253 - (invoker.chargemeter / 23)); A_StopSound(CHAN_5); }
	SRB0 EF 1;
	SRB0 G 2
	{
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_SiriusLaser();
		invoker.chargemeter = 0;
	}
	SRB0 HIJKL 2 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	SRB0 MNO 2 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	SRB0 AAAAABBBCCBB 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	Goto Ready;
  Superfire:
    SRB0 A 1 Bright { A_StopSound(CHAN_5); A_QuakeEx(3, 3, 3, 45, 0, 500, "", QF_RELATIVE|QF_SCALEUP); invoker.shakemeter = 0; A_StartSound("SiriusBFG/Charge", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.645); }
	SRB0 BCBABCBABCBABCBABCDEF 1 Bright
	{
		A_WeaponOffset(frandom(-2.0, 2.0) * invoker.shakemeter, 32 + frandom(-2.0, 2.0) * invoker.shakemeter, WOF_INTERPOLATE);
		invoker.shakemeter += 0.35;
	}
	SRB0 G 3 Bright
	{
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_SiriusBlast();
	}
	SRB0 HIJKL 3 Bright;
	SRB0 MNO 3 Bright;
	SRB0 AAAAABBBCCBB 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	Goto Ready;
  Flash:
    TNT1 A 0;
	TNT1 A 3 A_Light1();
	TNT1 A 5 A_Light2();
	TNT1 A 4 A_Light0();
	Goto LightDone;
  }
}

Class DCY_SiriusLaser : DCY_TracerPlayer
{
  Default
  {
	Damage 10;
	Radius 10;
	Height 10;
	Scale .6;
	Translation "0:255=%[0.00,0.00,0.00]:[1.06,1.66,1.83]";
	DeathSound "";
	+BRIGHT
	+FORCERADIUSDMG
  }
  
  States
  {
  Spawn:
	TRAC E 1 A_Explode(10, 30);
	Loop;
  Death:
  XDeath:
	TNT1 A 10
	{
		bEXTREMEDEATH = true;
		bNOEXTREMEDEATH = false;
		let ring = Spawn("DCY_OverdemoniacPuff", pos);
		
		if (ring)
		{
			ring.translation = translation;
			ring.scale *= 3;
		}
		
		for (int i = 15; i > 0; i--)
			A_SpawnItemEx("DCY_SiriusSmoke3", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(0, 360), SXF_NOCHECKPOSITION);
		
		A_QuakeEx(4, 4, 4, 40, 0, 900, "", QF_SCALEDOWN);
		A_Explode(300, 350);
	}
	TNT1 AAAAAAAA 4
	{
		A_Explode(300, 350);
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_SiriusSmoke3", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(0, 360), SXF_NOCHECKPOSITION);
	}
	Stop;
  }
}

Class DCY_SiriusBFGBall : CacodemonBall
{
  Default
  {
	  Damage 10;
	  Radius 35;
	  Height 35;
	  Speed 3;
	  DamageType "Player";
	  RenderStyle "Add";
	  Alpha 0.85;
	  Scale 1;
	  SeeSound "";
	  DeathSound "SiriusBFG/Explode";
	  +RIPPER;
	  +NEVERFAST;
	  +EXTREMEDEATH;
	  +FORCEXYBILLBOARD;
	  +BRIGHT;
	  +FORCERADIUSDMG;
	  Translation "0:255=%[1.02,1.85,1.99]:[0.00,0.00,0.34]";
  }
  
  int deathloop;
  int sspeed;
  
  void A_SiriusTrail()
  {
	A_SpawnItemEx("DCY_SiriusSmoke", xvel: frandom(-2, 2), yvel: frandom(-2, 2), zvel: frandom(-2, 2), flags: SXF_NOCHECKPOSITION);
	A_SpawnItemEx("DCY_SiriusArc", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_NOCHECKPOSITION, 200);
	
	if (sspeed > 64 && sspeed < 128)
			vel += vel.unit() * 0.875;
		
	sspeed++;
  }
  
  void SiriusGoBoom()
  {
	for (int i = 2; i > 0; i--)
	{
		A_SpawnItemEx("DCY_SiriusSmoke2", random(-50, 50), random(-50, 50), random(-50, 50), random(-20, 20), random(-20, 20), random(-20, 20), random(0, 360), SXF_NOCHECKPOSITION, 200);
	}
	
	A_SpawnItemEx("DCY_SiriusBFGTrailAlpha", flags: SXF_NOCHECKPOSITION);
	A_Explode(80, 300, 0, True);
  }
  
  void SiriusGoKaboom()
  {
	for (int i = 0; i < 4; i++)
		A_StartSound("SiriusBFG/DeathStart", i, CHANF_DEFAULT, 1.0, 0.45);
	
	A_QuakeEx(8, 8, 8, 90, 0, 1400, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(6, 6, 6, 100, 0, 1800, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(3, 3, 3, 125, 0, 2222, "", QF_RELATIVE|QF_SCALEDOWN);
	A_QuakeEx(1, 1, 1, 105, 0, 2000, "", QF_SCALEDOWN, 0, 0, 0, 0, 0, frandom(0.7, 1.0), frandom(-0.65, 0.65));
	
	A_Explode(700, 400, 0, True);
	A_Explode(350, 800, 0, True);
	A_Explode(200, 1200, 0, True);
	A_Explode(100, 2255, 0, True);
  }
  
  States
  {
  Spawn:
    TNT1 A 0;
	BVDP A 1
	{
		A_SiriusTrail();
		A_StartSound("SiriusBFG/Loop", CHAN_5, CHANF_LOOPING, 1.0);
	}
	BVDP ABBCC 1 A_SiriusTrail();
	Loop;
  Death:
	BVDP A 1
	{
		SiriusGoBoom();
		A_StopSound(CHAN_5);
		A_StartSound("SiriusBFG/DeathWait", CHAN_BODY, 0, 1.0, 0.5);
		A_SpawnItemEx("DCY_SiriusBFGSphereTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	}
	BDVP AAABBBBBBBBCCCCCCCCC 2 SiriusGoBoom();
	Goto DeathStart;
  DeathStart:
	TNT1 A 0;
	TNT1 A 1 SiriusGoKaboom();
	TNT1 AAAAAA 1
	{
		A_Explode(80, 2000, 0, True);
		A_SpawnItemEx("DCY_SiriusBFGRing", flags: SXF_NOCHECKPOSITION, failchance: 80);
		
		for (int i = 8; i > 0; i--)
			A_SpawnItemEx("DCY_SiriusSmoke2", frandom(-50, 50), frandom(-50, 50), frandom(-50, 50), frandom(-150, 150), frandom(-150, 150), frandom(-150, 150), frandom(0, 360), SXF_NOCHECKPOSITION, 160);
	}
	TNT1 A 1
	{
		A_SpawnItemEx("DCY_SiriusBFGSphere2", flags: SXF_NOCHECKPOSITION);
		A_Explode(80, 2000, 0, True);
		A_Explode(700, 400, 0, True);
		A_Explode(350, 800, 0, True);
		A_Explode(200, 1200, 0, True);
		A_Explode(100, 2255, 0, True);
		A_SpawnItemEx("DCY_SiriusBFGRing", flags: SXF_NOCHECKPOSITION, failchance: 80);
		
		for (int i = 8; i > 0; i--)
			A_SpawnItemEx("DCY_SiriusSmoke2", frandom(-50, 50), frandom(-50, 50), frandom(-50, 50), frandom(-150, 150), frandom(-150, 150), frandom(-150, 150), frandom(0, 360), SXF_NOCHECKPOSITION, 160);
		
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_SpawnItemEx("DCY_SiriusBFGRing", flags: SXF_NOCHECKPOSITION, failchance: 80);
		
		for (int i = 8; i > 0; i--)
			A_SpawnItemEx("DCY_SiriusSmoke2", frandom(-50, 50), frandom(-50, 50), frandom(-50, 50), frandom(-150, 150), frandom(-150, 150), frandom(-150, 150), frandom(0, 360), SXF_NOCHECKPOSITION, 160);
		
		A_Explode(80, 4959, 0, True);
	}
	TNT1 A 125;
	Stop;
  }
}

Class DCY_SiriusBFGTrail : Actor
{
  Default
  {
	Radius 0;
	Height 0;
	RenderStyle "Add";
	Scale 0.4;
	Alpha 0.1825;
	+NOCLIP;
	+NOINTERACTION;
	+FORCEXYBILLBOARD;
	+THRUSPECIES;
	+THRUGHOST;
	+THRUACTORS;
	+DONTSPLASH;
	+MTHRUSPECIES;
	+BRIGHT;
  }
  States
  {
  Spawn:
    SIRL A 0;
	SIRL A 0 A_JumpIf(Scale.X <=0.0000000000000001, "Stahp");
	SIRL A 0 A_JumpIf(Alpha <=0.0000000000000001, "Stahp");
	SIRL A 0 A_SetScale(Scale.X-0.0055, Scale.Y-0.0055);
	SIRL A 1 A_FadeOut(0.015215, 0);
	Loop;
  Stahp:
    TNT1 A 1;
	Stop;
  }
}

Class DCY_SiriusBFGTrailAlpha : DCY_SiriusBFGTrail
{
  Default
  {
	Alpha 0.1;
  }
}

Class DCY_SiriusBFGTrail2 : DCY_SiriusBFGTrail
{
  Default
  {
	Scale 0.01;
	+ROLLSPRITE
	+FLATSPRITE
  }
  
  States
  {
  Spawn:
	EF3_ K 0 NoDelay
	{
		A_SetRoll(frandom(0, 360));
		A_SetPitch(frandom(0, 360));
		A_SetAngle(frandom(0, 360));
	}
    SIRL A 0;
	SIRL A 0 A_JumpIf(Scale.X <=0.0000000000000001, "Stahp");
	SIRL A 0 A_JumpIf(Alpha <=0.0000000000000001, "Stahp");
	SIRL A 0 A_SetScale(Scale.X-0.00025, Scale.Y-0.00025);
	SIRL A 0 A_ChangeVelocity(0, 0, -2);
	SIRL A 1 A_FadeOut(0.0205, 0);
	Loop;
  }
}

Class DCY_SiriusArc : DCY_Arc
{
  Default
  {
	Scale 1.3;
	Translation "0:255=%[0.00,0.00,0.00]:[0.62,1.31,2.00]";
  }
}

Class DCY_SiriusBFGSphereTrail : DCY_SiriusBFGTrail
{
  Default
  {
	Scale 10;
	Alpha 0.00;
  }
  
  float num;
  float num2;
  float zalpha;
  
  States
  {
  Spawn:
	PKCH AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_SetScale(Scale.X - (0.05 + num));
		A_SetAngle(Angle + (5 + (num * 10)), SPF_INTERPOLATE);
		
		if (alpha < 2)
			A_FadeIn(0.1);
		
		num += 0.0025;
		zalpha = 2;
	}
	PKCH A 1 { num = 3; }
	PKCH AAAAAAAAAAAA 1
	{
		num -= 0.25;
		
		A_SetScale(Scale.X + num);
		A_SetAngle(Angle + 15, SPF_INTERPOLATE);
	}
	PKCH AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		zalpha -= 0.05;
		
		if (num > 0.1)
			num -= 0.05;
		
		A_SetRenderStyle(zalpha, STYLE_ADD);
		A_SetScale(Scale.X + num);
		A_SetAngle(Angle + (15 - num2), SPF_INTERPOLATE);
		
		num2 += 0.25;
	}
	Stop;
  }
}

Class DCY_SiriusBFGSphere2 : DCY_SiriusBFGSphereTrail
{
  Default
  {
  Scale 0.01;
  Alpha 1.0;
  }
  
  States
  {
  Spawn:
	PKCH A 1 NoDelay { num = 3; zalpha = 1; }
	PKCH AAAAAAAAAAAA 1
	{
		num -= 0.185;
		
		A_SetScale(Scale.X + num);
		A_SetAngle(Angle + 15, SPF_INTERPOLATE);
	}
	PKCH AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		zalpha -= 0.025;
		
		if (num > 0.1)
			num -= 0.05;
		
		A_SetRenderStyle(zalpha, STYLE_ADD);
		A_SetScale(Scale.X + num);
		A_SetAngle(Angle + (15 - num2), SPF_INTERPOLATE);
		
		num2 += 0.25;
	}
	Stop;
  }
}

Class DCY_SiriusSmoke : DCY_SiriusBFGTrail
{
  Default
  {
	Scale 1;
	Alpha 0.85;
	+ROLLSPRITE
	+SOLID
	BounceFactor 1.0;
	BounceType "Hexen";
  }
  
  override void tick()
  {
	super.tick();
	A_Explode(3, 200, damagetype: "Player");
  }
  
  States
  {
  Spawn:
	SE1Z A 0 NoDelay A_SetRoll(frandom(0, 360));
	SE1Z A 15;
	SE1Z AAAAAAAAAA 1 A_FadeOut(0.1);
	Stop;
  }
}

Class DCY_SiriusSmokeBig : DCY_SiriusSmoke
{
  Default
  {
  Scale 3;
  Alpha 0.65;
  Alpha 0.25;
  }
  
  override void tick()
  {
	super.tick();
  }
  
  States
  {
  Spawn:
	SE1Z A 0 NoDelay A_SetRoll(frandom(0, 360));
	SE1Z A 15;
	SE1Z AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.015);
	Stop;
  }
}

Class DCY_SiriusSmoke3 : DCY_SiriusBFGTrail
{
  Default
  {
  Scale 2.25;
  Alpha 0.45;
  +ROLLSPRITE
  +FORCERADIUSDMG
  +SOLID
  +NOGRAVITY
  RenderStyle "Add";
  BounceFactor 1.0;
  BounceType "Hexen";
  }
  
  override void tick()
  {
	super.tick();
	A_Explode(4, 150, damagetype: "Player");
  }
  
  States
  {
  Spawn:
	SE1Z A 0 NoDelay A_SetRoll(frandom(0, 360));
	SE1Z A 15;
	SE1Z AAAAAAAAAAAAAAAAAAAA 2 A_FadeOut(0.015);
	Stop;
  }
}

Class DCY_SiriusSmoke2 : DCY_SiriusSmoke
{
  Default
  {
  -NOCLIP;
  +SOLID;
  BounceFactor 1.0;
  BounceType "Hexen";
  }
  
  States
  {
  Spawn:
	TNT1 AAAAAA 1 A_SpawnItemEx("DCY_SiriusSmokeBig", frandom(-15, 15), frandom(-15, 15), frandom(-15, 15), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2));
	Stop;
  }
}

Class DCY_SiriusBFGRing : DCY_SiriusBFGTrail
{
  Default
  {
	Scale 0;
	Alpha 0.85;
	+ROLLSPRITE
	+FLATSPRITE
  }
  
  States
  {
  Spawn:
	EF3_ K 0 NoDelay
	{
		A_SetRoll(frandom(0, 360));
		A_SetPitch(frandom(0, 360));
		A_SetAngle(frandom(0, 360));
	}
    EF3_ K 1
	{
		if (Scale.X > 3)
		{
			if (Alpha < 0.00)
				Destroy();
			
			A_FadeOut(0.1);
		}
		
		A_SetScale(Scale.X + 1);
	}
	Goto Spawn+1;
  }
}