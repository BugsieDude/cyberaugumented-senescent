Class DCY_BallistaManefista : Weapon
{
  Default
  {
	Tag "Ballista";
	Weapon.AmmoType "Cell";
	Weapon.AmmoGive 50;
	Weapon.AmmoUse 15;
	Inventory.PickupMessage "The Ballista, a crossbow built with technology that has unimaginable strength inside of it; feels like guts and glory.";
	Weapon.BobStyle "Alpha";
	Weapon.BobRangeX 0.45;
	Weapon.BobRangeY 0.25;
	Weapon.BobSpeed 1.45;
	Weapon.SlotNumber 6;
	AttackSound "Ballista/Explode";
	DamageType "Ballista";
	+FLOORCLIP;
	+DONTGIB;
	+WEAPON.EXPLOSIVE;
	Inventory.Icon "PWBXZ0";
	Inventory.PickupSound "dcy/plasmariflepickup";
  }
  
  action void A_Ballista()
  {
	A_QuakeEx(3, 3, 3, 50, 0, 150, "none", QF_SCALEDOWN, falloff: 250);
	A_StartSound("Ballista/Fire", CHAN_WEAPON, CHANF_OVERLAP);
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_RailAttack(450, 0, 0, flags: RGF_FULLBRIGHT|RGF_SILENT|RGF_NOPIERCING, pufftype: "DCY_BallistaPuff", sparsity: 64, spawnclass: "DCY_BallistaTrail");
	A_GunFlash();
  }
  
  States
  {
  Spawn:
	PWBX Z -1;
	Stop;
  Ready:
	PWBX AABBCCDDCCBB 1 Bright A_WeaponReady();
	Loop;
  Select:
	PWBX WXY 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	PWBX YXW 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Fire:
	PW3X A 1 Bright;
	PW3X B 1 Bright A_Ballista();
	PW3X CDE 1 Bright;
	PW3X FGH 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	PW3X I 30 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	PW3X I 2 A_StartSound("Ballista/Reload", CHAN_WEAPON, CHANF_OVERLAP);
	PW3X IJKL 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	PW3X MNOPQ 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	PWBX AA 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	Goto Ready;
  }
}

Class DCY_NeonicWand : Weapon 
{
  Default
  {
	Tag "Caduceus";
	Obituary "%o couldn't even surpass %k's Caduceus azure power.";
	Weapon.AmmoType "DCY_UltramodernEnergy";
	Weapon.AmmoUse 0;
	Weapon.AmmoGive 40;
	Weapon.BobStyle "Alpha";
	Weapon.BobRangeX 0.45;
	Weapon.BobRangeY 0.25;
	Weapon.BobSpeed 1.45;
	+FLOORCLIP;
	+DONTGIB;
	+FLOATBOB;
	+BRIGHT;
	+WEAPON.EXPLOSIVE;
	DamageType "Neonic";
	Weapon.SlotNumber 6;
	Inventory.Icon "NNW3Z0";
	Inventory.PickupSound "dcy/plasmariflepickup";
	Inventory.PickupMessage "You hands hold the presence of the \c[l5]Caduceus.\cL Not your general plasma rifle.";
	Radius 23;
	Height 53;
  }
  
  action void A_NeonLaser()
  {
	A_GunFlash();
	A_StartSound("Lazer/Fire", CHAN_WEAPON);
	A_RailAttack(random(50, 100), 0, 0, flags: RGF_FULLBRIGHT|RGF_SILENT, pufftype: "DCY_NeonicRing", sparsity: 30, spawnclass: "DCY_NeonicWandTrail");
	invoker.DepleteAmmo(invoker.bAltFire, true);
  }

  action void A_NeonSphere()
  {
	A_StartSound("NeonicBall/Fire", CHAN_WEAPON);
	A_FireProjectile("DCY_NeonicBall", 0, 0);
	A_GunFlash("Flash2");
	invoker.DepleteAmmo(invoker.bAltFire, ammouse: 4);
  }
  
  int weaponmode;
  
  States
  {
  Spawn:
    NNW3 Z -1;
	Stop;
  Select:
	NEWN XYZ 1
	{
		If(invoker.weaponmode == 1)
			Return ResolveState("Select2");
		
		A_WeaponReady(WRF_NOFIRE);
		Return ResolveState(Null);
	}
	Goto Ready;
  Deselect:
	NEWN ZYX 1
	{
		If(invoker.weaponmode == 1)
			Return ResolveState("Deselect2");
		
		A_WeaponOffset(0, 32);
		Return ResolveState(Null);
	}
	TNT1 A 0 A_Lower();
	Wait;
  Select2:
	N9WN XYZ 1
	{
		If(invoker.weaponmode == 0)
			Return ResolveState("Select");
		
		A_WeaponReady(WRF_NOFIRE);
		Return ResolveState(Null);
	}
	Goto Ready;
  Deselect2:
	N9WN ZYX 1
	{
		If(invoker.weaponmode == 0)
			Return ResolveState("Deselect");
		
		A_WeaponOffset(0, 32);
		Return ResolveState(Null);
	}
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	TNT1 A 0 A_JumpIf(invoker.weaponmode == 1, "Ready2");
    NEWN A 5 Bright A_WeaponReady(WRF_ALLOWRELOAD);
	NEWN BCDCB 4 Bright A_WeaponReady(WRF_ALLOWRELOAD);
	Loop;
  Ready2:
	TNT1 A 0 A_JumpIf(invoker.weaponmode == 0, "Ready");
	N9WN C 5 Bright A_WeaponReady(WRF_ALLOWRELOAD);
	N9WN DEF 5 Bright A_WeaponReady(WRF_ALLOWRELOAD);
	Loop;
  Reload: //AKA. Switch Modez
	TNT1 A 0
	{
		A_StartSound("NeonicWand/Switch", CHAN_WEAPON);
		if (invoker.weaponmode == 1)
			Return ResolveState("Reload2");
		
		invoker.weaponmode = 1;
		A_SetBlend("00 77 FF", 0.3, 10);
		DCY_notHudMessageHandler.WaggleMsg("SMALLFONT", "Neonic Spherule", (240, 150), (480, 360), color: Font.CR_SAPPHIRE, time: (0.3, 2, 1), speed: 0.35, strength: 0.75, frequency: 6.0, id: 78);
		Return ResolveState(null);
	}
    N9WN ABC 5 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH);
	Goto Ready2;
  Reload2:
	TNT1 A 0 
	{
		A_StartSound("NeonicWand/Switch", CHAN_WEAPON);
		if (invoker.weaponmode == 0)
			Return ResolveState("Reload");
		
		invoker.weaponmode = 0;
		A_SetBlend("00 77 FF", 0.3, 10);
		DCY_notHudMessageHandler.WaggleMsg("SMALLFONT", "Purifying Light Powered-Tech Lasers", (240, 150), (480, 360), color: Font.CR_SAPPHIRE, time: (0.3, 2, 1), speed: 0.35, strength: 0.75, frequency: 6.0, id: 78);
		Return ResolveState(null);
	}
    N9WN CBA 5 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH);
	Goto Ready;
  Fire:
	NEWN A 0
	{
	  if(invoker.weaponmode == 1)
		Return ResolveState("FireII");
	  Return ResolveState(null);
	}
	Goto Fire1;
  Fire1:
	NEWN EFGH 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH|WRF_NOBOB);
	Goto Hold;
  Hold:
    NEWN I 1 Bright A_NeonLaser();
	NEWN JKLM 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	TNT1 A 0 A_Refire("Hold");
	NEWN NOPDCB 2 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
	Goto Ready;
  Fire2:
	N9WN G 0
	{
	  if(invoker.weaponmode == 0)
		Return ResolveState("Fire1");
	  Return ResolveState(null);
	}
	Goto FireII;
  FireII:
	N9WN G 1 A_NeonSphere();
	N9WN HIJKL 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOBOB);
	N9WN MNO 3 Bright A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
	Goto Ready2;
  Flash:
	TNT1 A 2 A_Light1();
	TNT1 A 1 A_Light0();
	Goto LightDone;
  Flash2:
	TNT1 A 5 A_Light1();
	TNT1 A 4 A_Light0();
	Goto LightDone;
  }
}

Class DCY_NeonicBall : FastProjectile
{
  Default
  {
	Damage 15;
	Radius 24;
	Height 24;
	Speed 60;
	DamageType "Player";
	RenderStyle "Add";
	Alpha 1.0;
	Scale 1.0;
	+BRIGHT;
	+NOEXTREMEDEATH;
	+FORCERADIUSDMG;
	Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
	SeeSound "NeonicBall/Fire";
	DeathSound "NeonicBall/Death";
	MissileType "DCY_NeonicTrail";
	MissileHeight 6;
  }
  
  void A_NeonDestruct()
  {
	for (int i = 12; i > 0; i--)
		A_SpawnParticle(0xaaddff, SPF_FULLBRIGHT, random(40, 55), 10, frandom(0, 360), frandom(-7.5, 7.5), frandom(-7.5, 7.5), frandom(-7.5, 7.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(0.5, 11.4), accelz: -0.85, fadestepf: 0, sizestep: -0.425);
	
	for (int i = 2; i > 0; i--)
		A_SpawnItemEx("DCY_NeonExplode", flags: SXF_NOCHECKPOSITION);
	
	A_Explode(256, 128, XF_NOTMISSILE);
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_SpawnParticle(0xaaddff, SPF_FULLBRIGHT, random(40, 55), 10, frandom(0, 360), frandom(-17.5, 17.5), frandom(-17.5, 17.5), frandom(-17.5, 17.5), fadestepf: 0, sizestep: -0.425);
		A_Weave(1, 1, 0.5, 0.5);
	}
	Loop;
  Death:
	TNT1 A 2 A_NeonDestruct();
	Stop;
  }
}

Class DCY_NeonicTrail : Actor
{
  Default
  {
	Radius 0;
	Height 0;
	Alpha 0.5;
	Scale 1.2;
	Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
	RenderStyle "Add";
	+NOINTERACTION;
	+NOCLIP;
	+ROLLSPRITE;
	+BRIGHT;
  }
  
  States
  {
  Spawn:
    M_TR C 1 NoDelay A_SetRoll(frandom(0, 360));
	M_TR CCDDDEEEFFFGGGH 1
	{
		A_SetScale(Scale.X - 0.175);
		A_FadeOut(0.05);
	}
	Stop;
  }
}

Class DCY_NeonExplode : Actor
{
  Default
  {
  Scale 1.275;
  Alpha 1.0;
  RenderStyle "Add";
  +BRIGHT
  +ROLLSPRITE
  +NOINTERACTION
  -SOLID
  Translation "0:255=%[0.00,0.00,0.00]:[0.07,0.36,0.83]";
  }
  
  States
  {
  Spawn:
    KABE A 1 NoDelay A_SetRoll(frandom(0, 360));
	KABE BCDEFGHIJKLMNOPQRSTUVW 1;
	Stop;
  }
}