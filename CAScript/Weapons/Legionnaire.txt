Class DCY_Legionnaire : DCY_Weapon
{
  Default
  {
	Tag "X40-DR Legionnaire";
	Weapon.SelectionOrder 2500;
	Weapon.SlotNumber 5;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.EXPLOSIVE;
	Weapon.AmmoGive 2;
	Weapon.AmmoUse 1;
	Weapon.AmmoType "RocketAmmo";
	Inventory.PickupSound "dcy/rocketpickup";
	Inventory.PickupMessage "You got the Legionnaire! I'm pretty sure you know how to make a confetti party, right?";
  }
  
  action void A_CyberaugumentedRocket()
  {
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_StartSound("RocketLauncher/Kaboom", CHAN_WEAPON, 0, 1.0, 0.85);
	A_FireProjectile("DCY_OverhauledRocket", 0, 0);
	A_QuakeEx(1, 1, 1, 25, 0, 150, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 295);
	A_WeaponOffset(frandom(-3.5, 3.5), frandom(0, 4.35), WOF_ADD|WOF_INTERPOLATE);
	A_GunFlash();
  }

  States
  {
  Spawn:
	RCK_ Z -1;
	Stop;
  Select:
	RCC_ WXY 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	RCC_ YXW 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	RCC_ A 1 A_WeaponReady();
	Loop;
  Fire:
	RCC_ A 0;
	RCC_ ABC 1;
	RCC_ D 1 A_CyberaugumentedRocket();
	RCC_ E 1 Bright;
	RCC_ FGHI 1 Bright;
	RCC_ J 0 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	RCC_ J 2 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
	RCC_ KLMN 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	RCC_ N 0 A_Refire;
	Goto Ready;
  Flash:
	TNT1 A 3 Bright A_Light1();
    TNT1 B 2 Bright;
    TNT1 CD 3 Bright A_Light2();
    Goto LightDone;
  }
}