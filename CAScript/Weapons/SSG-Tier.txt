Class DCY_SuperShotty : SuperShotgun Replaces SuperShotgun
{
  Default
  {
	Tag "Super Shotgun";
	Inventory.PickupSound "dcy/shotgunpickup";
	Inventory.PickupMessage "You got the Super Shotgun, also known as the Double Barrel Shotgun! Time to kick ass.";
	Weapon.BobStyle "Alpha";
	Weapon.BobRangeX 0.45;
	Weapon.BobRangeY 0.25;
	Weapon.BobSpeed 1.45;
	Weapon.SlotNumber 3;
  }
  
  action void A_SuperShotgunFire()
  {
	A_GunFlash();
	
	invoker.DepleteAmmo(invoker.bAltFire, true);
	
	for (int i = 7; i > 0; i--)
    {
		for (int i = -4; i < 5; i += 8)
			A_FireProjectile(CheckInventory("DCY_PhotocoagulatorPower", 1, AAPTR_DEFAULT) ? "DCY_ExplosiveSSGTracer" : "DCY_SSGTracer", frandom(-3.25, 3.25), 0, -4, pitch: frandom(-3.0, 3.0));
	}
	
	A_QuakeEx(1, 1, 1, 30, 0, 90, "none", QF_SCALEDOWN, falloff: 180);
	A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
  }
  
  States
  {
  Select:
	_S2G ABC 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  Deselect:
	_S2G CBA 1 A_WeaponOffset(0, 32);
	TNT1 A 0 A_Lower();
	Wait;
  Ready:
	_SSG A 1 A_WeaponReady();
	Loop;
  Fire:
	_SSG A 0;
	_SSG A 1
	{
		A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		A_StartSound("SuperShotgun/Fire", CHAN_WEAPON);
	}
	_SSG B 1 Bright A_SuperShotgunFire();
	_SSG CDE 1 Bright A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_SSG FGH 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_SSG I 0 A_JumpIfNoAmmo("Ready");
	_SSG IJKLM 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_SSG N 1
	{
		A_StartSound("SuperShotgun/Reload", CHAN_WEAPON, CHANF_OVERLAP, 0.5);
		A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	}
	_SSG OP 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_SSG QRSTUVWXYZ 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_S2G ABC 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	_S2G C 0 A_Refire();
	Goto Ready;
  Flash:
	TNT1 A 2 A_Light1();
	TNT1 A 2 A_Light2();
	Goto LightDone;
  }
}

Class DCY_Deathbringer : Weapon
{
  Default
  {
	  Tag "The ''Deathbringer''";
	  Inventory.PickupSound "dcy/shotgunpickup";
	  Inventory.PickupMessage "You hold the Deathbringer! It is like some kind of weapon to save you when the boogeyman comes near ya. Help yourself.";
	  Weapon.BobStyle "Alpha";
	  Weapon.BobRangeX 0.45;
	  Weapon.BobRangeY 0.25;
	  Weapon.BobSpeed 1.45;
	  Weapon.AmmoType "Shell";
	  Weapon.AmmoUse 5;
	  Weapon.AmmoGive 10;
	  Weapon.SlotNumber 8;
	  Inventory.PickupSound "Deathbringer/Pickup";
	  +FLOORCLIP;
	  +DONTGIB;
  }
  
  action void A_BringouttheDeathbringer()
  {
	A_GunFlash();
	
	invoker.DepleteAmmo(invoker.bAltFire, true);
	
    for (int i = 17; i > 0; i--)
    {
		for (int i = -5; i < 6; i += 10)
			A_FireProjectile(CheckInventory("DCY_PhotocoagulatorPower", 1, AAPTR_DEFAULT) ? "DCY_ExplosiveDeathbringerTracer" : "DCY_DeathbringerTracer", frandom(-14.5, 14.5), 0, -4, pitch: frandom(-8.0, 8.0));
	}
	
	A_Recoil(10);
	
	A_QuakeEx(2, 2, 2, 55, 0, 300, "none", QF_SCALEDOWN|QF_RELATIVE, falloff: 750);
	A_QuakeEx(1, 1, 1, 55, 0, 250, "none", QF_SCALEDOWN, falloff: 600, rollintensity: 0.5, rollwave: frandom(-0.25, 0.25));
	A_StartSound("Deathbringer/Shot", CHAN_WEAPON, CHANF_OVERLAP);
  }
	
  States
  {
  Spawn:
	OSG_ Z -1;
	Stop;
  Select:
	OGS_ A 1 A_Raise(18);
	Loop;
  Deselect:
	OGS_ A 1 A_Lower(18);
	Loop;
  Ready:
	OGS_ A 1 A_WeaponReady;
	Loop;
  Fire:
	OGS_ A 2;
	OGS_ B 2 Bright A_BringouttheDeathbringer();
	OGS_ CD 2 Bright A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSECONDARY);
	OGS_ EFGHIJ 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	OGS_ KL 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	OGS_ M 2
	{
		A_StartSound("Deathbringer/Checkcheck", CHAN_WEAPON, CHANF_OVERLAP);
		A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	}
	OGS_ NOPQ 2 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY);
	OGS_ AAAA 3;
	OGS_ A 0 A_Refire;
	Goto Ready;
  }
}