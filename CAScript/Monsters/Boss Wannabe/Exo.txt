Class DCY_ExoImp : DCY_Impe
{
  Default
  {
  PainChance 20;
  Health 1100;
  SeeSound "ExoImp/Sight";
  ActiveSound "ExoImp/Active";
  PainSound "ExoImp/Pain";
  DeathSound "ExoImp/Death";
  BloodColor "Purple";
  Translation "48:63=[255,255,255]:[191,191,191]", "64:79=[191,191,191]:[32,32,32]", "236:239=%[0.00,0.00,0.00]:[0.95,0.95,0.95]", "232:235=%[0.00,0.00,0.00]:[1.01,0.00,2.00]", "1:3=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "5:15=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "16:47=%[0.08,0.00,0.15]:[0.70,0.40,1.74]", "168:191=%[0.08,0.00,0.15]:[0.70,0.40,1.75]", "112:127=%[0.08,0.00,0.15]:[0.70,0.40,1.75]", "128:159=%[0.00,0.00,0.00]:[0.67,0.67,0.67]";
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +BOSS
  +NEVERFAST
  +NOGRAVITY
  +FLOAT
  DamageFactor 0.85;
  FloatSpeed 14;
  Speed 7;
  DamageType "Exo";
  Tag "\c[z7]Fallen Angel \c[m8]Dastard \c[q0]Exo\c-";
  Obituary "%o got sodomized by an fallen angel.";
  HitObituary "%o no longer has a functioning heart.";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  DCYMonster.ScientificName "Decidit caelum";
  +DCYMONSTER.DCYBOSS;
  DCYMonster.WD 1000;
  DCYMonster.CanAnnounce true;
  DCYMonster.AnnounceSound "EXOIMPCHOIR";
  DCYMonster.AnnounceColor "m8";
  DCYMonster.AnnounceMessage "An angel has fallen.";
  }
  
  int emissile;
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o got sodomized by an fallen angel.",
		"Rest in peace, %o"
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
	FAEM A 10 A_Look();
	Loop;
  See:
	FAEM AA 3 DCY_Chase();
	FAEM B 3
	{
		DCY_Chase();
		A_StartSound("IMPFLY2", CHAN_BODY, CHANF_OVERLAP);
		Return A_Jump(80, "Dashe");
	}
	FAEM BCCDD 3 DCY_Chase();
	Loop;
  Dashe:
	FAEM A 10;
	FAEM A 1
	{
		A_Dodging(frandom(25, 32));
		DCY_Chase(null, "Missile", CHF_DONTMOVE);
		A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	FAEM AAAAAAAAA 1 A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	FAEM A 1
	{
		A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		DCY_Chase(null, "Missile", CHF_DONTMOVE);
	}
	FAEM AAA 1 A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	FAEM A 1
	{
		A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		DCY_Chase(null, "Missile", CHF_DONTMOVE);
	}
	FAEM AAA 1 A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	FAEM A 5
	{
		DCY_Chase(null, "Missile", CHF_DONTMOVE);
		A_Stop();
	}
	Goto See;
  Melee:
  Missile:
	FAEM E 1
	{
		if (!random(0, 1)) SetStateLabel("ManyBalls");
		Return A_JumpIf(target && Distance3D(target) < 350, "Charge");
	}
	FAEM EF 10 A_FaceTarget();
	FAEM O 6
	{
		for (int i = 3; i > 0; i--) A_SpawnProjectile("DCY_ExoBall", 35, 0, frandom(-10.0, 10.0), CMF_OFFSETPITCH, pitch + frandom(-5.0, 5.0));
		A_SpawnProjectile("DCY_ExoBall", 35, 0, 0);
	}
	FAEM F 6 A_Jump(100, "BishopBalls");
	Goto See;
  ManyBalls:
	FAEM EF 10 A_FaceTarget();
	FAEM OOOOOOO 8
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ExoBall", 35, 0, frandom(-3, 3), CMF_OFFSETPITCH, pitch + frandom(-3, 3));
	}
	FAEM F 6 A_Jump(100, "BishopBalls");
	Goto See;
  BishopBalls:
    FAEM F 8
	{
		emissile = 0;
		A_FaceTarget();
	}
	FAEM E 8 A_FaceTarget();
	FAEM E 6
	{
		if (emissile >= 14)
			Return ResolveState("See");
		
		A_SpawnProjectile("DCY_ExoWeaver2", 32, 9);
		A_SpawnProjectile("DCY_ExoWeaver", 32, 9);
		A_StartSound("STRKBALL", CHAN_WEAPON);
		A_FaceTarget();
		emissile++;
		Return ResolveState(null);
	}
	Goto BishopBalls+2;
  Charge:
	FAEM EF 10 A_FaceTarget();
	FAEM O 1
	{
		A_Recoil(frandom(-15, -25));
		A_FaceTarget();
		A_ChangeVelocity(0, 0, -20);
		A_StartSound("EXOLUNGE", CHAN_BODY, attenuation: 0.45);
	}
	FAEM OOOOOOOOOOOOOOOOOOOOO 1
	{
		If(target && Distance3D(target) < 70)
			A_CustomMeleeAttack(random(2, 13), "ExoImp/ChargeHit", "", "Exo", True);
		
		A_SpawnItemEx("DCY_ExoTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_ChangeVelocity(0, 0, 2);
	}
	FAEM O 10
	{
		A_ChangeVelocity(0, 0, 0, CVF_REPLACE);
		A_FaceTarget();
		A_Stop();
	}
	Goto Missile+1;
  Pain:
	FAEM H 3 A_Stop();
	FAEM H 5
	{
		A_Stop();
		A_Pain();
		Return A_Jump(50, "Dashe");
	}
	Goto See;
  Death:
  XDeath:
  Death.Fire:
	FAEM H 1;
	FAEM H 6 A_ScreamAndUnblock();
	FAEM IJKLM 5;
	FAEM N -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_ExoBall : DCY_ImpFireball
{
  int rollpick;
  
  Default
  {
  Damage 7;
  DamageType "Exo";
  SeeSound "EXOWATK";
  DeathSound "EXOWATK";
  Speed 17;
  FastSpeed 24;
  Scale 1.25;
  +BRIGHT
  +NOEXTREMEDEATH
  Translation "0:255=%[0.28,0.00,0.55]:[1.68,0.97,2.00]";
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	roll = frandom(-360, 360);
	rollpick = randompick(-8, 8);
  }
  
  States
  {
  Spawn:
	9_AL AAABBBCCC 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x471a87, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), (Vel.x / 3.75), (Vel.y / 3.75), (Vel.z / 3.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.25);
			A_SpawnFlames("b85cff", 15, 12, 0, false, thrust: 2.25, ember: false);
		}
	}
	Loop;
  Death:
	TNT1 A 0
	{
		for (int i = 12; i > 0; i--)
			A_SpawnParticleEx(0x693294, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 45), 5, frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), fadestepf: 0, sizestep: -0.125);
		A_SpawnParticleEx(0x693294, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
	}
	9_AL DEFGH 3;
	Stop;
  }
}

Class DCY_ExoWeaver : DCY_Projectile
{
  int direction;
  property Dir : direction;
  
  Default
  {
  Radius 10;
  Height 6;
  +SEEKERMISSILE
  -ACTIVATEIMPACT
  -ACTIVATEPCROSS
  +STRIFEDAMAGE
  RenderStyle "Add";
  Speed 21.5;
  Damage 4;
  DCY_ExoWeaver.Dir 1;
  +NEVERFAST
  SeeSound "STRKBALL";
  DeathSound "STRKBALL";
  }
  
  States
  {
  Spawn:
    EBO_ ABAB 1 Bright
	{
		A_Weave(2, 3, direction, direction);
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x693294, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 60, frandom(1.00, 4.00), angle, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), (Vel.x / 2.75), (Vel.y / 2.75), (Vel.z / 2.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.25);
			A_SpawnFlames("693294", 20, 20, 0, false, thrust: 3, ember: false);
		}
	}
    EBO_ B 0 Bright A_SeekerMissile(3, 5);
    Loop;
  Death:
	TNT1 A 0 A_SpawnParticleEx(0x693294, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 60, frandom(0, 360), fadestepf: 0.05, sizestep: -3.25);
    EBO_ CDEF 3 Bright;
    EBO_ GH 2 Bright;
    Stop;
  }
}

Class DCY_ExoWeaver2 : DCY_ExoWeaver { Default { DCY_ExoWeaver.Dir -1; } }