Class DCY_Arachnoaversion : DCY_ArachnoParent
{
  Default
  {
  Health 2000;
  Translation "128:159=%[0.00,0.00,0.00]:[0.84,0.82,1.03]", "192:255=%[0.00,0.00,0.00]:[0.95,0.69,1.63]", "13:79=%[0.00,0.00,0.00]:[0.84,0.82,1.03]";
  BloodColor "Purple";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  Speed 14;
  FastSpeed 20;
  +NOGRAVITY
  +FLOAT
  +MISSILEEVENMORE
  +NOTARGET
  +DONTHARMSPECIES
  +FIRERESIST
  +MISSILEMORE
  +DONTFALL
  +AVOIDMELEE
  +BRIGHT
  +BOSS
  Scale 1.25;
  DamageFactor 0.65;
  SeeSound "ARPHSIT";
  ActiveSound "";
  PainSound "ARPHPAI";
  DeathSound "ARPHDTH";
  Tag "\c[a0]Arachnoaversion\c-";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  }
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen())
	{
		A_SpawnItemEx("DCY_NimbusJetFlareSmall", frandom(35, 32), frandom(35, 32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_NimbusJetFlareSmall", frandom(-35, -32), frandom(-35, -32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_NimbusJetFlareSmall", frandom(-35, -32), frandom(35, 32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_NimbusJetFlareSmall", frandom(35, 32), frandom(-35, -32),35,0,0,frandom(-4,-6),0,128);
		
		if (strafing == true)
		{
			ThrustThing(angle * 256 / 360 + 38.7, randompick(-2, 2), 0, 0);
			
			if (target && distance3D(target) < 300)
				A_Recoil(frandompick(0, 0.25));
			if (target && distance3D(target) < 900)
				A_Recoil(frandompick(0, -0.25));
		}
	}
  }
  
  bool strafing;
  int spinloop;
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("FACE YOUR FEARS", "SECOIN", "a0");
  }
  
  States
  {
  Spawn:
	TBB2 AB 2 A_Look();
	Loop;
  See:
	TBB2 AB 2
	{
		A_Chase();
		strafing = false;
		Return A_Jump(20, "Dodge", "Dash");
	}
	Loop;
  Dash:
	TBB2 A 0 A_StartSound("MREIDASH");
	TBB2 AABB 1
	{
		for (int i = 4; i > 0; i--)
			A_Chase(null, null);
		
		A_SpawnItemEx("DCY_AversionTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		
		Return A_Jump(12, "See");
	}
	Goto Dash+1;
  Dodge:
	TBB2 AB 2;
	TBB2 A 2
	{
		A_Dodging(40);
	}
	TBB2 BABABABA 2;
	TBB2 B 2 A_Stop();
	Goto See;
  Missile:
	TBB2 A 0 A_Jump(128, "GroundSlam");
	TBB2 ABABAB 2 A_FaceTarget();
	TBB2 C 10
	{
		strafing = true;
		A_FaceTarget();
	}
	TBB2 CD 8
	{
		for (int i = -10; i < 11; i += 20)
			A_SpawnProjectile("DCY_AversionPlasma", 22, i, frandom(-3.0, 3.0));
		
		tics = random(4, 10);
		
		A_FaceTarget();
		
		Return A_MonsterRefire(20, "See");
	}
	Goto Missile+7;
  GroundSlam:
	TBB2 K 1 { spinloop = 0; bNOPAIN = true; vel.z += 2.5; vel.x /= 2; vel.y /= 2; A_FaceTarget(); }
	TBB2 KKKKKKKKKKKKKKKKKKKKK 1 A_FaceTarget();
	TBB2 K 1 A_ChangeVelocity(0, 0, -20, CVF_REPLACE|CVF_RELATIVE);
	TBB2 K 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_AversionTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		
		if (vel.z == 0)
		{
			A_QuakeEx(7, 7, 0, 20, 0, 3000, "", QF_SCALEDOWN|QF_3D);
			SetStateLabel("GroundMissile");
			A_StartSound("B0SSSMSH", CHAN_BODY, attenuation: 0.2);
			A_Explode(150, 200, 0, 0);
			A_RadiusThrust(2500, 420);
			bNOPAIN = false;
		}
	}
	Goto GroundSlam+21;
  GroundMissile:
	TBB2 K 20 A_FaceTarget();
	TBB2 DL 1
	{
		A_SetAngle(Angle+20);
		
		for (int i = 3; i > 0; i--)
			A_SpawnProjectile("DCY_AversionPlasma", random(15, 35), frandom(-15, 15), frandom(-0, 360), CMF_OFFSETPITCH, frandom(-1, -20));
		
		if (spinloop > 35)
			SetStateLabel("See");
		
		spinloop++;
	}
	Goto GroundMissile+1;
  Pain:
	TBB2 A 2 { strafing = false; }
	TBB2 B 2 A_Pain();
	Goto See;
  Death:
	TBB2 E 1;
	TBB2 E 10
	{
		strafing = false;
		A_Scream();
	}
	TBB2 F 6;
	TBB2 G 6
	{
		A_StartSound("Archon/Explode", CHAN_BODY);
		
		for (int i = 5; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Obliteration", frandom(-5.0, 5.0), frandom(-5.0, 5.0), 30 + frandom(-5.0, 5.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), 0, SXF_NOCHECKPOSITION);
	}
	TBB2 HIJ 5;
	TBB2 J 1 A_BossDeath2();
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_AversionPlasma : DCY_ReceptacleBigPlasma
{
  Default
  {
  DeathSound "CONVEXPL";
  Speed 28;
  FastSpeed 34;
  Scale 0.85;
  Damage 2;
  Translation "0:255=%[0.00,0.00,0.00]:[1.46,1.01,2.00]";
  }
}