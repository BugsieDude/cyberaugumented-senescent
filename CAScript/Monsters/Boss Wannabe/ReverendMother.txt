Class DCY_MotherGhost : DCY_TransfixerTrail { Default { StencilColor "3e5c30"; } }
Class DCY_ReverendMother : DCY_TheGiantEnemySpider
{
  Default
  {
  Health 9000;
  Speed 17;
  FloatSpeed 19;
  Tag "\c[q6]Reverend Mother\c-";
  +MISSILEMORE
  +QUICKTORETALIATE
  +NEVERFAST
  +FLOAT
  +NOGRAVITY
  -FLOORCLIP
  +NOICEDEATH
  +DONTMORPH
  +DONTFALL
  Speed 14;
  BloodColor "a1ed7b";
  BloodType "DCY_SpiritBlood";
  DamageFactor 0.65;
  SeeSound "DCYMother/Sight";
  ActiveSound "DCYMother/Active";
  PainSound "DCYMother/Pain";
  DeathSound "DCYMother/Death";
  }
  
  int laserloop;
  bool strafing;
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen())
	{
		A_SpawnItemEx("DCY_HyperclusterCommanderJetFlare", frandom(55, 65), frandom(55, 65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_HyperclusterCommanderJetFlare", frandom(-55, -65), frandom(-55, -65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_HyperclusterCommanderJetFlare", frandom(-55, -65), frandom(55, 65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_HyperclusterCommanderJetFlare", frandom(55, 65), frandom(-55, -65),74,0,0,frandom(-6,-8),0,128);
		
		if (strafing == true)
		{
			ThrustThing(angle * 256 / 360 + 38.7, randompick(-3, 3), 0, 0);
			
			if (target && distance3D(target) < 300)
				A_Recoil(frandompick(0, 0.45));
			if (target && distance3D(target) < 900)
				A_Recoil(frandompick(0, -0.45));
		}
	}
  }
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("Everything is feeling strange all of a sudden.", "THEWARN", "q6");
  }
  
  void A_MotherTrail(double xvel, double yvel, double zvel)
  {
	let trail = Spawn("DCY_MotherGhost", pos);
			
	if (trail)
	{
		trail.vel.x = xvel;
		trail.vel.y = yvel;
		trail.vel.z = zvel;
		trail.sprite = GetSpriteIndex("M0TH");
		trail.frame = frame;
		trail.scale = scale;
		trail.angle = angle;
	}
  }
  
  States
  {
  Spawn:
	M0TH AB 2 A_Look();
	Loop;
  See:
	M0TH AB 2
	{
		A_Chase();
		Return A_Jump(10, "Dodge");
	}
	Loop;
  Dodge:
	M0TH A 2 A_Dodging(random(13, 20));
	M0TH ABABAB 2;
	M0TH A 2 A_Stop();
	Goto See;
  Missile:
	M0TH A 0 A_Jump(90, "Teleporting", "Laser");
	Goto See;
  Laser:
	M0TH A 0 { strafing = true; laserloop = 60; A_StartSound("DCYMother/Attack", CHAN_VOICE, attenuation: ATTN_NONE); }
	M0TH ABABABAB 2 A_FaceTarget();
	M0TH N 2 { A_FaceTarget(); }
	M0TH NNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNNOOO 2 A_FaceTarget();
	M0TH N 0 A_StartSound("EXLRFIRE", 12, attenuation: ATTN_NONE);
	Goto LaserLoop;
  LaserLoop:
	M0TH PO 1
	{
		if (laserloop <= 0)
		{
			strafing = false;
			A_StopSound(12);
			A_StartSound("EXLREND", 12, attenuation: ATTN_NONE);
			SetStateLabel("See");
		}
		
		if (laserloop == 25)
			A_StartSound("EXLRLOOP", 12, attenuation: ATTN_NONE);
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_MotherLaser", 50);
		laserloop--;
	}
	Loop;
  Teleporting:
	M0TH AB 2 A_FaceTarget();
	M0TH A 0
	{
		for (int i = 55; i > 0; i--)
		{
			A_MotherTrail(0, 0, 0);
			A_Chase(null, null, CHF_NOPLAYACTIVE);
		}
		
		A_QuakeEx(3, 3, 3, 30, 0, 1200, "", QF_SCALEDOWN|QF_3D);
		A_StartSound("dcyminishield/die", CHAN_BODY, attenuation: 0.5, pitch: frandom(0.6, 0.7));
	}
	M0TH AB 2 A_FaceTarget();
	Goto See;
  Pain:
	M0TH A 2 { strafing = false; A_StopSound(12); }
	M0TH B 2 A_Pain();
	Goto See;
  Death:
	M0TH A 2 { strafing = false; A_StopSound(12); }
	Stop;
  }
}