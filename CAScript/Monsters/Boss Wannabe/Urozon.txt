Class DCY_UrozonHornLaser : DCY_MeatballElecball
{
  Default
  {
  Speed 25;
  +EXTREMEDEATH
  +NEVERFAST
  +RIPPER
  +DONTTHRUST
  DamageFunction 3;
  SeeSound "TwirlingLaser";
  DeathSound "URZXPL";
  Translation "0:255=%[0.00,0.00,0.00]:[0.83,0.89,1.25]";
  }
  
  double weavecount;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	for (int i = weavecount; i > 0; i--) A_Weave(3, 3, 4, 4);
  }
	
  States
  {
  Spawn:
	ETV6 AB 1
	{
		A_Weave(3, 3, 4, 4);
		A_SpawnItemEx("DCY_WastelandBallTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_Explode(128, 128, XF_NOTMISSILE, 0);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Stop;
  }
}

Class DCY_Urozon : DCY_Pinke
{
  Default
  {
  Health 1300;
  Translation "16:47=%[0.00,0.00,0.00]:[0.40,0.43,1.64]", "168:191=%[0.00,0.05,0.20]:[0.40,0.43,1.65]", "128:159=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "48:79=%[0.00,0.00,0.00]:[1.68,0.52,0.52]", "160:167=%[0.00,0.00,0.00]:[2.00,0.67,0.67]", "80:111=%[0.00,0.00,0.00]:[0.79,0.79,0.79]", "0:15=%[0.00,0.00,0.00]:[0.78,0.78,0.78]", "208:239=%[0.00,0.00,0.00]:[0.76,0.76,0.76]", "160:165=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  Speed 15;
  FastSpeed 20;
  PainChance 100;
  Mass 600;
  SeeSound "Urozon/Sight";
  ActiveSound "Urozon/Active";
  AttackSound "";
  PainSound "Urozon/Pain";
  DeathSound "Urozon/Death";
  DamageFactor 0.65;
  BloodColor "Blue";
  +BOSS
  +BOSSDEATH
  +NEVERRESPAWN
  +NOICEDEATH
  +DONTGIB
  +MISSILEMORE
  +MISSILEEVENMORE
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOTARGET
  +NOTIMEFREEZE
  +NOFEAR
  +NODROPOFF
  +NORADIUSDMG
  Tag "\c[l5]Urozon\c-";
  Obituary "%o got mauled into pieces by Urozon.";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  DCYMonster.ScientificName "Insana risus";
  +DCYMONSTER.DCYBOSS;
  DCYMonster.CanAnnounce true;
  DCYMonster.AnnounceSound "UROZONHASRISEN";
  DCYMonster.AnnounceColor "Red";
  DCYMonster.AnnounceMessage "To the pits with you.";
  }
  
  int missile;
  int flamethrower;
  
  void A_UrozonBalls()
  {
	A_StartSound("TwirlingLaser", CHAN_WEAPON, attenuation: 0.25);
	for (int i = 3; i > 0; i--)
	{
		DCY_UrozonHornLaser l = DCY_UrozonHornLaser(Spawn("DCY_UrozonHornLaser", pos + (0, 0, 60)));
		
		if (l)
		{
			l.target = self;
			l.angle = angle;
			l.Vel3DFromAngle(GetDefaultSpeed("DCY_UrozonHornLaser"), l.angle, BulletSlope());
			
			switch(i)
			{
				case 3: l.weavecount = 0; break;
				case 2: l.weavecount = 6; break;
				case 1: l.weavecount = 14; break;
			}
		}
	}
  }
  
  States
  {
  Spawn:
	UROZ A 10 A_Look();
	Loop;
  See:
	UROZ A 0 A_Jump(13, "NothingPersonal");
	UROZ A 0 A_Jump(40, "Dash");
	UROZ AABBCCDD 3 DCY_Chase();
	Loop;
  Dash:
	UROZ AAABBBCCCDDD 1
	{
		DCY_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		if (DCY_EffectDistance(4000)) A_SpawnItemEx("DCY_UrozonAura", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	UROZ A 0 A_Jump(80, "See");
	Loop;
  NothingPersonal:
	UROZ A 0 Fast;
	UROZ A 0 A_JumpIf(target && Distance3D(target) < 250, "Lunge");
	UROZ A 0 Fast { bGhost = true; }
	UROZ AAA 5 A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION);
	UROZ A 0 A_JumpIf(target && Distance3D(target) < 250, "flamethrower");
	UROZ AAA 5 A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION);
	UROZ A 0
	{
		bNoPain = true;
		A_SetSpeed(55);
		A_SetTranslucent(0.00, 0);
	}
	UROZ ABCDABCDABCDABCDABCDABCDABCDABCD 1
	{
		for (int i = 7; i > 0; i--)
		{
			DCY_Chase(null, null, CHF_NOPLAYACTIVE);
			DCY_Chase(null, null, CHF_NOPLAYACTIVE);
			
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_Wander();
		}
	}
	UROZ A 10
	{
		bNoPain = false;
		bGhost = false;
		A_SetSpeed(15);
		A_SetTranslucent(1.00, 0);
	}
	Goto See;
  Retreat:
	UROZ A 0
	{
		bNoPain = true;
		bGhost = false;
		A_SetSpeed(55);
		A_SetTranslucent(0.00, 0);
	}
	UROZ ABCDABCDABCDABCD 1
	{
		for (int i = 7; i > 0; i--)
		{
			DCY_Chase(null, null, CHF_NOPLAYACTIVE);
			DCY_Chase(null, null, CHF_NOPLAYACTIVE);
			
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_Wander();
		}
	}
	UROZ A 10
	{
		bNoPain = false;
		bGhost = false;
		A_SetSpeed(15);
		A_SetTranslucent(1.00, 0);
	}
	Goto See;
  Lunge:
	UROZ E 0;
	UROZ E 0 { bGhost = false; }
	UROZ EEEEEEEE 1 A_FaceTarget();
	UROZ E 1
	{
		A_FaceTarget();
		A_StartSound("Urozon/Sight", CHAN_VOICE);
		Return A_Jump(40, "JumpNLunge", "JumpNLunge", "flamethrower");
	}
	UROZ FFFFFFFF 1
	{
		A_CustomMeleeAttack(random(2, 10), "", "");
		A_Recoil(-9);
	}
	UROZ G 5 A_CustomMeleeAttack(10 * random(2, 10), "", "");
	Goto See;
  SuperLunge:
	UROZ EEEEOEOEOEO 1 A_FaceTarget();
	UROZ P 1 A_StartSound("Urozon/Scream", CHAN_VOICE, attenuation: ATTN_NONE);
	UROZ PFPFPFPFPFP 2
	{
		bNoPain = true;
		A_QuakeEx(2, 2, 2, 30, 0, 1000, "", QF_SCALEDOWN);
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_FaceTarget();
		A_CustomMeleeAttack(2 * random(2, 10), "", "");
	}
	UROZ PPPPPPPPPPPPP 1
	{
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_SpawnItemEx("DCY_UrozonAura", -32, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_Recoil(-13);
		A_CustomMeleeAttack(12 * random(2, 10), "", "");
		bNoPain = false;
		bNoDropoff = true;
	}
	UROZ G 5 { A_CustomMeleeAttack(20 * random(2, 10), "", ""); bNoDropOff = false; }
	Goto See;
  Melee:
	UROZ EF 6 A_FaceTarget();
	UROZ G 4
	{
		A_FaceTarget();
		A_StartSound("demon/melee", CHAN_VOICE);
		A_CustomMeleeAttack(10 * random(2, 10), "", "");
	}
	Goto See;
  Missile:
	UROZ A 0
	{
		If (target && Distance3D(target) < 250)
			Return ResolveState("Lunge");
		If (target && Distance3D(target) < 300)
			Return ResolveState("Flamethrower");
		If (target && Distance3D(target) < 600)
			Return ResolveState("DualRockets");
		
		Return A_Jump(40, "Lunge", "JumpNLunge", "MissileFire");
	}
	UROZ AABBCCDD 3 DCY_Chase("Melee", null);
	Loop;
  MissileFire:
	UROZ EOEOEFPFP 2 A_FaceTarget();
	UROZ P 2
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ArbiterTorpedo", 35, 0, 0);
		A_SpawnProjectile("DCY_TransmuterTorpedo", 35, 0, -10);
		A_SpawnProjectile("DCY_TransmuterTorpedo", 35, 0, 10);
	}
	UROZ FE 10;
	Goto See;
  Flamethrower:
	UROZ EOEPFP 1
	{
		flamethrower = 0;
		A_FaceTarget();
	}
	UROZ P 0 A_StartSound("ROBOFIRL", CHAN_6, CHANF_OVERLAP, 1.0, 0.5);
	UROZ PFPF 1
	{
		If(flamethrower >= 30)
		{
			A_FaceTarget();
			A_StartSound("ROBOFIRE", CHAN_6, CHANF_OVERLAP, 1.0, 0.5);
			Return ResolveState("FlamethrowerEnd");
		}
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_UrozonFirethrower", 35, 0, frandom(-1.0, 1.0));
		flamethrower += 1;
		Return ResolveState(null);
	}
	Goto flamethrower+6;
  FlamethrowerEnd:
	UROZ FE 6;
	Goto See;
  JumpNLunge:
	UROZ E 10 A_FaceTarget();
	UROZ E 1
	{
		A_Hop(17.5);
		A_StartSound("MonsterSpit", CHAN_VOICE, attenuation: 0.25);
		bNoPain = true;
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	UROZ EEEEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	UROZ E 1
	{
		bNoGravity = true;
		A_FaceTarget();
	}
	UROZ EEEEEFFFFF 1 A_FaceTarget();
	UROZ P 10
	{
		A_UrozonBalls();
		A_FaceTarget();
	}
	UROZ PE 5;
	UROZ E 10
	{
		bNoGravity = false;
		bNoPain = false;
	}
	Goto See;
  DualRockets:
	UROZ E 10
	{
		missile = random(1, 4);
		A_FaceTarget();
	}
	UROZ Q 5
	{
		A_FaceTarget();
		A_StartSound("ROBOMIS2", CHAN_WEAPON, pitch: 1.5);
		for (int i = -14; i < 15; i += 28) A_SpawnProjectile("DCY_UltracerestRocket", 46, i, frandom(-10, 10), CMF_OFFSETPITCH, pitch + frandom(-5, 5));
		missile--;
	}
	UROZ E random(5, 10)
	{
		if (missile <= 0) SetStateLabel("See");
		A_FaceTarget();
	}
	Goto DualRockets+1;
  Pain:
	UROZ H 3;
	UROZ H 0 A_Jump(60, "SuperLunge");
	UROZ H 0 A_Jump(100, "Lunge", "Retreat", "JumpNLunge", "MissileFire");
	UROZ H 3 A_Pain();
	Goto See;
  Death:
	UROZ I 1;
	UROZ I 6 A_ScreamAndUnblock();
	UROZ JKLM 6;
	UROZ N -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_UrozonFirethrower : DCY_SulliedFirethrower
{
  Default
  {
  Projectile;
  Radius 15;
  Height 15;
  +RIPPER
  +MTHRUSPECIES
  +NOCLIP
  +NOTIMEFREEZE
  -PIERCEARMOR
  DamageFunction 3;
  Renderstyle "Add";
  DamageType "Fire";
  Speed 20.5;
  Scale 0.3;
  +NEVERFAST
  +ROLLSPRITE
  Translation "0:255=%[0.00,0.00,0.00]:[0.83,0.89,1.25]";
  }
  
  States
  {
  Spawn:
	BEWM A 0 NoDelay A_SetRoll(frandom(0, 360));
	BEWM ACDEGHJKMOPRSUVXZ 1 { bNoClip = false; }
	Stop;
  Death:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_UrozonAura : DCY_TenebrosityAura { Default { +NOTIMEFREEZE; StencilColor "26 33 73"; } }