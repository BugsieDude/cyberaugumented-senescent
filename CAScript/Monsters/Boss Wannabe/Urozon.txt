Class DCY_Urozon : DCY_Pinke
{
  Default
  {
  Health 1300;
  Translation "16:47=%[0.00,0.00,0.00]:[0.40,0.43,1.64]", "168:191=%[0.00,0.05,0.20]:[0.40,0.43,1.65]", "128:159=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "48:79=%[0.00,0.00,0.00]:[1.68,0.52,0.52]", "160:167=%[0.00,0.00,0.00]:[2.00,0.67,0.67]", "80:111=%[0.00,0.00,0.00]:[0.79,0.79,0.79]", "0:15=%[0.00,0.00,0.00]:[0.78,0.78,0.78]", "208:239=%[0.00,0.00,0.00]:[0.76,0.76,0.76]";
  Speed 15;
  FastSpeed 20;
  PainChance 100;
  Mass 600;
  SeeSound "Urozon/Sight";
  ActiveSound "Urozon/Active";
  AttackSound "";
  PainSound "Urozon/Pain";
  DeathSound "Urozon/Death";
  DamageFactor 0.65;
  BloodColor "Blue";
  +BOSS
  +BOSSDEATH
  +NEVERRESPAWN
  +NOICEDEATH
  +DONTGIB
  +MISSILEMORE
  +MISSILEEVENMORE
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOTARGET
  +NOTIMEFREEZE
  +NOFEAR
  +NODROPOFF
  +NORADIUSDMG
  Tag "\c[l5]Urozon\c-";
  Obituary "%o got mauled into pieces by Urozon.";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  }
  
  int flamethrower;
  
  States
  {
  Spawn:
	CYF3 A 10 A_Look();
	Loop;
  See:
	CYF3 A 0 A_Jump(13, "NothingPersonal");
	CYF3 A 0 A_Jump(40, "Dash");
	CYF3 AABBCCDD 3 A_Chase();
	Loop;
  Dash:
	CYF3 AAABBBCCCDDD 1
	{
		A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		if (DCY_EffectDistance(4000)) A_SpawnItemEx("DCY_UrozonAura", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	CYF3 A 0 A_Jump(80, "See");
	Loop;
  NothingPersonal:
	CYF3 A 0 Fast;
	CYF3 A 0 A_JumpIf(target && Distance3D(target) < 250, "Lunge");
	CYF3 A 0 Fast { bGhost = true; }
	CYF3 AAA 5 A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION);
	CYF3 A 0 A_JumpIf(target && Distance3D(target) < 250, "flamethrower");
	CYF3 AAA 5 A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION);
	CYF3 A 0
	{
		bNoPain = true;
		A_SetSpeed(55);
		A_SetTranslucent(0.00, 0);
	}
	CYF3 ABCDABCDABCDABCDABCDABCDABCDABCD 1
	{
		for (int i = 7; i > 0; i--)
		{
			A_Chase(null, null, CHF_NOPLAYACTIVE);
			A_Chase(null, null, CHF_NOPLAYACTIVE);
			
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_Wander();
		}
	}
	CYF3 A 10
	{
		bNoPain = false;
		bGhost = false;
		A_SetSpeed(15);
		A_SetTranslucent(1.00, 0);
	}
	Goto See;
  Retreat:
	CYF3 A 0
	{
		bNoPain = true;
		bGhost = false;
		A_SetSpeed(55);
		A_SetTranslucent(0.00, 0);
	}
	CYF3 ABCDABCDABCDABCD 1
	{
		for (int i = 7; i > 0; i--)
		{
			A_Chase(null, null, CHF_NOPLAYACTIVE);
			A_Chase(null, null, CHF_NOPLAYACTIVE);
			
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_Wander();
		}
	}
	CYF3 A 10
	{
		bNoPain = false;
		bGhost = false;
		A_SetSpeed(15);
		A_SetTranslucent(1.00, 0);
	}
	Goto See;
  Lunge:
	CYF3 E 0;
	CYF3 E 0 { bGhost = false; }
	CYF3 EEEEEEEE 1 A_FaceTarget();
	CYF3 E 1
	{
		A_FaceTarget();
		A_StartSound("Urozon/Sight", CHAN_VOICE);
		Return A_Jump(40, "JumpNLunge", "JumpNLunge", "flamethrower");
	}
	CYF3 FFFFFFFF 1
	{
		A_CustomMeleeAttack(random(2, 10), "", "");
		A_Recoil(-9);
	}
	CYF3 G 5 A_CustomMeleeAttack(10 * random(2, 10), "", "");
	Goto See;
  SuperLunge:
	CYF3 EEEEOEOEOEO 1 A_FaceTarget();
	CYF3 P 1 A_StartSound("Urozon/Scream", CHAN_VOICE, attenuation: ATTN_NONE);
	CYF3 PFPFPFPFPFP 2
	{
		bNoPain = true;
		A_QuakeEx(2, 2, 2, 30, 0, 1000, "", QF_SCALEDOWN);
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_FaceTarget();
		A_CustomMeleeAttack(2 * random(2, 10), "", "");
	}
	CYF3 PPPPPPPPPPPPP 1
	{
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_SpawnItemEx("DCY_UrozonAura", -32, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_Recoil(-13);
		A_CustomMeleeAttack(12 * random(2, 10), "", "");
		bNoPain = false;
		bNoDropoff = true;
	}
	CYF3 G 5 { A_CustomMeleeAttack(20 * random(2, 10), "", ""); bNoDropOff = false; }
	Goto See;
  Melee:
	CYF3 EF 6 A_FaceTarget();
	CYF3 G 4
	{
		A_FaceTarget();
		A_StartSound("demon/melee", CHAN_VOICE);
		A_CustomMeleeAttack(10 * random(2, 10), "", "");
	}
	Goto See;
  Missile:
	CYF3 A 0
	{
		If (target && Distance3D(target) < 250)
			Return ResolveState("Lunge");
		If (target && Distance3D(target) < 300)
			Return ResolveState("Flamethrower");
		If (target && Distance3D(target) < 600)
			Return ResolveState("MissileFire");
		
		Return A_Jump(40, "Lunge", "JumpNLunge");
	}
	CYF3 AABBCCDD 3 A_Chase("Melee", null);
	Loop;
  MissileFire:
	CYF3 EOEOEFPFP 2 Bright A_FaceTarget();
	CYF3 P 2 Bright
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ArbiterTorpedo", 35, 0, 0);
		A_SpawnProjectile("DCY_TransmuterTorpedo", 35, 0, -10);
		A_SpawnProjectile("DCY_TransmuterTorpedo", 35, 0, 10);
	}
	CYF3 FE 10;
	Goto See;
  Flamethrower:
	CYF3 EOEPFP 1 Bright
	{
		flamethrower = 0;
		A_FaceTarget();
	}
	CYF3 P 0 Bright A_StartSound("ROBOFIRL", CHAN_6, CHANF_OVERLAP, 1.0, 0.5);
	CYF3 PFPF 1 Bright
	{
		If(flamethrower >= 30)
		{
			A_FaceTarget();
			A_StartSound("ROBOFIRE", CHAN_6, CHANF_OVERLAP, 1.0, 0.5);
			Return ResolveState("FlamethrowerEnd");
		}
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_UrozonFirethrower", 35, 0, frandom(-1.0, 1.0));
		flamethrower += 1;
		Return ResolveState(null);
	}
	Goto flamethrower+6;
  FlamethrowerEnd:
	CYF3 FE 6;
	Goto See;
  JumpNLunge:
	CYF3 E 10 A_FaceTarget();
	CYF3 E 1
	{
		A_Hop(17.5);
		A_StartSound("MonsterSpit", CHAN_VOICE, attenuation: 0.25);
		bNoPain = true;
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	CYF3 EEEEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_UrozonAura", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	CYF3 E 1
	{
		bNoGravity = true;
		A_FaceTarget();
	}
	CYF3 EEEEEFFFFF 1 A_FaceTarget();
	CYF3 P 10
	{
		A_StartSound("TwirlingLaser", CHAN_WEAPON, 1.0, 0, 0.25);
		A_SpawnProjectile("DCY_UrozonHornLaser", 60);
		A_SpawnProjectile("DCY_UrozonHornLaser2", 60);
		A_SpawnProjectile("DCY_UrozonHornLaser3", 60);
		A_FaceTarget();
	}
	CYF3 PE 5;
	CYF3 E 10
	{
		bNoGravity = false;
		bNoPain = false;
	}
	Goto See;
  Pain:
	CYF3 H 3;
	CYF3 H 0 A_Jump(60, "SuperLunge");
	CYF3 H 0 A_Jump(100, "Lunge", "Retreat", "JumpNLunge");
	CYF3 H 3 A_Pain();
	Goto See;
  Death:
	CYF3 I 1;
	CYF3 I 6 A_ScreamAndUnblock();
	CYF3 JKLM 6;
	CYF3 N -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_UrozonHornLaser : CacodemonBall
{
  Default
  {
  Speed 25;
  +EXTREMEDEATH
  +NEVERFAST
  +RIPPER
  DamageFunction 3;
  SeeSound "TwirlingLaser";
  DeathSound "URZXPL";
  Translation "0:255=%[0.00,0.00,0.00]:[0.83,0.89,1.25]";
  }
  
  States
  {
  Spawn:
	ETV6 A 0;
	Goto Idle;
  Idle:
	ETV6 AB 1
	{
		A_Weave(3, 3, 4, 4);
		A_SpawnItemEx("DCY_WastelandBallTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_Explode(128, 128, XF_NOTMISSILE, 0);
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Stop;
  }
}

Class DCY_UrozonHornLaser2 : DCY_UrozonHornLaser
{
  States
  {
  Spawn:
	ETV6 AAAAAA 0 A_Weave(3, 3, 4, 4);
	Goto Idle;
  }
}

Class DCY_UrozonHornLaser3 : DCY_UrozonHornLaser
{
  States
  {
  Spawn:
	ETV6 AAAAAAAAAAAAAA 0 A_Weave(3, 3, 4, 4);
	Goto Idle;
  }
}

Class DCY_UrozonFirethrower : DCY_SulliedFire
{
  Default
  {
  Projectile;
  Radius 15;
  Height 15;
  +RIPPER
  +MTHRUSPECIES
  +NOCLIP
  +NOTIMEFREEZE
  DamageFunction (5);
  DamageType "Fire";
  Speed 20.5;
  Scale 0.3;
  +NEVERFAST
  +ROLLSPRITE
  Translation "0:255=%[0.00,0.00,0.00]:[0.83,0.89,1.25]";
  }
  
  States
  {
  Spawn:
	BEWM A 0 NoDelay A_SetRoll(frandom(0, 360));
	BEWM ACDEGHJKMOPRSUVXZ 1
	{
		bNoClip = false;
		A_Explode((2), 40);
	}
	Stop;
  Death:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_UrozonAura : DCY_TenebrosityAura { Default { +NOTIMEFREEZE; StencilColor "26 33 73"; } }