Class DCY_Xusqoox : DCY_VileParent
{
  Default
  {
  Health 2500;
  Mass 500;
  DamageFactor 0.65;
  DamageFactor "Neonic", 0.467;
  DamageFactor "Holy", 0.35;
  Scale 1.165495;
  BloodColor "Black";
  Speed 20;
  PainChance 50;
  +NOTARGET
  +NORADIUSDMG
  +BOSS
  +NEVERRESPAWN
  +NEVERFAST
  +BOSSDEATH
  +FLOORCLIP
  +QUICKTORETALIATE
  +MISSILEMORE
  +MISSILEEVENMORE  
  +NOICEDEATH
  +DONTGIB
  +DONTHARMSPECIES
  +NOTARGET
  +NOTIMEFREEZE
  +NOFEAR
  SeeSound "xusqoox/sight";
  PainSound "xusqoox/pain";
  DeathSound "xusqoox/death";
  ActiveSound "xusqoox/active";
  MeleeSound "xusqoox/attack";
  Tag "\c[w0]Xusqoox\c-";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  }
  
  bool awaken;
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o knelt before Xusqoox.",
		"%o felt what it was to be outwitted by Xusqoox."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("Ready to feel the definition of badassery?", "SDANGER0", "q0");

	A_SpawnItemEx("DCY_XusqooxCrystal", flags: SXF_SETMASTER);
	A_SpawnItemEx("DCY_XusqooxCrystal2", flags: SXF_SETMASTER);
  }
  
  States
  {
  Spawn:
	XUSQ A 10
	{
		if (awaken == false)
			A_LookEx(LOF_FULLVOLSEESOUND, 0, 0, 0, 180, "Awaken");
		else
			A_Look();
	}
	Loop;
  Awaken:
	XUSQ K 4
	{
		bNoPain = true;
		A_SpawnItemEx("DCY_XusqooxAuraStart");
		A_SpawnItemEx("DCY_XusqooxAuraStart2");
	}
	XUSQ LKLKLKLKJI 4;
	XUSQ H 10
	{
		A_QuakeEx(5, 5, 3, 100, 0, 1000, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 0, 0, 1.0, frandom(-1.0, 1.0));
		A_StartSound("IAEXPL1", CHAN_WEAPON, 1.0, attenuation: 0.5, pitch: .85);
		A_StartSound("EmptinessBlockbuster/Explosion", CHAN_5, 1.0, attenuation: 0.5, pitch: .85);
		A_StartSound("SupraSpell/Activate", CHAN_6, attenuation: 0.35, 0);
		
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Obliteration", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		
		A_Explode(500, 180, XF_NOTMISSILE, 0);
	}
	XUSQ GHIGHIGHIGHGIHGGHIGH 3;
	XUSQ I 3
	{
		bNoPain = false;
		bPainless = false;
		awaken = true;
	}
	Goto See;
  See:
	XUSQ AABBCCDDDEEEFFF 3 A_Chase(Null, "Missile", CHF_RESURRECT);
	Loop;
  Heal:
	XSQ2 A 0 A_RadiusGive("DCY_MassResurrection", 800, RGF_KILLED);
	XSQ2 ABCABCABCABCABCABCABCABCABCABC 2;
	Goto See;
  Missile:
	XUSQ A 0 A_JumpIf(target && Distance3D(target) < 275, "RayExplode");
	Goto VileAttack;
  RayExplode:
	XUSQ KJI 6;
	XUSQ H 6
	{
		for (int i = 0; i < 360; i += 45)
			A_SpawnProjectile("DCY_XusqooxRayProjectile", 0, 0, i, CMF_AIMDIRECTION);
	}
	XUSQ GHIHGIGHIGHIGHIGHIGHIGAAA 2;
	Goto See;
  VileAttack:
    XUSQ A 10 A_FaceTarget();
	XUSQ G 5
	{
		bNoPain = true;
		A_StartSound("MREIDIS2", CHAN_WEAPON);
		A_VileTarget("DCY_XusqooxVile");
		A_FaceTarget();
	}
	XUSQ HIHGHIHGHIHGHIHGHIHGHIHGHIHGHIHGHIHGHIHIHIJKLMN 5 A_Facetarget();
	XUSQ O 5 A_VileAttack("Xusqoox/Explode", 600, 800, 300, 0.65, "Transcendent");
	XUSQ PA 5;
	XUSQ A 5 { bNoPain = false; }
	Goto See;
  Pain:
    XUSQ Q 3;
	XUSQ Q 3
	{
		if (awaken == false)
		{
			bNoPain = true;
			bPainless = true;
			SetStateLabel("Awaken");
		}
		
		A_Pain();
	}
	Goto See;
  Death:
	XUSQ Q 1;
    XUSQ Q 1
	{
		A_RemoveChildren(1, RMVF_EVERYTHING);
		A_Remove(AAPTR_TRACER, RMVF_EVERYTHING, "DCY_XusqooxVile");
	}
	XUSQ Q 30;
	XUSQ Q 1
	{
		A_ChangeVelocity(0, 0, 3, CVF_REPLACE);
		bFloatBob = true;
		bNoGravity = true;
		A_StartSound("Power/Recharge", CHAN_WEAPON, attenuation: 0.3);
		A_StartSound("Power/Recharge", CHAN_6, attenuation: 0.3);
		A_SpawnItemEx("DCY_XusqooxDeathRay");
	}
	XUSQ QQQQQQ 1;
	XUSQ Q 1 A_Stop();
	XUSQ QQQQQQQQQQQQQQQQQQQQQQQQQQQQQQ 1 A_SetAngle(Angle+7);
	XUSQ QQQQQQQQQQQQQQQQQQQQQQQQQQQ 1 A_SetAngle(Angle+16);
	XUSQ QQQQQQQQQQQQQQQQQQQQQ 1 A_SetAngle(Angle+22);
	XUSQ QQQQQQQQQQQQQQQQQQ 1 A_SetAngle(Angle+39);
	XUSQ QQQQQQQQQQQ 1 A_SetAngle(Angle+44);
	XUSQ R 1
	{
		A_Scream();
		bFloatBob = false;
		bNoGravity = false;
		A_QuakeEx(5, 5, 3, 100, 0, 1000, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 0, 0, 1.0, frandom(-1.0, 1.0));
		A_StartSound("IAEXPL1", CHAN_WEAPON, 1.0, attenuation: 0.5, pitch: .85);
		A_StartSound("EmptinessBlockbuster/Explosion", CHAN_5, 1.0, attenuation: 0.5, pitch: .85);
		A_StartSound("SupraSpell/Activate", CHAN_6, attenuation: 0.35, 0);
		A_SpawnItemEx("DCY_Explosion_1_Obliteration", random(-10, 10), random(-10, 10), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Obliteration", random(-10, 10), random(-10, 10), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Obliteration", random(-10, 10), random(-10, 10), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Obliteration", random(-10, 10), random(-10, 10), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_Explode(500, 180, XF_NOTMISSILE, 0);
		A_ChangeVelocity(0, 0, 15, CVF_REPLACE|CVF_RELATIVE);
	}
	XUSQ RSTUVWX 10;
	XUSQ Y 10 A_NoBlocking();
	XUSQ Z -1;
	Stop;
  Death.Massacre:
	XUSQ Q 1;
	XUSQ Q 10
	{
		A_Scream();
		A_RemoveChildren(1, RMVF_EVERYTHING);
		A_Remove(AAPTR_TRACER, RMVF_EVERYTHING, "DCY_XusqooxVile");
	}
	XUSQ RSTUVWX 10;
	XUSQ Y 10 A_NoBlocking();
	XUSQ Z -1;
	Stop;
  }
}

Class DCY_XusqooxDeathRay : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  YScale 100.0;
  XScale 0.95;
  RenderStyle "AddStencil";
  StencilColor "Purple";
  Alpha 0.000001;
  +NOCLIP
  +NOINTERACTION
  +FLOORHUGGER
  +FORCEYBILLBOARD
  +BRIGHT
  }
  
  int loope;
  
  States
  {
  Spawn:
	XUS4 A 0;
	XUS4 A 1
	{
		If(Alpha >= 0.65)
			Return ResolveState("SpawnII");

		A_SetScale(Scale.X+0.5, 100);
		A_FadeIn(0.1);
		Return ResolveState(null);
	}
	Loop;
  SpawnII:
	XUS4 A 1
	{
		If(loope >= 120)
			Return ResolveState("Dissapear");

		A_SetScale(frandom(2.45, 3.35), 100);
		loope++;
		Return ResolveState(null);
	}
	Loop;
  Dissapear:
	XUS4 A 1 A_SetTranslucent(1.0, 1);
	XUS4 AAAAA 1
	{
		A_SetScale(Scale.X+1, 100);
		A_FadeOut(0.2);
	}
	Stop;
  }
}

Class DCY_XusqooxAuraStart : Actor
{
  Default
  {
  Scale 4.85;
  RenderStyle "AddStencil";
  StencilColor "Purple";
  Alpha 0.0000001;
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  }
  
  States
  {
  Spawn:
	XUSQ K 1
	{
		If(Scale.Y >= 1.3 || Scale.X >= 1.3)
		{
			A_SetScale(Scale.X-0.15, Scale.Y-0.15);
			A_FadeIn(0.035);
			Return ResolveState(null);
		}
		Else
		{
			Return ResolveState("Null");
		}
	}
	Loop;
  }
}

Class DCY_XusqooxAuraStart2 : DCY_XusqooxAuraStart
{
  Default
  {
  Scale 6.45;
  }
  
  States
  {
  Spawn:
	XUSQ K 1
	{
		If(Scale.Y >= 1.3 || Scale.X >= 1.3)
		{
			A_SetScale(Scale.X-0.25, Scale.Y-0.25);
			A_FadeIn(0.035);
			Return ResolveState(null);
		}
		Else
		{
			Return ResolveState("Null");
		}
	}
	Loop;
  }
}

Class DCY_XusqooxCrystal : DCY_SorcererOrbit
{
  Default
  {
  +NOTIMEFREEZE
  +NOCLIP
  +NOBLOCKMAP
  Radius 0;
  Height 0;
  Scale 1.5;
  DamageType "Transcendent";
  +BRIGHT
  }
  
  States
  {
  Spawn:
    XSQ3 A 0 NoDelay
	{
		A_SetAngle(90);
		A_SpawnItemEx("DCY_XusqooxCAura");
	}
	XSQ3 A 1
	{
		Warp(Master, 0, 55, 45, Angle, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE);
		A_SpawnItemEx("DCY_XusqooxCAura");
		A_SetAngle(Angle+7);
	}
	Goto Spawn+1;
  Death:
    TNT1 A 2 A_Die();
	Stop;
  }
}

Class DCY_XusqooxCAura : DCY_SorcererAura { Default { Scale 1.05; +NOTIMEFREEZE; +NOCLIP; Alpha 0.15; } }

Class DCY_XusqooxCrystal2 : DCY_SorcererOrbit
{
  Default
  {
  +NOTIMEFREEZE
  +NOCLIP
  +NOBLOCKMAP
  Radius 0;
  Height 0;
  Scale 1.5;
  DamageType "Transcendent";
  +BRIGHT;
  }
  States
  {
  Spawn:
    XSQ3 A 0 NoDelay
	{
		A_SetAngle(270);
		A_SpawnItemEx("DCY_XusqooxCAura");
	}
	XSQ3 A 1
	{
		Warp(Master, 0, 55, 45, Angle, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE);
		A_SpawnItemEx("DCY_XusqooxCAura");
		A_SetAngle(Angle+7);
	}
	Goto Spawn+1;
  Death:
    TNT1 A 2 A_Die();
	Stop;
  }
}

Class DCY_XusqooxVile : ArchvileFire
{
  Default
  {
  DamageType "Transcendent";
  }
  
  int looping;
  
  States
  {
  Spawn:
	TNT1 A 0 NoDelay
	{
		A_StartSound("MREIDIS2", CHAN_WEAPON, 1.0, 1, 0.5);
		A_StartSound("MREIDIS3", CHAN_6, CHANF_OVERLAP, 0.5);
		A_QuakeEx(4, 4, 4, 50, 0, 140, "None", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 240);
	}
	TNT1 A 1
	{
		If(looping >= 28)
		{
			Return ResolveState("Stahp");
		}
		Else
		{
			A_StartSound("MREIDISR", CHAN_BODY, CHANF_DEFAULT, 1.0, 0.5);
			A_Fire();
			A_SpawnItemEx("DCY_XusqooxVileFX1", 0, 0, 32, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 359), SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_XusqooxVileFX2", 0, 0, 0, 0, 0, 0, random(0, 359), SXF_NOCHECKPOSITION);
			looping++;
			Return ResolveState(null);
		}
	}
	TNT1 AAAAAAAA 1
	{
		If(looping >= 28)
		{
			Return ResolveState("Stahp");
		}
		Else
		{
			A_Fire();
			A_SpawnItemEx("DCY_XusqooxVileFX1", 0, 0, 32, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 359), SXF_NOCHECKPOSITION);
			Return ResolveState(null);
		}
	}
	Goto Spawn+1;
  Stahp:
	TNT1 A 1;
	TNT1 A 1 A_StopSound(CHAN_WEAPON);
	Stop;
  }
}

Class DCY_XusqooxVileFX1 : Actor
{
  Default
  {
  RenderStyle "Add";
  Alpha 0.8;
  Scale 0.7;
  Translation "0:255=%[0.00,0.03,0.28]:[0.70,0.00,2.00]";
  +NOCLIP
  +BRIGHT
  +NOGRAVITY
  +THRUSPECIES
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	DS0X A 0 NoDelay
	{
		A_SetPitch(randompick(2, 1, 0, -1, -2));
		A_SetTranslucent(frandom(0.8, 1.0), 1);
		Return A_Jump(128, "Spawn2");
	}
	DS0X ABCDEFG 1
	{
		If(Pitch == 1)
		{
			A_SetScale(Scale.X-0.09, Scale.Y+0.09);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == 2)
		{
			A_SetScale(Scale.X-0.05, Scale.Y+0.05);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == -1)
		{
			A_SetScale(Scale.X-0.05, Scale.Y+0.08);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == -2)
		{
			A_SetScale(Scale.X+0.14, Scale.Y-0.05);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If (Pitch == 0)
		{
			A_SetScale(Scale.X+0.15, Scale.Y-0.08);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else
		{
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
	}
	Stop;
  Spawn2:
	DS0X AGFEDCB 1
	{
		If(Pitch == 1)
		{
			A_SetScale(Scale.X-0.09, Scale.Y+0.09);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == 2)
		{
			A_SetScale(Scale.X-0.05, Scale.Y+0.05);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == -1)
		{
			A_SetScale(Scale.X-0.05, Scale.Y+0.08);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If(Pitch == -2)
		{
			A_SetScale(Scale.X+0.14, Scale.Y-0.05);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else If (Pitch == 0)
		{
			A_SetScale(Scale.X+0.15, Scale.Y-0.08);
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
		Else
		{
			A_FadeOut(0.17, 0);
			Return ResolveState(null);
		}
	}
	Stop;
  }
}

Class DCY_XusqooxVileFX2 : Actor
{
  Default
  {
  Radius 0;
  Height 1;
  +FLATSPRITE
  +BRIGHT
  +NOCLIP
  +THRUACTORS
  +NOGRAVITY
  +FLOORHUGGER
  RenderStyle "Add";
  Alpha 1.0;
  Scale 0.0001;
  }
  
  States
  {
  Spawn:
	EF3_ E 0 NoDelay A_SetAngle(0.6);
	EF3_ E 1
	{
		If(Alpha <= 0.000000001)
		{
			Return ResolveState("Null");
		}
		Else
		{
			A_SetScale(Scale.X+0.1, Scale.Y+0.1);
			A_FadeOut(0.1, 0);
			A_Fire();
			Return ResolveState(null);
		}
	}
	Loop;
  }
}

Class DCY_XusqooxRayProjectile : CacodemonBall
{
  Default
  {
  Speed 60;
  FastSpeed 60;
  +FLOORHUGGER
  +BRIGHT
  DamageType "Xusqoox";
  SeeSound "";
  DeathSound "";
  }
  
  int loop;
  
  States
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 2
	{
		If(loop >= 4)
		{
			Return ResolveState("Null");
		}
		Else
		{
			A_SpawnItemEx("DCY_XusqooxRay", zofs: 11, flags: SXF_NOCHECKPOSITION);
			loop++;
			Return ResolveState(null);
		}
	}
	Loop;
  Death:
    TNT1 A 10;
	Stop;
  }
}

Class DCY_XusqooxRay : Actor
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.37]:[2.00,1.01,1.96]";
  Radius 20;
  Height 67;
  RenderStyle "Add";
  Speed 0;
  VSpeed 0;
  Projectile;
  +FLOORHUGGER
  +RANDOMIZE
  Damage 30;
  Scale 1.5;
  Alpha 1.0;
  }
  States
  {
  Spawn:
	RAY_ A 1;
	RAY_ A 1
	{
		A_QuakeEx(0, 0, 7, 26, 0, 300, "", QF_RELATIVE|QF_SCALEDOWN, falloff: 700);
		A_Explode(randompick(90, 100, 110, 120, 130), 130, XF_NOTMISSILE);
		A_StartSound("LEKICK", CHAN_7, attenuation: 0.4);
	}
	RAY_ BCDEFG 2;
	TNT1 A 30;
	Stop;
  }
}