Class DCY_Upheavalbringer : DCY_VileParent
{
  Default
  {
  Health 2600;
  Tag "\c[f7]Upheavalbringer\c-";
  SeeSound "Upheaval/Sight";
  ActiveSound "Upheaval/Active";
  PainSound "Upheaval/Pain";
  DeathSound "Upheaval/Pain";
  BloodType "AndroidNeonFluid";
  BloodColor "ff4dbe";
  +NOBLOODDECALS
  +BOSS
  +BOSSDEATH
  +NEVERFAST
  +NOICEDEATH
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +DONTMORPH
  +BRIGHT
  Speed 25;
  FastSpeed 30;
  Scale 1.15;
  StencilColor "White";
  DamageFactor 0.85;
  DamageFactor "Holy", 0.35;
  DamageFactor "DCY_Empty", 0.5;
  DamageFactor "BFG", 0.35;
  DamageFactor "Rail", 0.4;
  DamageFactor "Lunacy", 0.00;
  DamageFactor "LunacyDeath", 1.0;
  PainChance "LunacyDeath", 256;
  Obituary "The Upheavalbringer stole a march from %o.";
  HitObituary "%o got out of bounds.";
  PainChance 15;
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  }
  
  int shiploop;
  bool spawnwings;
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	A_SpawnItemEx("DCY_UpheavalWings", flags: SXF_SETMASTER);
	ThingAnnounce("You need to leave.", "WHATDONE", "t8");
  }
  
  States
  {
  Spawn:
	TRXU A 10
	{
		A_SpawnItemEx("DCY_UpheavalIdleAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
		A_Look();
	}
	Loop;
  See:
	TRXU A 0 A_Jump(122, "Dash", "Dodge", "Dash", "Dodge", "Teleporting");
	TRXU AAA 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU BBB 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU CCC 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU DDD 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU EEE 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU FFF 2 A_Chase(null, "Missile");
	TRXU A 0 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	Loop;
  Dash:
	TRXU AABBCCDDEEFF 1
	{
		A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
		A_Chase(null, "Missile");
	}
	TRXU F 0 A_Jump(100, "See");
	Loop;
  Teleporting:
	TRXU A 10;
	TRXU A 0
	{
		for (int i = 70; i > 0; i--)
		{
			A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
			A_Chase(null, null);
		}
	}
	TRXU A 10;
	Goto See;
  Dodge:
	TRXU A 5;
	TRXU A 1
	{
		A_Dodging(45);
		A_StartSound("OTHRDASH", CHAN_5, attenuation: 0.7);
		A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	}
	TRXU AAAAAAA 1 A_SpawnItemEx("DCY_UpheavalAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	TRXU A 0 A_Stop();
	Goto See;
  Missile:
	TRXU A 0 A_JumpIf(Health < 1200, "MissileII");
	Goto MissileI;
  MissileI:
	TRXU A 0 A_Jump(128, "OrbitalLasers");
	TRXU A 0 A_Jump(90, "OrbitalLazerOnUrFace");
	Goto Orbs;
  MissileII:
	TRXU A 0 A_Jump(128, "OrbitalLasers");
	TRXU A 0 A_Jump(90, "OrbitalLazerOnUrFace");
	TRXU A 0 A_Jump(80, "SummonShip");
	Goto Orbs;
  Orbs:
	TRXU JKLMN 3 A_FaceTarget();
	TRXU O 3
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalOrb", 35, 0, random(-12, 12));
		A_SpawnProjectile("DCY_UpheavalOrb", 35, 0, random(-12, 12));
		A_SpawnProjectile("DCY_UpheavalOrb", 35, 0, random(-12, 12));
	}
	TRXU OPN 3 A_FaceTarget();
	TRXU O 3
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalOrb", 35, 0, random(-23, 23));
		A_SpawnProjectile("DCY_UpheavalOrb", 35, 0, random(-23, 23));
	}
	TRXU PA 3;
	Goto See;
  OrbitalLasers:
	TRXU GGGHHHIIIHH 1 A_FaceTarget();
	TRXU H 0 A_SpawnProjectile("DCY_UpheavalOrbitalLaserSpawner", 5, 0, 0, CMF_OFFSETPITCH|CMF_SAVEPITCH|CMF_AIMDIRECTION);
	TRXU GGHHIIHHGGHHIIHHGGHHIIGGHHIIHHGGHHIIHHGGHHII 1;
	Goto See;
  OrbitalLazerOnUrFace:
	TRXU G 0 A_JumpIf(target && Distance3D(target) < 600, "Orbs");
	TRXU GGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXU G 0 A_VileTarget("DCY_UpheavalLazer");
	TRXU GGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXU G 0 A_VileTarget("DCY_UpheavalLazer");
	TRXU G 0 A_JumpIf(target && Distance3D(target) < 600, "Orbs");
	TRXU GGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXU G 0 A_VileTarget("DCY_UpheavalLazer");
	TRXU GGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXU G 0 A_VileTarget("DCY_UpheavalLazer");
	TRXU G 0 A_JumpIf(target && Distance3D(target) < 600, "Orbs");
	TRXU GGHHIIHHGGHHIIHHGGHHIIGGHHIIHHGGHHIIHHGGHHIIGGHHIIHHGGHHIIHHGGHHIIGGHHIIHHGGHHIIHHGGHHII 1;
	Goto See;
  SummonShip:
	TRXU A 1;
	TRXU A 1 A_StartSound("Upheaval/Spell", CHAN_VOICE, attenuation: 0.5);
	TRXU GGGHHHIIIHHHGGGHHHIIIHHH 1 A_FaceTarget();
	TRXU H 1
	{
		shiploop = 0;
		bInvulnerable = true;
		bDontThrust = true;
		A_StartSound("MindLift", CHAN_WEAPON, attenuation: ATTN_NONE);
		A_StartSound("MindLift", CHAN_5, attenuation: ATTN_NONE);
		A_SpawnItemEx("DCY_UpheavalShip", 0, 0, 100, 0, 0, 0, Angle, SXF_SETMASTER|SXF_NOCHECKPOSITION);
	}
	Goto ShipWait;
  ShipWait:
	TRXU GGGHHHIIIHHH 1
	{
		if(shiploop > 300)
		{
			A_RemoveChildren(1, RMVF_EVERYTHING);
			SetStateLabel("See");
		}
		
		If(Pitch <= 0.000001)
			A_FaceTarget();
		
		A_SoundVolume(CHAN_WEAPON, Pitch);
		A_SetPitch(Pitch-0.06);
		A_FaceTarget();
		shiploop++;
	}
	Loop;
  Pain:
	TRXU Q 5;
    TRXU Q 5 A_Pain();
    Goto See;
  Pain.LunacyDeath:
	TRXU Q 1;
	TRXU Q 4
	{
		A_StopSound(CHAN_WEAPON);
		bPainless = true;
		bDontThrust = false;
		bNoPain = true;
		bInvulnerable = false;
		A_StartSound("STUNNED3", CHAN_BODY, attenuation: 0.35);
	}
    TRXU Q 5 A_Pain();
	TRXU Q 127;
	TRXU A 1
	{
		bPainless = false;
		bNoPain = false;
	}
	Goto See;
  Death:
	TNT1 A 0 A_StopSound(CHAN_6);
	TRXU Q 2;
	TRXU Q 20
	{
		A_Scream();
		A_QuakeEx(2, 2, 2, 20, 0, 200, "none", QF_SCALEDOWN);
	}
	TRXU AAAAAAAAAAAAAA 5 A_SpawnItemEx("DCY_UpheavalAura", 0, 0, 5);
	TRXU GGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXU G 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_UpheavalLazer", zofs: 5, flags: SXF_NOCHECKPOSITION);
	}
	TRXU GGHHIIHHGGHHIIGGHHIIHHGGHHIIHHGGHHIIGGHHIIHH 1 A_FaceTarget();
	TRXO Q 5
	{
		A_KillChildren("DED", RMVF_EVERYTHING);
		A_Scream();
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_StartSound("misc/kaboom", CHAN_BODY, attenuation: ATTN_NONE);
		A_StartSound("Upheaval/OrbitalLaser", CHAN_6, attenuation: ATTN_NONE);
		A_QuakeEx(6, 6, 6, 75, 0, 2000, "none", QF_SCALEDOWN);
		bRollSprite = true;
		A_ChangeVelocity(random(-3, 8), random(-6, 9), 20, CVF_REPLACE|CVF_RELATIVE);
		A_SetRenderStyle(1.0, STYLE_STENCIL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 1
	{
		A_SetRoll(Roll+40);
		A_SetAngle(Angle+40);
		A_SetRenderStyle(1.0, STYLE_NORMAL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 1
	{
		A_SetRoll(Roll+40);
		A_SetAngle(Angle+40);
		A_SetRenderStyle(1.0, STYLE_STENCIL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 1
	{
		A_SetRoll(Roll+40);
		A_SetAngle(Angle+40);
		A_SetRenderStyle(1.0, STYLE_NORMAL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 1
	{
		A_SetRoll(Roll+40);
		A_SetAngle(Angle+40);
		A_SetRenderStyle(1.0, STYLE_STENCIL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 1
	{
		A_SetRoll(Roll+40);
		A_SetAngle(Angle+40);
		A_SetRenderStyle(1.0, STYLE_NORMAL);
	}
	TRXO QQQQQ 1
	{
		A_SetAngle(Angle+40);
		A_SetRoll(Roll+40);
	}
	TRXO Q 4
	{
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Upheaval", random(-100, 100), random(-100, 100), random(35, 80), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION);
		A_QuakeEx(3, 3, 3, 75, 0, 2000, "none", QF_SCALEDOWN);
		A_StartSound("Upheaval/DeathBoom", CHAN_BODY, attenuation: ATTN_NONE);
	}
	TNT1 A 1 A_NoBlocking();
    Stop;
  Death.Massacre:
	TRXU A 10;
	TRXU Q 10 A_Scream();
	TRXU QQ 1 A_FadeOut(0.5, 0);
	TRXU Q 0
	{
		A_RemoveChildren(1, RMVF_EVERYTHING);
		A_NoBlocking();
	}
	Stop;
  }
}

Class DCY_UpheavalShip : Cacodemon
{
  Default
  {
  SeeSound "";
  PainSound "BLASFAR";
  DeathSound "RTROBOM2";
  ActiveSound "";
  RenderStyle "Add";
  Alpha 1.0;
  Health 1000;
  Speed 0;
  VSpeed 0;
  Tag "\c[Gray]upheavalbringer - stance\c- [\c[f7]Disarray\c-]";
  +FLOATBOB
  -COUNTKILL
  +MISSILEMORE
  +DONTFALL
  +DONTTHRUST
  +NOGRAVITY
  +NOBLOOD
  +BRIGHT
  +DONTHARMCLASS
  +NORADIUSDMG
  -FORCERADIUSDMG
  DamageType "Lunacy";
  DamageFactor "Lunacy", 0.00;
  DamageFactor "LunacyDeath", 1.0;
  DamageFactor 0.665;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.24,0.91]";
  XScale 0.85;
  YScale 0.765;
  Radius 115;
  Height 85;
  }
  
  override void Tick()
  {
	super.Tick();
	
	if (!isFrozen() || Health > 0)
		A_SpawnItemEx("DCY_ProjectileBaseTrailU", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
  }
  
  States
  {
  Spawn:
	SI3R C 1 NoDelay
	{
		A_SpawnItemEx("DCY_UpheavalShipAura", 0, 0, 98, 0, 0, 0, 0, SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_UpheavalShipAura2", 0, 0, 98, 0, 0, 0, 0, SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_FaceTarget();
		A_QuakeEx(3, 3, 3, 40, 0, 1000, "none", QF_SCALEDOWN|QF_RELATIVE);
		A_SpawnItemEx("DCY_BHFX3", 0, 0, 98, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 30);
		A_SpawnItemEx("DCY_BHFX3", 0, 0, 98, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 30);
		A_SpawnItemEx("DCY_BHFX3", 0, 0, 98, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 30);
		A_SpawnItemEx("DCY_BHFX3", 0, 0, 98, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 30);
	}
	SI3R C 8;
	Goto See;
  Idle:
  See:
	SI3R C 1 A_Chase("Missile", "Missile", CHF_DONTMOVE);
	Loop;
  Missile:
  Melee:
	SI3R C 0 A_Jump(40, "Vagues");
	SI3R CCCCCCCCCC 1 A_FaceTarget();
	SI3R C 1
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, -70, random(-10, 10));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 0, random(-22, 22));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 70, random(-10, 10));
		A_StartSound("GIGALAS2", CHAN_WEAPON, attenuation: 0.5);
	}
	SI3R CCCCCCCCCCCCC 1 A_FaceTarget();
	SI3R C 1
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, -70, random(-10, 10));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 0, random(-22, 22));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 70, random(-10, 10));
		A_StartSound("GIGALAS2", CHAN_WEAPON, attenuation: 0.5);
	}
	SI3R CCCCCCCCCCCCC 1 A_FaceTarget();
	SI3R C 1
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, -70, random(-10, 10));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 0, random(-22, 22));
		A_SpawnProjectile("DCY_UpheavalShipLaser", 75, 70, random(-10, 10));
		A_StartSound("GIGALAS2", CHAN_WEAPON, attenuation: 0.5);
	}
	SI3R CCCCCCCCCCCCC 1 A_FaceTarget();
	Goto See;
  Vagues:
	SI3R CCCCCCCCCC 1 A_FaceTarget();
	SI3R CCCCC 1
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UpheavalVagueBall", 75, random(-60, 60), random(-30, 30), CMF_OFFSETPITCH, random(-15, 15));
		A_SpawnProjectile("DCY_UpheavalVagueBall", 75, random(-60, 60), random(-30, 30), CMF_OFFSETPITCH, random(-15, 15));
		A_StartSound("GIGALAS2", CHAN_WEAPON, attenuation: 0.5);
	}
	SI3R CCCCCCCCCCCCC 1 A_FaceTarget();
	Goto See;
  Pain:
	SI3R C 0 A_Pain();
	Goto See;
  Death:
	SI3R CCCCCCCCCCCCCCC 3
	{
		A_SetTics(random(2, 5));
		A_SpawnItemEx("DCY_Explosion_1", frandom(-90.0, 90.0), frandom(-90.0, 90.0), frandom(20.0, 80.0), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.00, 360.00), SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_StartSound("ROBOEXPL", CHAN_BODY, attenuation: 0.24);
		A_SpawnItemEx("DCY_ProjectileBaseTrailU", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	}
	SI3R C 5
	{
		A_SetDamageType("LunacyDeath");
		A_Scream();
		A_Explode(300, 555, XF_NOTMISSILE, 0, 10);
		A_DamageMaster(50, "LunacyDeath", DMSS_FOILINVUL|DMSS_NOPROTECT);
		A_QuakeEx(4, 4, 4, 30, 0, 1000, "none", QF_SCALEDOWN);
		
		for (int i = 2; i > 0; i--)
		{
			A_SpawnItemEx("DCY_UpheavalShipAura", 0, 0, 98, 0, 0, 0, 0, SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_UpheavalShipAura2", 0, 0, 98, 0, 0, 0, 0, SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		}
		
		for (int i = 6; i > 0; i--)
			A_SpawnItemEx("DCY_BHFX3", 0, 0, 98, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 30);
			
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Biggie", frandom(-2, 2), frandom(-2, 2), frandom(10, 50), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.00, 360.00), SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	}
	TNT1 A 25;
	Stop;
  }
}

Class DCY_ProjectileBaseTrailU : DCY_ProjectileBaseTrail { Default { Translation "0:255=%[0.00,0.00,0.00]:[1.11,0.01,0.58]"; } }

Class DCY_UpheavalShipAura : DCY_EmptyAura
{
  States
  {
  Spawn:
	SIER C 1;
	"####" "########" 1
	{
		A_SetScale(Scale.X+0.65, Scale.Y+0.065);
		A_FadeOut(0.125, 0);
	}
	Stop;
  }
}

Class DCY_UpheavalShipAura2 : DCY_EmptyAura
{
  States
  {
  Spawn:
	SIER C 1;
	"####" "#####" 1
	{
		A_SetScale(Scale.X+1, Scale.Y+1);
		A_FadeOut(0.2, 0);
	}
	Stop;
  }
}

Class DCY_UpheavalShipLaser : DCY_TLaser
{
  Default
  {
  Damage 4;
  Speed 55;
  Scale 1.2345;
  +NEVERFAST
  +EXTREMEDEATH
  SeeSound "BLASER";
  DeathSound "BLASFAR";
  DamageType "Lunacy";
  MissileType "DCY_UpheavalLaserTrail";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,0.65]";
  }
  
  States
  {
  Death:
	OURA A 1;
    OURA B 1
	{
		A_Explode(20, 120, XF_NOTMISSILE, 10);
		A_QuakeEx(2, 2, 2, 43, 0, 300, "none", QF_SCALEDOWN, 0, 0, 0, 600);
		A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.00, 360.00), SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.00, 360.00), SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0.00, 360.00), SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	}
	OURA CDEFGHIJLM 1;
	Stop;
  }
}

Class DCY_UpheavalLaserTrail : DCY_TLTrail
{
  Default { Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,0.65]"; }
}

Class DCY_UpheavalVagueBall : DCY_VagueBall
{
  Default
  {
  SeeSound "GIGALAS2";
  DeathSound "GIGALASR";
  DamageType "Lunacy";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,0.65]";
  Speed 15;
  +NEVERFAST
  }
  
  States
  {
  Spawn:
	BL48 AB 1
	{
		A_Weave(2, 2, frandom(2.0, 3.0), frandom(0.45, 1.0));
		A_SpawnItemEx("DCY_VagueBallAura", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERTRANSLATION);
	}
	Loop;
  Death:
	TNT1 A 1 A_SetScale(0.65, 0.65);
	BL48 A 1
	{
		A_SpawnItemEx("DCY_VagueBallRing", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERTRANSLATION);
		A_SetScale(Scale.X+0.1, Scale.Y+0.1);
	}
	BL48 BAB 1 A_SetScale(Scale.X+0.1, Scale.Y+0.1);
	ETV4 C 0
	{
		A_Explode(100, 80, XF_NOTMISSILE, 1);
		A_SpawnItemEx("DCY_VaguePillar", 0, 0, 3, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_QuakeEx(2, 2, 2, 43, 0, 300, "none", QF_SCALEDOWN, 0, 0, 0, 600);
		A_StartSound("SMEXPL3", CHAN_BODY, attenuation: 0.45);
	}
	TNT1 A 44;
	Stop;
  }
}

Class DCY_UpheavalIdleAura : DCY_EmptyAura
{
  Default
  {
  Scale 1.175;
  Alpha 0.6;
  }
  
  States
  {
  Spawn:
    TNT1 A 0;
	TRXU A 1;
	Goto FadeOut;
  FadeOut:
	"####" "######" 1
	{
		A_SetScale(Scale.X+0.045, Scale.Y+0.045);
		A_FadeOut(0.165, 0);
	}
	Stop;
  }
}

Class DCY_UpheavalWings : LostSoul
{
  Default
  {
  Painchance 0;
  Radius 0;
  Height 0;
  Damage 0;
  Alpha 1.0;
  Scale 1.15;
  Health 15;
  RenderStyle "Add";
  Mass 300;
  Monster;
  -COUNTKILL
  +NOGRAVITY
  +DROPOFF
  +PAINLESS
  -SOLID
  +NOPAIN
  +NOICEDEATH
  +NOBLOODDECALS
  +DONTHARMSPECIES
  +NEVERRESPAWN
  +NOBLOOD
  +NOCLIP
  +BRIGHT
  DamageFactor 0.00;
  DamageFactor "Ded", 1.5;
  DamageFactor "Lunacy", 0.00;
  SeeSound "";
  AttackSound "";
  PainSound "";
  DeathSound "";
  ActiveSound "";
  }
  
  States
  {
  Spawn:
  See:
  Missile:
  Pain:
  Melee:
	UPW1 AAAAAAAAAAAAAAAAAAAABBBBBBBBBBCCCCCCCCCCDDDDDDDDDDEEEEEEEEEE 1
	{
		If(!master)
			Destroy();
		
		If(Health <= 2)
			Destroy();
		
		A_SetRenderStyle(frandom(0.55, 1.0), STYLE_ADD);
		A_Warp(AAPTR_MASTER, -2, 0, 14, 0, WARPF_INTERPOLATE);
		Return ResolveState(null);
	}
	Loop;
  Death:
	UPW1 A 1 A_SetTranslucent(0.00, 1);
	UPW1 A 1 A_Die();
	Stop;
  }
}

Class DCY_UpheavalBomb : Actor
{
  Default
  {
  Translation "168:191=%[0.00,0.00,0.00]:[2.00,0.00,1.01]", "80:111=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "0:15=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "16:47=%[0.00,0.00,0.00]:[2.00,0.00,1.01]";
  Radius 12;
  Height 12;
  Scale 1.1;
  RenderStyle "Normal";
  Speed 0;
  }
}

Class DCY_UpheavalLazer : ArchvileFire
{
  Default
  {
  Alpha 0.1;
  YScale 100.0;
  XScale 0.25;
  +BRIGHT
  +THRUACTORS
  +FORCEYBILLBOARD
  +FLOORHUGGER
  }
  
  int loopone;
  int looptwo;
  
  void A_UpheavalFadeIn(double fade, double fscale)
  {
	A_Fire();
	A_FadeIn(fade);
	A_SetScale(Scale.X+fscale, Scale.Y);
  }
	
  States
  {
  Spawn:
	UPHL ABA 1 A_UpheavalFadeIn(0.2, 0.3);
	UPHL B 1 A_UpheavalFadeIn(0.15, 0.2);
	UPHL AB 1 A_UpheavalFadeIn(0.1, 0.2);
	UPHL A 1 A_UpheavalFadeIn(0.1, 0.1);
	UPHL BAB 1 A_UpheavalFadeIn(0, 0.05);
	Goto SpawnLoop;
  SpawnLoop:
	UPHL AB 1
	{
		If(loopone >= 22)
			Return ResolveState("SpawnLoop2");

		A_Warp(AAPTR_TRACER, 24, flags: WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		loopone++;
		Return ResolveState(null);
	}
	Loop;
  SpawnLoop2:
	UPHL AB 1
	{
		If(looptwo >= 18)
			Return ResolveState("Explode");
		
		looptwo++;
		Return ResolveState(null);
	}
	Loop;
  Explode:
	UPHL C 1
	{
		A_QuakeEx(2, 2, 4, 100, 0, 500, "none", QF_SCALEDOWN, 0, 0, 0, 1000, 0, frandom(0.1, -0.1), frandom(-0.1, 0.1));
		A_FadeIn(1.0);
		
		for (int i = 1; i < 5; i++)
			A_StartSound("Upheaval/OrbitalLaser", i, CHANF_OVERLAP, 1.0, 0.45, 1.0);
		
		A_Explode(428, 199, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.75, Scale.Y+0.00);
	}
	UPHL CDECDECDE 1;
	UPHL CECDE 1 A_SetScale(Scale.X-0.1, Scale.Y+0.00);
	UPHL C 1
	{
		A_SetScale(Scale.X-0.15, Scale.Y+0.00);
		A_FadeOut(0.25);
	}
	UPHL ECDE 1 A_SetScale(Scale.X-0.15, Scale.Y+0.00);
	UPHL C 1
	{
		A_SetScale(Scale.X-0.15, Scale.Y+0.00);
		A_FadeOut(0.25);
	}
	UPHL ECDE 1 A_SetScale(Scale.X-0.2, Scale.Y+0.00);
	UPHL C 1
	{
		A_SetScale(Scale.X-0.15, Scale.Y+0.00);
		A_FadeOut(0.25);
	}
	UPHL ECDE 1 A_SetScale(Scale.X-0.25, Scale.Y+0.00);
	Stop;
  }
}

Class DCY_UpheavalOrb : FastProjectile
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.52,0.00,0.98]";
  Radius 20;
  Height 20;
  Scale 0.7;
  Alpha 0.8;
  DamageFunction (1);
  Speed 22.5;
  FastSpeed 25;
  RenderStyle "Add";
  +NOEXTREMEDEATH
  +SEEKERMISSILE
  +RIPPER
  +MISSILE
  SeeSound "PlasmaLaser2";
  DeathSound "R3EXPL2";
  }
  
  States
  {
  Spawn:
	UPH1 ACJHBGEIHF 1
	{
		A_SpawnItemEx("DCY_UpheavalVapor2", flags: SXF_NOCHECKPOSITION);
		A_Weave(random(2, 4), random(2, 4), random(0, 1), random(0, 1));
	}
	Loop;
  Death:
	TNT1 A 1;
	TNT1 A 4
	{
		A_Explode(40, 65, 0, 1);
		A_QuakeEx(2, 2, 2, 40, 0, 300, "none", QF_SCALEDOWN|QF_RELATIVE);
		
		for (int i = 7; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", random(-20, 20), random(-20, 20), random(-20, 20), random(-4, 4), random(-4, 4), random(-4, 4), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 55);
		
		A_Die();
	}
	Stop;
  }
}

Class DCY_UpheavalOrbitalLaserSpawner : CacodemonBall
{
  Default
  {
  SeeSound "";
  DeathSound "";
  Alpha 0.000001;
  Radius 5;
  Height 5;
  Speed 100.5;
  FastSpeed 100.5;
  Damage 0;
  +NOTIMEFREEZE
  +THRUGHOST
  +THRUSPECIES
  +THRUACTORS
  +NOINTERACTION
  +FLOORHUGGER
  +FLOORCLIP
  -SOLID
  +BRIGHT
  }
  
  int loop;
  
  States
  {
  Spawn:
	TNT1 A 2
	{
		If(loop >= 20)
			SetStateLabel("Stahp");
			
		loop++;
		A_SpawnItemEx("DCY_UpheavalOrbitalLaser", zofs: 5, flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Stahp:
    TNT1 A 5 A_Die();
	Stop;
  }
}

Class DCY_UpheavalOrbitalLaser : Actor
{
  Default
  {
  Radius 0;
  Height 1;
  +OLDRADIUSDMG
  +BRIGHT
  +NOCLIP
  +FLOORCLIP
  +THRUACTORS
  +FORCERADIUSDMG
  +FLOORHUGGER
  +FORCEYBILLBOARD
  DamageType "Lunacy";
  RenderStyle "Add";
  Alpha 0.1;
  Scale 0.75;
  Damage 0;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.07,1.33]";
  }
  
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("DCY_UpheavalVapor", random(-35, 35), random(-35, 35), 4, 0, 0, frandom(4.0, 7.0), 0, SXF_NOCHECKPOSITION);
	TNT1 A 5
	{
		A_QuakeEx(3, 3, 3, 27, 0, 400, "none", QF_SCALEDOWN, falloff: 1200);
		A_StartSound("Upheaval/OrbitalBoom", CHAN_BODY, attenuation: 0.5);
		A_SpawnItemEx("DCY_UpheavalOrbitalLaser2", 0, 0, 4, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
	Stop;
  }
}

Class DCY_UpheavalOrbitalLaser2 : DCY_UpheavalOrbitalLaser
{
  Default
  {
  Damage (0);
  Alpha 1.0;
  XScale 0.75;
  YScale 0.75;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.07,1.33]";
  }
  
  States
  {
  Spawn:
    ETV4 AAABBC 1 A_Explode(15, 128, XF_NOTMISSILE, 1);
	ETV4 A 0 A_SetScale(0.75, 10.0);
	ETV4 DDEEFFGHIIJKL 1 A_Explode(25, 128, XF_NOTMISSILE, 1);
	Stop;
  }
}

Class DCY_UpheavalVapor : Actor
{
  Default
  {
  Height 0;
  Radius 0;
  RenderStyle "Add";
  Scale 2.5;
  Alpha 0.5;
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  +THRUACTORS
  +DONTSPLASH
  }
  
  States
  {
  Spawn:
	EF1_ R 1
	{
		If(Scale.X <= 0 || Alpha <= 0)
			Return ResolveState("Null");
		
		A_FadeOut(0.05, 0);
		A_SetScale(Scale.X-0.15, Scale.Y-0.15);
		A_ChangeVelocity(random(-1, 1), random(-1, 1), 1, CVF_RELATIVE);
		Return ResolveState(null);
	}
	Loop;
  }
}

Class DCY_UpheavalVapor2 : DCY_UpheavalVapor
{
  Default
  {
  Scale 0.8;
  }
  
  States
  {
  Spawn:
	EF1_ R 1
	{
		If(Scale.X <= 0 || Alpha <= 0)
			Return ResolveState("Null");
		
		A_FadeOut(0.0385, 0);
		A_SetScale(Scale.X-0.05, Scale.Y-0.05);
		A_ChangeVelocity(random(-1, 1), random(-1, 1), random(-1, 1), CVF_RELATIVE);
		Return ResolveState(null);
	}
	Loop;
  }
}