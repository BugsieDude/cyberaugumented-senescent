Class DCY_DivinePhantom : DCY_Violator //Not a boss, but something else
{
  Default
  {
  Speed 18;
  Tag "\c[z3]Divine \c[a4]Phantom\c-";
  Obituary "%o percieved the absolute death sun by a Divine Phantom.";
  HitObituary "%o percieved the absolute death sun by a Divine Phantom.";
  Health 400;
  PainChance 20;
  DamageFactor 0.9;
  SeeSound "Prowler/Active";
  ActiveSound "Prowler/Active";
  PainSound "Prowler/Pain";
  DeathSound "Prowler/BurningScream"; 
  +NOBLOOD
  +NEVERFAST
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +VISIBILITYPULSE
  +THRUSPECIES
  +BOSS
  +BRIGHT
  }
  
  int death;
  
  override void Tick()
  {
	Super.Tick();
	
	if (Health > 0 && !IsFrozen())
		A_SpawnItemEx("DCY_DivinePhantomExplosion3", zofs: 5, xvel: frandom(-2, 2), yvel: frandom(-2, 2), zvel: frandom(0.35, 2), flags: SXF_TRANSFERALPHA|SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME, 150);
  }
  
  void A_PhantomBall()
  {
	if (target)
	{
	  A_StartSound("WAVESPIT", CHAN_WEAPON, attenuation: 0.5);
	  A_SpawnProjectile("DCY_DivinePhantomBall", 30, 0);
	}
  }
  
  States
  {
  Spawn:
	AQHW A 10 A_Look();
	Loop;
  See:
    AQHW BBCCDDEE 2 A_Chase;
	Loop;
  Melee:
    AQHW EF 4 A_FaceTarget();
	AQHW G 4 A_CustomMeleeAttack(random(1, 8) * 12, "imp/melee");
	TNT1 A 0 A_Jump(150, "Missile");
	Goto See;
  Missile:
    TNT1 A 0 A_JumpIf(target && Distance3D(target) < 355, "Jump");
	Goto MissileContinue;
  MissileContinue:
    AQHW EF 3 A_FaceTarget();
	AQHW G 3 A_PhantomBall();
	AQHW EF 3 A_FaceTarget();
	AQHW G 3 A_PhantomBall();
	AQHW EF 3 A_FaceTarget();
	AQHW G 3 A_PhantomBall();
	AQHW EF 3 A_FaceTarget();
	AQHW G 3 A_PhantomBall();
	AQHW EF 3 A_FaceTarget();
	AQHW G 3 A_PhantomBall();
	Goto See;
  Jump:
    AQHW A 8
	{
		A_FaceTarget();
		Return A_Jump(100, "Missile");
	}
	AQHW F 8;
	AQHW G 14
	{
		A_Recoil(-33);
		A_Hop(3);
		A_FaceTarget();
	}
	Goto MissileContinue;
  Pain:
    AQHW H 2;
    AQHW H 4 A_Pain();
	Goto See;
  Death:
  XDeath:
	AQHW H 1;
    AQHW H 10 A_StartSound("Prowler/BurningScream", CHAN_VOICE, attenuation: 0.3);
	Goto DeathLoop;
  DeathLoop:
	AQHW H 2
	{
		If (death >=20)
			Return ResolveState("DeathInitiate");
		
		A_SpawnItemEx("DCY_DivinePhantomExplosion2", zofs: 6, xvel: frandom(-1.0, 1.0), yvel: frandom(-1.0, 1.0), zvel: random(3, 8), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		
		death += 1;
		Return ResolveState(null);
	}
	Loop;
  DeathInitiate:
	AQHW H 2
	{
		death = 0;
		A_StartSound("DivinePhantom/Explode");
		A_SpawnItemEx("DCY_DivinePhantomExplosion", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	TNT1 A 5 A_NoBlocking();
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_DivinePhantomBall : DCY_ViolationBall
{
  Default
  {
  Speed 20;
  Damage 4;
  +NEVERFAST
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  SeeSound "WAVESPIT";
  DeathSound "WAVESPIX";
  }
  
  States
  {
  Spawn: 
	SBAL AABBCC 1 Bright
	{
		A_SpawnItemEx("DCY_ViolationBallTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Weave(3, 3, 1, 1);
	}
	Loop;
  }
}