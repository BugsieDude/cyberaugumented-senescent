Class DCY_MultidimensionalCaster : DCY_Cybie //Strong boi
{ //To have a Voice Actor (WIP)
  Default
  {
  Health 14000;
  Tag "\c[p7]Multidimensional Caster\c-";
  Radius 25;
  Height 75;
  Obituary "The power %o witnessed by the \c[p7]Multidimensional Caster\c- was in a lunatic passsion.";
  HitObituary "The \c[p7]Multidimensional Caster\c-'s pushed %o into the n-th dimension! It's fake... A-Actually you're dead.";
  Scale 1.1;
  Species "Lunacy";
  Monster;
  +BOSS
  +FLOORCLIP
  +NOFEAR
  +BOSSDEATH
  +NOICEDEATH
  +NOTIMEFREEZE
  +DONTMORPH
  +DONTBLAST
  +NORADIUSDMG
  -FRIGHTENED
  +NOTARGET
  +NOINFIGHTING
  +DONTRIP
  +TELESTOMP
  +NOTELEFRAG
  +NEVERRESPAWN
  +SEEINVISIBLE
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +DONTHARMSPECIES
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  Speed 20;
  FastSpeed 30;
  //DropItem "DCY_TheCinerealOrdnance";
  DamageFactor 0.65;
  DamageFactor "Holy", 0.35;
  DamageFactor "Emptiness", 0.5;
  DamageFactor "BFG", 0.35;
  DamageFactor "Rail", 0.4;
  DamageFactor "Lunacy", 0.00;
  DamageFactor "Help", 0.00;
  SeeSound "MW/Sight";
  ActiveSound "MW/Active";
  PainSound "MW/Pain";
  AttackSound "CinerealOrdnanceFire";
  HowlSound "MW/Pain";
  DeathSound "MW/Death";
  PainChance 15;
  TeleFogDestType "DCY_BossTeleport";
  }
  
  int ultralaser;
  int siriuscatharsis;
  int vegacrisis;
  int vegacrisiswait;
  int vegacrisiscooldown;
  int railcooldown;
  int fuckery;
  int barragestart;
  int barragefire;
  bool speak;
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("TREMBLE, AND KNOW YOUR DOOM!!", "DANGERMW", "l7");
  }
  
  States
  {
  Spawn:
    MDWR M 5 A_LookEx(0, 0, 0, 0, 270, "See");
	Loop;
  See:
    MDWR A 0;
	MDWR M 2
	{
		A_Chase("Melee", "Missile");
		Return A_Jump(20, "Teleport");
	}
	MDWR MMMNNNN 2 A_Chase("Melee", "Missile");
	MDWR O 2
	{
		A_Chase("Melee", "Missile");
		Return A_Jump(20, "Teleport");
	}
	MDWR OOOPPPP 2 A_Chase("Melee", "Missile");
	Loop;
  Teleport:
	MDWR A 0 A_FaceTarget();
	MDW3 BCD 2 A_FaceTarget();
	MDW5 AB 1 A_FaceTarget();
	MDW5 B 1
	{
		A_StartSound("Enemy/Teleport7A", CHAN_WEAPON, attenuation: 0.25);
		bInvulnerable = true;
		A_HideThing();
	}
	TNT1 A 1
	{
		A_QuakeEx(3, 3, 3, 30, 0, 700, "", QF_SCALEDOWN, 0, 0, 0, 1400);
		A_SpawnItemEx("DCY_MultidimensionalTeleportation", 0, 0, 30, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		
		for (int i = 0; i < 360; i += 90)
		{
			A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 5, 10, 0, 0, i, SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 5, 10, 0, 0, i, SXF_NOCHECKPOSITION);
		}
	}
	MDWR AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase(null, null, CHF_NIGHTMAREFAST|CHF_NOPLAYACTIVE);
	MDWR AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase(null, null, CHF_FASTCHASE|CHF_NIGHTMAREFAST|CHF_NOPLAYACTIVE);
	MDWR AAAAAAAAAAA 0 A_Chase(null, null, CHF_FASTCHASE|CHF_NIGHTMAREFAST|CHF_NOPLAYACTIVE);
	TNT1 A 20;
	MDW5 B 1
	{
		A_StartSound("Enemy/Teleport7B", CHAN_WEAPON, attenuation: 0.25);
		bInvulnerable = false;
		A_UnhideThing();
		A_SpawnItemEx("DCY_MultidimensionalTeleportation", 0, 0, 30, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		
		for (int i = 0; i < 360; i += 90)
		{
			A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 5, 10, 0, 0, i, SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 5, 10, 0, 0, i, SXF_NOCHECKPOSITION);
		}
		
		A_QuakeEx(3, 3, 3, 30, 0, 700, "", QF_SCALEDOWN, 0, 0, 0, 1400);
	}
	MDW5 A 1;
	MDW3 DCBB 2;
	Goto See;
  Melee:
    Goto See;
  Missile:
	MDWR M 0 A_Jump(128, "Rain", "ZapperBall");
	MDWR M 0 A_Jump(128, "GodSaveYou", "ZapperBarrage", "ObliterationBFG");
	Goto ZapperBarrage;
  ZapperBarrage:
    MDWR R 15
	{
		A_StartSound("MW/Spell2", CHAN_VOICE, attenuation: 0.2065);
		A_StartSound("LIGTCHRG", CHAN_WEAPON, attenuation: ATTN_NONE);
		A_FaceTarget();
		barragefire = 0;
	}
	MDWR S 3;
	MDWR S 20
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_MultidimensionalWizardAura", 0, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	MDWR SSS 20
	{
		A_FaceTarget();
		A_QuakeEx(4, 4, 4, 30, 0, 500, "", QF_SCALEDOWN, 0, 0, 0, 1500);
		A_SpawnItemEx("DCY_SpiritRing", 0, 0, 35, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		A_SpawnItemEx("DCY_MultidimensionalWizardAura", 0, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
    MDWR S 10 A_FaceTarget();
	Goto ZapperBarrageStartLoop;
  ZapperBarrageStartLoop:
	MDWR T 4
	{
		If(barragefire >= 26)
			Return ResolveState("ZapperBarrageCooldown");
		
		A_SpawnProjectile("DCY_MultidimensionalZapperBall", 35, 0, random(-15, 15), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, frandom(-3.5, 3.5));
		A_FaceTarget();
		A_StartSound("LIGTFIRE", CHAN_WEAPON, attenuation: 0.25);
		barragefire++;
		Return ResolveState(null);
	}
	Loop;
  ZapperBarrageCooldown:
    MDWR T 20;
	Goto See;
  ZapperBall:
	MDWR R 15 A_FaceTarget();
	MDWR S 15 A_FaceTarget();
	MDWR T 15
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_MultidimensionalZapperBall", 35, 0, random(-2, 2));
		Return A_Jump(100, "Dual");
	}
	MDWR M 5;
	Goto See;
  Dual:
    MDWR R 15 A_FaceTarget();
	MDWR S 15 A_FaceTarget();
	MDWR T 15
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_MultidimensionalZapperBall", 35, 0, random(-2, 2));
	}
	MDWR M 5;
	Goto See;
  Rain:
	MDWR RS 10 A_FaceTarget();
	MDWR SSSSSSSSS 2
	{
		A_SpawnItemEx("DCY_MultidimensionalFallingTrail", 0, frandom(-17.5, 17.5), 75, 0, 0, 70, 0, SXF_NOCHECKPOSITION);
		A_StartSound("ELECSPRK", CHAN_WEAPON, attenuation: 0.35);
	}
	MDWR R 10
	{
		A_SpawnProjectile("DCY_MultidimensionalRainSpawner", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
		A_SpawnProjectile("DCY_MultidimensionalRainSpawner", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
		A_SpawnProjectile("DCY_MultidimensionalRainSpawner", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
		A_SpawnProjectile("DCY_MultidimensionalRainSpawner", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
		A_FaceTarget();
	}
	MDW3 BCDCBC 4 A_SpawnProjectile("DCY_MultidimensionalRainSpawner", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
	MDW3 DCBCDCB 4;
	MDWR M 5;
	Goto See;
  ObliterationBFG:
	MDWR M 10 A_FaceTarget();
	MDW3 E 6
	{
		A_StartSound("ObliterationBFG/Raise", CHAN_WEAPON, attenuation: 0.5);
		A_FaceTarget();
	}
	MDW3 FGGG 4 A_FaceTarget();
	MDW4 A 6 A_FaceTarget();
	MDW4 A 1
	{
		A_FaceTarget();
		A_StartSound("ObliterationBFG/Fire", CHAN_WEAPON, attenuation: 0.25);
	}
	MDW4 AAAAAAAAAAAAAAAAAAA 1 A_FaceTarget();
	MDW4 B 10 A_SpawnProjectile("DCY_ObliterationBFGProjectileBlu", 32, 8);
	MDW4 A 15;
	MDW3 GFE 5 A_FaceTarget();
    Goto See;
  GodSaveYou:
	MDWR M 10 A_FaceTarget();
	MDW3 E 6
	{
		A_StartSound("ObliterationBFG/Raise", CHAN_WEAPON, attenuation: 0.5);
		A_FaceTarget();
		bNoPain = true;
	}
	MDW3 FGGG 4 A_FaceTarget();
	MDW4 A 6 A_FaceTarget();
	MDW4 A 20
	{
		A_FaceTarget();
		A_StartSound("ObliterationBFG/Raise", CHAN_WEAPON, attenuation: 0.5, 0, 0.25);
	}
	MDW4 A 20 A_StopSound(CHAN_WEAPON);
	MDWR Q 10 A_Pain();
	MDW3 GGGFFFFFF 5;
	MDW3 H 35
	{
		A_QuakeEx(4, 4, 4, 100, 0, 2500, "");
		A_StartSound("DSHOLY2", CHAN_ITEM, attenuation: ATTN_NONE);
		A_StartSound("SAMRRIS1", CHAN_WEAPON, attenuation: ATTN_NONE);
	}
	MDW4 A 1
	{
		A_FaceTarget();
		A_StartSound("ObliterationBFG/Fire", CHAN_WEAPON, attenuation: 0.25, 0, 1.4);
		A_StartSound("MW/LaserBeam", CHAN_5, 1, 0, ATTN_NONE, 0);
	}
	MDW4 CCCCCC 1 A_FaceTarget();
	MDW4 DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD 1
	{
		A_SetPitch(Pitch+20, SPF_INTERPOLATE);
		A_Recoil(4);
		A_QuakeEx(4, 4, 4, 100, 0, 1000, "", QF_SCALEDOWN|QF_RELATIVE, 0, 0, 0, 3000, 0, frandom(-1.0, 1.0), frandom(0, 1.0));
		A_StartSound("ROBOMIS2", CHAN_BODY, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_StartSound("Android/LaserX", CHAN_VOICE, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_StartSound("ROBOMISL", CHAN_WEAPON, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_SpawnProjectile("DCY_MMissile", frandom(20, 80), frandom(-60, 60), random(-1, 1), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, random(-1, 1));
		A_SpawnProjectile("DCY_MMissile1", frandom(20, 80), frandom(-60, 60), random(-1, 1), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, random(-1, 1));
		A_SpawnProjectile("DCY_MMissile2", frandom(20, 80), frandom(-60, 60), random(-1, 1), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, random(-1, 1));
	}
	MDW4 A 30
	{
		for (int i = 8; i > 0; i--)
			A_StopSound(i);
		
		A_StartSound("ROBOMIS2", CHAN_BODY, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_StartSound("Android/LaserX", CHAN_VOICE, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_StartSound("ROBOMISL", CHAN_WEAPON, CHANF_DEFAULT|CHANF_OVERLAP, 1.0, 0.2, frandom(1.1, 1.2), frandom(0, 1));
		A_StartSound("MW/LaserBeamStop", CHAN_5, 1, 0, 0.2, 0);
	}
	MDWR Q 150
	{
		bPainless = true;
		A_StartSound("DSHOLY2A", CHAN_AUTO, attenuation: ATTN_NONE);
		A_QuakeEx(3, 3, 3, 100, 0, 1000, "", QF_SCALEDOWN|QF_RELATIVE, 0, 0, 0, 3000, 0);
	}
	MDWR Q 1
	{
		bNoPain = false;
		bPainless = false;
	}
	Goto See;
  Pain:
	MDWR Q 3;
    MDWR Q 8
	{
		A_Pain();
		A_UnhideThing();
		A_SetRenderStyle(1.0, STYLE_NORMAL);
		Return A_Jump(50, "Teleport");
	}
	TNT1 A 0 A_Jump(50, "Teleport");
	Goto See;
  Death:
	MDW6 A 40 A_Scream();
	MDW6 A 1
	{
		A_StartSound("MW/Explode", CHAN_BODY, attenuation: ATTN_NONE);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", zofs: 30, flags: SXF_NOCHECKPOSITION);
	}
	MDW6 AAAAAAAAAAAAAAAAA 2
	{
		A_SpawnItemEx("DCY_BlueVoidExplosions", frandom(-20, 20), frandom(-20, 20), frandom(20, 50), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_SiriusArc", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", zofs: 30, flags: SXF_NOCHECKPOSITION, failchance: 200);
	}
	MDW6 ABC 15;
	MDW6 C 150;
	TNT1 A 1
	{
		A_NoBlocking();
		A_SpawnItemEx("DCY_BossTeleport", zofs: 30);
	}
	Stop;
  Death.Massacre:
    TNT1 A 5;
	Stop;
  }
}

Class DCY_MultidimensionalWizardAura : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "AddStencil";
  StencilColor "21 a1 f2";
  Alpha 0.01;
  Scale 2.65;
  +BRIGHT
  +NOINTERACTION
  +NOCLIP
  +THRUACTORS
  }
  
  States
  {
  Spawn:
	"####" "####################" 1
	{
		If(Scale.X <= 1.125)
			Return ResolveState("Null");
		
		A_SetScale(Scale.X-0.15, Scale.Y-0.15);
		A_FadeIn(0.035, 0);
		Return ResolveState(null);
	}
	Stop;
  }
}

Class DCY_MultidimensionalTeleportation : Actor
{
  Default
  {
  Height 0;
  Radius 0;
  RenderStyle "Add";
  Scale 0.9;
  Alpha 1.0;
  +NOCLIP
  +BRIGHT
  +ROLLSPRITE
  +NOGRAVITY
  Translation "0:255=%[0.00,0.00,0.00]:[0.03,0.62,1.98]";
  }
  
  States
  {
  Spawn:
	TNT1 A 1 NoDelay
	{
		for (int i = 5; i > 0; i--)
			A_SpawnItemEx("DCY_MTeleaura", frandom(-30, 30), frandom(-30, 30), frandom(10, 68), frandom(0.4, -0.4), frandom(0.4, -0.4), frandom(0.4, -0.4), frandom(0, 360), SXF_NOCHECKPOSITION, 40);
		
		A_SpawnItemEx("DCY_MultidimensionalTeleportationRing", 0, 0, 5, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION);
	}
	TNT1 AAA 2
	{
		for (int i = 40; i < 70; i += 10)
			A_SpawnItemEx("DCY_MTeleaura", frandom(-30, 30), frandom(-30, 30), frandom(10, 68), frandom(0.4, -0.4), frandom(0.4, -0.4), frandom(0.4, -0.4), frandom(0, 360), SXF_NOCHECKPOSITION, i);
	}
	Stop;
  }
}

Class DCY_MTeleaura : Actor
{
  Default
  {
  Height 0;
  Radius 0;
  RenderStyle "Add";
  Scale 0.9;
  Alpha 1.0;
  +NOCLIP
  +BRIGHT
  +NOINTERACTION
  +ROLLSPRITE
  +NOGRAVITY
  Translation "0:255=%[0.00,0.00,0.34]:[1.54,2.00,2.00]";
  }
  
  States
  {
  Spawn:
	OURA A 0 NoDelay A_SetRoll(frandom(0.0, 360.0));
	OURA ABCDEFGHIJKLM 1;
	Stop;
  }
}

Class DCY_MultidimensionalTeleportationRing : Actor
{
  Default
  {
  Height 0;
  Radius 0;
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.22;
  +NOCLIP
  +NOINTERACTION
  +FLATSPRITE
  +BRIGHT
  +ROLLSPRITE
  +NOGRAVITY
  Translation "0:255=%[0.69,0.98,2.00]:[0.00,0.00,0.00]";
  }
  
  States
  {
  Spawn:
	R1NG ABCDEFGHIIJJKKLL 1 A_SetAngle(Angle+20);
	Stop;
  }
}

Class DCY_MultidimensionalTeleportationRing2 : DCY_MultidimensionalTeleportationRing { Default { Scale 1.88; } }

Class DCY_MProjectile : FastProjectile
{
  Default
  {
  Radius 18;
  Height 18;
  Speed 47.5;
  FastSpeed 50.5;
  Damage 15;
  DamageType "Lunacy";
  Translation "0:255=%[0.00,0.00,0.00]:[1.19,1.19,2.00]";
  Alpha 1.0;
  RenderStyle "Add";
  Scale 1.45;
  SeeSound "ELECSPRK";
  DeathSound "FIREBOOM";
  +BRIGHT
  +NOEXTREMEDEATH
  +NOTIMEFREEZE
  }
  
  States
  {
  Spawn:
    SOFX AABBCC 1;
	Loop;
  Death:
	SOFX G 3
	{
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SetScale(1.1, 1.1);
	}
    SOFX HIJKL 3;
	Stop;
  }
}

Class DCY_MFallingProjectile : DCY_MProjectile
{
  Default
  {
  Speed 0;
  +NEVERFAST;
  FastSpeed 0;
  Damage 35;
  }
  
  States
  {
  Spawn:
    SOFX MMNNOO 1;
	Loop;
  }
}

Class DCY_MultidimensionalFallingTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  YScale -1.45;
  XScale -1.45;
  +NOCLIP
  +NOINTERACTION
  DamageType "Lunacy";
  Translation "0:255=%[0.00,0.00,0.00]:[1.19,1.19,2.00]";
  Alpha 1.0;
  RenderStyle "Add";
  +BRIGHT
  }
  
  States
  {
  Spawn:
    SOFX MMNNOOMMNNOO 1 A_FadeOut(0.01, 0);
	Loop;
  }
}

Class DCY_MultidimensionalRainSpawner : CacodemonBall
{
  Default
  {
  SeeSound "";
  DeathSound "";
  Alpha 0.01;
  Radius 3;
  Height 3;
  Speed 150;
  FastSpeed 155;
  DamageFunction 0;
  +BOUNCEONWALLS
  +CANBOUNCEWATER
  +NOTIMEFREEZE
  +THRUACTORS
  +CEILINGHUGGER
  +FLOORCLIP
  +THRUSPECIES
  +THRUGHOST
  +NOCLIP
  -SOLID
  +NOINTERACTION
  +BRIGHT
  BounceCount 1000;
  BounceFactor 1.5;
  BounceType "Hexen";
  Species "Lunacy";
  DamageType "Lunacy";
  }
  
  int loop;
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		If(loop >= 20)
			Destroy();
		
		A_SetAngle(random(0, 360));
		
		for (int i = 2; i > 0; i--)
		{
			A_Wander();
			A_Chase(null, null);
		}
		
		A_ChangeVelocity(random(-10, 10), random(-10, 10), 0, CVF_REPLACE);
		loop++;
		tics = randompick(5, 6, 7);
		A_SpawnItemEx("DCY_MFallingProjectile", 0, 0, -16, 0, 0, random(-30, -60), 0, SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
    TNT1 A 5 A_Die();
	Stop;
  }
}

Class DCY_MultidimensionalRain : DCY_MultidimensionalRainSpawner
{
  Default
  {
  Speed 0;
  FastSpeed 0;
  +NEVERFAST
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	TNT1 A 2
	{
		loop += 1;
		A_SpawnItemEx("DCY_MultidimensionalRainSpawner", random(-1500, 1500), random(-1500, 1500), -5, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
	TNT1 A 0 A_JumpIf(loop >= 150, "Stahp");
	Loop;
  }
}

Class DCY_MultidimensionalFuckeryTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Translucent";
  Alpha 1.0;
  Scale 1.1;
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  }
  
  States
  {
  Spawn:
    MDWR R 2 A_FaceTarget();
	MDWR RRRRRRRRRR 1 A_FadeOut(0.1, 0);
	Stop;
  }
}

Class DCY_MultidimensionalZapperBall : FastProjectile
{
  Default
  {
  Radius 25;
  Height 25;
  Speed 110;
  FastSpeed 115;
  Damage 40;
  DamageType "Lunacy";
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,1.13,2.00]";
  Alpha 0.9;
  Scale 1.0;
  MissileType "DCY_MultidimensionalZapperTrail";
  SeeSound "LIGTFIRE";
  DeathSound "LIGT002";
  MissileHeight 5;
  Projectile;
  +BRIGHT
  +EXTREMEDEATH
  +THRUGHOST
  +NOTIMEFREEZE
  +FORCERADIUSDMG
  }
  
  States
  {
  Spawn:
	LFX2 KLMNO 1 A_Weave(6, 6, 1, 1);
	Loop;
  Death:
    SFE1 A 1;
	SFE1 A 0 
	{
		A_QuakeEx(6, 6, 6, 15, 0, 400, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(4, 4, 4, 25, 0, 600, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(3, 3, 3, 32, 0, 800, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(2, 2, 2, 38, 0, 1000, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 45, 0, 1250, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 35, 0, 1250, "", QF_SCALEDOWN, 0, 0, 0, 0, 0, 0.7, frandom(0.7, -0.7));
		A_Explode(100, 250, XF_NOTMISSILE, True);
		A_SpawnItemEx("DCY_MultidimensionalZapperExplodeTrailBarrage", flags: SXF_NOCHECKPOSITION);
	}
    SFE1 AAAABBBBCCCCDDDDEEEEFFFF 3 A_Explode(50, 100, 0, 1);
	Stop;
  }
}

Class DCY_MultidimensionalZapperTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 0.825;
  Scale 1.0;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,1.13,2.00]";
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +NOTIMEFREEZE
  }
  
  States
  {
  Spawn:
	LFX2 K 0 NoDelay A_ChangeVelocity(frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-2.5, 2.5));
	LFX2 KLMNO 1
	{
		If(Alpha <= 0.01)
			Destroy();
		
		A_FadeOut(0.075, 0);
	}
	Goto Spawn+1;
  }
}

Class DCY_MultidimensionalZapperExplodeTrailBarrage : Actor
{
  Default
  {
  Radius 1;
  Height 1;
  +NOCLIP
  +NOINTERACTION
  +NOTIMEFREEZE
  +BRIGHT
  }
  
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_SpawnItemEx("DCY_MultidimensionalZapperExplodeTrail", 0, 0, 0, random(-10, 10), random(-10, 10), random(-10, 10), random(0, 360), SXF_NOCHECKPOSITION);
	Stop;
  }
}

Class DCY_MultidimensionalZapperExplodeTrail : DCY_MultidimensionalZapperTrail
{
  States
  {
  Spawn:
	SFE1 AAAABBBCCCDDDEEE 1 A_FadeOut(0.05, 0);
	Stop;
  }
}

Class DCY_MultidimensionalOrbitalLaser : Actor
{
  Default
  {
  Radius 0;
  Height 1;
  +OLDRADIUSDMG
  +BRIGHT
  +NOCLIP
  +THRUACTORS
  +FORCERADIUSDMG
  +FORCEYBILLBOARD
  DamageType "Lunacy";
  RenderStyle "Add";
  Alpha 0.1;
  XScale 0.5;
  YScale 5.0;
  Damage 0;
  Translation "0:255=%[0.00,0.00,0.00]:[0.69,0.78,1.32]";
  }
  
  States
  {
  Spawn:
	TNT1 A 0 NoDelay A_StartSound("MW/OrbitalBeamAppear", CHAN_BODY, attenuation: 0.5);
	ETV4 EFEFEFEFEFEFEFEFEFEFEF 1 A_FadeIn(0.015);
	TNT1 A 0 A_StartSound("MW/OrbitalBeam", CHAN_BODY, 0, 0.65, 0.35);
	TNT1 A 30
	{
		A_QuakeEx(0, 0, 5, 30, 0, 1000, "", QF_SCALEDOWN);
		A_StartSound("MW/OrbitalBeam", CHAN_BODY, 0, 0.65, 0.35);
		A_SpawnItemEx("DCY_MultidimensionalOrbitalLaser2", 0, 0, 4, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
	Stop;
  }
}

Class DCY_MultidimensionalOrbitalLaser2 : DCY_MultidimensionalOrbitalLaser
{
  Default
  {
  Damage (0);
  Alpha 1.0;
  XScale 1.0;
  YScale 1.0;
  Translation "0:255=%[0.00,0.00,0.00]:[1.10,1.56,2.00]";
  }
  
  States
  {
  Spawn:
    ETV4 AAAABBBCC 1 A_Explode(45, 165, XF_NOTMISSILE, 1);
	ETV4 A 0 A_SetScale(1.0, 5.0);
	ETV4 DDEEFFGHIIJKL 1 A_Explode(45, 135, XF_NOTMISSILE, 1);
	Stop;
  }
}

Class DCY_MultidimensionalLaser : DCY_EmptyLaser
{
  Default
  {
  Damage 50;
  MissileType "DCY_MultidimensionalLaserTrail";
  MissileHeight 6;
  DamageType "Lunacy";
  Obituary "The power %o witnessed by the \c[p7]Multidimensional Caster\c- was excruciating in lunatic magical STYLE.";
  +NOTIMEFREEZE;
  }
  
  States
  {
  Death:
	TNT1 A 1;
	TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("DCY_AndroidExplosionSmall", random(-2, 2), random(-2, 2), 0, random(-2, 2), random(-2, 2), random(1, 2), 0, SXF_NOCHECKPOSITION);
	TNT1 A 5 A_QuakeEx(5, 5, 5, 20, 0, 300, "", QF_SCALEDOWN);
	Stop;
  }
}


Class DCY_MultidimensionalLaserTrail : DCY_EmptyLaserTrail
{
  Default { Scale 0.6; }
  States
  {
  Spawn:
    EF1_ C 1;
	Goto Liwp;
  Liwp:
	EF1_ C 0 A_JumpIf(Alpha <= 0.000000001, "Null");
	EF1_ C 1 A_FadeOut(0.075, 0);
	Loop;
  }
}

Class DCY_MMissile : DCY_ArbiterTorpedo { Default { SeeSound ""; Speed 105; -SEEKERMISSILE; +NEVERFAST; DamageType "Help"; } }
Class DCY_MMissile1 : DCY_TransmuterTorpedo { Default { SeeSound ""; Speed 116; +NEVERFAST; DamageType "Help"; } }
Class DCY_MMissile2 : DCY_UltracerestRocket { Default { SeeSound ""; Speed 130; +NEVERFAST; DamageType "Help"; } }