ACTOR OmegaSp00der : SpiderMastermind
{
  Health 18888
  DamageFactor 0.65
  DamageFactor "OmegaishPower", 0.00
  DamageFactor "Lunacy", 0.00
  DamageFactor "BFG", 0.7
  DamageFactor "Cosmical", 0.45
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid"
  Monster
  +BRIGHT
  +QUICKTORETALIATE
  +NOTIMEFREEZE
  +NORADIUSDMG
  +PIERCEARMOR
  +MISSILEEVENMORE
  +NOTARGET
  +BOSS
  +BOSSDEATH
  +NEVERRESPAWN
  +MISSILEMORE
  +FIRERESIST
  +DONTMORPH
  +NOICEDEATH
  +LOOKALLAROUND
  +DONTGIB
  +NOTELEFRAG
  +DONTRIP
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +CANTSEEK
  +NOFEAR
  +AVOIDMELEE
  +NOPAIN
  +PAINLESS
  +DONTFALL
  +INVULNERABLE
  -FLOORCLIP
  Species "Lunacy"
  DropItem "HelperSphereSpawnerCommon"
  DropItem "Helper_sphere" 40
  SeeSound "" //"OmegaSp00der/Sight"
  ActiveSound "OmegaSp00der/Active"
  PainSound "OmegaSp00der/Pain"
  DeathSound "OmegaSp00der/Death"
  Tag "\c[l5]''The Enigma'' \c[White][Codenamed - \c[Sapphire]Omega-Stormcaster-Battlemind\c[White]]\c-"
  var int user_the2ndoutburst;
  var int user_awakening;
  States
  {
  Spawn:
	MS24 D 1
	{
		If(user_awakening = 0)
		{
			Return State("Idle");
		}
		Else
		{
			A_LookEx(LOF_FULLVOLSEESOUND, 0, 0, 0, 180, "AWAKEN");
			Return State("");
		}
	}
	Loop
  Idle:
	MS23 AB 2 A_LookEx(LOF_FULLVOLSEESOUND, 0, 0, 0, 180, "Welcome Back.")
	Loop
  AWAKEN:
	MS24 D 2
	MS24 D 1
	{
		A_ChangeFlag(NOGRAVITY, 1);
		A_ChangeVelocity(0, 0, 1, CVF_RELATIVE);
		A_PlaySound("OmegaSp00der/Sight", CHAN_VOICE, 1.0, 0, ATTN_NONE);
		A_QuakeEx(4, 4, 4, 200, 0, 2000, "misc/quake", QF_RELATIVE|QF_SCALEUP, 0, 0, 0, 4000, 0, 1.0, frandom(-1.0, 1.0));
	}
	MS24 DDDDDDCCCCCCCCBBBBB 3
	MS24 A 3
	{
		A_ChangeFlag(FLOATBOB, 1);
		A_Stop;
	}
	MS24 A 3 A_SpawnItemEx("OmegaSp00derAuraAwaken")
	MS24 AAA 3
	MS24 A 3 A_SpawnItemEx("OmegaSp00derAuraAwaken")
	MS24 AAA 3
	MS24 A 3 A_SpawnItemEx("OmegaSp00derAuraAwaken")
	MS24 AAA 3
	MS24 A 3 A_SpawnItemEx("OmegaSp00derAuraAwaken")
	MS24 AAA 3
	MS23 A 2
	{
		A_SpawnItemEx("OmegaSp00derAura");
		A_FaceTarget;
		A_SpawnItemEx("LunisJetFlareSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
		A_SpawnItemEx("LividJetFlareSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER);
	}
	MS23 BAB 2 A_FaceTarget
	MS23 A 2
	{
		A_SpawnItemEx("OmegaSp00derAura");
		A_FaceTarget;
	}
	MS23 BAB 2 A_FaceTarget
	MS23 A 2
	{
		A_SpawnItemEx("OmegaSp00derAura");
		A_FaceTarget;
	}
	MS23 BAB 2 A_FaceTarget
	MS23 A 2
	{
		A_SpawnItemEx("OmegaSp00derAura");
		A_FaceTarget;
	}
	MS23 BAB 2 A_FaceTarget
	MS23 P 1 A_FaceTarget
	MS23 P 1
	{
		A_QuakeEx(10, 10, 10, 150, 0, 3000, "misc/quake", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 6000, 0, 1.3, frandom(-1.3, 1.3));
		A_PlaySound("OmegaSp00der/Pain", CHAN_BODY, 1.0, 0, ATTN_NONE, 0, 0.5);
		A_SetUserVar(user_awakening, 1);
		A_ChangeFlag(FLOAT, 1);
		A_ChangeFlag(FLOATBOB, 0);
		A_FaceTarget;
	}
	MS23 PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 1 A_FaceTarget
	MS23 ABABABABABABABABA 2
	MS23 B 2
	{
		A_ChangeFlag(PAINLESS, 0);
		A_ChangeFlag(NOPAIN, 0);
		A_ChangeFlag(INVULNERABLE, 0);
		A_PlaySound("OmegaSp00der/Awakened", CHAN_5, 1.0, 0, ATTN_NONE);
	}
	Goto See
  "Welcome Back.":
	MS23 A 2 A_PlaySound("OmegaSp00der/SecondSee", CHAN_VOICE, 1.0, 0, ATTN_NONE)
	MS23 BABABABABABABABABABABABABABABABABABABABABABA 2 A_FaceTarget
	Goto See
  See:
	MS23 ABABABAB 2
	{
		A_Chase;
		Return A_Jump(10, "Teleporten", "EvadeShit");
	}
	Loop
  Teleporten:
	MS23 B 2
	MS23 A 1
	{
		A_HideThing;
		A_ChangeFlag(INVULNERABLE, 1);
		A_CHangeFlag(NOPAIN, 1);
		A_SpawnItemEx("OmegaSp00derAuraStencil");
		A_SpawnItemEx("OmegaSp00derTeleportFX", 0, 0, 40);
		A_PlaySound("Enemy/Teleport5", CHAN_BODY, 1.0, 0, 0.5, 0, 0.745732832);
		A_PlaySound("Enemy/Teleport3", CHAN_6, 1.0, 0, 0.5, 0, 0.85732832);
		A_PlaySound("Enemy/Teleport2", CHAN_5, 1.0, 0, 0.5, 0, 0.85732832);
	}
	MS23 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 { A_Wander; A_Wander; }
	MS23 A 1
	{
		A_UnHideThing;
		A_ChangeFlag(INVULNERABLE, 0);
		A_CHangeFlag(NOPAIN, 0);
		A_SpawnItemEx("OmegaSp00derTeleportFX2", 0, 0, 40);
		A_SpawnItemEx("OmegaSp00derAuraStencil");
	}
	MS23 ABAB 2
	Goto See
  Missile:
	MS23 A 1
	{
		A_FaceTarget;
		A_Stop;
	}
	MS23 A 1 A_FaceTarget
	MS23 BAB 2 A_FaceTarget
	MS23 B 0 A_Jump(192, "ElectricalBolz")
	MS23 E 1
	{
		A_FaceTarget;
		A_PlaySound("ZSPIATKS", CHAN_WEAPON, 1.0, 0, 0.65, 0, 0.75);
	}
	MS23 EEEEEEEEEEE 2 A_FaceTarget
	Goto MissileLoop
  MissileLoop:
	MS23 F 1
	{
		A_FaceTarget;
		A_PlaySound("ZSPIATKL", CHAN_WEAPON, 1.0, 1, 0.65);
		ThrustThing(Angle*256/360+frandom(64.5, 38.7), frandom(-12.0, 12.0), 0, 0);
		A_QuakeEx(1, 1, 1, 20, 0, 1000, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 2000);
		A_CustomMissile("ArbiterTracer", 52, -25, frandom(-3.5, 3.5), CMF_OFFSETPITCH, frandom(-2.0, 2.0));
		A_CustomMissile("ArbiterTracer", 52, 0, frandom(-3.5, 3.5), CMF_OFFSETPITCH, frandom(-2.0, 2.0));
		A_CustomMissile("ArbiterTracer", 52, 25, frandom(-3.5, 3.5), CMF_OFFSETPITCH, frandom(-2.0, 2.0));
	}
	MS23 E 2 A_MonsterRefire(128, "MissileStop")
	Loop
  MissileStop:
	MS23 P 10
	{
		A_Stop;
		A_StopSound(CHAN_WEAPON);
	}
	Goto See
  ElectricalBolz:
	MS23 P 10 A_FaceTarget
	MS23 PPPPPP 5
	{
		A_FaceTarget;
		A_CustomMissile("OmegaSp00derElectricSpheroid", frandom(100.0, 200.0), frandompick(180.0, -180.0, -100.0, 100.0));
	}
	Goto See
  EvadeShit:
    TNT1 A 0 A_Jump(256, "Evade1", "Evade2")
  Evade1:
    TNT1 A 0 ThrustThing(Angle*256/360+192,20,0,0)
	MS23 ABABABAB 2 Bright A_Chase
    Goto See
  Evade2:
    TNT1 A 0 ThrustThing(Angle*256/360+164,20,0,0)
	MS23 ABABABAB 2 Bright A_Chase
    Goto See
  Pain:
	MS23 AB 2 A_StopSound(CHAN_WEAPON)
	MS23 A 2 A_Pain
	MS23 ABAB 2
	MS23 A 1 A_Jump(80, "EvadeShit")
	Goto See
  Death:
	MS23 A 0
	{
		A_StopSound(CHAN_WEAPON);
		A_Stop;
	}
	MS23 ABABABABABABABABABAB 2
	MS23 P 20
	{
		A_PlaySound("Power/Void", CHAN_BODY, 1.0, 0, ATTN_NONE);
		A_PlaySound("Power/Void", CHAN_6, 1.0, 0, ATTN_NONE);
		A_SpawnItemEx("OmegaSp00derAuraDeath", 0, 0, 0);
	}
	MS23 PPPPPPP 20 A_SpawnItemEx("OmegaSp00derAuraDeath", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	TNT1 A 1
	{
		A_PlaySound("Transcendent/Explode", CHAN_5, 1.0, 0, ATTN_NONE);
		A_PlaySound("Transcendent/Explode", CHAN_BODY, 1.0, 0, ATTN_NONE);
		A_QuakeEx(10, 10, 10, 150, 0, 3000, "misc/quake", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 6000, 0, 1.3, frandom(-1.3, 1.3));
	}
	MS23 A 1
	{
		A_Scream;
		A_Fall;
		A_NoBlocking;
		A_RemoveChildren(1, RMVF_EVERYTHING);
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("SiriusBFGTrail3", 0, 0, 0, random(-62, 62), random(-62, 62), random(-62, 62), random(0, 360), SXF_NOCHECKPOSITION);
		A_Explode(80, 1000, XF_NOTMISSILE, True);
	}
	TNT1 A 200
	Stop
  }
}

ACTOR OmegaSp00derAura
{
  Scale 1.0
  RenderStyle Translucent
  Alpha 0.9
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +NOGRAVITY
  +THRUSPECIES
  Radius 0
  Height 0
  States
  {
  Spawn:
	MS23 AB 1
	{
		If(Alpha >= 0.0000000000001)
		{
			A_SetScale(ScaleX+0.045, ScaleY+0.045);
			A_FadeOut(0.1, 0);
			Return State("");
		}
		Else
		{
			Return State("Null");
		}
	}
	Loop
  }
}

ACTOR OmegaSp00derAuraStencil : OmegaSp00derAura { RenderStyle AddStencil StencilColor "a5 e6 ff" }

ACTOR OmegaSp00derAuraAwaken : OmegaSp00derAura
{
  States
  {
  Spawn:
	MS24 A 1
	{
		If(Alpha >= 0.0000000000001)
		{
			A_SetScale(ScaleX+0.045, ScaleY+0.045);
			A_FadeOut(0.1, 0);
			Return State("");
		}
		Else
		{
			Return State("Null");
		}
	}
	Loop
  }
}

ACTOR OmegaSp00derAuraDeath : OmegaSp00derAura
{
  States
  {
  Spawn:
	MS23 P 3
	{
		If(Alpha >= 0.0000000000001)
		{
			A_SetScale(ScaleX+0.045, ScaleY+0.045);
			A_FadeOut(0.1, 0);
			Return State("");
		}
		Else
		{
			Return State("Null");
		}
	}
	Loop
  }
}


ACTOR OmegaSp00derTeleportFX : OmegaSp00derAura
{
  Scale 3
  Translation "0:255=%[0.00,0.00,0.22]:[0.95,1.75,2.00]"
  RenderStyle Add
  Alpha 1.0
  States
  {
  Spawn:
	SPTP ABCDEF 1 Bright
	Stop
  }
}
ACTOR OmegaSp00derTeleportFX2 : OmegaSp00derTeleportFX
{
  States
  {
  Spawn:
	SPTP FEDCBA 1 Bright
	Stop
  }
}

ACTOR OmegaSp00derElectricSpheroid : CacodemonBall
{
  Speed 0
  Damage (300)
  DamageType "Lunacy"
  Species "Lunacy"
  VSpeed 0
  RenderStyle Add
  Scale 1.5
  +BRIGHT
  +MTHRUSPECIES
  +NOGRAVITY
  +SEEKERMISSILE
  Radius 30
  Height 30
  Translation "0:255=%[0.00,0.00,0.50]:[1.46,2.00,2.00]"
  SeeSound "LIGT001"
  States
  {
  Spawn:
	B0L_ ABCDE 3
	B0L_ F 3 A_SpawnItemEx("OmegaSp00derElectricSpheroidAura")
	B0L_ GHIJKLMNGHIJKLMNGHIJKLMN 1
	Goto Start
  Start:
	B0L_ G 1
	{
		A_FaceTarget;
		A_PlaySound("SUPREMZ", CHAN_BODY, 1.0, 0, 0.45, 0, 1.02600483950);
		A_SetSpeed(50);
		A_Recoil(-Speed);
		A_SeekerMissile(1000, 1000, SMF_LOOK|SMF_PRECISE);
	}
	B0L_ HIJKLMNG 1
	{
		If(Speed >= 100)
		{
		A_SpawnItemEx("OmegaElectricTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		Return State("");
		}
		Else
		{
		A_SetSpeed(Speed+4);
		A_Recoil(-4);
		A_SpawnItemEx("OmegaElectricTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		Return State("");
		}
	}
	Goto Start+1
  Death:
	SFE1 A 1
	SFE1 A 0 
	{
		A_QuakeEx(6, 6, 6, 15, 0, 400, "none", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(4, 4, 4, 25, 0, 600, "none", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(3, 3, 3, 32, 0, 800, "none", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(2, 2, 2, 38, 0, 1000, "none", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 45, 0, 1250, "none", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 35, 0, 1250, "none", QF_SCALEDOWN, 0, 0, 0, 0, 0, 0.7, frandom(0.7, -0.7));
		A_Explode(200, 250, XF_NOTMISSILE, True);
		A_PlaySound("LIGT002", CHAN_BODY, 1.0, 0, 0.45, 0, 0.85);
		A_SpawnItemEx("MultidimensionalZapperExplodeTrailBarrage", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	SFE1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("MultidimensionalZapperAura", 0, 0, 2, random(-2, 2), random(-2, 2), random(-2, 2), 0, SXF_NOCHECKPOSITION)
    SFE1 AABBCCDDEEFF 3 A_Explode(100, 100, 0, 1)
	Stop
  }
}

ACTOR OmegaSp00derElectricSpheroidAura
{
  Radius 0
  Height 0
  RenderStyle Add
  +BRIGHT
  +MTHRUSPECIES
  +THRUSPECIES
  +NOINTERACTION
  +NOGRAVITY
  +NOCLIP
  Species "Lunacy"
  Scale 1.5
  Alpha 0.95
  Translation "0:255=%[0.00,0.00,0.50]:[1.46,2.00,2.00]"
  States
  {
  Spawn:
	B0L_ F 1
	{
		If(Alpha >= 0.00001)
		{
		A_SetScale(ScaleX+0.5, ScaleY+0.5);
		A_FadeOut(0.1, 0);
		Return State("");
		}
		Else
		{
		Return State("Null");
		}
	}
	Loop	
  }
}

ACTOR OmegaElectricTrail : OmegaSp00derElectricSpheroidAura
{
  Alpha 1.0
  Translation "0:255=%[0.00,0.00,0.50]:[1.46,2.00,2.00]"
  States
  {
  Spawn:
	B0L_ HIJKLMNG 1
	{
		If(Alpha >= 0.00001)
		{
		A_FadeOut(0.1, 0);
		Return State("");
		}
		Else
		{
		Return State("Null");
		}
	}
	Loop	
  }
}