Class DCY_Calignosity : DCY_HeckBaron
{
  Default
  {
  Translation "160:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "128:159=%[0.00,0.00,0.00]:[0.00,0.75,0.79]", "16:79=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "0:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]";
  Health 3000;
  SeeSound "Calignosity/Sight";
  ActiveSound "Calignosity/Active";
  PainSound "Calignosity/Pain";
  DeathSound "Calignosity/Death";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  Tag "\c[n0]Calignosity\c-";
  Speed 11;
  FastSpeed 21;
  BloodColor "Cyan";
  DamageFactor 0.75;
  BloodType "AndroidNeonFluid";
  DamageFactor "Calignosity", 0.00;
  +MISSILEMORE
  +BOSS
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOINFIGHTING
  +FRIGHTENING
  +NOFEAR
  PainChance 30;
  }
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("Yehawdoo", "DANGERT", "n0");
  }
  
  void A_CalignosityStep()
  {
	A_StartSound("CYBRFOOT", CHAN_BODY);
	A_QuakeEx(0, 0, 2, 8, 0, 500, "", QF_SCALEDOWN|QF_3D);
	A_Chase();
  }
  
  void A_CalignosityStomp()
  {
	A_StartSound("B0SSSMSH", CHAN_BODY, attenuation: 0.45);
	A_QuakeEx(0, 0, 6, 20, 0, 2000, "", flags: QF_SCALEDOWN|QF_3D);
	A_Explode(300, 128, XF_NOTMISSILE, 0, 100, damagetype: "Calignosity");
  }
  
  int calimissile;
  
  States
  {
  Spawn:
	P1SS A 10 A_Look();
	Loop;
  See:
	P1SS AABBB 3 A_Chase();
	P1SS C 3 A_CalignosityStep();
	P1SS CCDDD 3 A_Chase();
	P1SS A 3 A_CalignosityStep();
	Loop;
  Melee:
  Missile:
	P1SS E 10
	{
		if (target && distance3D(target) < 350)
			SetStateLabel("Lunge");
			
		calimissile = 0;
		A_FaceTarget();
	}
	P1SS EE 10 A_FaceTarget();
	P1SS G 3
	{
		for (int i = 15; i > -16; i -= 30)
			A_SpawnProjectile("DCY_CalignosityLaser", 65, i);
		A_FaceTarget();
		A_StartSound("REFLSR", CHAN_WEAPON, attenuation: 0.5);
		calimissile++;
	}
	P1SS E 4
	{
		if (calimissile >= 4)
			SetStateLabel("See");
		
		A_FaceTarget();
	}
	Goto Missile+3;
  Lunge:
	P1SS MMMMMMMMMMMMMMM 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		A_Recoil(-4);
	}
	P1SS M 10 A_Stop();
	P1SS N 5 A_CalignosityStomp();
	P1SS ONNN 5;
	Goto See;
  Pain:
	P1SS I 4;
	P1SS I 4 A_Pain();
	Goto See;
  Death:
	TNT1 A 0;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_CalignosityTrailX : DCY_HexodenicanTrail { Default { StencilColor "Cyan"; } }

Class DCY_CalignosityLaser : CacodemonBall
{
  Default
  {
  DamageFunction (0);
  Speed 50;
  Radius 15;
  Height 11;
  Alpha 0.75;
  RenderStyle "Add";
  SeeSound "";
  DeathSound "";
  Scale 0.05;
  BounceCount 3;
  BounceType "Hexen";
  BounceFactor 1.0;
  +BOUNCEONWALLS
  +NEVERFAST
  +RIPPER
  +THRUACTORS
  +NOEXTREMEDEATH
  +DONTSPLASH
  -RANDOMIZE
  +BRIGHT
  DamageType "Calignosity";
  }
  
  States
  {
  Spawn:
    TNT1 A 1
	{
		A_SpawnItemEx("DCY_CalignosityTrail", frandom(-15.0, 15.0), frandom(-15.0, 15.0), frandom(-15.0, 15.0), flags: SXF_TRANSFERSCALE|SXF_TRANSFERALPHA|SXF_NOCHECKPOSITION);
		
		for (int i = 3; i < 42; i += 9)
			A_SpawnItemEx("DCY_CalignosityTrail", (i*Vel.x)/-35.0, -(i*Vel.y)/-35.0, 2+(i*Vel.z)/-35.0, 0, 0, 0, 0, SXF_ABSOLUTEANGLE);
		
		A_Explode(10, 45, XF_NOTMISSILE, false);
	}
	Loop;
  Death:
	TNT1 A 2;
	Stop;
  }
}

Class DCY_CalignosityTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Scale 1.0;
  Alpha 0.5;
  RenderStyle "Add";
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  +BRIGHT
  +DONTSPLASH
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn:
	C3AL ABCDE 2;
	Stop;
  }
}