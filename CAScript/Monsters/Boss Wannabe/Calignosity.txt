Class DCY_Calignosity : DCY_HeckBaron
{
  Default
  {
  Translation "160:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "128:159=%[0.00,0.00,0.00]:[0.00,0.75,0.79]", "16:79=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "0:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]";
  Health 3000;
  SeeSound "Calignosity/Sight";
  ActiveSound "Calignosity/Active";
  PainSound "Calignosity/Pain";
  DeathSound "Calignosity/Death";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  Tag "\c[n0]Caliginos Megidora\c-";
  DCYMonster.ScientificName "Nebulosum nanctus dominus";
  DCYMonster.GibType "DCY_MechaGiblets";
  Speed 11;
  FastSpeed 21;
  BloodColor "Cyan";
  DamageFactor 0.75;
  BloodType "AndroidNeonFluid";
  DamageFactor "Calignosity", 0.00;
  +MISSILEMORE
  +BOSS
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOINFIGHTING
  +FRIGHTENING
  +NOFEAR
  PainChance 30;
  Mass 1200;
  +DCYMONSTER.DCYBOSS;
  DCYMonster.WD 1300;
  DCYMonster.CanAnnounce true;
  DCYMonster.AnnounceSound "DANGERT";
  DCYMonster.AnnounceColor "n0";
  DCYMonster.AnnounceMessage "You know, things are starting to get... cold and catastrophic around here.";
  }
  
  void A_CalignosityChase()
  {
	 if(calirush)
	 {
	  A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, zvel:frandom(0.7,1.2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	  A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 8, zvel:frandom(1.5,2.5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	  A_SetTics(1);
	  calirush--;
	 }
	DCY_Chase();
  }
  
  
  void A_CalignosityStep()
  {
	if (target && checkSight(target))
	{
    if(random(1,20) == 1)
	 { calirush = random(45,70); }
	}
	
	if(calirush)
	 A_SetTics(1);
	A_StartSound("CYBRFOOT", CHAN_BODY);
	A_QuakeEx(0, 0, 2, 8, 0, 500, "", QF_SCALEDOWN|QF_3D);
	DCY_Chase();
  }
  
  void A_CalignosityStomp()
  {
	A_StartSound("B0SSSMSH", CHAN_BODY, attenuation: 0.45);
	A_QuakeEx(0, 0, 6, 20, 0, 2000, "", flags: QF_SCALEDOWN|QF_3D);
	A_PoundEffect(80, 200);
	A_Explode(300, 128, XF_NOTMISSILE, 0, 100, damagetype: "Calignosity");
  }
  
  void A_CalignosityArm()
  {
  	A_FaceTarget();
	A_StartSound("Calignosity/Arm",10,attenuation: 0.4);
	calimissile = 0;
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o was subjected to an eternal hell of lead.",
		"You got very unlucky meeting Caliginos, %o...",
		"Caliginos towers over your decrepit body.",
		"You actually thought you were invincible?"
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  int calimissile;
  int calirush;
  int calideath;
  
  States
  {
  Spawn:
	P1SS A 10 A_Look();
	Loop;
  See:
	P1SS AABBB 3 A_CalignosityChase();
	P1SS C 3 A_CalignosityStep();
	P1SS CCDDD 3 A_CalignosityChase();
	P1SS A 3 A_CalignosityStep();
	Loop;
  Melee:
  Missile:
    TNT1 A 0 A_Jump(87,"VulcanLaser","OrbitalStrikes");/*"OrbMines");*/
	TNT1 A 0
	{
	if(target && distance3D(target) > 900)
	SetStateLabel("ArmHowitzer");
	/*else
	{
	 switch (random(0, 12))
	 {
	  case 1: Return ResolveState("OrbMines"); break;
	  case 12: Return ResolveState("VulcanLaser"); break;
	 }
	return A_Jump(256,1);*/
	}
	P1SS E 10
	{
		if (target && distance3D(target) < 350)
		{
			A_StartSound("Calignosity/Glide", pitch: 1.2);
			SetStateLabel("Lunge");
		}
		else
		{
			A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
			A_CalignosityArm();
		}
	}
	P1SS EE 10 A_FaceTarget;
	P1SS G 3
	{
		for (int i = 15; i > -16; i -= 30)
			A_SpawnProjectile("DCY_CalignosityLaser", 65, i,frandom(-10,10),CMF_OFFSETPITCH,frandom(-1,1));
		A_FaceTarget();
		A_StartSound("DCY/HomingLaser",CHAN_WEAPON,CHANF_OVERLAP,attenuation: 0.5);
		calimissile++;
	}
	P1SS E 4
	{
		if(calimissile >= 4)
			SetStateLabel("See");
		A_FaceTarget();
	}
	Goto Missile+5;
  VulcanLaser:
    P1SS E 30
	{
	 A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	 A_CalignosityArm();
	}
	P1SS PPPPP 3
	{
	if(calimissile > random(5,30))
	  SetStateLabel("See");

	A_FaceTarget();
	A_StartSound("DCY/ChargedLaser",11,attenuation:0.65);
	A_SpawnProjectile("DCY_CalignosityBullet",36,12,random(-3,3),CMF_OFFSETPITCH,frandom(-2,2));
	calimissile++;
	}
	P1SS E 4
	{
	if(random(1,3) == 1)
	  A_SetTics(randompick(2, 8));
	  
	A_FaceTarget();
	}
	Goto VulcanLaser+1;
  ArmHowitzer:
   P1SS E 30
	{
	 A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	 A_CalignosityArm();
	}
	P1SS E 1 A_FaceTarget();	   
	P1SS Q 5
	{
	A_FaceTarget();
	A_StartSound("DarkLord/Bomb/Fire",CHAN_WEAPON,CHANF_OVERLAP,attenuation: 0.5);
	A_SpawnProjectile("DCY_CalignosityHowzShell",36,-14,frandom(-0.3,0.3),CMF_OFFSETPITCH,frandom(-0.5,0.5));
	calimissile++;
	}
	P1SS E 15 
	{
    if(calimissile > random(3,5))
	  SetStateLabel("See");
	A_FaceTarget();
	}
	Goto ArmHowitzer+1;
  OrbitalStrikes:
   P1SS N 40
	{
	 A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
  	A_FaceTarget();
	A_StartSound("Calignosity/Arm",10,attenuation: 0.4,pitch:0.75);
	calimissile = 0;
	}
   P1SS N 4
   {
   if(calimissile > random(6,10))
     SetStateLabel("See");
   if(random(1,4) == 1)
     A_SetTics(2);
   A_SpawnProjectile("DCY_CalignosityOrbitalRainSpawner",36,frandom(-20,20),frandom(-180,180),CMF_AIMDIRECTION);
   calimissile++;
   }
   P1SS N 6
   {
   if(random(1,4) == 1)
     A_SetTics(3);
   A_FaceTarget();
   }
   Goto OrbitalStrikes+1;
  Lunge:
	P1SS MMMMMMMMMMMMMMM 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		A_Recoil(-4);
	}
	P1SS M 15 A_Stop();
	P1SS N 5 A_CalignosityStomp();
	P1SS ONNN 5;
	Goto See;
  Pain:
	P1SS I 4;
	P1SS I 4 A_Pain();
	Goto See;
  XDeath:
  Death:
	P1SS I 4;
	P1SS I 4 A_Pain();
	P1SS AAAA 10
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-40, 40), frandom(-40, 40), frandom(10, 40), flags: SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR, failchance: 100);
	}
	P1SS N 1
	{
	 A_SpawnItemEx("DCY_CalignosityTrailX", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
  	A_FaceTarget();
	A_StartSound("Calignosity/Arm",10,attenuation: 0.4,pitch:0.75);
	calimissile = 0;
	}
	P1SS NNNNNNNN 5
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-40, 40), frandom(-40, 40), frandom(10, 40), flags: SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR, failchance: 100);
	}
	P1SS N 0
	{
		A_StartSound("Calignosity/Orbital",12,attenuation: 0.4);
		A_StartSound("ROBOEXP2",13,attenuation: 0.4);
		A_StartSound("ROBORAI2",14,attenuation: 0.8);
	}
	P1SX AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_FaceTarget();
		A_QuakeEx(2, 2, 3, 30, 0, 700, "", QF_SCALEDOWN|QF_3D);
		A_SpawnItemEx("DCY_Explosion_1_Small",0,0,random(5,32),random(-3,3),random(-3,3),random(-3,3),random(0, 360),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-40, 40), frandom(-40, 40), frandom(10, 40), flags: SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR, failchance: 100);
		A_SpawnItemEx("DCY_CalignosityOrbitalRain", flags: SXF_NOCHECKPOSITION);
	}
	Goto DeathL;
  DeathL:
	P1SX A 5 
	{
	A_ScreamandUnblock();
	calideath = 10;
	}
	P1SX A 7 Bright
	{
	tics = frandom(10, 2);
	
	if(calideath < 0)
	  SetStateLabel("DeathEnd");
	
	for (int i = random(5, 1); i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-40, 40), frandom(-40, 40), frandom(10, 40), flags: SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR, failchance: 100);
	A_SpawnItemEx("DCY_CalignosityTrailX",zofs:8,xvel:frandom(-1.5,1.5),yvel:frandom(-1.5,1.5),zvel:frandom(0.8,2.2),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	A_SpawnItemEx("DCY_Explosion_1_Small",frandom(-10, 10), frandom(-10, 10), random(5,32),random(-3,3),random(-3,3),random(-3,3),random(0, 360),SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR);
	A_StartSound("Calignosity/Damage",pitch:frandom(0.75,1.15));
	calideath--;
	}
	Goto DeathL+1;
  DeathEnd:
	P1SX A 5 Bright;
	P1SX B 15 Bright;
	P1SX B 1 Bright
	{
		A_SpawnItemEx("DCY_GibletsLava", zofs: 30, xvel: vel.x, yvel: vel.y, zvel: vel.z, flags: SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
		A_StartSound("BigExplod", CHAN_BODY, attenuation: 0.5);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Biggie", random(-10, 10), random(-10, 10), random(30, 70), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR);
	}
	P1SX C 6 Bright A_NoBlocking();
	P1SX DEFGH 5 Bright
	{
		DCY_Gibs(false);
		for (int i = random(1, 2); i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", random(-10, 10), random(-10, 10), random(30, 70), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR);
	}
	P1SX I -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_CalignosityTrailX : DCY_HexodenicanTrail { Default { StencilColor "Cyan"; } }

Class DCY_CalignosityLaser : DCY_MeatballElecball
{
  Default
  {
  DamageFunction 30;
  Speed 30;
  Radius 15;
  Height 11;
  Alpha 0.75;
  RenderStyle "Add";
  SeeSound "";
  DeathSound "";
  Scale 0.05;
  +SEEKERMISSILE
  +NEVERFAST
  +NOEXTREMEDEATH
  +DONTSPLASH
  -RANDOMIZE
  +BRIGHT
  DamageType "Calignosity";
  }
  
  override void Tick()
  {
   Super.Tick();
   {
    if(!IsFrozen())
	{
		A_SpawnItemEx("DCY_CalignosityTrail", frandom(-15.0, 15.0), frandom(-15.0, 15.0), frandom(-15.0, 15.0), flags: SXF_TRANSFERSCALE|SXF_TRANSFERALPHA|SXF_NOCHECKPOSITION);
		
		for (int i = 3; i < 42; i += 9)
			A_SpawnItemEx("DCY_CalignosityTrail", (i*Vel.x)/-35.0, -(i*Vel.y)/-35.0, 2+(i*Vel.z)/-35.0, 0, 0, 0, 0, SXF_ABSOLUTEANGLE);
	}
   }
  }
  
  States
  {
  Spawn:
    TNT1 A 1;
	Loop;
  Death:
	TNT1 A 2;
	Stop;
  }
}

Class DCY_CalignosityTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Scale 1.0;
  Alpha 0.5;
  RenderStyle "Add";
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  +BRIGHT
  +DONTSPLASH
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn:
	C3AL ABCDE 2;
	Stop;
  }
}

Class DCY_CalignosityBullet : DCY_UltracerestLaser
{
 Default
 {
 SeeSound "DCY/ChargedLaser";
 DeathSound "";
 DamageType "Calignosity";
 Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
 DCY_FastProjectile.CT true;
 Scale 0.65;
 DamageFunction 25;
 }
}

Class DCY_CalignosityHowzShell : DCY_HomingMissile
{
  Default
  {
  Radius 8;
  Height 5;
  Speed 35;
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,2.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.46,0.46,0.46]";
  DamageFunction 50;
  Scale 0.80;
  DeathSound "";
  SeeSound "ROBOBOMB";
  }
  
  
  States
  {
  Spawn:
	TMIS A 0 NoDelay A_StartSound("skeleton/seeker", CHAN_6, CHANF_LOOPING, 0.85, pitch: 3.4567);
	TMIS A 2 Bright
	{
		A_SpawnItemEx("DCY_CalignosityTrailX", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 3; i < 40; i += 15)
			{
				A_SpawnParticleEx(0x42eff5, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 10, 25, angle, frandom(-0.1, 0.1) + (i*Vel.x)/-35.0, frandom(-0.1, 0.1) + (i*Vel.y)/-35.0, frandom(-0.1, 0.1) + (i*Vel.z)/-35.0, frandom(-0.1, 0.1) + (Vel.x / 3), frandom(-0.1, 0.1) + (Vel.y / 3), frandom(-0.1, 0.1) + (Vel.z / 3), startalphaf: 1.0, fadestepf: 0.025, sizestep: -2, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
				A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 50, 20, angle, frandom(-1, 1) - 3  + (i*Vel.x)/-35.0, frandom(-1, 1) + (i*Vel.y)/-35.0, frandom(-1, 1) + (i*Vel.z)/-35.0, frandom(-.1, .1), frandom(-.1, .1), frandom(-.1, .1), startalphaf: 0.3, fadestepf: 0.01, sizestep: 0.01, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
			}
		}
	}
	Goto Spawn+1;
  Death:
	TMIS B 0 Bright;
  	TMIS B 6 Bright
	{
		A_Scream();
		A_StopSound(CHAN_6);
		A_QuakeEx(2, 2, 2, 17, 0, 250, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 500);
		A_SetTranslucent(1.0, 1);
		A_RadiusThrust(3500, 140, RTF_NOIMPACTDAMAGE);
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	TNT1 A 0 A_StartSound("Calignosity/Damage",pitch:frandom(0.75,1.15));
	TMIS CD 6 Bright;
	Stop;
  }
}

Class DCY_CalignosityOrbitalRainSpawner : DCY_Projectile
{
 Default
 {
 Speed 15;
 +NOINTERACTION
 +THRUACTORS
 }
 int timer;
 
 override void PostBeginPlay()
 {
 Super.PostBeginPlay();
 timer = random(3,6);
 }
 States
 {
 Spawn:
 TNT1 A 1
 {
 if(timer < 0)
  SetStateLabel("Death");
 timer--;
 }
 Loop;
 Death:
  TNT1 A 0 A_StartSound("Calignosity/Orbital",12,attenuation: 0.4);
  TNT1 A 0 A_SpawnProjectile("DCY_CalignosityOrbitalRain",flags:CMF_TRACKOWNER);
  TNT1 A 10 A_SpawnProjectile("DCY_CalignosityOrbitalRain2",32,random(-10, 10), random(0, 360), CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
  Stop;
 }
}

Class DCY_CalignosityOrbitalRain : DCY_Projectile
{
 Default
  {
  YScale 512.256;
  XScale 2.5;
  Speed 0;
  RenderStyle "Add";
  DamageType "Lunacy";
  Alpha 1.0;
  +OLDRADIUSDMG
  +BRIGHT
  +NOCLIP
  +THRUACTORS
  +FLOORHUGGER
  +SEEKERMISSILE
  +FORCERADIUSDMG
  +FORCEYBILLBOARD
  +NOBLOCKMAP
  +BOUNCEONWALLS
  BounceFactor 1.0;
  BounceType "Hexen";
  Radius 45;
  DamageFunction 0;
  Translation "33:255=@59[0,255,255]";
  }
  
  int loop;
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   loop = 10;
  }
  
  States
  {
  Spawn:
    L2ZO B 1
	{
		A_SetScale(frandom(1.6, 2.1), 512.256);
		A_SpawnItemEx("DCY_AuteurBeam", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
		A_QuakeEx(0, 0, 0, 10, 0, 2000, "", QF_SCALEDOWN|QF_3D);
		loop--;
		if (loop <= 0) SetStateLabel("Fade");
	}
	Loop;
  Fade:
	L2ZO BBBBBBBBBBBB 1
	{
		A_FadeOut(0.1);
		A_SetAngle(Angle+13);
		A_SetScale(Scale.X-0.1, 256.256);
	}
	Stop;
  }
}

Class DCY_CalignosityOrbitalRain2 : DCY_Projectile //aaaAAAAAAAA
{
 Default
 {
  Damage 0;
  Speed 2;
  +THRUACTORS
  Radius 8;
  Height 11;
  DeathSound "";
  +SKYEXPLODE;
  }
  
  States
  {
  Spawn:
    TNT1 AAAAAAAAAAAAAAAAAAAA 1 A_ChangeVelocity(0, 0, 5);
	Goto Death;
  Death:
	TNT1 A 10 A_Stop();
	TNT1 A 10
	{
		for (int i = random(1, 5); i > 0; i--)
			A_SpawnProjectile("DCY_CalignosityOrbitalRain3", -40, random(-10, 10), random(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
	}
	Stop;
  }
}
 

Class DCY_CalignosityOrbitalRain3 : DCY_BlueVoidMissileRain //copying state code here for the sake of replacing the actors                                                       
{             
 Default
 {
 +SOLID;
 Speed 25;
 Translation "0:255=%[0.00,0.00,0.00]:[0.00,2.00,2.00]";
 Scale 1.7;
 }
 States
 {
  Spawn:
    TNT1 A 0;
    TNT1 A 0 ThrustThing(random(0, 360), random(-1, 1), 0, 1);
    TNT1 AAAAAAAAAAAA 0 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 A 0 
    {
		tics = flength[random(0,7)];
		A_Chase(null, null, CHF_NOPLAYACTIVE);
    }
	Goto Spawn1;
  Spawn1:
    TNT1 A 0 ThrustThing(random(0, 360), random(-1, 1), 0, 1);
    TNT1 A 0 
    {
		if(loop >= 2) { Return ResolveState("Stahp"); }
		loop += 1;
		ThrustThing(random(0, 360), random(-1, 1), 0, 1);
		Return ResolveState(null);
    }
    TNT1 AAAAAAAAAAAA 0 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 AAAAAAAAA 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 A 0 ThrustThing(random(0, 360), random(-1, 1), 0, 1);
    TNT1 A 0
	{
		A_SpawnItemEx("DCY_FallingCaligTorpedo", zofs: -10, zvel: -30, angle: random(-88, 88), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_TorpedoAlert", zofs: -10, zvel: -99999, angle: random(-88, 88), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
	}
    Goto Spawn1+1;
  Death:
  Stahp:
    TNT1 A 5 A_Die();
    Stop;
  }
}

Class DCY_FallingCaligTorpedo : DCY_TransmuterTorpedo
{
 Default
 {
 Translation "0:255=%[0.00,0.00,0.00]:[0.00,2.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.46,0.46,0.46]";
 DeathSound "";
 }
 States
 {
  Spawn:
    HMIS B 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 3; i < 40; i += 15)
			{
				A_SpawnParticleEx(0x42eff5, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 10, 25, angle, frandom(-0.1, 0.1) + (i*Vel.x)/-35.0, frandom(-0.1, 0.1) + (i*Vel.y)/-35.0, frandom(-0.1, 0.1) + (i*Vel.z)/-35.0, frandom(-0.1, 0.1) + (Vel.x / 3), frandom(-0.1, 0.1) + (Vel.y / 3), frandom(-0.1, 0.1) + (Vel.z / 3), startalphaf: 1.0, fadestepf: 0.025, sizestep: -2, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
				A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 50, 20, angle, frandom(-1, 1) - 3  + (i*Vel.x)/-35.0, frandom(-1, 1) + (i*Vel.y)/-35.0, frandom(-1, 1) + (i*Vel.z)/-35.0, frandom(-.1, .1), frandom(-.1, .1), frandom(-.1, .1), startalphaf: 0.3, fadestepf: 0.01, sizestep: 0.01, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
			}
		}
		
		if ((level.time % random(1, 9)) == 0) A_SpawnParticleEx(0x174ee6, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
		A_SpawnItemEx("DCY_CalignosityTrailX", zofs: -5, flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	TMIS B 0 Bright;
  	TMIS B 6 Bright
	{
		A_Scream();
		A_StopSound(CHAN_6);
		A_StartSound("Calignosity/Damage",pitch:frandom(0.55,1.15));
		A_QuakeEx(2, 2, 2, 17, 0, 250, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 500);
		A_SetTranslucent(1.0, 1);
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 5, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 5, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		A_RadiusThrust(4500, 140, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
		A_RadiusThrust(110, 140, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE|RTF_THRUSTZ);
			
		for (int i = 0; i < 360; i += random(25, 65))
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnParticleEx(0xaaaaaa, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE, random(90, 95), 60, i, zoff: 5, velx: frandom(1, 3), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelz: 0.01, startalphaf: 0.35, fadestepf: 0.0075, sizestep: 1);
			A_SpawnParticleEx(0x666666, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_RELATIVE, random(90, 95), 60, i, zoff: 5, velx: frandom(1, 3), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelz: 0.01, startalphaf: 0.35, fadestepf: 0.0075, sizestep: 1);
		}
	}
	TMIS CD 6 Bright;
	Stop;
 }
}