Class DCY_Ultracerest : DCY_Impe //stronk
{
  Default
  {
  Health 1400;
  Tag "\c[l5]Ultracerest\c-";
  Translation "168:173=[255,255,255]:[145,255,255]", "174:180=[145,255,255]:[17,117,238]", "181:191=[17,117,238]:[0,0,64]", "224:231=[255,255,255]:[145,255,255]", "208:216=[255,255,255]:[145,255,255]", "217:223=[145,255,255]:[17,117,238]", "232:235=%[0.00,0.00,0.00]:[0.13,0.92,1.87]", "236:239=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[145,255,255]:[17,117,238]", "16:23=[255,255,255]:[145,255,255]", "24:31=[145,255,255]:[17,117,238]", "32:47=[17,117,238]:[0,0,64]", "192:195=[255,255,255]:[255,176,248]", "196:201=[247,140,255]:[128,0,255]", "202:207=[128,0,255]:[64,0,128]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[0.59,1.34,1.95]", "1:2=%[0.00,0.00,0.00]:[0.69,0.69,0.69]";
  Speed 5;
  FastSpeed 7;
  Obituary "$o was inferior to Ultracerest.";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  +MISSILEEVENMORE
  +NOTARGET
  +NOINFIGHTING
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +FORCERADIUSDMG
  +NOBLOODDECALS
  +NEVERRESPAWN
  +NORADIUSDMG
  +DONTTHRUST
  +BOSS
  DamageFactor "Fire", 0.1;
  DamageFactor "Ice", 0.3;
  DamageFactor "Lunacy", 0.00;
  DamageFactor "Cosmical", 0.5;
  DamageFactor "Neonic", 0.35;
  PainChance 15;
  DropItem "Megasphere";
  DropItem "DCY_Palisade";
  Species "AssemblyoftheUnseen";
  SeeSound "Ultracerest/Sight";
  ActiveSound "Ultracerest/Active";
  PainSound "Ultracerest/Pain";
  DeathSound "Ultracerest/Death";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  }
  
  int dash;
  int speen;
  
  void A_Ultralaser()
  {
	A_SpawnProjectile("DCY_UltracerestLaser", 34, -5, frandom(-3.0, 3.0));
	A_StartSound("Android/LaserX", CHAN_WEAPON, attenuation: 0.5);
	A_FaceTarget();
  }
  
  void A_Ultrarocket()
  {
	A_SpawnProjectile("DCY_UltracerestRocket", 34, 5, frandom(-3.0, 3.0));
	A_StartSound("ROBOMISL", CHAN_WEAPON, attenuation: 0.5);
	A_FaceTarget();
  }
  
  int attack;
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	ThingAnnounce("???: Sup, fuckface!", "Warp/Choir", "l5");
  }
  
  States
  {
  Spawn:
	ANDV A 10 A_Look();
	Loop;
  See:
	ANDV AAAABBBB 3 A_Chase();
	ANDV CCCCDDDD 3 A_Chase();
	Loop;
  Missile:
	ANDV F 1;
	ANDV F 5 A_FaceTarget();
	ANDV F 0 A_JumpIf(target && Distance3D(target) < 370, "YEEEEEEEEEEEET");
	Goto MissileContinue;
  MissileContinue:
	ANDV F 0 A_Jump(128, "Missile2");
	ANDV F 0 A_Jump(80, "Railguns");
	ANDV F 0 A_Jump(70, "Speen");
	ANDV F 0 A_Jump(65, "PowerBallIKThisIsAStupidJokeHelpPls");
	Goto Lasers;
  Lasers:
	ANDV F 3
	{
		attack = 0;
		A_FaceTarget();
	}
	ANDV E 7 A_FaceTarget();
	ANDV W 2
	{
		attack++;
		A_Ultralaser();
	}
	ANDV E 2 A_FaceTarget();
	ANDV E 0 A_JumpIf(attack > 7, "See");
	Goto Lasers+2;
  Missile2:
	ANDV F 3
	{
		attack = 0;
		A_FaceTarget();
	}
	ANDV G 7 A_FaceTarget();
	ANDV V 2
	{
		attack++;
		A_Ultrarocket();
	}
	ANDV G 6 A_FaceTarget();
	ANDV G 0 A_JumpIf(attack >= 5, "See");
	Goto Missile2+2;
  Railguns:
	ANDV F 5;
	ANDV E 6 A_FaceTarget();
	ANDV W 4
	{
		A_StartSound("UCLASER", CHAN_WEAPON, attenuation: 0.35);
		A_CustomRailgun(100, 0, "", "", RGF_FULLBRIGHT|RGF_SILENT, 0, 0, "DCY_UltracerestPuff", 0, 0, 0, 0, 40, 0, "DCY_VirtuosoTrail");
		A_FaceTarget(1.2, 1.2);
	}
	ANDV FGGG 4 A_FaceTarget(1.3, 1.3);
	ANDV V 4
	{
		A_StartSound("UCLASER", CHAN_WEAPON, attenuation: 0.35);
		A_CustomRailgun(100, 0, "", "", RGF_FULLBRIGHT|RGF_SILENT, 0, 0, "DCY_UltracerestPuff", 0, 0, 0, 0, 40, 0, "DCY_VirtuosoTrail");
	}
	ANDV FA 4;
	Goto See;
  Speen:
	ANDV F 3
	{
		speen = 180;
		A_FaceTarget();
	}
	ANDV E 7 A_FaceTarget();
	ANDV E 1;
	ANDV W 1
	{
		A_SetAngle(Angle+30);
		A_SpawnProjectile("DCY_UltracerestLaser", 34, -5, frandom(-3.0, 3.0), CMF_AIMDIRECTION);
		A_StartSound("Android/LaserX", CHAN_WEAPON, attenuation: 0.5);
		speen -= 5;
			
		if (speen <= 0) SetStateLabel("CooldownSpeen");
	}
	Goto Speen+2;
  CooldownSpeen:
	ANDV E 6;
	ANDV A 5 A_FaceTarget();
	Goto See;
  PowerBallIKThisIsAStupidJokeHelpPls:
    ANDV F 10 A_FaceTarget();
	ANDV E 10 A_FaceTarget();
	ANDV E 1
	{
		A_StartSound("ULCRCHRG", CHAN_WEAPON, attenuation: 0.5);
		A_FaceTarget();
	}
	ANDV EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	ANDV E 1
	{
		A_StartSound("ULCRCHR2", CHAN_WEAPON, attenuation: 0.5);
		A_FaceTarget();
		A_SpawnItemEx("DCY_UCAura", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSPRITEFRAME);
	}
	ANDV EEEEEEEEEEE 1 A_FaceTarget();
	ANDV E 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_UCAura", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSPRITEFRAME);
	}
	ANDV EEEEEEEEEEE 1 A_FaceTarget();
	ANDV E 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_UCAura", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSPRITEFRAME);
	}
	ANDV W 40
	{
		A_StartSound("ULCRFIRE", CHAN_WEAPON, attenuation: 0.5);
		A_FaceTarget();
		A_SpawnItemEx("DCY_UCAura", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSPRITEFRAME);
		A_QuakeEx(3, 3, 3, 20, 0, 666, "", QF_SCALEDOWN, 0, 0, 0, 1288);
		A_SpawnProjectile("DCY_UltracerestPowerBall", 35, 0, 0);
	}
	ANDV EF 5;
	Goto See;
  YEEEEEEEEEEEET:
	ANDV A 0
	{
		A_Stop();
		Return A_Jump(128, "MissileContinue");
	}
	Goto YEET;
  YEET:
	ANDV F 10
	{
		A_Stop();
		dash = 0;
		A_FaceTarget();
	}
	ANDV E 0 A_JumpIf(dash >= 30, "See");
	ANDV E 0 A_JumpIf(target && Distance3D(target) < 90, "DashExplode");
	ANDV E 1
	{
		A_Recoil(-3);
		A_FaceTarget(1.3, 1.3);
		dash++;
		A_SpawnItemEx("DCY_UCAura", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	}
	Goto YEET+1;
  Melee:
  DashExplode:
	ANDV E 1;
	ANDV E 10
	{
		A_SpawnItemEx("DCY_OverdemoniacPuff", 0, 0, 40);
		A_SpawnItemEx("DCY_OverdemoniacExplosion", 0, 0, 40);
		
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_AndroidExplosion", random(-20, 20), random(-20, 20), random(10, 60), random(-10, 10), random(-10, 10), random(-10, 10), frandom(360, 0), SXF_NOCHECKPOSITION);
		
		A_Explode(200, 111, XF_NOTMISSILE, True);
		
		for (int i = 10; i < 14; i++)
			A_StartSound("DASH/EXPLODE", CHAN_6, attenuation: 0.75);
			
		A_QuakeEx(4, 4, 4, 23, 0, 600, "", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 900);
	}
	ANDV A 10;
	Goto See;
  Pain:
	ANDV H 4;
	ANDV H 4 A_Pain();
	Goto See;
  Death:
  XDeath:
	ANDV H 1;
	ANDV H 30 A_ScreamAndUnblock();
	ANDV HHHHHHHHH 4
	{
		A_StartSound("UCKABEWM", CHAN_BODY);
		A_SpawnItemEx("DCY_AndroidExplosion", random(-20, 20), random(-20, 20), random(10, 60), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
	ANDV H 1
	{
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", random(-20, 20), random(-20, 20), random(10, 60), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_StartSound("ENIGEXPL", CHAN_WEAPON, attenuation: 0.25);
		/*A_StartSound("DASH/EXPLODE", CHAN_WEAPON, attenuation: 0.25);
		A_StartSound("Android/BossExplosion", CHAN_7, attenuation: 0.25);
		A_StartSound("Android/BossExplosion", CHAN_6, attenuation: 0.25);
		A_StartSound("Android/BossExplosion", CHAN_5, attenuation: 0.25);*/
	}
	ANDV NOPQRST 6;
	ANDV U -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_UCAura : DCY_HexodenicanTrail { Default { StencilColor "02 73 C4"; +NOTIMEFREEZE; Scale 1; } }

Class DCY_UltracerestRocket : DCY_OverhauledRocket
{
  Default
  {
  DamageFunction 8;
  SeeSound "ROBOMISL";
  DeathSound "UCKABEWM";
  Speed 19;
  FastSpeed 25;
  DamageType "ExtraExplosion";
  +EXTREMEDEATH
  +BRIGHT
  -ROCKETTRAIL
  Translation "168:173=[255,255,255]:[128,255,255]", "0:0=[0,0,0]:[255,255,255]", "174:180=[121,255,255]:[0,128,255]", "181:191=[0,115,230]:[0,0,64]", "224:231=[255,255,255]:[128,255,255]", "208:216=[255,255,255]:[128,255,255]", "217:223=[119,255,255]:[0,128,192]", "232:235=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[128,255,255]:[0,0,128]", "16:23=[255,255,255]:[128,255,255]", "24:31=[111,255,255]:[0,128,255]", "32:47=[0,124,249]:[0,0,64]", "192:195=[255,255,255]:[128,255,255]", "196:201=[106,255,255]:[0,128,255]", "202:207=[0,116,232]:[0,0,64]", "112:119=[0,128,255]:[0,0,128]", "120:127=[0,0,119]:[0,0,64]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn:
	NMSL A 1 Bright A_SpawnItemEx("DCY_TransmuterFlare", frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-2.5, 2.5), flags: SXF_NOCHECKPOSITION);
	Loop;
  Death:
	TNT1 A 1;
	TNT1 A 1
	{
		A_Explode(random(1, 20), 100, XF_NOTMISSILE, 0);
		A_SpawnItemEx("DCY_AndroidExplosion");
	}
	Stop;
  }
}

Class DCY_UltracerestPowerball : DCY_MeatballElecball
{
  Default
  {
  Speed 40;
  FastSpeed 40;
  +NEVERFAST
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  SeeSound "";
  DeathSound "StarProjectile/Explode";
  Scale 1.25;
  Radius 25;
  Height 25;
  +BRIGHT
  +EXTREMEDEATH
  +PIERCEARMOR
  Damage 20;
  }
  
  States
  {
  Spawn:
	SPIR P 1
	{
		A_SpawnItemEx("DCY_Arc", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 140);
		roll = frandom(0, 360);
		for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x0341fc, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 80, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.0215, sizestep: 0.55);
		A_SpawnItemEx("DCY_ProjectileBaseTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Loop;
  Death:
    SPIR P 1;
	SPIR P 1
	{
		A_QuakeEx(5, 5, 5, 60, 0, 777, "", SXF_NOCHECKPOSITION, 0, 0, 0, 1566);
		A_SpawnItemEx("DCY_UltracerestPuff", flags: SXF_NOCHECKPOSITION);
		A_Explode(200, 200, XF_NOTMISSILE);
		for (int i = 8; i > 0; i--) A_SpawnItemEx("DCY_AndroidExplosionBiggie", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-6, 6), frandom(-6, 6), frandom(-6, 6), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_OverdemoniacPuff", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_CWFXDeathTrail2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		for (int i = 20; i > 0; i -= random(1, 3)) A_SpawnParticleEx(0x0341fc, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 300, random(100, 300), 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0015, sizestep: 1.525);
		for (int i = 12; i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-10, 10), frandom(-10, 10), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE, 20);
	}
	TNT1 A 60;
	Stop;
  }
}

Class DCY_UltracerestLaser : DCY_KaiserLaser
{
  Default
  {
  SeeSound "Android/Laser";
  DeathSound "";
  Translation "0:255=%[0.00,0.00,0.00]:[0.46,0.76,1.82]";
  Damage 5;
  Speed 50;
  FastSpeed 60;
  Scale 0.6;
  }
}