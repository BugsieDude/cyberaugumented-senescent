Class DCY_Visitant : DCY_CacoParent
{
  Default
  {
  Health 2000;
  Tag "\c[x1]The Visitant\c-";
  Speed 8;
  FastSpeed 12;
  +NOBLOOD
  Translation "0:255=%[0.27,0.00,0.00]:[2.00,1.30,0.98]";
  SeeSound "Visitant/Sight";
  ActiveSound "Visitant/Active";
  PainSound "Visitant/Pain";
  DeathSound "Visitant/Death";
  TeleFogSourceType "DCY_BossTeleport";
  TeleFogDestType "DCY_BossTeleport";
  DamageType "Visitant";
  DamageFactor "Visitant", 0;
  DamageFactor 0.65;
  PainChance 10;
  Obituary "FACILIS DECENSUS AVERNO";
  DCYMonster.ScientificName "NO DATA";
  +DCYMONSTER.DCYSUPERBOSS;
  DCYMonster.WD 2000;
  DCYMonster.CanAnnounce true;
  DCYMonster.AnnounceSound "ORBWARN0";
  DCYMonster.AnnounceColor "x1";
  DCYMonster.AnnounceMessage "Someone petitioned to put a huge fucking orb";
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_SpawnItemEx("DCY_VisitantBody", flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);
  }
  
  override void Tick()
  {
	Super.Tick();
	
	if (!isFrozen() && health > 0) vel /= 1.1;
	If (!IsFrozen() && !sv_effectchoker && DCY_EffectDistance(sfxdistance))
	{
		A_SpawnItemEx("DCY_VisitantGlow", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0) + 30, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION, 100);
		A_SpawnItemEx("DCY_VisitantElectricity", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0) + 30, flags: SXF_NOCHECKPOSITION, 250);
		A_SpawnItemEx("DCY_Arc", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0) + 30, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 200);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0) + 30, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 240);
	}
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o met death at the sight of the soul of hell.",
		"You saw something horrible."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void A_VisitantChase()
  {
	A_Chase("Missile", "Missile", CHF_DONTMOVE);
	vel.x += frandom(-2, 2);
	vel.y += frandom(-2, 2);
	vel.z += frandom(-2, 2);
	angle += frandom(-10, 10);
	A_Recoil(-(speed / 20));
  }
  
  States
  {
  Spawn:
	TNT1 A 10 A_Look();
	Loop;
  See:
	TNT1 A 1
	{
		A_VisitantChase();
		Return A_Jump(2, "Teleporting", "Dash");
	}
	Loop;
  Dash:
	TNT1 A 1
	{
		A_SpawnItemEx("DCY_VisitantGlow", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0) + 30, frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION);
		for (int i = 3; i > 0; i--) A_VisitantChase();
		Return A_Jump(2, "See", "Teleporting");
	}
	Loop;
  Teleporting:
	TNT1 A 10;
	TNT1 A 1 A_SpawnItemEx("DCY_BionicRing", zofs: 30, flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
	TNT1 A 0
	{
		for (int i = 300; i > 0; i--)
			A_Wander();
	}
	TNT1 A 2
	{
		A_StartSound("Visitant/Teleport", attenuation: 0.45);
		A_SpawnItemEx("DCY_BionicRing", zofs: 30, flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
	}
	Goto See;
  Pain:
	TNT1 A 4;
	TNT1 A 4
	{
		A_Pain();
		Return A_Jump(64, "Teleporting");
	}
	Goto See;
  Missile:
  FastBolt:
	TNT1 A 20;
	TNT1 A 10
	{
		A_StartSound("Visitant/Attack", CHAN_VOICE, attenuation: 0.5);
		A_FaceTarget();
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_CustomRailgun(0, 0, "", "", RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT|RGF_NORANDOMPUFFZ, 1, sparsity: 40, spawnclass: "DCY_GehennaTrack");
		A_FaceTarget();
	}
	TNT1 AAAAA 2 A_FaceTarget(1.5, 1.5);
	TNT1 A 20
	{
		A_FaceTarget(1.5, 1.5);
		A_Stop();
		A_StartSound("LightningBewm", attenuation: 0.5);
		A_QuakeEx(3, 3, 3, 20, 0, 1000, "", QF_SCALEDOWN|QF_3D);
		A_SpawnProjectile("DCY_VisitantBolt", 30, 0, 0, CMF_AIMDIRECTION, pitch);
	}
	TNT1 A 1 A_Stop();
	Goto See;
  Death:
	TNT1 A 1;
	TNT1 A 100 A_Scream();
	TNT1 A 0
	{
		for (int i = 0; i < 360; i += 30)
		{
			A_SpawnItemEx("DCY_VisitantGasSpawner", zofs: 30, xvel: 60, angle: i);
			A_SpawnItemEx("DCY_Explosion_1", zofs: 30, xvel: frandom(17, 20), angle: i);
			A_SpawnItemEx("DCY_Explosion_1", zofs: 30, xvel: frandom(17, 20), angle: i * 2);
		}
		
		A_SpawnItemEx("DCY_VisitantPuff", zofs: 30);
		
		A_SpawnItemEx("DCY_Explosion_1_Biggie", zofs: 30);
	}
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_VisitantBolt : FastProjectile
{
  Default
  {
  Damage 60;
  Speed 200;
  +NEVERFAST;
  +ROLLSPRITE
  Scale 0.425;
  RenderStyle "Add";
  MissileType "DCY_VisitantBoltTrail";
  MissileHeight 5;
  DamageType "Visitant";
  Radius 10;
  Height 10;
  }
  
  States
  {
  Spawn:
	LFX2 KLMNO 1 A_SetRoll(frandom(0, 360));
	Loop;
  Death:
	TNT1 A 1 A_SpawnItemEx("DCY_VisitantPuff");
	Stop;
  }
}

Class DCY_VisitantBoltTrail : Actor
{
  Default
  {
  Scale 0.425;
  RenderStyle "Add";
  DamageType "Visitant";
  Alpha 1.0;
  +NOCLIP
  +ROLLSPRITE
  -SOLID
  +NOINTERACTION
  +BRIGHT
  Translation "0:255=%[0.00,0.00,0.00]:[1.61,0.56,0.00]";
  }
  
  States
  {
  Spawn:
	LFX2 KLMNO 1
	{
		A_SetRoll(frandom(0, 360));
		
		if (Alpha < 0.000)
			Destroy();
		
		A_FadeOut(0.085);
	}
	Loop;
  }
}

Class DCY_VisitantThunder : DCY_VirtuosoTrail { Default { Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.30,0.98]"; } }
Class DCY_VisitantPuff : DCY_UltracerestPuff
{
  Default
  {
  Translation "0:255=%[0.27,0.00,0.00]:[2.00,1.30,0.98]";
  DamageType "Visitant";
  }
  
  States
  {
  Spawn:
	PQBM A 1;
	PQBM B 1
	{
		A_QuakeEx(4, 4, 4, 40, 0, 200, "", QF_SCALEDOWN, 0, 0, 0, 400);
		A_Explode(50, 25, 0, 0);
		A_StartSound("THNDSHCK", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.35);
		A_SpawnItemEx("DCY_AndroidExplosion", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		for (int i = 10; i > 0; i--)
			A_SpawnItemEx("DCY_VisitantGas", frandom(-50.0, 50.0), frandom(-50.0, 50.0), frandom(-50.0, 50.0), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(0, 360), SXF_NOCHECKPOSITION, 50);
	}
	PQBM CDEFGHIKNPRTU 1;
	TNT1 A 40;
	Stop;
  }
}

Class DCY_VisitantGasSpawner : DCY_VisitantPuff
{
  States
  {
  Spawn:
	TNT1 AAAAAA 1 A_SpawnItemEx("DCY_VisitantGas", frandom(-50.0, 50.0), frandom(-50.0, 50.0), frandom(-50.0, 50.0), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(0, 360), SXF_NOCHECKPOSITION, 50);
	Stop;
  }
}

Class DCY_VisitantBody : Actor
{
  Default
  {
  Scale 0.865;
  RenderStyle "Add";
  Alpha 1.0;
  +NOCLIP
  -SOLID
  +NOINTERACTION
  +BRIGHT
  Translation "0:255=%[0.27,0.00,0.00]:[2.00,1.30,0.98]";
  }
  
  override void Tick()
  {
	super.Tick();
	
	if (master)
		Warp(Master, 0, 0, 30, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION);
	else
		Destroy();
  }
  
  States
  {
  Spawn:
	F0RC ABCDEFGHIJKLMNOPQRSTUVWXYZ 1;
	F1RC ABCDEFGHIJKLMNOPQRSTU 1;
	Loop;
  }
}

Class DCY_VisitantTrail : DCY_ProjectileBaseTrail
{
  Default
  {
  Scale 0.865;
  Alpha 0.5;
  Translation "0:255=%[0.27,0.00,0.00]:[2.00,1.30,0.98]";
  }
  
  States
  {
  Spawn:
	F0RC ABCDEFGHIJKLMN 1 A_FadeOut(0.05);
	Stop;
  }
}

Class DCY_VisitantGlow : DCY_EmptySmoke
{
  Default { Scale 1.3; DamageType "Visitant"; }

  States
  {
  Spawn:
	ASUZ "#" 0 NoDelay
	{
		A_SetRoll(frandom(0, 360));
		A_SetScale(frandom(2.75, 3.25) * frandom(1.0, -1.0));
		frame = random(0, 3);
	}
	"####" "########" 1 A_FadeIn(0.01, 0);
	"####" "########" 1 A_FadeOut(0.01, 0);
	Stop;
  }
}

Class DCY_VisitantGas : DCY_EmptySmoke
{
  Default { Scale 2.0; Alpha 0.00; }
  States
  {
  Spawn:
	ASUZ D 0 NoDelay
	{
		A_SetRoll(frandom(0, 360));
		A_SetScale(frandom(2.75, 3.25) * frandom(1.0, -1.0));
	}
	"####" "########" 1 A_FadeIn(0.05, 0);
	"####" "#" 1
	{
		A_Explode(2, 150);
		A_FadeOut(0.005, 0);
		
		if (alpha <= 0.00)
			Destroy();
	}
	Goto Spawn+9;
  }
}

Class DCY_VisitantElectricity : DCY_Effect
{ 
  Default
  {
  Scale 2.4;
  Translation "0:255=%[0.27,0.00,0.00]:[2.00,1.30,0.98]";
  RenderStyle "Add";
  Alpha 1.0;
  Scale 0.85;
  +ROLLSPRITE;
  +NOGRAVITY;
  +NOCLIP;
  +BRIGHT;
  }
  
  States
  {
  Spawn:
	3L3C A 0 NoDelay A_SetRoll(frandom(0, 360));
	3L3C ABCDE 2 { tics = random(1, 3); }
	Stop;
  }
}