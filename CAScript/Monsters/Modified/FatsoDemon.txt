Class DCY_SkinnyFat : DCYMonster
{
  Default
  {
	Health 600;
	Radius 48;
	Height 64;
	Mass 1000;
	Speed 8;
	PainChance 80;
	Monster;
	+FLOORCLIP
	+BOSSDEATH
	SeeSound "juggernaut/sight";
	PainSound "juggernaut/pain";
	DeathSound "juggernaut/death";
	ActiveSound "juggernaut/active";
	AttackSound "juggernaut/attack";
	Obituary "%o is now charred meat due to the Juggernaut.";
	Tag "Juggernaut";
	Species "Mancubi";
	MeleeRange 90;
	DCYMonster.GibType "DCY_ShorterGiblets";
	DCYMonster.ScientificName "Adipem cadaverosa";
	DCYMonster.FootstepFrames 0, 3, 6;
	DCYMonster.FootstepSound "dcyfat/walk";
  }
  
  void A_FatThrower()
  {
	for (int i = -18; i < 19; i += 36) A_SpawnProjectile("DCY_FatThrower", 30, i);
	A_FaceTarget();
  }
  
  int ang;
  int missile;
  
  States
  {
  Spawn:
	_JUG A 10 DCY_Look();
	Loop;
  Look:
	_JUG AABBCCDDEEFF 5 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	#### AABBCCDDEEFF 4 DCY_Chase();
	Loop;
  Melee:
	#### J 15 { bNOPAIN = true; A_StartSound(AttackSound, CHAN_VOICE); A_FaceTarget(); }
	#### G 1 A_Hop(10);
	#### G 1
	{
		if (pos.z == floorz)
		{
			A_QuakeEx(1, 1, 4, 50, 0, 300, "", QF_SCALEDOWN|QF_3D);
			A_Explode(10 * random(2, 15), 128, XF_NOTMISSILE);
			A_StartSound("EarthPound", pitch: 0.8);
			A_PoundEffect(150);
			A_RadiusThrust(3500, 130, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
			A_RadiusThrust(100, 130, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE|RTF_THRUSTZ);
			SetStateLabel("Cooldown");
		}
	}
	Goto Melee+2;
  Cooldown:
	#### I 20;
	Goto See;
  Missile:
    _JUG G 0;
	_JUG G 0 A_JumpIf(target && distance3D(target) < 256, "FLAMETHROWERATK");
	Goto Fireballs;
  Fireballs:
    _JUG G 20
	{
		if (target)
		{
			A_StartSound("Juggernaut/Attack", CHAN_VOICE);
			A_FaceTarget();
		}
		ang = randompick(-1, 1);
		missile = randompick(2, 2, 2, 4, 4, 6);
	}
    _JUG H 7
	{
		if (missile <= 0) SetStateLabel("Firedone");
		else
		{
			A_FaceTarget();
			ang = !ang;
			A_SpawnProjectile("DCY_LeFatFireball", 30, (ang) ? -18 : 18, frandom(-11.25, 11.25) / 2, CMF_TRACKOWNER);
		}
	}
	_JUG I 7
	{
		missile--;
		A_FaceTarget();
		if (missile <= 0) SetStateLabel("Firedone");
	}
	Goto Fireballs+1;
  Firedone:
    _JUG IIIGG 7 A_FaceTarget();
    Goto See;
  FLAMETHROWERATK:
    _JUG G 1
	{
		if (target)
		{
			A_StartSound("Juggernaut/Attack", CHAN_VOICE);
			A_FaceTarget();
		}
	}
	_JUG GGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_FaceTarget();
	_JUG G 1 A_StartSound("FLAMTHRW", CHAN_WEAPON);
	_JUG HHHHHHHHHHHH 2 Bright A_FatThrower();
	_JUG HIG 10;
	Goto See;
  Pain:
	_JUG J 3;
	_JUG J 3 A_Pain();
	Goto See;
  Death:
	_JUG K 6;
	_JUG L 6 A_Scream();
	_JUG M 6 { DCY_Gibs(false); A_NoBlocking(); }
	_JUG NOPQRS 6;
	_JUG T -1 A_BossDeath2;
	Stop;
  Raise:
	_JUG R 5;
	_JUG QPONMLK 5;
	Goto See;
  }
}