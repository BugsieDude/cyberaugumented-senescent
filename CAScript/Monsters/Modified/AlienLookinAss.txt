Class DCY_VileParent : DCYMonster
{ 
  Default
  {
	Health 700;
	Radius 20;
	Height 56;
	Mass 500;
	Speed 15;
	PainChance 10;
	Monster;
	MaxTargetRange 896;
	+QUICKTORETALIATE 
	+FLOORCLIP 
	+NOTARGET
	SeeSound "vile/sight";
	PainSound "vile/pain";
	DeathSound "vile/death";
	ActiveSound "vile/active";
	AttackSound "vile/stop";
	Obituary "$OB_VILE";
	Tag "$FN_ARCH";
	DamageFactor "ArchvileDMG", 0.00;
	DamageType "ArchvileDMG";
	Species "Archviles";
	DCYMonster.GibType "DCY_ShorterGiblets";
	DCYMonster.ScientificName "Alienata";
	DCYMonster.FootstepFrames 0, 3, 6;
	DCYMonster.FootstepSound "dcyimp/walk";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"$OB_VILE",
		"%o got castrated to death by an Archvile.",
		"I hate Archviles.",
		"Burned."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
	VILE A 10 DCY_Look();
	Loop;
  Look:
	VILE AABBCCDDEEFF 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    VILE AABBCCDDEEFF 2 A_VileChase();
    Loop;
  Heal:
	VILE "[\][\]" 5 Bright;
	Goto See;
  Missile:
	VILE G 0 BRIGHT A_VileStart;
	VILE G 10 BRIGHT A_FaceTarget;
	VILE H 8 BRIGHT A_VileTarget;
	VILE IJKLMN 8 BRIGHT A_FaceTarget;
	VILE O 8 BRIGHT A_VileAttack;
	VILE P 20 BRIGHT;
	Goto See;
  Pain:
	VILE Q 4;
	VILE Q 4 A_Pain();
	Goto See;
  Death:
	VILE Q 5;
	VILE Q 1 A_ScreamAndUnblock();
	VILE RST 5;
	VILE UVWXYZ 4;
	VILE Z -1;
	Stop;
  }
}

Class DCY_ALIEN : DCY_VileParent { Default { BloodColor "Orange"; Translation "16:23=[255,255,255]:[255,255,128]", "24:37=[255,255,113]:[255,77,0]", "38:47=[242,72,0]:[64,0,0]", "168:176=[255,255,255]:[255,255,128]", "177:185=[255,255,113]:[255,77,0]", "186:191=[242,72,0]:[64,0,0]", "0:3=%[0.00,0.00,0.00]:[2.00,0.61,0.00]", "5:15=%[0.00,0.00,0.00]:[2.00,0.61,0.00]", "48:79=%[0.00,0.00,0.00]:[1.52,1.01,0.72]", "128:159=%[0.00,0.00,0.00]:[1.53,1.02,0.72]", "232:235=%[0.00,0.00,0.00]:[2.00,1.01,0.00]"; Bloodtype "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid"; } }

//Projectiles
Class DCY_ArchvileFlameWall : DCY_MeatballElecball
{
  Default
  {
  Speed 15;
  Damage (1);
  +BRIGHT;
  +RIPPER;
  +NEVERFAST;
  +THRUSPECIES;
  +THRUGHOST;
  +FLOORCLIP;
  +FLOORHUGGER;
  DamageType "ArchvileDMG";
  Obituary "$OB_VILE";
  SeeSound "vile/flamewall";
  DeathSound "";
  Radius 15;
  Height 64;
  Translation "0:255=%[0.00,0.00,0.00]:[1.33,0.54,0.01]";
  }
  States
  {
  Spawn:
	TNT1 A 1
	{
	  if (DCY_EffectDistance(1200))
	  {
		  for (int i = 3; i > 0; i--)
		  {
			A_SpawnItemEx("DCY_ImpBigFlame", random(-18, 18), random(-30, 30), random(28, 0), zvel: frandom(0, 3), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 200);
			A_SpawnItemEx("DCY_ImpFlame1", random(-18, 18), random(-30, 30), random(28, 0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 200);
			A_SpawnParticleEx(0xc24d0e, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 87, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5) + 1.5, startalphaf: 0.35, fadestepf: 0.0065, sizestep: -1.5);
		  }
	  }
	}
	Loop;
  Death:
	TNT1 A 1
	{
	  A_StartSound("vile/flameexplode", CHAN_WEAPON);
	  A_Explode(35, 100, XF_NOTMISSILE);
	}
	Stop;
  }
}

Class DCY_ArchvileBlaze : ArchvileFire
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.20,0.39,0.00]";
  Obituary "$OB_VILE";
  }
  
  int fx;
  
  States
  {
  Spawn:
    TNT1 A 1 Bright NoDelay
	{
		fx = 200;
		A_StartFire();
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 Bright
	{
		A_Fire();
		A_SpawnParticleEx(0xff6830, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 40, 70, 0, frandom(-14, 14), frandom(-14, 14), 1, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(3, 5.25), startalphaf: 1.0, fadestepf: 0.025, sizestep: -4, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);

		if (fx == 150 || fx == 10) A_FireCrackle();
		fx -= 5;
	}
	TNT1 AA 1 A_Fire();
	TNT1 A 0
	{
		for (int i = 20; i > 0; i--)
			A_SpawnItemEx("DCY_ImpBigFlame", random(-15, 15), random(-15, 15), random(8, 36), random(-10, 10), random(-10, 10), random(6, 2), random(0, 360), SXF_NOCHECKPOSITION, 100);
    }
	Stop;
  }
}