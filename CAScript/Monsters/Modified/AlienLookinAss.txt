Class DCY_VileParent : DCYMonster
{ 
  Default
  {
	Health 700;
	Radius 20;
	Height 56;
	Mass 500;
	Speed 15;
	PainChance 10;
	Monster;
	MaxTargetRange 896;
	+QUICKTORETALIATE 
	+FLOORCLIP 
	+NOTARGET
	SeeSound "vile/sight";
	PainSound "vile/pain";
	DeathSound "vile/death";
	ActiveSound "vile/active";
	AttackSound "vile/stop";
	Obituary "$OB_VILE";
	Tag "$FN_ARCH";
	DamageFactor "ArchvileDMG", 0.00;
	DamageType "ArchvileDMG";
	Species "Archviles";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"$OB_VILE",
		"%o got castrated to death by an Archvile.",
		"I hate Archviles.",
		"Burned."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
	
  int shockwaveangle;
  
  States
  {
  Spawn:
	VILE A 10 DCY_Look();
	Loop;
  Look:
	VILE AABBCCDDEEFF 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    VILE AABBCCDDEEFF 2 A_Chase("Shockwave", "Missile", CHF_RESURRECT);
    Loop;
  Shockwave:
    VILE M 0;
	VILE M 0 { int shockwaveangle = 0; }
	VILE MLMLMLMLMLMLMLM 1 Bright;
	VILE JJJII 4 Bright;
	VILE IIIIIIIIIIII 0
	{
		shockwaveangle += 30;
		A_SpawnProjectile("DCY_ArchvileShockwave", 5, 0, shockwaveangle);
	}
	VILE I 1
	{
		A_QuakeEx(5, 5, 0, 20, 0, 1330, "none", QF_SCALEDOWN);
		A_StartSound("vile/shockwave", CHAN_BODY);
	}
	VILE HGHIHGHIHGHIHGHIHGHIHGHI 1 Bright;
	Goto See;
  Heal:
	VILE "[\][\]" 5 Bright;
	Goto See;
  Missile:
	VILE L 2 Bright A_FaceTarget();
	VILE LKKJJGGGH 2 Bright A_FaceTarget(1.4, 1.4, 1.4);
	VILE I 2 Bright A_SpawnProjectile("DCY_ArchfireProjectile", 10, 0, 0, CMF_AIMDIRECTION);
	VILE IIHHGGHHIIHHGG 2 Bright;
	VILE G 0 A_Jump(100, "Firewall");
	Goto See;
  Firewall:
    VILE JJJLMLM 2 A_FaceTarget();
	VILE N 4
	{
		A_FaceTarget();
		A_StartSound("vile/flamewall", CHAN_WEAPON);
		A_SpawnProjectile("DCY_ArchvileFlamewall", 5, 0);
		A_SpawnProjectile("DCY_ArchvileFlamewall", 5, -30);
		A_SpawnProjectile("DCY_ArchvileFlamewall", 5, 30);
		A_SpawnProjectile("DCY_ArchvileFlamewall", 5, -60);
		A_SpawnProjectile("DCY_ArchvileFlamewall", 5, 60);
	}
	VILE NNNNNOOOOOOOPPPP 1 A_FaceTarget();
	Goto See;
  Pain:
	VILE Q 4;
	VILE Q 4 A_Pain();
	Goto See;
  Death:
	VILE Q 5;
	VILE Q 1
	{
		A_Scream();
		A_NoBlocking();
	}
	VILE RST 5;
	VILE UVWXYZ 4;
	VILE Z -1;
	Stop;
  }
}

Class DCY_ALIEN : DCY_VileParent { Default { BloodColor "Orange"; Translation "16:23=[255,255,255]:[255,255,128]", "24:37=[255,255,113]:[255,77,0]", "38:47=[242,72,0]:[64,0,0]", "168:176=[255,255,255]:[255,255,128]", "177:185=[255,255,113]:[255,77,0]", "186:191=[242,72,0]:[64,0,0]", "0:3=%[0.00,0.00,0.00]:[2.00,0.61,0.00]", "5:15=%[0.00,0.00,0.00]:[2.00,0.61,0.00]", "48:79=%[0.00,0.00,0.00]:[1.52,1.01,0.72]", "128:159=%[0.00,0.00,0.00]:[1.53,1.02,0.72]", "232:235=%[0.00,0.00,0.00]:[2.00,1.01,0.00]"; Bloodtype "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid"; } }

Class DCY_ArchfireProjectile : CacodemonBall
{
  Default
  {
  Radius 10;
  Height 10;
  +FLOORHUGGER;
  +DONTSPLASH;
  +THRUSPECIES;
  +THRUGHOST;
  +THRUACTORS;
  +FIRERESIST;
  +BRIGHT;
  Speed 40;
  FastSpeed 45;
  DamageType "Fire";
  SeeSound "FIRE001";
  DeathSound "";
  }
  
  int fire;
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (fire > 40)
			Return ResolveState("Death");
		
		A_SpawnItemEx("DCY_Archfire", 25, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		fire++;
		Return ResolveState(null);
	}
	Loop;
  Death:
	Stop;
  }
}

Class DCY_Archfire : FastProjectile
{
  Default
  {
  Radius 1;
  Height 1;
  YScale 0.75;
  XScale 1.205;
  RenderStyle "Add";
  Alpha 0.85;
  +BRIGHT;
  +DONTSPLASH;
  +FLOORHUGGER;
  DamageType "Fire";
  Translation "0:255=%[0.25,0.00,0.00]:[2.00,1.25,0.50]";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"$OB_VILE",
		"%o got castrated to death by an Archvile.",
		"Burned."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
	F43E A 1 A_SetScale(frandompick(-1.205, 1.205), 0.75);
	F43E A 2
	{
		A_Explode(10, 80, XF_NOTMISSILE, 0);
		A_QuakeEx(3, 3, 3, 20, 0, 400, "none", QF_RELATIVE, 0, 0, 0, 800);
	}
	F43E BCDEFG 2 A_SetScale(Scale.X+0.05, Scale.Y+0.05);
	Stop;
  }
}

//Projectiles
Class DCY_ArchvileFlameWall : CacodemonBall
{
  Default
  {
  Speed 15;
  Damage (1);
  +BRIGHT;
  +RIPPER;
  +NEVERFAST;
  +THRUSPECIES;
  +THRUGHOST;
  +FLOORCLIP;
  +FLOORHUGGER;
  DamageType "ArchvileDMG";
  Obituary "$OB_VILE";
  SeeSound "vile/flamewall";
  DeathSound "";
  Radius 15;
  Height 64;
  }
  States
  {
  Spawn:
	TNT1 A 1
	{
	  for (int i = 10; i > 0; i--)
	  {
		A_SpawnItemEx("DCY_ImpFlame1", random(-18, 18), random(-30, 30), random(28, 0), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 200);
	  }
	  
	  for (int i = 3; i > 0; i--)
	  {
		A_SpawnItemEx("DCY_ImpBigFlame", random(-18, 18), random(-30, 30), random(28, 0), zvel: frandom(0, 3), flags: SXF_NOCHECKPOSITION, failchance: 200);
	  }
	}
	Loop;
  Death:
	TNT1 A 1
	{
	  A_StartSound("vile/flameexplode", CHAN_WEAPON);
	  A_Explode(35, 100, XF_NOTMISSILE);
	  
	  for (int i = 15; i > 0; i--)
	  {
		A_SpawnItemEx("DCY_ImpBigFlame", random(-30, 30), random(-1, 1), random(8, 50), zvel: frandom(0, 3), flags: SXF_NOCHECKPOSITION, failchance: 200);
	  }
	}
	Stop;
  }
}

Class DCY_ArchvileShockwave : DCY_ArchvileFlamewall
{
  Default
  {
  Damage 0;
  Speed 45;
  FastSpeed 45;
  +NEVERFAST;
  Obituary "$OB_VILE";
  DamageType "ArchvileDMG";
  SeeSound "";
  }
  
  int loop;
  States
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 0 A_StartSound("vile/flamewall", CHAN_BODY);
	TNT1 A 1
	{
		if (loop >= 5)
		{
			Return ResolveState("Death");
		}
		
		A_Explode(15, 80, XF_NOTMISSILE, 1);
		
		for (int i = 15; i > 0; i--)
		{
			A_SpawnItemEx("DCY_ImpFlame1", random(-28, 28), random(-50, 50), random(28, 0), zvel: frandom(1, 5), flags: SXF_NOCHECKPOSITION, failchance: 200);
			A_SpawnItemEx("DCY_ImpBigFlame", random(-28, 28), random(-50, 50), random(28, 0), zvel: frandom(1, 5), flags: SXF_NOCHECKPOSITION, failchance: 140);
		}
		
		loop++;
		
		Return ResolveState(null);
	}
	Goto Spawn+2;
  Death:
    TNT1 A 1;
	Stop;
  }
}

Class DCY_ArchvileBlaze : ArchvileFire Replaces ArchvileFire
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.20,0.39,0.00]";
  Obituary "$OB_VILE";
  }
  
  int fx;
  
  States
  {
  Spawn:
    FIRE A 1 Bright NoDelay
	{
		fx = 200;
		A_StartFire();
	}
	FIRE ABBAABBCCBBCCBBCCDDCCDDCCDDEEDDEEDDEEDDEEFFEEFFEEFFGGHHGGHHGGHH 1 Bright
	{
		A_Fire();
		A_SpawnItemEx("DCY_ImpFlame1", random(-20, 20), random(-20, 20), random(-2, 20), 0, 0, random(5, 12), 0, SXF_NOCHECKPOSITION, 120);
		
		if (fx == 150 || fx == 10)
			A_FireCrackle();
			
		fx -= 5;
	}
	TNT1 AA 1 A_Fire();
	TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DCY_ImpFlame1", random(-15, 15), random(-15, 15), random(8, 36), random(-10, 10), random(-10, 10), random(6, 2), random(0, 360), SXF_NOCHECKPOSITION, 100);
    Stop;
  }
}