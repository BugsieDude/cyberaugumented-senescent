Class DCY_SOULKING : DCYMonster
{
  Default
  {
	Health 400;
	Radius 31;
	Height 56;
	Mass 400;
	Speed 8;
	PainChance 128;
	-FLOORCLIP
	+FLOAT 
	+NOGRAVITY
	+DONTFALL;
	SeeSound "EternalPainElemental/Sight";
	PainSound "EternalPainElemental/Pain";
	DeathSound "EternalPainElemental/NormalDeath";
	ActiveSound "EternalPainElemental/Active";
	Tag "Befouler";
	Species "Pain Elementals";
	BloodColor "Orange";
	BloodType "AndroidNeonFluid";
  }
  States
  {
  Spawn:
	DAJK A 10 DCY_Look();
	Loop;
  Look:
	DAJK AABBCC 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0
	{
		if (random(0, 1) == 0)
			SetStateLabel("ScreamAtPlayer");
		else
			DCY_AlertSound();
	}
	Goto See;
  ScreamAtPlayer:
	DAJK D 3
	{
		A_StartSound("EternalPainElemental/WarCry", CHAN_VOICE, attenuation: 0.85);
		A_FaceTarget();
	}
	DAJK NO 4 Bright A_FaceTarget();
	DAJK OOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO 1 Bright
	{
		A_FaceTarget();
		A_QuakeEx(6, 0, 0, 30, 0, 600, "", QF_SCALEDOWN|QF_RELATIVE|QF_3D, rollintensity: 0.5, rollwave: 0.5);
	}
	DAJK ND 5;
	Goto See;
  See:
	DAJK AABBCC 3 A_Chase();
	Loop;
  Missile:
	DAJK D 0
	{
		if (random(0, 4) == 0)
			SetStateLabel("ScreamAtPlayer");
	}
	DAJK DE 5 A_FaceTarget();
	DAJK F 4 BRIGHT A_FaceTarget();
	DAJK F 1 BRIGHT A_PainAttack("DCY_FlyingSkullWithFIRE");
	Goto See;
  Pain:
	DAJK G 6;
	DAJK G 6 A_Pain();
	Goto See;
  Death:
	DAJK G 6 BRIGHT;
	DAJK G 6 BRIGHT
	{
		if (health < -200)
			A_StartSound("EternalPainElemental/Death", CHAN_VOICE);
		else
			A_Scream();
	}
	DAJK HI 6;
	DAJK JJKK 3 BRIGHT A_SpawnItemEx("DCY_Explosion_1", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(10.0, 15.0), flags: SXF_NOCHECKPOSITION);
	DAJK L 6 BRIGHT A_PainDie("DCY_FlyingSkullWithFIRE");
	DAJK M 6 BRIGHT;
	Stop;
  Raise:
	DAJK MLKJIH 8;
	Goto See;
  }
}