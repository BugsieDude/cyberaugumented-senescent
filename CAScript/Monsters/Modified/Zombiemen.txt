Class DCY_ZombeParent : DCYMonster
{
  Default
  {
  Health 20;
  Radius 20;
  Height 56;
  Speed 8;
  PainChance 200;
  SeeSound "dcygrunt/sight";
  PainSound "dcygrunt/pain";
  DeathSound "dcygrunt/death";
  ActiveSound "dcygrunt/active";
  DCYMonster.BurnSound "dcygrunt/burn";
  Obituary "$OB_ZOMBIE";
  Tag "$FN_ZOMBIE";
  DropItem "Clip";
  Tag "Hell Soldier";
  Species "Former Humans";
  DCYMonster.ScientificName "Homo sapien";
  DCYMonster.TagColor "l5";
  +DOHARMSPECIES;
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_SetHumanNickname();
  }
  
  States
  {
  Spawn:
	POSP A 10 DCY_Look();
	Loop;
  Look:
	POSP AABBCCDD 4
	{
		DCY_LookingForPlayer();
		
		if (random(0, 50) == 1)
			SetStateLabel("Stand");
	}
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	POSP AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	POSP AABBCCDD 3 A_Chase();
	Loop;
  Missile:
	POSP E 0 A_Jump(50, "Run");
	POSP E 0 A_JumpIfCloser(256, "BackUp");
	POSP E 10 A_FaceTarget();
	POSP F 4 Bright
	{
		A_StartSound("BurstPistol/Fire", CHAN_WEAPON, 0, 1.0);
		A_SpawnProjectile("DCY_ZombieTracer", 32, -2, frandom(-1.0, 1.0), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-1.0, 1.0));
		A_FaceTarget();
	}
	POSP E 12;
	Goto See;
  Run:
	POSP AABBCCDD 2
	{
		A_Chase(null, null);
		A_Chase(null, null);
	}
	Goto Missile+2;
  BackUp:
	POSP E 12
	{
		A_FaceTarget();
		A_Recoil(4);
		A_Hop(5);
	}
	POSP F 4 Bright
	{
		A_StartSound("BurstPistol/Fire", CHAN_WEAPON, 0, 1.0);
		A_SpawnProjectile("DCY_ZombieTracer", 32, -2, frandom(-1.0, 1.0), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-1.0, 1.0));
		A_FaceTarget();
	}
	POSP E 12;
	Goto See;
  Pain:
	POSP G 4;
	POSP G 4 A_Pain();
	Goto See;
  Death:
	POSP H 0 A_Flip();
	POSP H 3;
	POSP H 3 A_Scream();
	POSP I 5 A_Fall();
	POSP JK 4;
	POSP L -1;
	Stop;
  XDeath:
	POSP M 6;
    POSP N 4 DCY_Gibs();
    POSP O 4 A_NoBlocking();
    POSP PQRST 4;
    POSP U -1;
    Stop;
  Raise:
	POSS K 5 A_Restore();
    POSS JIH 5;
    Goto See;
  }
}

Class DCY_Zombe : DCY_ZombeParent { Default { Obituary "%o was scared to stand against a hell soldier."; Translation "112:159=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "236:239=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "13:15=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "77:79=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "48:76=%[0.00,0.00,0.00]:[1.13,0.78,0.54]", "208:223=%[0.00,0.00,0.00]:[1.46,0.98,0.55]", "168:191=%[0.00,0.00,0.00]:[2.00,0.00,0.00]", "16:47=%[0.00,0.00,0.00]:[1.24,0.00,0.00]"; } }

Class DCY_ShotgunZombe : DCYMonster
{
  Default
  {
  Health 30;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 8;
  PainChance 170;
  SeeSound "dcyshotguy/sight";
  PainSound "dcyshotguy/pain";
  DeathSound "dcyshotguy/death";
  ActiveSound "dcyshotguy/active";
  DCYMonster.BurnSound "dcyshotguy/burn";
  Obituary "%o has annoyed a hell sergeant.";
  DropItem "DCY_CombatShotgun";
  Tag "Hell Sergeant";
  DCYMonster.ScientificName "Homo sapien";
  Species "Sergeants";
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_SetHumanNickname("Sgt.");
  }
  
  States
  {
  Spawn:
    C0L1 A 10 DCY_Look();
	Loop;
  Look:
	C0L1 AABBCCDD 4
	{
		DCY_LookingForPlayer();
		
		if (random(0, 50) == 1)
			SetStateLabel("Stand");
	}
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	C0L1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	C0L1 AABBCCDD 3 A_Chase();
	Loop;
  Missile:
	C0L1 E 0 A_JumpIfCloser(280, "BackUp");
	C0L1 E 10 A_FaceTarget();
	C0L1 F 4 Bright
	{
		A_StartSound("DCYShotgun/Fire", CHAN_WEAPON, 0, 1.0);
		
		for (int i = 7; i > 0; i--)
			A_SpawnProjectile("DCY_Zombie2Tracer", 32, -2, frandom(-4.0, 4.0), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-2.5, 2.5));
		
		A_FaceTarget();
	}
	C0L1 E 20;
	Goto See;
  BackUp:
	C0L1 AABBCCDDAABBCCDD 2
	{
		A_FaceTarget();
		A_Recoil(-1);
	}
	Goto Missile+1;
  Pain:
	C0L1 G 4;
	C0L1 G 4 A_Pain();
	Goto See;
  Death:
	C0L1 H 3
	{
		if (!random(0, 1)) SetStateLabeL("Death2");
		A_Flip();
	}
	C0L1 H 3 A_Scream();
	C0L1 I 5 A_Fall();
	C0L1 JKLM 4;
	C0L1 N -1;
	Stop;
  Death2:
	C0L2 A 6 A_Flip();
	C0L2 B 6 A_Scream();
	C0L2 C 6 A_Fall();
	C0L2 D 6;
	C0L2 E -1;
	Stop;
  XDeath:
	C0L2 F 6;
    C0L2 G 4 DCY_Gibs();
    SRG_ O 4 A_NoBlocking();
    SRG_ PQRST 4;
    SRG_ U -1;
    Stop;
  Raise:
	SRG_ K 5 A_Restore();
    SRG_ JIH 5;
    Goto See;
  }
}

Class DCY_Plutonia : DCYMonster
{
  Default
  {
  Health 70;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 8;
  PainChance 170;
  SeeSound "dcychainguy/sight";
  PainSound "dcychainguy/pain";
  DeathSound "dcychainguy/death";
  ActiveSound "dcychainguy/active";
  DCYMonster.BurnSound "dcychainguy/burn";
  Obituary "%o has been ripped off by the Hell Besieger.";
  Tag "Hell Besieger";
  DCYMonster.ScientificName "Homo sapien";
  Dropitem "DCY_CyberRifle";
  Species "Chaingunners";
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_SetHumanNickname("Cpt.");
  }
  
  States
  {
  Spawn:
	RE1_ A 10 DCY_Look();
	Loop;
  Look:
	RE1_ AABBCCDD 4
	{
		DCY_LookingForPlayer();
		Return A_Jump(20, "Stand");
	}
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	RE1_ AAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	RE1_ AABBCCDD 3 A_Chase();
	Loop;
  Missile:
	RE1_ E 14 A_FaceTarget();
	RE1_ F 2 Bright
	{
		A_StartSound("PIS3FIR", CHAN_WEAPON, 0, 1.0);
		A_SpawnProjectile("DCY_ZombieTracer", 35, 5, frandom(-1.75, 1.75), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-1.75, 1.75));
		A_FaceTarget();
	}
	RE1_ E 2 A_FaceTarget();
	RE1_ E 0 A_MonsterRefire(64, "See");
	Goto Missile+1;
  Pain:
	RE1_ G 4;
	RE1_ G 4
	{
		{ bNoDropOff = false; }
		A_Pain();
	}
	Goto See;
  Death:
	RE1_ H 0 A_Flip();
	RE1_ H 5 { bNoDropOff = false; }
	RE1_ H 5 A_Scream();
	RE1_ I 5 A_Fall;
	RE1_ JK 4;
	RE1_ L -1;
	Stop;
  XDeath:
	RE1_ O 6;
    RE1_ P 4 DCY_Gibs();
    RE1_ Q 4 A_NoBlocking();
    RE1_ RSTUV 4;
    RE1_ W -1;
    Stop;
  Raise:
	RE1_ L 6;
    RE1_ KJI 6;
	RE1_ H 6 A_Restore();
    Goto See;
  }
}