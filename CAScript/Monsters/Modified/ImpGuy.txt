Class DCY_Impe : DCYMonster
{
  Default
  {
  Tag "$FN_IMP";
  Health 60;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 8;
  PainChance 200;
  SeeSound "imp/sight";
  PainSound "imp/pain";
  DeathSound "imp/death";
  ActiveSound "imp/active";
  HitObituary "$OB_IMPHIT";
  Obituary "$OB_IMP";
  Species "Imps";
  DCYMonster.ScientificName "Iniquum hominem";
  DCYMonster.FootstepSound "dcyimp/walk";
  }
  
  void A_ImpLunge()
  {
	A_FaceTarget();
	A_Recoil(-10);
	A_ChangeVelocity(0, 0, 1.0, CVF_RELATIVE);
  }
  
  States
  {
  Spawn:
	TROO A 10 DCY_Look();
	Loop;
  Look:
	TROO AABBCCDD 4
	{
		DCY_LookingForPlayer();
		
		if (random(0, 50) == 1)
			SetStateLabel("Stand");
	}
	Loop;
  Stand:
	TROO AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	Goto Look;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	TROO AABBCCDD 3 A_Chase();
	Loop;
  Melee:
	TROO E 0;
	TROO EF 5 A_FaceTarget();
	TROO G 5
	{
		A_CustomMeleeAttack(3 * random(1, 8), "imp/melee", "");
		A_FaceTarget();
	}
	TROO F 5;
	Goto See;
  Missile:
	TROO EF 8 A_FaceTarget();
	TROO G 4
	{
		A_SpawnProjectile("DCY_ImpFireball", 32, 0, frandom(-0.5, 0.5));
		A_FaceTarget();
	}
	TROO F 4;
	Goto See;
  Pain:
	TROO H 4;
	TROO H 4 A_Pain();
	Goto See;
  Death:
    TROO I 1 A_Flip();
	TROO I 1 A_Scream();
	TROO I 5 A_NoBlocking();
	TROO JKL 4;
	TROO M -1;
	Stop;
  XDeath:
    TROO N 1;
	TROO N 1 DCY_Gibs();
	TROO N 5 A_NoBlocking();
	TROO OPQRST 4;
	TROO U -1;
	Stop;
  Raise:
	TROO M 3;
    TROO LKHJI 4;
	TROO H 4 A_Restore();
    Goto See;
  }
}

Class DCY_Dastard : DCY_Impe
{
  Default
  {
  SeeSound "dastard/sight";
  ActiveSound "dastard/active";
  PainSound "dastard/pain";
  DeathSound "dastard/death";
  Tag "Dastard";
  }
  
  States
  {
  Spawn:
	DAS_ A 10 DCY_Look();
	Loop;
  Look:
	DAS_ AABBCCDD 4
	{
		DCY_LookingForPlayer();
		Return A_Jump(20, "Stand");
	}
	Loop;
  Stand:
	DAS_ AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	Goto Look;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	DAS_ AABBCCDD 3 DCY_Chase();
	Loop;
  Melee:
	DAS_ E 0;
	DAS_ EF 5 A_FaceTarget();
	DAS_ G 5
	{
		A_CustomMeleeAttack(3 * random(1, 8), "imp/melee", "");
		A_FaceTarget();
	}
	DAS_ F 5;
	Goto See;
  SLASHER:
	DAS_ E 1 A_ImpLunge();
	DAS_ EFGF 4 A_FaceTarget();
	DAS_ E 1 A_ImpLunge();
	DAS_ F 5 A_FaceTarget();
	DAS_ G 5 A_SpawnProjectile("DCY_ImpFireball", 32, 0, frandom(-0.5, 0.5));
	DAS_ F 3;
	Goto See;
  Missile:
	DAS_ E 0 A_JumpIfCloser(128, "SLASHER");
	DAS_ E 0 A_Jump(100, "Dodge", "Run");
	DAS_ FE 4 A_FaceTarget();
	DAS_ EEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnParticleEx(0x703912, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 18, 0, frandom(-2.5, 2.5), 30 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 67, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -1.325);
	}
	DAS_ F 4 A_FaceTarget();
	DAS_ G 4
	{
		A_SpawnProjectile("DCY_ImpFireball", 32, 0, frandom(-0.5, 0.5));
		A_FaceTarget();
	}
	DAS_ F 4;
	Goto See;
  Dodge:
	DAS_ A 15 A_ChangeVelocity(randompick(-10, 10), 0, 1.0, CVF_RELATIVE);
	DAS_ A 1 A_Stop();
	Goto Missile+2;
  Run:
	DAS_ ABCDABCDABCD 3
	{
		for (int i = 2; i > 0; i--)
			A_Chase(null, null);
		
		DCY_Foot();
	}
	DAS_ FE 4 A_FaceTarget();
	DAS_ EEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnParticleEx(0x703912, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 18, 0, frandom(-2.5, 2.5), 30 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -1.325);
	}
	DAS_ F 4 A_FaceTarget();
	DAS_ G 4
	{
		A_SpawnProjectile("DCY_ImpFireball", 32, 0, frandom(-0.5, 0.5));
		A_FaceTarget();
	}
	DAS_ F 4;
	DAS_ ABCDABCDABCD 3
	{
		for (int i = 2; i > 0; i--)
			A_Chase(null, null);
	}
	DAS_ FE 4 A_FaceTarget();
	DAS_ EEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnParticleEx(0x703912, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 18, 0, frandom(-2.5, 2.5), 30 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -1.325);
	}
	DAS_ F 4 A_FaceTarget();
	DAS_ G 4
	{
		A_SpawnProjectile("DCY_ImpFireball", 32, 0, frandom(-0.5, 0.5));
		A_ChangeVelocity(randompick(-10, 10), 0, 1.0, CVF_RELATIVE);
		A_FaceTarget();
	}
	DAS_ F 4;
	DAS_ F 1 A_Stop();
	Goto See;
  Pain:
	DAS_ H 4;
	DAS_ H 4 A_Pain();
	Goto See;
  Death:
    DAS_ I 1 A_Flip();
	DAS_ I 1 A_Scream();
	DAS_ I 5 A_NoBlocking();
	DAS_ JKL 4;
	DAS_ M -1;
	Stop;
  XDeath:
    DAS_ N 1;
	DAS_ N 1 DCY_Gibs();
	DAS_ N 5 A_NoBlocking();
	DAS_ OPQRST 4;
	DAS_ U -1;
	Stop;
  Raise:
	DAS_ M 3;
    DAS_ LKHJI 4;
	DAS_ H 4 A_Restore();
    Goto See;
  }
}