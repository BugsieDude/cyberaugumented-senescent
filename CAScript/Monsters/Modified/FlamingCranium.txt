Class DCY_SkullParent : DCYMonster
{
  action void A_SkullFlame(string fcolor = "ffffff", float ftimer = 20, float fsize = 45, float faccel = 0,
							bool smonk = false, float offsets = 5, bool ember = true, float zvel2 = 1.5)
  {
	A_SpawnParticleEx(fcolor, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, ftimer, fsize, angle, frandom(-offsets, offsets), frandom(-offsets, offsets), frandom(-offsets, offsets) + 20, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1) + zvel2, accelz: faccel, startalphaf: 1.0, fadestepf: 0.025, sizestep: -(fsize / 10) / (ftimer / 10), startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.5 : -0.5);
	
	if ((ember) && ((level.time % random(1, 9)) == 0)) A_SpawnParticleEx(fcolor, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, random(3, 6), angle, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1) + 20, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.1, 0.2), fadestepf: 0, sizestep: -0.115);
	
	if (smonk)
		if ((level.time % random(3, 9)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, ftimer * 1.5, fsize, angle, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1) + 30, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1) + (zvel2 / 1.5), startalphaf: 0.3, fadestepf: 0.01, sizestep: 0.1, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
  }
  
  Default
  {
  Radius 16;
  Height 56;
  Mass 50;
  Damage 3;
  PainChance 256;
  -FLOORCLIP +FLOAT +NOGRAVITY +MISSILEMORE +DONTFALL +NOICEDEATH +RETARGETAFTERSLAM
  AttackSound "skull/melee";
  PainSound "skull/pain";
  DeathSound "skull/death";
  ActiveSound "skull/active";
  Obituary "$OB_SKULL";
  Tag "$FN_LOST";
  RenderStyle "Normal";
  Alpha 1.0;
  Scale 0.82;
  +NOBLOOD;
  +DONTHARMCLASS;
  Species "LeFlameSoul";
  Species "Souls";
  Speed 3;
  Health 50;
  DCYMonster.ScientificName "Soul";
  }
  
  States
  {
  Spawn:
	L0ST A 10 DCY_Look();
	Loop;
  Look:
	L0ST A 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	L0ST A 2 A_Chase();
	Loop;
  Missile:
	L0ST AAAAAA 2 A_FaceTarget();
	L0ST B 1
	{
		A_Weave(1, 1, 1, 1);
		A_SkullAttack();
	}
	L0ST BBBBBBBBBBBBBBBBBBBBBBB 1;
	L0ST A 2;
	Goto See;
  Pain:
	L0ST C 3;
    L0ST C 3 A_Pain();
    Goto See;
  Death:
    L0ST F 3;
    L0ST G 3 A_ScreamAndUnblock();
    L0ST H 3
	{
		A_RemoveChildren(true, flags: RMVF_EVERYTHING);
		A_Stop();
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
    L0ST I 3;
    Stop;
  }
}

Class DCY_FlamingCranium : DCY_SkullParent
{
  Default
  {
    Tag "Tormented Psyche";
	Translation "128:159=%[0.00,0.00,0.00]:[0.65,0.65,0.65]", "48:79=[144,144,144]:[0,0,0]", "160:167=%[1.92,0.49,0.00]:[2.00,1.14,0.27]", "16:47=%[0.20,0.00,0.00]:[1.12,0.28,0.00]";
	DeathSound "DCYLost/Death";
	AttackSound "DCYLost/Scream";
	ActiveSound "DCYLost/Active";
	PainSound "DCYLost/Pain";
  }
  
  override void Tick()
  {
	Super.Tick();
	
	if (Health > 0 && !IsFrozen() && DCY_EffectDistance(sfxdistance)) A_SkullFlame("eb7134");
	
	if (!isFrozen())
	{
		A_SoundVolume(12, v);
		if ((screamsoundtimer <= 0)) v -= (self.health <= 0) ? 0.1 : 0.02;
		if (v <= 0) { v = 0; A_StopSound(12); }
		if (screamsoundtimer > 0) screamsoundtimer--;
	}
  }
  
  float p, v;
  int screamsoundtimer;
  Vector3 oldvel;
  
  States
  {
  Spawn:
	S0_L A 10 DCY_Look();
	Loop;
  Look:
	S0_L A 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	S0_L A 2
	{
		if (vel != oldvel) screamsoundtimer = 0;
		A_Chase();
	}
	Loop;
  Missile:
	S0_L AAAAAA 2 A_FaceTarget();
	S0_L B 1
	{
		p = frandom(1.1, 1.4);
		A_StopSound(12);
		v = 0.7;
		screamsoundtimer = 128;
		A_StartSound("GoreNest/Scream", 12, CHANF_LOOPING, v, pitch: p, starttime: frandom(0.4, 0.7));
		A_Weave(1, 1, 1, 1);
		A_SkullAttack();
		oldvel = vel;
	}
	S0_L BBBBBBBBBBBBBBBBBBBBBBB 1
	{
		if (vel != oldvel) screamsoundtimer -= 128;
		if ((level.time % 2) == 0)
			A_Weave(1, 1, 1, 1);
	}
	S0_L A 2;
	Goto See;
  Pain:
	S0_L C 3 { screamsoundtimer = 0; }
    S0_L C 3 A_Pain();
    Goto See;
  Death:
    S0_L F 3;
    S0_L G 3 { screamsoundtimer = 0; A_ScreamAndUnblock(); }
    S0_L H 3 Bright
	{
		A_RemoveChildren(true, flags: RMVF_EVERYTHING);
		A_Stop();
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
    S0_L I 3 Bright;
    Stop;
  }
}