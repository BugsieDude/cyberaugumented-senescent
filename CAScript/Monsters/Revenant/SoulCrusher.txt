Class DCY_SoulCrusher : DCY_Rev
{
  Default
  {
  Health 700;
  Speed 20;
  FastSpeed 25;
  MeleeDamage 18;
  Tag "\c[Blue]Soul Crusher\c-";
  Obituary "%o is now a pile of scattered bits and atoms by the Soul Crusher.";
  HitObituary "%o faced the Soul Crusher's wrath.";
  Translation "80:111=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "128:151=%[0.00,0.00,0.00]:[0.75,0.75,0.75]", "152:159=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "13:15=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "236:239=%[0.00,0.00,0.00]:[0.62,0.66,0.92]", "48:79=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "1:3=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "5:12=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "16:47=%[0.00,0.00,0.50]:[0.00,0.00,2.00]", "168:191=%[0.00,0.00,0.50]:[0.00,0.00,2.00]", "160:167=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "208:235=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "248:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  Scale 1.0896545;
  BloodColor "Blue";
  StencilColor "Blue";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +DONTHARMSPECIES
  +DONTHARMCLASS
  DropItem "DCY_UltramodernEnergy", 45;
  SeeSound "roborev/sight";
  ActiveSound "roborev/active";
  PainSound "roborev/pain";
  DeathSound "roborev/death";
  DCYMonster.FootstepSound "dcyabater/walk";
  DCYMonster.FootstepFrames 0, 2, 4;
  }
  
  void A_BVITorpedo()
  {
	A_StartSound("ROBOMISL", CHAN_WEAPON, 0, 1, 0.495);
	A_SpawnProjectile("DCY_TransmuterTorpedo", 40, -5, random(-2, 2));
	A_FaceTarget();
  }
  
  void A_BVIFall()
  {
	A_StartSound("ROBOMISL", CHAN_WEAPON, 0, 1, 0.25);
	A_SpawnProjectile("DCY_BlueVoidTorpedoRainStart", 70, 20, random(10, -10), CMF_AIMDIRECTION|CMF_OFFSETPITCH, frandom(-88, -94));
  }
  
  int missile;
  bool rushing;
  
  void A_CrusherChase()
  {
	if (rushing) A_SetSpeed((skill >= 4) ? 35 : 25);
	else A_SetSpeed((skill >= 4) ? 25 : 20);
	
	if (target && checkSight(target) && rushing)
	{
		A_Recoil(-2);
		tics = 2;
		A_FaceTarget(5, 5);
		A_SpawnItemEx("DCY_HexodenicanTrail2", -0.5, zofs: 6, flags: SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSTENCILCOL);
		DCY_Chase("Melee", "Missile", CHF_DONTMOVE);
	}
	else
		DCY_Chase();
	
	if (!random(0, 1) && (!(getAge() % 100))) rushing = !rushing;
  }
  
  States
  {
  Spawn:
    XX3_ A 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" I 30
	{
		A_FaceTarget();
		A_QuakeEx(3, 3, 0, 40, 0, 600, "", QF_SCALEDOWN|QF_3D);
		DCY_AlertSound();
	}
	Goto See;
  See:
	XX3_ AAABBBCCCDDD 3 A_CrusherChase();
	Loop;
  Missile:
	XX3_ A 0
	{
		if (!random(0, 1) && (target && checksight(target) && distance3D(target) < 256)) SetStateLabel("Puff");
		
		switch (random(0, 1))
		{
			case 0: A_StartSound("roborev/rocketload", 15); SetStateLabel("Barrage"); break;
			case 1: A_StartSound("roborev/rocketload", 15); SetStateLabel("Knockback"); break;
		}
	}
	Goto See;
  Puff:
  Melee:
	XX3_ E 19
	{
		A_FaceTarget();
		A_Pain();
	}
	XX3_ F 2 { A_FaceTarget(); if (target) target.A_StartSound("roborev/flamecannon", CHAN_WEAPON); A_StartSound("roborev/flamecannon", CHAN_WEAPON); }
	XX3_ FFFF random(0, 2)
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_SoulKnockback", 40, -5, frandom(-10, 10), CMF_OFFSETPITCH|CMF_AIMDIRECTION, frandom(-10, 10));
		
		if (target && checksight(target) && distance3D(target) < 245)
		{
			int damage = random[SkelFist](1, 3) * 9;
			int newdam = target.DamageMobj(self, self, damage, 'Plasma');
			target.TraceBleed(newdam > 0 ? newdam : damage, self);
			target.Vel3DFromAngle(20, self.angle, self.pitch);
			target.vel.z += 8;
		}
			
		for (int i = random(1, 3); i > 0; i--)
			if (DCY_EffectDistance(sfxdistance))
			{
				A_SpawnParticleEx(0x3449eb, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 100, 100, 0, frandom(-5, 5), frandom(-5, 5) - 5, frandom(-5, 5) + 40, frandom(-1, 1) + 2, frandom(-1, 1), frandom(-1, 1), fadestepf: 0.01, sizestep: 0.725);
				for (int i = random(1, 2); i > 0; i--) A_SpawnParticleEx(0x3449eb, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), random(3, 10), 0, frandom(-5, 5), frandom(-5, 5) - 5, frandom(-5, 5) + 40, frandom(2, 10), frandom(-4, 4), frandom(-4, 4), accelz: -0.1, fadestepf: 0.05, sizestep: -0.135);
			}
	}
	XX3_ E 20;
	Goto See;
  Knockback:
	XX3_ E 0
	{
		A_StartSound((!random(0, 2)) ? "roborev/active" : "roborev/attack", CHAN_VOICE);
		A_FaceTarget();
	}
	XX3_ EEEEEEEEEE 1 A_FaceTarget();
	XX3_ E 0 A_StartSound("roborev/rocketaim", 16);
	XX3_ EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		if (!(getAge() % 2)) A_CustomRailgun(false, -5, "", "", RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 2, pufftype: "DCY_Effect", sparsity: 70, spawnclass: "DCY_SoulTrack", spawnofs_z: 8);
	}
	XX3_ F 10
	{
		A_FaceTarget();
		A_StartSound("roborev/rocket", 18, attenuation: 0.5);
		A_SpawnProjectile("DCY_SoulMissile", 40, -5);
	}
	XX3_ EA 10;
	Goto See;
  Barrage:
	XX3_ A 0
	{
		missile = 7;
		Return A_Jump(90, "MissileRain");
	}
	XX3_ EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	XX3_ F 3
	{
		if (missile <= 0)
			SetStateLabel("MissileS");
			
		A_BVITorpedo();
		missile -= 1;
	}
	XX3_ E 3
	{
		tics = random(3, 5);
		if (missile <= 0)
			SetStateLabel("MissileS2");
	}
	Goto Barrage+38;
  MissileS:
	XX3_ "#" 14;
	Goto See;
  MissileS2:
	XX3_ "#" 30;
	Goto See;
  MissileRain:
	XX3_ G 30
	{
		missile = 6;
		A_StartSound("roborev/attack", CHAN_VOICE);
		A_FaceTarget();
	}
	XX3_ H 3
	{
		if (missile <= 0)
			SetStateLabel("MissileS2");
			
		A_BVIFall();
		missile -= 1;
	}
	XX3_ G 4
	{
		if (missile <= 0)
			SetStateLabel("MissileS2");
	}
	Goto MissileRain+1;
  Pain:
    XX3_ I 3 A_StopSound(16);
	XX3_ I 3 A_Pain();
	Goto See;
  Death:
    XX3_ I 1;
	XX3_ I 1
	{
		A_StopSound(16);
		A_QuakeEx(3, 3, 3, 100, 0, 500, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 1500, 300);
		A_QuakeEx(1, 1, 1, 130, 0, 1500, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 1500, 300);
	}
	XX3_ I 40 A_Scream();
	XX3_ I 0 A_StartSound("AOI/Explode", CHAN_BODY, 0, 1, 0.4);
	XX3_ IIIIIIII 3
	{
		A_SpawnItemEx("DCY_BlueVoidExplosions", 0, 0, 0, random(-10, 10), random(-10, 10), random(-5, 15), random(-180, 180), SXF_NOCHECKPOSITION);
		A_StartSound("AOI/Explode", CHAN_BODY, 0, 1, 0.4);
	}
	XX3_ I 0
	{
		DCY_Gibs(false);
		for (int i = 5; i > 0; i--) A_SpawnItemEx("DCY_BlueVoidExplosions", 0, 0, 0, random(-15, 15), random(-15, 15), random(-15, 25), random(-180, 180), SXF_NOCHECKPOSITION);
	}
	XX3_ J 5 A_NoBlocking();
	XX3_ KLMN 5;
	XX3_ N -1;
	Stop;
  Raise:
	XX3_ NMLKJI 6;
	Goto See;
  }
}

Class DCY_SoulKnockback : DCY_AndroidExplosion
{
  Default { Translation "0:255=%[0.00,0.00,0.25]:[0.67,1.14,2.00]"; Scale 0.35; Species "Revenants"; Speed 15; }
}

Class DCY_SoulMissile : DCY_GRocket
{
  Default
  {
  Scale 0.9;
  Speed 25;
  SeeSound "roborev/rocket";
  Translation "0:255=%[0.00,0.00,0.00]:[0.33,0.33,0.33]", "16:47=%[0.00,0.00,0.17]:[0.61,1.28,2.00]", "168:191=%[0.00,0.00,0.17]:[0.61,1.28,2.00]", "160:167=%[0.00,0.00,0.17]:[0.61,1.28,2.00]", "208:235=%[0.00,0.00,0.17]:[0.61,1.28,2.00]";
  }
  
  States
  {
  Spawn:
	64MS A 1
	{
		if (getAge() < 10) A_Tracer2(4);
		
		if (DCY_EffectDistance(1000))
		{
			if ((level.time % random(1, 5)) == 0) A_SpawnParticleEx(0x152ed1, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 80, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.2, 0.2), frandom(-0.2, 0.2), frandom(-0.2, 0.2), fadestepf: 0, sizestep: -0.075);	
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, random(10, 25), angle,  frandom(-10, -2), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.5, fadestepf: 0.01, sizestep: 0.1, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
			A_SpawnFlames("4287f5", 20, 23, 0, true, frandom(-2.5, 2.5), allsmoke: true, thrust: 2);
			A_SpawnParticleEx(0x152ed1, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 65, 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.02, sizestep: 0.725);
		}
	}
	Loop;
  Death:
	TNT1 A 5
	{
		A_Explode(2, 128);
		A_RadiusThrust(5000, 128, RTF_NOIMPACTDAMAGE|RTF_NOTMISSILE);
		A_Explode(50, 128, XF_NOTMISSILE);
		
		if (DCY_EffectDistance(1000))
		{
			for (int i = 3; i > 0; i--)
				A_SpawnItemEx("DCY_AndroidExplosion", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(0.00, 360.0), SXF_NOCHECKPOSITION);
			
			A_SpawnItemEx("DCY_AndroidExplosionBiggie", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(0.00, 360.0), SXF_NOCHECKPOSITION);
			
			for (int i = 15; i > 0; i--)
				A_SpawnParticleEx(0x4287f5, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, 100, random(50, 100), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
		}
		
		A_QuakeEx(2, 2, 2, 60, 0, 800, "none", QF_SCALEDOWN|QF_3D);
	}
	TNT1 A 60;
	Stop;
  }
}

Class DCY_SoulTrack : DCY_GehennaTrack { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.00,0.00,1.45]"; } }

Class DCY_BlueVoidExplosions : DCY_AndroidExplosion
{
  Default
  {
  Scale 0.10;
  Alpha 0.0001;
  RenderStyle "Add";
  +BRIGHT
  +THRUSPECIES
  +THRUGHOST
  +THRUACTORS
  }
  States
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 1 A_StartSound("AOI/Explode", CHAN_BODY, 0, 1.0, 0.4);
	TNT1 AAAAA 1 A_SpawnItemEx("DCY_BlueVoidExplosion", random(-15, 15), random(-15, 15), random(15, 30), random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION, 100);
    TNT1 A 1;
	Stop;
  }
}

Class DCY_BlueVoidTorpedoRainStart : DCY_TransmuterTorpedo
{
  Default
  {
  Damage 0;
  Speed 2;
  +THRUACTORS
  Radius 8;
  Height 11;
  DeathSound "";
  +SKYEXPLODE;
  }
  
  override void Tick()
  {
	super.tick();
	
	if (DCY_EffectDistance(sfxdistance) && !level.isFrozen() && alpha > 0.1)
	{
		vel.x += frandom(-1, 1);
		vel.y += frandom(-1, 1);
		vel.z += frandom(-0.3, 0.3);
		A_SpawnFlames("174ee6", 40, 25, 0, false);
		if ((level.time % random(1, 9)) == 0) A_SpawnParticleEx(0x174ee6, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
		A_SpawnItemEx("DCY_TransmuterFlare", frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-2.5, 2.5), flags: SXF_NOCHECKPOSITION);
	}
  }
  
  States
  {
  Spawn:
    HMIS ZZZZZZZZZZZZ 1 A_ChangeVelocity(0, 0, 5);
	HMIS ZZZZZZZZZ 1
	{
		A_FadeOut(0.1, 0);
		A_ChangeVelocity(0, 0, 10);
	}
	Goto Death;
  Death:
	TNT1 A 20 A_Stop();
	TNT1 A 10 A_SpawnProjectile("DCY_BlueVoidMissileRain", -40, random(-10, 10), random(0, 360), CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH);
	Stop;
  }
}

Class DCY_BlueVoidMissileRain : CacodemonBall
{
  Default
  {
  SeeSound "";
  DeathSound "";
  Alpha 0.000001;
  Radius 5;
  Height 5;
  Speed 50;
  Damage 0;
  +BOUNCEONWALLS;
  +NEVERFAST;
  +THRUGHOST;
  +THRUSPECIES;
  +THRUACTORS;
  +NOINTERACTION;
  +CEILINGHUGGER;
  -SOLID;
  +BRIGHT;
  BounceCount 99999999999;
  BounceFactor 1.0;
  BounceType "Hexen";
  }
  
  static const int flength[] = {8,3,8,8,12,8,15,8};
  int loop;

  States
  {
  Spawn:
    TNT1 A 0;
    TNT1 A 0 ThrustThing(random(0, 360), random(-50, 50), 0, 1);
    TNT1 AAAAAAAAAAAA 0 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 A 0 
    {
		tics = flength[random(0,7)]; // eventually = randompick(8,3,8,8,12,8,15,8);
		A_Chase(null, null, CHF_NOPLAYACTIVE);
    }
	Goto Spawn1;
  Spawn1:
    TNT1 A 0 ThrustThing(random(0, 360), random(-50, 50), 0, 1);
    TNT1 A 0 
    {
		if(loop >= 2) { Return ResolveState("Stahp"); }
		loop += 1;
		ThrustThing(random(0, 360), random(-50, 50), 0, 1);
		Return ResolveState(null);
    }
    TNT1 AAAAAAAAAAAA 0 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 AAAAAAAAA 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
    TNT1 A 0 ThrustThing(random(0, 360), random(-50, 50), 0, 1);
    TNT1 A 0
	{
		A_SpawnItemEx("DCY_FallingLapisTorpedo", zofs: -10, zvel: -30, angle: random(-88, 88), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_TorpedoAlert", zofs: -10, zvel: -99999, angle: random(-88, 88), flags: SXF_NOCHECKPOSITION);
	}
    Goto Spawn1+1;
  Death:
  Stahp:
    TNT1 A 5 A_Die();
    Stop;
  }
}

Class DCY_FallingLapisTorpedo : DCY_TransmuterTorpedo
{
  Default
  {
  Speed 1;
  Radius 8;
  Height 11;
  SeeSound "";
  }
  States
  {
  Spawn:
    HMIS B 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnFlames("174ee6", 40, 25, 0, false);
			if ((level.time % random(1, 9)) == 0) A_SpawnParticleEx(0x174ee6, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
			A_SpawnItemEx("DCY_TransmuterFlare", frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-2.5, 2.5), flags: SXF_NOCHECKPOSITION);
		}
	}
	Loop;
  }
}

Class DCY_TorpedoAlert : Actor
{
  Default
  {
  RenderStyle "Add";
  +BRIGHT
  +FLATSPRITE
  Alpha 0;
  Scale 1.2;
  }
  
  double anglestart;
  
  override void PostBeginPlay() { super.postBeginPlay(); anglestart = frandom(-12, 12); }
  override void Tick()
  {
	super.tick();
	if (!level.isFrozen()) angle += anglestart;
  }
  
  States
  {
  Spawn:
	TNT1 A 3;
	A349 AAAAA 1 A_FadeIn(0.1);
	A349 A 20;
	A349 AAAAA 1 A_FadeOut(0.1);
	Stop;
  }
}

Class DCY_SoulGrenade : DCY_GoreBase
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "16:47=%[0.00,0.00,0.15]:[0.64,1.25,2.00]", "160:191=%[0.00,0.00,0.15]:[0.64,1.26,2.00]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "208:235=%[0.00,0.00,0.15]:[0.64,1.27,2.00]", "248:249=%[0.00,0.00,0.15]:[0.64,1.28,2.00]";
  +NOGRAVITY
  }
  
  States
  {
  Spawn:
	XZXZ ABCD 4;
	Loop;
  }
}