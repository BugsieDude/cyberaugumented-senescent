Class DCY_Remainder : DCY_Rev
{
  Default
  {
  Tag "Remainder";
  SeeSound "Remainder/Sight";
  PainSound "Remainder/Pain";
  ActiveSound "Remainder/Active";
  DeathSound "Remainder/Death";
  BloodColor "Blue";
  BloodType "AndroidNeonFluid";
  StencilColor "1f53ff";
  Obituary "%o bathed in the fog of the Remainder.";
  Translation "16:47=[96,96,96]:[0,0,0]", "80:111=[192,192,192]:[0,0,0]", "4:4=[216,216,216]:[192,192,192]", "112:127=%[0.00,0.00,0.00]:[0.00,0.80,2.00]", "160:254=%[0.00,0.00,0.00]:[0.00,0.80,2.00]", "168:191=[96,96,96]:[0,0,0]", "224:224=[216,216,216]:[192,192,192]", "208:208=[216,216,216]:[192,192,192]", "255:255=%[0.00,0.00,0.00]:[0.57,0.57,0.57]";
  Health 310;
  }
  
  int phasecooldown;
  
  States
  {
  Spawn:
	R3M_ A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDDEEFF 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	R3M_ AABBCCDDEEFF 3
	{
		if ((!random(0, 60)) && phasecooldown <= 0)
		{
			SetStateLabel("Phase");
			phasecooldown = 400;
		}
		
		if (phasecooldown > 0) phasecooldown -= random(1, 3);
		DCY_Chase();
	}
	Loop;
  Phase:
	R3M_ A 10;
	R3M_ A 1 A_SpawnItemEx("DCY_HexodenicanTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL);
	TNT1 A 3
	{
		A_StartSound("Remainder/Phase");
		for (int i = random(10, 100); i > 0; i--) A_Wander();
	}
	R3M_ A 1 A_SpawnItemEx("DCY_HexodenicanTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL);
	Goto See;
  Melee:
	R3M_ G 6;
	R3M_ H 6 A_SkelWhoosh();
	R3M_ I 6 A_CustomMeleeAttack(random[SkelFist](1, 10) * 6, "skeleton/melee", damagetype: "Fire");
	Goto See;
  Missile:
	R3M_ A 15
	{
		A_FaceTarget();
		Return A_Jump(128, "Fireball");
	}
	R3M_ J 10
	{
		A_FaceTarget();
		
		for (int i = 17; i > -18; i -= 34)
			A_SpawnProjectile("DCY_RemainderRocket", 70, i, frandom(-3, 3), CMF_OFFSETPITCH, frandom(-3, 3));
	}
	R3M_ K 10;
	Goto See;
  Fireball:
	R3M_ G 2 A_StartSound("Remainder/Cast", CHAN_VOICE);
	R3M_ GGGGGGGGGGGGGGGGGGGG 2
	{
		A_FaceTarget();
		if (DCY_EffectDistance(sfxdistance)) A_SpawnParticleEx(0x1f53ff, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 60, 40, 0, frandom(-5, 5) - 10, frandom(-5, 5) + 20, frandom(-5, 5) + 36, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -2.325);
	}
	R3M_ H 5 A_FaceTarget();
	R3M_ I 5 A_SpawnProjectile("DCY_RemainderFireball", 30);
	Goto See;
  Pain:
	R3M_ L 4;
	R3M_ L 4 A_Pain();
	Goto See;
  Death:
	R3M_ M 6;
	R3M_ N 6 A_ScreamAndUnblock();
	R3M_ OP 6;
	R3M_ Q -1;
	Stop;
  Raise:
	R3M_ PONML 6;
	Goto See;
  }
}

Class DCY_RemainderRocket : DCY_HomingMissile
{
  Default
  {
  Speed 15;
  Translation "0:255=%[0.00,0.00,0.35]:[0.09,0.80,2.00]", "80:111=[255,255,255]:[0,0,0]";
  Damage 5;
  }
  
  bool home;
  
  States
  {
  Spawn:
	TMIS A 0 NoDelay A_StartSound("skeleton/seeker", CHAN_6, CHANF_LOOPING, 0.85, pitch: 1.4);
	TMIS A 2 Bright
	{
		if (!home && (!(level.time % 15)) && (tracer && distance3D(tracer) < 400))
		{
			A_StartSound("AirbusterLaser", CHAN_BODY);
			A_ScaleVelocity(0.8);
			for (int i = 2; i > 0; i--) A_SeekerMissile(999, 999, SMF_LOOK|SMF_PRECISE);
			home = true;
		}
		
		if (home && timer < 8)
		{
			timer++;
			A_Tracer2(6.25);
		}
		
		A_SpawnItemEx("DCY_FluorescentJetFlareSmall", flags: SXF_NOCHECKPOSITION);
		if (DCY_EffectDistance(sfxdistance)) A_SpawnFlames("1f53ff", 25, 30, 0, true, frandom(-2.5, 2.5), allsmoke: true);
	}
	Goto Spawn+1;
  Death:
	TMIS B 0 Bright;
  	TMIS B 6 Bright
	{
		A_Scream();
		A_StopSound(CHAN_6);
		A_QuakeEx(2, 2, 2, 17, 0, 250, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 500);
		A_SetTranslucent(1.0, 1);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	TMIS CD 6 Bright;
	Stop;
  }
}

Class DCY_RemainderFireball : DCY_ImpFireball
{
  Default
  {
  +SEEKERMISSILE
  +BRIGHT
  Damage 8;
  Speed 15;
  FastSpeed 22;
  DamageType "Fire";
  Scale 1.1;
  SeeSound "LAVABUBL";
  DeathSound "DCYLavaImpact";
  Translation "0:255=%[0.00,0.00,0.24]:[0.94,1.36,2.00]";
  }
  
  States
  {
  Spawn:
	C3AL AB 1
	{
		if (tracer && CheckSight(tracer)) A_Tracer2(3.25);
		A_SpawnParticleEx(0x1f53ff, Texman.CheckforTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(1.5, 7.4), accelz: -0.55);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		A_SpawnFlames("1f53ff", 40, 40, allsmoke: true);
	}
	Loop;
  Death:
	C3AL A 1
	{
		A_Explode(12, 180);
		
		for (int i = 25; i > 0; i--)
			A_SpawnParticleEx(0x1f53ff, Texman.CheckforTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_TRANSFERTRANSLATION);
	}
	C3AL BCDE 1;
	Stop;
  }
}