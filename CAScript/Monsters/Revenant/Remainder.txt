Class DCY_Remainder : DCY_Rev
{
  Default
  {
  DamageType "Fire";
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.25;
  Tag "Remainder";
  SeeSound "Remainder/Sight";
  PainSound "Remainder/Pain";
  ActiveSound "Remainder/Active";
  DeathSound "Remainder/Death";
  BloodColor "Orange";
  Translation "16:47=%[0.00,0.00,0.00]:[0.65,0.65,0.65]", "255:255=%[0.00,0.00,0.00]:[0.65,0.65,0.65]", "169:191=%[0.00,0.00,0.00]:[0.65,0.65,0.65]", "112:115=[255,255,255]:[255,255,128]", "116:120=[255,255,128]:[255,128,0]", "121:125=[255,128,0]:[64,0,0]", "125:127=[64,0,0]:[0,0,0]", "0:0=[0,0,0]:[255,255,255]", "0:0=[0,0,0]:[255,255,255]", "0:0=[0,0,0]:[255,255,255]", "83:83=[0,0,0]:[255,255,255]";
  Health 388;
  }
  
  States
  {
  Spawn:
	R3M_ A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDDEEFF 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	R3M_ AABBCCDDEEFF 3 A_Chase();
	Loop;
  Melee:
	R3M_ G 6;
	R3M_ H 6 A_SkelWhoosh();
	R3M_ I 6 A_CustomMeleeAttack(random[SkelFist](1, 10) * 6, "skeleton/melee", damagetype: "Fire");
	Goto See;
  Missile:
	R3M_ A 15
	{
		A_FaceTarget();
		Return A_Jump(128, "Fireball");
	}
	R3M_ JJJJJ 4
	{
		A_FaceTarget();
		
		for (int i = 17; i > -18; i -= 34)
			A_SpawnProjectile("DCY_SmallFireball", 70, i, frandom(-3, 3), CMF_OFFSETPITCH, frandom(-3, 3));
	}
	R3M_ K 10;
	Goto See;
  Fireball:
	R3M_ G 2 A_StartSound("Remainder/Sight", CHAN_VOICE);
	R3M_ GGGGGGGGGGGGGG 2
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-5, 5) - 10, frandom(-5, 5) + 20, frandom(-5, 5) + 36, zvel: 2, flags: SXF_NOCHECKPOSITION);
	}
	R3M_ H 5 A_FaceTarget();
	R3M_ I 5 A_SpawnProjectile("DCY_RemainderFireball", 30);
	Goto See;
  Pain:
	R3M_ L 4;
	R3M_ L 4 A_Pain();
	Goto See;
  Death:
	R3M_ M 6;
	R3M_ N 6 A_ScreamAndUnblock();
	R3M_ OP 6;
	R3M_ Q -1;
	Stop;
  Raise:
	R3M_ PONML 6;
	Goto See;
  }
}

Class DCY_RemainderFireball : DoomImpBall
{
  Default
  {
  +SEEKERMISSILE
  +BRIGHT
  Damage 8;
  Speed 15;
  FastSpeed 22;
  DamageType "Fire";
  Scale 1.1;
  SeeSound "LAVABUBL";
  DeathSound "DCYLavaImpact";
  Translation "0:255=%[0.25,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  States
  {
  Spawn:
	C3AL AB 1
	{
		A_Tracer2(4.5);
		A_SpawnParticle(0xffdc9c, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(1.5, 7.4), accelz: -0.55);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	Loop;
  Death:
	C3AL A 1
	{
		A_Explode(8, 180);
		
		for (int i = 25; i > 0; i--)
			A_SpawnParticle(0xffdc9c, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		
		A_SpawnItemEx("DCY_Explosion_1");
	}
	C3AL BCDE 1;
	Stop;
  }
}

Class DCY_SmallFireball : DCY_RemainderFireball
{
  Default
  {
  Damage 2;
  Speed 20;
  FastSpeed 23;
  Scale 0.35;
  Radius 4;
  Height 8;
  SeeSound "Painsend/Fireball";
  DeathSound "FIR4IMPC";
  }
  
  States
  {
  Spawn:
	BAL7 AB 1
	{
		A_Tracer2(1);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	Loop;
  Death:
	C3AL A 1
	{
		for (int i = 6; i > 0; i--)
			A_SpawnParticle(0xffdc9c, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		
		A_SpawnItemEx("DCY_Explosion_1_Smaller");
	}
	C3AL BCDE 1;
	Stop;
  }
}