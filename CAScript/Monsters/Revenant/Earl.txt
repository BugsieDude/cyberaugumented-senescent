Class DCY_DestructiveEarl : DCY_SoulCrusher
{
  Default
  {
  Health 1000;
  DamageFactor 0.5;
  Translation "16:47=%[0.37,0.00,0.00]:[2.00,0.71,0.02]", "48:79=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "128:159=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "80:111=%[0.00,0.00,0.00]:[0.51,0.51,0.51]", "1:3=%[0.00,0.00,0.00]:[0.51,0.51,0.51]", "5:8=%[0.00,0.00,0.00]:[0.51,0.51,0.51]", "168:191=%[0.37,0.00,0.00]:[2.00,0.71,0.02]";
  BloodColor "DarkRed";
  SeeSound "Earl/Sight";
  ActiveSound "Earl/Active";
  PainSound "Earl/Pain";
  DeathSound "Earl/Death";
  Tag "\c[x1]Destructive \c[d4]Earl\c-";
  +QUICKTORETALIATE
  +BOSS
  PainChance 30;
  PainThreshold 500;
  DCYMonster.GibType "DCY_MechaGiblets";
  +DCYMONSTER.DCYNORMALBOSS;
  DCYMonster.FootstepFrames 0, 2, 4;
  DCYMonster.FootstepSound "dcyabater/walk";
  }
  
  int missilecount;
  
  void A_EarlBombs(int dist)
  {
	A_FaceTarget();
	A_SpawnProjectile("DCY_LaserBombBase", 80, dist, frandom(-5, 5), CMF_OFFSETPITCH|CMF_TRACKOWNER, pitch - 3);
	A_SpawnProjectile("DCY_LaserBombBase", 80, -dist, frandom(-5, 5), CMF_OFFSETPITCH|CMF_TRACKOWNER, pitch - 3);
  }
  
  States
  {
  Spawn:
	E3RL A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	E3RL AABBCCDD 4 DCY_Chase();
	Loop;
  Melee:
  Missile:
	E3RL E 0
	{
		missilecount = 0;
		
		if (distance3D(target) > 400)
			Return A_Jump(256, "LaserBombs", "Grenades", "Rockets");
		else
			Return A_Jump(256, "Rockets", "Grenades");
	}
	Goto Rockets;
  Rockets:
	E3RL E 20 A_FaceTarget();
	E3RL F 3
	{
		for (int i = 19; i > -20; i -= 38)
			A_SpawnProjectile("DCY_EarlRocket", 45, i, frandom(-5, 5), CMF_OFFSETPITCH|CMF_TRACKOWNER, pitch - frandom(-2, 2));
	}
	E3RL E 2
	{
		A_SetTics(random(1, 7));
		A_FaceTarget();
		missilecount++;
		
		if (missilecount >= 7)
			SetStateLabel("See");
		
		Return A_Jump(20, "Grenades");
	}
	Goto Rockets+1;
  Grenades:
	E3RL E 20 A_FaceTarget();
	E3RL F 3
	{
		for (int i = 19; i > -20; i -= 38)
			A_SpawnProjectile("DCY_EarlGrenade", 45, i, frandom(-20, 20), CMF_OFFSETPITCH|CMF_TRACKOWNER, pitch - frandom(8, 20));
	}
	E3RL E 2
	{
		A_SetTics(random(1, 4));
		A_FaceTarget();
		missilecount++;
		
		if (missilecount >= 10)
			SetStateLabel("See");
		
		Return A_Jump(20, "Rockets");
	}
	Goto Grenades+1;
  LaserBombs:
	E3RL E 10 A_FaceTarget();
	E3RL G 3 A_EarlBombs(18);
	E3RL E 10 { A_SetTics(random(8, 15)); A_FaceTarget(); }
	E3RL G 3 A_EarlBombs(22);
	E3RL E 10
	{
		A_SetTics(random(8, 15));
		A_FaceTarget();
		missilecount++;
		
		if (missilecount >= 2)
			SetStateLabel("See");
	}
	Goto Missile+1;
  Pain:
	E3RL J 4;
	E3RL J 4 A_Pain();
	Goto See;
  Death:
	E3RL J 2;
	E3RL J 15 A_Scream();
	E3RL PJPJPJPJPJPJ 3;
	E3RL P 1
	{
		A_Explode(128, 256, XF_NOTMISSILE);
		A_StartSound("BigExplod", CHAN_BODY, attenuation: 0.5);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Biggie", random(-10, 10), random(-10, 10), random(30, 70), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION);
	}
	E3RL K 6 A_NoBlocking();
	E3RL LMNO 5
	{
		DCY_Gibs(false);
		for (int i = random(1, 2); i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", random(-10, 10), random(-10, 10), random(30, 70), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(0, 360), SXF_NOCHECKPOSITION);
	}
	E3RL O -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_EarlRocket : DCY_OverhauledRocket
{
  Default
  {
  DamageFunction 2;
  Speed 25;
  FastSpeed 30;
  Scale 0.785;
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	64MS A 1
	{
		A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_MastermindJetFlareSmall", flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	M_TR C 1;
	M_TR C 3
	{
		A_SetScale(1.5);
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_StopSound(CHAN_WEAPON);
		A_StartSound("Explod", CHAN_7, attenuation: 0.5);
		A_Explode(35, 200, XF_NOTMISSILE, 1);
		A_QuakeEx(3, 3, 3, 50, 0, 600, "none", QF_SCALEDOWN, 0, 0, 0, 1255);
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION);
	}
	M_TR DEFGH 3;
	Stop;
  }
}
  
Class DCY_EarlGrenade : DCY_EarlRocket
{
  int bombtimer;
  
  Default
  {
  -NOGRAVITY
  Gravity 0.75;
  Speed 20;
  FastSpeed 30;
  BounceType "Doom";
  BounceFactor 0.6;
  Scale 0.95;
  Translation "128:159=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "48:79=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "5:15=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "1:2=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  }
  
  States
  {
  Spawn:
	MNAD A 1 A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
	Loop;
  Death:
	NBKL A 1;
	NBKL A 3
	{
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_StopSound(CHAN_WEAPON);
		A_StartSound("Explod", CHAN_7, attenuation: 0.5);
		A_Explode(35, 200, XF_NOTMISSILE, 1);
		A_QuakeEx(3, 3, 3, 50, 0, 600, "none", QF_SCALEDOWN, 0, 0, 0, 1255);
		A_SpawnItemEx("DCY_Explosion_1", zofs: 25, flags: SXF_NOCHECKPOSITION);
	}
	NBKL CDEGHIJKLM 3;
	Stop;
  }
}