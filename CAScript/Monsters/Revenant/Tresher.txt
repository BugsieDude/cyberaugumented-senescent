Class DCY_TresherDoot : DCY_Rev
{
  Default
  {
  Health 600;
  Tag "\c[c4]Tresher\c-";
  Translation "168:173=[255,255,255]:[255,128,128]", "174:180=[255,128,128]:[255,0,0]", "181:191=[255,0,0]:[64,0,0]", "224:231=[255,255,255]:[255,128,128]", "208:216=[255,255,255]:[255,128,128]", "217:223=[255,128,128]:[255,66,66]", "232:235=%[0.00,0.00,0.00]:[2.00,0.00,0.00]", "236:239=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[255,83,83]:[64,0,0]", "16:23=[255,255,255]:[255,128,128]", "24:31=[255,128,128]:[255,0,0]", "32:47=[255,0,0]:[64,0,0]", "192:195=[255,255,255]:[255,128,128]", "196:201=[255,128,128]:[255,0,0]", "202:207=[255,0,0]:[64,0,0]", "112:119=[255,0,0]:[128,0,0]", "120:127=[119,0,0]:[64,0,0]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  +NOBLOOD
  Speed 10;
  FastSpeed 15;
  SeeSound "angydoot/sight";
  ActiveSound "";
  PainSound "angydoot/pain";
  DeathSound "angydoot/death";
  Obituary "$o was trashed by a Tresher, a skeleton of bloodlust.";
  HitObituary "$o was trashed by a Tresher, a skeleton of bloodlust.";
  +MISSILEMORE
  +MISSILEEVENMORE
  +NOTARGET
  }
  
  void A_TresherShot()
  {
	A_StartSound("LASER001", CHAN_WEAPON);
	A_SpawnProjectile("DCY_TresherL", 68, -16);
	A_FaceTarget();
  }
  
  bool fast;
  
  States
  {
  Spawn:
	SKE2 A 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDDEEEFFF 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	SK14 A 3
	{
		A_FaceTarget();
		A_QuakeEx(3, 3, 0, 50, 0, 600, "", QF_SCALEDOWN|QF_3D);
		DCY_AlertSound();
	}
	SK14 BCDEFGHDEFGHDEFGH 3 A_FaceTarget();
	Goto See;
  See:
	SKE2 AABBCCDD 4
	{
		DCY_Chase();
		if (random(0, 60)) fast = (!fast) ? true : false;
		tics = (fast) ? 2 : 4;
	}
	Loop;
  Missile:
	SKE2 A 1 A_JumpIf(target && Distance3D(target) < 350, "REEEEE");
	Goto MissileNormalThing;
  Melee:
	SKE2 A 25 A_FaceTarget();
	SKE2 JK 5 A_FaceTarget();
	SKE2 L 5 A_CustomMeleeAttack(150, "BigHit", "", "Normal", True);
	Goto See;
  REEEEE:
	SKE2 J 10 { A_FaceTarget(); A_StartSound("angydoot/pain", CHAN_VOICE, 0, 1.0, 0.75); }
	SKE2 JK 7 A_FaceTarget();
	SKE2 L 1
	{
		A_Recoil(-23);
		A_ChangeVelocity(0, 0, randompick(3, 5, 6, 8, 12, 13), CVF_RELATIVE);
		A_FaceTarget();
		A_CustomMeleeAttack(250, "BigHit", "", "Normal", True);
	}
	SKE2 LLLLLLLLLLLLLLLLLLLL 1 A_SpawnItemEx("DCY_TresherTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	SKE2 A 5 A_Stop();
	SKE2 A 1 A_Jump(128, "MissileNormalThing");
	Goto See;
  MissileNormalThing:
	SKE2 G 10
	{
		A_StartSound("angydoot/grunt", CHAN_VOICE, 0, 1.0, 0.75);
		A_FaceTarget();
	}
	SKE2 H 4 A_TresherShot();
	SKE2 G 4 A_FaceTarget();
	SKE2 H 4 A_TresherShot();
	SKE2 G 4 A_FaceTarget();
	SKE2 H 4 A_TresherShot();
	SKE2 G 4 A_FaceTarget();
	SKE2 H 4 A_TresherShot();
	SKE2 G 4 A_FaceTarget();
	SKE2 H 4 A_TresherShot();
	SKE2 G 3 A_FaceTarget();
	SKE2 G 5 A_FaceTarget();
	SKE2 I 16;
	Goto See;
  Pain:
	SKE2 M 4;
	SKE2 M 4 A_Pain();
	Goto See;
  Death:
	SKE2 M 10
	{
		if (!random(0, 1)) SetStateLabel("Death2");
	}
	SKE2 M 1 A_ScreamAndUnblock();
	SKE2 NOPQR 6;
	SKE2 S -1;
	Stop;
  Death2:
	SK3L M 10;
	SK3L N 7 A_Scream();
	SK3L O 6 A_NoBlocking();
	SK3L P 6;
	SK3L Q -1;
	Stop;
  Raise:
	SKE2 RQPONM 6;
	Goto See;
  }
}

Class DCY_TresherL : DCY_TLaser { Default { SeeSound ""; DeathSound ""; Speed 55; +NEVERFAST Scale 0.6; Damage 5; } }