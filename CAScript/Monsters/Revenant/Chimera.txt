Class DCY_Chimera : DCY_Rev
{
  Default
  {
  BloodColor "2b50d6";
  BloodType "DCY_ElecDeathBase";
  Translation "0:224=%[0.00,0.00,0.00]:[0.43,0.43,0.43]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "80:111=[192,192,192]:[10,10,10]", "16:47=%[0.00,0.00,0.18]:[0.37,0.57,1.74]", "168:191=%[0.00,0.24,1.61]:[1.03,1.37,2.00]", "232:239=%[0.00,0.17,0.47]:[0.00,0.59,1.59]";
  SeeSound "PhantomRev/Scream";
  ActiveSound "PhantomRev/Active";
  PainSound "PhantomRev/Pain";
  DeathSound "LunarChiller/Death";
  Speed 8;
  FastSpeed 13;
  Tag "Chimera";
  Obituary "%o's lost %p hearing sensory system.";
  -MISSILEMORE
  +NOINFIGHTING
  +DONTHARMSPECIES
  +DONTHARMCLASS
  Health 400;
  }
  
  bool rush;
  bool enraged;
  
  States
  {
  Spawn:
	INCA A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDDEEFF 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	INCA AABBCCDDEEFF 3
	{
		if (rush)
		{
			tics = 1;
			A_SpawnItemEx("DCY_GTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		}
		else
			tics = 3;
		
		A_Chase();
		
		return A_Jump(10, "Rush");
	}
	Loop;
  Rush:
	INCA A 1;
	INCA A 10 { rush = !rush; }
	Goto See;
  Melee:
	INCA G 6 A_FaceTarget();
	INCA HHHH 2
	{
		A_Recoil(-2);
		A_FaceTarget();
	}
	INCA I 6 A_CustomMeleeAttack(10 * random(3, 8), "skeleton/melee");
	Goto See;
  Missile:
	INCA J 0
	{
		if (target && distance2D(target) < 260)
			SetStateLabel("Scream");
	}
	Goto Souls;
  Souls:
	INCA J 10 A_FaceTarget();
	INCA KKKK 6 { A_FaceTarget(); A_SpawnProjectile("DCY_ChimeraSoul", 60, randompick(-20, 20)); }
	INCA K 10;
	Goto See;
  Scream:
	INCA J 10 A_FaceTarget();
	INCA U 1
	{
		A_StartSound("PhantomRev/Scream", CHAN_VOICE, attenuation: 0.75);
		A_QuakeEx(3, 3, 3, 30, 0, 600, "", QF_SCALEDOWN|QF_3D, rollintensity: 3, rollwave: 0.1);
		A_Explode(6, 380, XF_NOTMISSILE);
	}
	INCA VVVVVVVVVVVVVVVVV 1
	{
		A_QuakeEx(3, 3, 3, 30, 0, 600, "", QF_SCALEDOWN|QF_3D, rollintensity: 3, rollwave: 0.1);
		A_Explode(6, 380, XF_NOTMISSILE);
	}
	INCA J 10;
	Goto See;
  Pain:
	INCA L 4;
	INCA L 4 A_Pain();
	Goto See;
  Death:
	INCX A 4;
	INCX B 4 A_ScreamAndUnblock();
	INCX CDEFGHIJ 4
	{
		if (DCY_EffectDistance(sfxdistance)) for (int i = random(2, 3); i > 0; i--) DCY_SoulExplode();
		A_SpawnItemEx("DCY_EspouserShriekingSoul", zofs: 30, xvel: frandom(10, 30), zvel: frandom(-10, 10), angle: frandom(0, 360), flags: SXF_NOCHECKPOSITION, failchance: 50);
	}
	INCX K -1;
	Stop;
  Death.Massacre:
	INCX A 4;
	INCX B 4 A_ScreamAndUnblock();
	INCX CDEFGHIJ 4;
	INCX K -1;
	Stop;
  }
}

Class DCY_ChimeraSoul : DCY_EspouserSoul
{
  Default
  {
  SeeSound "DCYGhostProjectile";
  DeathSound "imp/shotx";
  Speed 30;
  Scale 0.5;
  }
  
  States
  {
  Spawn:
	S0UX L 1
	{
		A_Tracer2(2);
		A_SpawnItemEx("DCY_RocketSmonk", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(1, -1), frandom(1, -1), frandom(1, -1), frandom(0, 360), SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
	}
	Loop;
  }
}