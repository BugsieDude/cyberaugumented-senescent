Class DCY_GloomKnight : DCY_Rev
{
  Default
  {
  Health 350;
  DamageFactor 0.995;
  Speed 10;
  SeeSound "GKnight/Sight";
  ActiveSound "GKnight/Active";
  PainSound "GKnight/Pain";
  DeathSound "GKnight/Death";
  DropItem "DCY_Eminence", 192;
  Tag "\c[j1]Gloom Knight\c-";
  +NOBLOOD
  }
  
  States
  {
  Spawn:
    GK38 A 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    GK38 AAABBBCCCDDD 2 A_Chase();
	Loop;
  Melee:
    GK38 A 0
	{
		if (random(0, 1) == 0)
			SetStateLabel("Melee2");
	}
	Goto Melee1;
  Melee1:
    GK38 EF 6 A_Facetarget();
	GK38 G 6 A_CustomMeleeAttack(15, "hknight/melee");
	Goto See;
  Melee2:
    GK38 HI 6 A_Facetarget();
	GK38 J 6 A_CustomMeleeAttack(15, "hknight/melee");
	Goto See;
  Missile:
    GK38 EF 6 A_FaceTarget();
	GK38 G 6
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_GAxe", 50, 0, random(-3, 3), 0);
	}
	Goto See;
  Pain:
    GK38 K 8 A_Pain();
	Goto See;
  Death:
  XDeath:
    GK38 K 6;
	GK38 L 6 A_Scream();
	TNT1 A 0 A_NoBlocking();
	GK38 MNO 6 { if (!random(0, 1)) DCY_SoulExplode(); }
	GK38 P -1;
	Stop;
  Raise:
    GK38 PONM 6;
	Goto See;
  }
}

Class DCY_GAxe : DCY_WAxe
{
  Default
  {
  Translation "48:79=%[0.00,0.00,0.00]:[1.18,0.61,0.51]", "128:159=%[0.00,0.00,0.00]:[0.76,0.76,0.76]", "80:111=%[0.00,0.00,0.00]:[0.76,0.76,0.76]", "232:239=%[0.00,0.00,0.00]:[1.19,0.61,0.51]", "112:127=%[0.00,0.00,0.24]:[0.42,0.66,2.00]", "160:231=%[0.00,0.00,0.24]:[0.42,0.66,2.00]", "248:255=%[0.00,0.00,0.24]:[0.42,0.66,2.00]";
  Damage 6;
  Speed 12;
  FastSpeed 18;
  StencilColor "00258a";
  DamageType "Poison";
  }
  States
  {
  Spawn:
	XXP_ AAABBBCCC 1
	{
		if ((level.time % 9) == 0)
			A_StartSound("hknight/axewhoosh");
		
		A_SpawnItemEx("DCY_BaronKnightFlame", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 150);
		A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSTENCILCOL);
		A_Tracer2(2);
	}
	Loop;
  }
}
