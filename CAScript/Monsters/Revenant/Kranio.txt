Class DCY_KranioThanatou : DCY_Rev
{
  Default
  {
  Translation "168:191=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "224:231=[255,255,255]:[191,255,128]", "208:223=[255,255,255]:[158,255,51]", "232:235=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "236:239=%[0.00,0.00,0.00]:[0.48,0.48,0.48]", "48:79=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "128:159=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "13:15=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "160:167=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "16:47=%[0.00,0.00,0.00]:[1.29,2.00,0.55]", "192:207=%[0.00,0.00,0.00]:[1.29,2.00,0.55]";
  +NOBLOOD;
  Health 600;
  SeeSound "D4Revenant/Sight";
  ActiveSound "D4Revenant/Idle";
  PainSound "D4Revenant/Pain";
  DeathSound "D4Revenant/Death";
  Speed 15;
  FastSpeed 20;
  Tag "\c[j5]Kranio Thanatou\c-";
  +MISSILEMORE
  +DONTHARMSPECIES
  +BRIGHT
  }
  
  double direction;
  int missile;
  bool dash;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	direction = randompick(14, -14);
  }
  
  static const string ParticleColors[] = { "ffffff", "f6ffcc", "cdff87" };
  
  void A_KranioChase()
  {
	A_Chase();
	
	if (random(0, 68) == 0)
		dash = true;
		
	if (dash == true)
	{
		tics = 1;
		A_SpawnItemEx("DCY_GFTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
		
		if (random(0, 70) == 0)
			dash = false;
	}
	
	if (random(0, 65) == 0)
		A_Dodging(10);
	if (random(0, 65) == 3 && vel.z == 0)
	{
		A_Dodging(5);
		A_Hop(7);
	}
  }
  
  States
  {
  Spawn:
	KRAV A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### J 10 DCY_AlertSound();
	Goto See;
  See:
	KRAV AABBCCDDEEFF 2 A_KranioChase();
	Loop;
  Melee:
	KRAV A 10 A_FaceTarget();
	Goto See;
  Missile:
	KRAV A 10 A_FaceTarget();
	KRAV J 1
	{
		if (health < 300)
		{
			if (random(0, 3) == 0)
			{
				missile = 20;
				A_StartSound(SeeSound, CHAN_VOICE);
				SetStateLabel("Barrage");
			}
		}
		
		if (random(0, 2) == 0)
		{
			A_Hop(10);
			A_Dodging(7);
		}
	}
	KRAV JJJJ 6
	{
		direction = direction * -1;
		A_SpawnProjectile("DCY_GFRocket", 58, direction, frandom(-2, 2));
		A_FaceTarget();
	}
	KRAV K 10;
	Goto See;
  Barrage:
	KRAV K 1 A_StartSound("Kranio/RocketCharge", CHAN_WEAPON, attenuation: 0.5);
	KRAV KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 1 A_FaceTarget();
	Goto BarrageLoop;
  BarrageLoop:
	KRAV J 3
	{
		direction = -direction;
		
		A_SpawnProjectile("DCY_GFMicroRocket", 58, direction, frandom(-5, 5), CMF_OFFSETPITCH, pitch + frandom(-2, 2));
		A_FaceTarget();
		
		missile--;
		if (missile <= 0)
			SetStateLabel("See");
	}
	Loop;
  Pain:
	KRAV L 4;
	KRAV L 4 A_Pain();
	Goto See;
  Death:
	KRAV L 5;
	KRAV M 5 A_ScreamAndUnblock();
	KRAV NO 4;
	KRAV P -1;
	Stop;
  Raise:
	KRAV PONML 6;
	Goto See;
  }
}

Class DCY_GFTrail : DCY_HexodenicanTrail
{
  Default
  {
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.59,1.19,0.00]";
  Scale 1.0;
  }
}

Class DCY_GFRocket : DCY_HomingMissile
{
  Default
  {
  Translation "80:111=%[0.00,0.00,0.00]:[0.75,0.75,0.75]", "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:255=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
  Speed 16;
  FastSpeed 21;
  DamageType "Fire";
  -ROCKETTRAIL
  }
  
  States
  {
  Spawn:
	HMIS A 0 NoDelay A_StartSound("skeleton/seeker", CHAN_6, CHANF_LOOPING, pitch: 1.375);
	HMIS A 2 Bright
	{
		A_SpawnItemEx("DCY_HyperclusterCommanderJetFlareSmall", frandom(-1, 1) - 16, frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_RocketSmonk", frandom(-1, 1) - 16, frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
		A_Weave(3, 3, 1, 1);
		A_Tracer2(2);
	}
	Goto Spawn+1;
  Death:
	MISL B 0 Bright;
  	TNT1 A 1 Bright
	{
		A_Scream();
		A_StopSound(CHAN_6);
		A_QuakeEx(2, 2, 2, 17, 0, 250, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 500);
		A_SetTranslucent(1.0, 1);
		A_SpawnItemEx("DCY_GreenFireEXPSmall", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_GreenFireEXPSmall", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
	}
	TNT1 A 17;
	Stop;
  }
}

Class DCY_GFMicroRocket : FastProjectile
{
  Default
  {
  Translation "80:111=%[0.00,0.00,0.00]:[0.75,0.75,0.75]", "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:255=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
  Speed 55;
  FastSpeed 60;
  DamageType "Fire";
  -ROCKETTRAIL
  Damage 4;
  Scale 1.1;
  MissileType "DCY_GFSmonk";
  MissileHeight 6;
  SeeSound "skeleton/attack";
  DeathSound "skeleton/tracex";
  }
  
  States
  {
  Spawn:
	SMOL A 1;
	Loop;
  Death:
  	TNT1 A 1 Bright
	{
		A_Scream();
		A_StopSound(CHAN_6);
		A_QuakeEx(2, 2, 2, 17, 0, 250, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 500);
		A_SetTranslucent(1.0, 1);
		A_SpawnItemEx("DCY_GreenFireEXPSmall", flags: SXF_NOCHECKPOSITION);
	}
	TNT1 A 17;
	Stop;
  }
}

Class DCY_GFSmonk : DCY_Smonk
{
  Default
  {
  Scale 0.35;
  Alpha 0.45;
  Translation "0:255=%[0.00,0.00,0.00]:[0.77,1.54,0.00]";
  }
  
  States
  {
  Spawn:
	SM03 A 0 NoDelay
	{
		A_SetRoll(frandom(0, 360));
		alpha = frandom(0.55, 0.65);
	}
	SM03 ABCDEF 1 A_FadeOut(0.05, 0);
	Stop;
  }
}

Class DCY_GreenFireEXP : DCY_Explosion_1 { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.77,1.54,0.00]"; } }
Class DCY_GreenFireEXPSmall : DCY_Explosion_1_Small { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.77,1.54,0.00]"; } }