Class DCY_VerdictPartisan : DCY_Rev
{
  Default
  {
  Tag "\c[a1]Verdict Partisan\c-";
  Translation "80:111=[104,114,151]:[6,0,0]", "112:127=[128,0,0]:[0,0,128]", "192:207=[128,0,255]:[64,0,64]", "48:79=[0,0,128]:[64,0,0]", "144:151=[0,0,128]:[64,0,0]", "128:143=[0,0,128]:[128,0,0]", "160:167=[255,0,0]:[64,0,128]", "208:223=[255,255,255]:[105,0,210]", "224:231=[255,255,255]:[187,119,255]", "16:47=[128,0,255]:[64,0,64]";
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +DONTGIB
  +ALWAYSFAST
  +NOBLOOD
  Obituary "%o was extirpated by a Verdict Partisan.";
  HitObituary "%o got fucked up by a Verdict Partisan's melee power.";
  SeeSound "VPartisan/Sight";
  ActiveSound "VPartisan/Active";
  PainSound "VPartisan/Pain";
  DeathSound "VPartisan/Death";
  MeleeSound "hknight/melee";
  Health 805;
  Speed 11;
  MeleeDamage 12;
  }
  States
  {
  Spawn:
    XXX_ C 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    XXX_ AAABBB 2 A_Chase(null, null);
	XXX_ CCCDDD 2 A_Chase();
	Loop;
  Melee:
  Missile:
    TNT1 A 0 A_Jump(40, "Wave");
    XXX_ E 10 A_FaceTarget();
	XXX_ FEFEFEFEF 4 Bright
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_UmbraLaser", 41, 4, random(-1, 1));
		Return A_Jump(40, "See");
	}
	Goto See;
  Wave:
    XXX_ I 0 A_StartSound("R3WAVCHR", CHAN_6, 0, 1.0, 0.45);
    XXX_ EEEEE 5 A_FaceTarget();
	XXX_ F 20
	{
		A_StartSound("R3WAVE", CHAN_WEAPON, 0, 1.0, 0.45);
		A_SpawnProjectile("DCY_VWave", 30, 0, random(-1, 1));
	}
	Goto See;
  Pain:
    XXX_ G 4;
    XXX_ G 4 A_Pain();
	Goto See;
  Death:
  XDeath:
    XXX_ G 1;
	XXX_ G 15 A_ScreamAndUnblock();
	XXX_ HIJKLM 7;
	XXX_ M -1;
	Stop;
  Raise:
    XXX_ LKJIH 8;
	Goto See;
  }
}

Class DCY_VWave : KnightAxe
{
  Default
  {
  Scale 2.5;
  RenderStyle "Add";
  Speed 30;
  Damage 21;
  +SEEKERMISSILE
  +NEVERFAST
  +BRIGHT
  }
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_SeekerMissile(1, 1);
		A_SpawnItemEx("DCY_VWaveEffects", 0, 0, -10, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), random(360, 0), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_VWaveEffects", 0, 0, 0, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), random(360, 0), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_VWaveEffects", 0, 0, 10, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), random(360, 0), SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		for (int i = 9; i > 0; i--)
			A_SpawnItemEx("DCY_VWaveEffects", 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION);
	}
	Stop;
  }
}

Class DCY_VWaveEffects : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Alpha 0.7555;
  Scale 0.7555;
  RenderStyle "Add";
  +NOINTERACTION
  +NOCLIP
  +BRIGHT
  }
  States
  {
  Spawn:
	EF1_ M 1
	{
		If (Alpha < 0.000000000000000000001)
			Destroy();
		
		A_SetScale(Scale.X-0.001, Scale.Y-0.001);
		A_FadeOut(0.05);
	}
	Loop;
  }
}
