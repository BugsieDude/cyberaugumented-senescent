Class DCY_Warwire : DCY_Rev
{
  Default
  {
  Tag "\c[n0]Warwire\c-";
  Speed 10;
  FastSpeed 14;
  SeeSound "Warwire/Sight";
  ActiveSound "Warwire/Active";
  PainSound "Warwire/Pain";
  DeathSound "Warwire/Death";
  MeleeSound "hknight/melee";
  Health 700;
  Mass 250;
  MeleeDamage 15;
  MeleeThreshold 200;
  DamageType "WarwireDamageType";
  DamageFactor "WarwireDamageType", 0.00;
  Obituary "%o could not fathom the Warwire.";
  HitObituary "%o couldn't resist.";
  +MISSILEMORE
  +QUICKTORETALIATE
  +NODROPOFF
  +ALWAYSFAST
  +NOBLOOD
  Translation "128:159=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "13:15=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "48:79=%[0.00,0.00,0.00]:[0.81,0.81,0.81]", "236:239=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "80:111=%[0.00,0.00,0.00]:[0.42,0.42,0.42]", "1:3=%[0.00,0.00,0.00]:[0.42,0.42,0.42]", "115:123=[128,255,255]:[0,0,0]", "112:114=[255,255,255]:[128,255,255]", "124:127=[0,0,0]:[0,0,0]", "16:47=%[0.00,0.31,0.31]:[0.00,2.00,2.00]", "168:191=%[0.00,0.31,0.31]:[1.01,2.00,2.00]", "208:255=%[0.00,0.00,0.00]:[0.00,2.00,2.00]", "160:167=%[0.00,0.00,0.00]:[0.00,2.00,2.00]";
  DCYMonster.FootstepFrames 0, 2, 4;
  }
  States
  {
  Spawn:
    HEK_ A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 { if (!random(0, 1)) SetStateLabel("BattleCry"); DCY_AlertSound(); }
	Goto See;
  BattleCry:
	H6X_ AB 6 A_FaceTarget();
	H6X_ B 0 DCY_AlertSound();
	H6X_ CDEDCDED 3 A_FaceTarget();
	Goto See;
  See:
    TNT1 A 0 A_Jump(128, "Dash");
    HEK_ AABBCCDD 3 DCY_Chase();
	Loop;
  Flee:
    HEK_ A 0
	{
		bFrightened = true;
		bNoPain = true;
	}
	Goto Dash;
  Dash:
    TNT1 A 0 A_StartSound("AM2DASH", CHAN_6, CHANF_DEFAULT, 1.0, 2);
	HEK_ AABBCCDD 2
	{
		A_SpawnItemEx("DCY_WarWireTrail", flags: SXF_NOCHECKPOSITION||SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		DCY_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		
		if (Health < 600)
		{
			for (int i = 2; i > 0; i--)
				DCY_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		}
	}
	TNT1 A 0 A_Jump(95, "DashDone");
	Goto Dash+1;
  DashDone:
    HEK_ A 0
	{
		bFrightened = false;
		bNoPain = false;
	}
	Goto See;
  Melee:
    HEK_ FE 10 A_FaceTarget();
	HEK_ E 5
	{
		A_Hop(3);
		
		If (Health < 1000) { A_Recoil(-33); }
		If (Health < 650) { A_Recoil(-25); }
		If (Health < 300) { A_Recoil(-15); }
		If (Health < 100) { A_FaceTarget(); }
	}
	HEK_ F 5;
	HEK_ G 6 A_CustomMeleeAttack(10 * random(1, 10), "hknight/melee");
	Goto See;
  Missile:
	TNT1 A 0 A_Jump(75, "Laser");
	Goto NormalMissile;
  NormalMissile:
    HEK_ EF 8
	{
		A_FaceTarget();
		if (Health < 895) { A_SetTics(7); }
		if (Health < 625) { A_SetTics(6); }
		if (Health < 450) { A_SetTics(5); }
		if (Health < 215) { A_SetTics(4); }
	}
    HEK_ G 8
	{
		A_StartSound("dcy/axefall", CHAN_WEAPON);
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2), CMF_OFFSETPITCH, pitch - random(10, 15));
	}
	HEK_ FEF 8 {
		if (Health < 500) A_FaceTarget(); else SetStateLabel("See");
	}
	HEK_ G 8
	{
		if (Health < 500)
		{
			A_FaceTarget();
			A_StartSound("dcy/axefall", CHAN_WEAPON);
			A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2), CMF_OFFSETPITCH, pitch - random(10, 15));
		}
		else SetStateLabel("See");
	}
    Goto See;
  Laser:
    HEK_ FE 9 A_FaceTarget();
	HEK_ EEE 5
	{
		A_FaceTarget();
		A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
		A_SpawnProjectile("DCY_WRay", 82, 0);
	}
	HEK_ E 5
	{
		if (Health < 400)
		{
			A_FaceTarget();
			A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
			A_SpawnProjectile("DCY_WRay", 82, 0);
		}
		else SetStateLabel("See");
	}
	HEK_ E 5
	{
		if (Health < 200)
		{
			A_FaceTarget();
			A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
			A_SpawnProjectile("DCY_WRay", 82, 0);
			A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
		}
		else SetStateLabel("See");
	}
	Goto See;
  Pain:
    TNT1 A 0 A_Jump(60, "Flee");
    HEK_ H 8 A_Pain();
    TNT1 A 0 A_Jump(60, "Flee");
	Goto See;
  Death:
    X3X_ A 1 Bright;
	X3X_ A 2 Bright A_ScreamAndUnblock();
	X3X_ BCABCABCA 3 Bright;
	X3X_ B 4
	{
		A_SpawnItemEx("DCY_Explosion_1_Warwire", zofs: 35, flags: SXF_NOCHECKPOSITION);
		A_StartSound("UCKABEWM");
		
		for (int i = random(10, 15); i > 0; i--)
		{
			A_SpawnItemEx("DCY_TSoul", frandom(-radius, radius), frandom(-radius, radius), frandom(0, height), zvel: frandom(1.5, 2.5), flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERALPHA);
			if (!random(0, 1)) A_SpawnParticleEx(0x3b94cc, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 180, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0025, sizestep: 1.525);
		}
	}
	X2X_ ABCDEF 4;
	X2X_ FIJKL 3;
	X2X_ M -1;
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_Explosion_1_Warwire : DCY_Explosion_1_Biggie { Default { Scale 0.625; Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]"; } }

Class DCY_WarWireTrail : DCY_GTrail
{
  Default
  {
  -BRIGHT
  Alpha 0.00;
  }
  States
  {
  Spawn:
    HEK_ "#" 1;
	HEK_ "#" 0 A_FadeIn(1.0, 0);
	"####" "##########" 1 A_FadeOut(0.1, 0);
	Stop;
  }
}

Class DCY_WRay : DCY_MechaZombiePlasma
{
  Default
  {
  SeeSound "Warwire/Laser";
  Height 10;
  Radius 10;
  Speed 45;
  FastSpeed 50;
  Damage 3;
  +THRUGHOST
  +BRIGHT
  +NOEXTREMEDEATH
  Scale .245;
  RenderStyle "Add";
  DamageType "WarwireDamageType";
  }
  
  static const string RayColors[] = { "ffffff", "ccfeff", "abfdff" };
  
  States
  {
  Spawn:
    TRAC E 1
	{
		for (int i = 2; i > 0; i--)
			A_SpawnParticle(RayColors[random(0, RayColors.Size() - 1)], SPF_FULLBRIGHT, random(10, 35), random(10, 15), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), sizestep: -0.625);
	}
	Loop;
  Death:
    TNT1 A 1 A_SpawnItemEx("DCY_WFlame");
	Stop;
  }
}

Class DCY_WFlame : DCY_ImpBigFlame { Default { Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]"; Scale 2.0; } }

Class DCY_WAxe : DCY_ImpFireball
{
  Default
  {
  Speed 20;
  Damage 10;
  +SEEKERMISSILE;
  +BRIGHT;
  -NOGRAVITY
  Gravity 0.5;
  StencilColor "Cyan";
  RenderStyle "Normal";
  DeathSound "SPIRBM2";
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn:
    XXP_ AABBCC 1
	{
		if ((level.time % 8) == 0)
			A_StartSound("hknight/axewhoosh", volume: 0.75, pitch: 1.2);
		
		if (DCY_EffectDistance(sfxdistance))
		{
		A_SpawnItemEx("DCY_BaronKnightFlame", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 150);
		A_SpawnItemEx("DCY_HexodenicanTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSTENCILCOL);
		A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSTENCILCOL);
		}
	}
	Loop;
  Death:
    TNT1 A 0
	{
		A_SetTranslucent(1.0, 1);
		A_Explode(30, 128);
		A_SpawnItemEx("DCY_TSoul", frandom(-radius, radius), frandom(-radius, radius), frandom(0, height), zvel: frandom(1.5, 2.5), flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERALPHA);
		if (!random(0, 1)) A_SpawnParticleEx(0x3b94cc, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 80, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0075, sizestep: 1.525);
		for (int i = 0; i < 360; i += 45) A_SpawnItemEx("DCY_Explosion_1", xvel: 8, angle: i, flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	}
    XXP_ DEF 4;
	Stop;
  }
}