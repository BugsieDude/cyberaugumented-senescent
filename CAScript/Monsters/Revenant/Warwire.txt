Class DCY_Warwire : DCY_Rev
{
  Default
  {
  Tag "\c[n0]Warwire\c-";
  Speed 10;
  FastSpeed 14;
  SeeSound "Warwire/Sight";
  ActiveSound "Warwire/Active";
  PainSound "Warwire/Pain";
  DeathSound "Warwire/Death";
  MeleeSound "hknight/melee";
  Health 700;
  Mass 250;
  MeleeDamage 15;
  MeleeThreshold 200;
  DamageType "WarwireDamageType";
  DamageFactor "WarwireDamageType", 0.00;
  Obituary "%o could not fathom the Warwire.";
  HitObituary "%o couldn't resist.";
  +MISSILEMORE
  +QUICKTORETALIATE
  +NODROPOFF
  +ALWAYSFAST
  +NOBLOOD
  Translation "128:159=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "13:15=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "48:79=%[0.00,0.00,0.00]:[0.81,0.81,0.81]", "236:239=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "80:111=%[0.00,0.00,0.00]:[0.42,0.42,0.42]", "1:3=%[0.00,0.00,0.00]:[0.42,0.42,0.42]", "115:123=[128,255,255]:[0,0,0]", "112:114=[255,255,255]:[128,255,255]", "124:127=[0,0,0]:[0,0,0]", "16:47=%[0.00,0.31,0.31]:[0.00,2.00,2.00]", "168:191=%[0.00,0.31,0.31]:[1.01,2.00,2.00]", "208:255=%[0.00,0.00,0.00]:[0.00,2.00,2.00]", "160:167=%[0.00,0.00,0.00]:[0.00,2.00,2.00]";
  }
  States
  {
  Spawn:
    HEK_ A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(128, "Dash");
    HEK_ AABBCCDD 3 A_Chase();
	Loop;
  Flee:
    HEK_ A 0
	{
		bFrightened = true;
		bNoPain = true;
	}
	Goto Dash;
  Dash:
    TNT1 A 0 A_StartSound("AM2DASH", CHAN_6, CHANF_DEFAULT, 1.0, 2);
	HEK_ AABBCCDD 2
	{
		A_SpawnItemEx("DCY_WarWireTrail", flags: SXF_NOCHECKPOSITION||SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
		A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		
		if (Health < 600)
		{
			for (int i = 2; i > 0; i--)
				A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
		}
	}
	TNT1 A 0 A_Jump(95, "DashDone");
	Goto Dash+1;
  DashDone:
    HEK_ A 0
	{
		bFrightened = false;
		bNoPain = false;
	}
	Goto See;
  Melee:
    HEK_ FE 10 A_FaceTarget();
	HEK_ E 5
	{
		A_Hop(3);
		
		If (Health < 1000) { A_Recoil(-33); }
		If (Health < 650) { A_Recoil(-25); }
		If (Health < 300) { A_Recoil(-15); }
		If (Health < 100) { A_FaceTarget(); }
	}
	HEK_ F 5;
	HEK_ G 6 A_CustomMeleeAttack(10 * random(1, 10), "hknight/melee");
	Goto See;
  Missile:
    TNT1 A 0 A_JumpIfHealthLower(115, "Missile4");
    TNT1 A 0 A_JumpIfHealthLower(450, "Missile3");
    TNT1 A 0 A_JumpIfHealthLower(895, "Missile2");
    TNT1 A 0 A_Jump(130, "FasterMissile");
	TNT1 A 0 A_Jump(90, "Laser", "Melee");
	TNT1 A 0 A_Jump(70, "Cluster");
	Goto NormalMissile;
  Missile2:
    TNT1 A 0 A_Jump(140, "FasterMissile");
	TNT1 A 0 A_Jump(125, "Laser");
	TNT1 A 0 A_Jump(100, "Cluster");
	Goto NormalMissile;
  Missile3:
	TNT1 A 0 A_Jump(150, "FasterMissile");
	TNT1 A 0 A_Jump(135, "Laser");
	TNT1 A 0 A_Jump(120, "Cluster");
	Goto NormalMissile;
  Missile4:
	TNT1 A 0 A_Jump(155, "FasterMissile");
	TNT1 A 0 A_Jump(145, "Laser");
	TNT1 A 0 A_Jump(135, "Cluster");
	Goto NormalMissile;
  NormalMissile:
	HEK_ E 0
	{
		if (Health < 895) { A_SetTics(7); }
		if (Health < 625) { A_SetTics(6); }
		if (Health < 450) { A_SetTics(5); }
		if (Health < 215) { A_SetTics(4); }
	}
    HEK_ EF 8 A_FaceTarget();
    HEK_ G 8 A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
    HEK_ F 8 A_FaceTarget();
    HEK_ G 8 A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
    HEK_ F 8 A_FaceTarget();
    HEK_ G 8 A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
	HEK_ F 8
	{
		if (Health < 500)
		{
			A_FaceTarget();
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	HEK_ G 8
	{
		if (Health < 500)
		{
			A_FaceTarget();
			A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	HEK_ F 8
	{
		if (Health < 100)
		{
			A_FaceTarget();
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	HEK_ G 8
	{
		if (Health < 100)
		{
			A_FaceTarget();
			A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
    Goto See;
  Laser:
    HEK_ FE 9 A_FaceTarget();
	HEK_ EEE 5
	{
		A_FaceTarget();
		A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
		A_SpawnProjectile("DCY_WRay", 82, 0);
	}
	HEK_ E 5
	{
		if (Health < 400)
		{
			A_FaceTarget();
			A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
			A_SpawnProjectile("DCY_WRay", 82, 0);
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	HEK_ E 5
	{
		if (Health < 200)
		{
			A_FaceTarget();
			A_StartSound("Warwire/Laser", CHAN_WEAPON, 1.0, 0, ATTN_NORM);
			A_SpawnProjectile("DCY_WRay", 82, 0);
			A_SpawnProjectile("DCY_WAxe", 32, 0, random(-2, 2));
		}
		else
		{
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	Goto See;
  Cluster:
    HEK_ EF 12 A_FaceTarget();
	HEK_ G 12
	{
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-3, 3), CMF_OFFSETPITCH, random(-3, 3));
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-4, 4), CMF_OFFSETPITCH, random(-4, 4));
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-5, 5), CMF_OFFSETPITCH, random(-5, 5));
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-6, 6), CMF_OFFSETPITCH, random(-6, 6));
		A_SpawnProjectile("DCY_WAxe", 32, 0, random(-7, 7), CMF_OFFSETPITCH, random(-7, 7));
	}
	Goto See;
  Pain:
    TNT1 A 0 A_Jump(60, "Flee");
    HEK_ H 8 A_Pain();
    TNT1 A 0 A_Jump(60, "Flee");
	Goto See;
  Death:
    X3X_ A 1 Bright;
	X3X_ A 2 Bright A_ScreamAndUnblock();
	X3X_ BCABCABCA 3 Bright;
	X3X_ B 4
	{
		A_SpawnItemEx("DCY_Explosion_1_Warwire", zofs: 35, flags: SXF_NOCHECKPOSITION);
		A_StartSound("UCKABEWM");
	}
	X2X_ ABCDEF 4;
	X2X_ FIJKL 3;
	X2X_ M -1;
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_Explosion_1_Warwire : DCY_Explosion_1_Biggie { Default { Scale 0.625; Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]"; } }

Class DCY_WarWireTrail : DCY_GTrail
{
  Default
  {
  -BRIGHT
  Alpha 0.00;
  }
  States
  {
  Spawn:
    HEK_ "#" 1;
	HEK_ "#" 0 A_FadeIn(1.0, 0);
	"####" "##########" 1 A_FadeOut(0.1, 0);
	Stop;
  }
}

Class DCY_WRay : DCY_MechaZombiePlasma
{
  Default
  {
  SeeSound "Warwire/Laser";
  Height 10;
  Radius 10;
  Speed 45;
  FastSpeed 50;
  Damage 3;
  +THRUGHOST
  +BRIGHT
  +NOEXTREMEDEATH
  Scale .245;
  RenderStyle "Add";
  DamageType "WarwireDamageType";
  }
  
  static const string RayColors[] = { "ffffff", "ccfeff", "abfdff" };
  
  States
  {
  Spawn:
    TRAC E 1
	{
		for (int i = 2; i > 0; i--)
			A_SpawnParticle(RayColors[random(0, RayColors.Size() - 1)], SPF_FULLBRIGHT, random(10, 35), random(10, 15), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), sizestep: -0.625);
	}
	Loop;
  Death:
    TNT1 A 1 A_SpawnItemEx("DCY_WFlame");
	Stop;
  }
}

Class DCY_WFlame : DCY_ImpBigFlame { Default { Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]"; Scale 2.0; } }

Class DCY_WAxe : KnightAxe
{
  Default
  {
  Speed 20;
  Damage 4;
  +SEEKERMISSILE;
  +BRIGHT;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn:
    XXP_ A 1
	{
		A_Tracer2(2.35);
		A_SpawnItemEx("DCY_WAxeTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
		A_StartSound("hknight/axewhoosh");
	}
	XXP_ AABBBCCC 1
	{
		A_Tracer2(2.35);
		A_SpawnItemEx("DCY_WAxeTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
    TNT1 A 0 A_SetTranslucent(1.0, 1);
    XXP_ DEF 4;
	Stop;
  }
}

Class DCY_WAxeTrail : DCY_GunmetalInfiltratorBallTrail
{
  Default
  {
	  RenderStyle "Add";
	  Alpha 0;
	  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  States
  {
  Spawn:
    XXP_ "#" 1;
	XXP_ "#" 0 A_FadeIn(1.0);
    XXP_ "#####" 1 A_FadeOut(0.2, 0);
	Stop;
  }
}