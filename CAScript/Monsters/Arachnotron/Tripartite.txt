Class DCY_Tripartite : DCY_SmallEnemySpider
{
  Default
  {
  Health 1000;
  SeeSound "BGSpider/Sight";
  ActiveSound "BGSpider/Active";
  DeathSound "BGSpider/AltDeath";
  PainSound "BGSpider/Pain";
  Tag "Tripartite";
  Translation "48:79=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "208:223=%[0.00,0.00,0.00]:[2.00,0.54,0.54]", "80:159=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "16:47=%[0.00,0.00,0.00]:[1.38,0.54,0.26]", "236:239=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "160:191=%[0.20,0.00,0.00]:[1.95,0.64,0.26]", "13:15=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "4:4=%[0.00,0.00,0.00]:[0.58,0.58,0.58]";
  GibHealth -1000;
  }
  
  int attackmode;
  
  override void Tick()
  {
	Super.Tick();
	
	if (health < 300)
		attackmode = 2;
	else if (health < 800)
		attackmode = 1;
	else if (health >= 600)
		attackmode = 0;
  }
  
  void A_Tripartite()
  {
	if (target)
	{
		if (attackmode == 1 || attackmode == 2)
		{
			for (int i = -20; i < 21; i += 40)
				A_SpawnProjectile("DCY_TripartiteTracer", 16, i, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
			
			A_StartSound("PIS4FIRE", CHAN_6, attenuation: 0.85);
		}
		
		if (attackmode == 0 || attackmode == 2)
		{
			A_SpawnProjectile("DCY_TripartiteTracer", 30, 0, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
			A_StartSound("PIS5FIRE", CHAN_WEAPON, attenuation: 0.85);
		}
		
		
		A_FaceTarget();
	}
  }
  
  States
  {
  Spawn:
	CSP3 A 10 A_Look();
	Loop;
  See:
	CSP3 A 10;
	CSP3 AABB 3 A_Chase();
	CSP3 C 3
	{
		A_Chase();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 CDDEE 3 A_Chase();
	CSP3 F 3
	{
		A_Chase();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 F 3 A_Chase();
	Goto See+1;
  Missile:
	CSP3 A 30
	{
		A_FaceTarget();
		
		if (attackmode == 2)
			tics = 40;
	}
	CSP3 GH 3
	{
		A_Tripartite();
		Return A_MonsterRefire(128, "See");
	}
	Goto Missile+1;
  Pain:
	CSP3 I 3;
	CSP3 I 4 A_Pain();
	Goto See;
  Death:
    CSP3 I 10;
	CSP3 I 5
	{
		A_NoBlocking();
		A_Scream();
	}
	CSP3 JKLMNO 9;
	CSP3 P -1 A_BossDeath2();
	Stop;
  XDeath:
  Death.Massacre:
	CSP3 I 10;
	CSP3 I 5
	{
		A_NoBlocking();
		A_Scream();
		A_StartSound("BGSpider/Scream", CHAN_VOICE, CHANF_OVERLAP, 1.0, 0.35);
	}
	CSP3 JKLMNO 9;
	CSP3 P -1 A_BossDeath2();
	Stop;
  Raise:
	CSP3 ONMLKJI 3 { attackmode = 0; }
	Goto See;
  }
}