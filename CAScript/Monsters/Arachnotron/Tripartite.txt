Class DCY_Tripartite : DCY_SmallEnemySpider
{
  Default
  {
  Health 1000;
  SeeSound "BGSpider/Sight";
  ActiveSound "BGSpider/Active";
  DeathSound "BGSpider/AltDeath";
  PainSound "BGSpider/Pain";
  Tag "Tripartite";
  Translation "48:79=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "208:223=%[0.00,0.00,0.00]:[2.00,0.54,0.54]", "80:159=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "16:47=%[0.00,0.00,0.00]:[1.38,0.54,0.26]", "236:239=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "160:191=%[0.20,0.00,0.00]:[1.95,0.64,0.26]", "13:15=%[0.00,0.00,0.00]:[0.58,0.58,0.58]", "4:4=%[0.00,0.00,0.00]:[0.58,0.58,0.58]";
  GibHealth -1000;
  DCYMonster.ScientificName "Triplum gaudium";
  }
  
  int attackmode;
  int runnin;
  int attackloop;
  
  override void Tick()
  {
	Super.Tick();
	
	if (health < 300)
		attackmode = 2;
	else if (health < 800)
		attackmode = 1;
	else if (health >= 600)
		attackmode = 0;
  }
  
  void A_TripartiteAttach()
  {
   A_SetSpeed(0);
   bNOPAIN = true;
   bMISSILEMORE = true;
   bMISSILEEVENMORE = true;
  }
  
  void A_TripartiteDetach()
  {
   A_SetSpeed(12);
   bNOPAIN = true;
   bMISSILEMORE = true;
   bMISSILEEVENMORE = false;
  }
  
  void A_Tripartite()
  {
	if (target)
	{
		if (attackmode == 1 || attackmode == 2)
		{
			for (int i = -20; i < 21; i += 40)
				A_SpawnProjectile("DCY_TripartiteTracer", 16, i, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
			
			A_StartSound("PIS4FIRE", CHAN_6, attenuation: 0.85);
		}
		
		if (attackmode == 0 || attackmode == 2)
		{
			A_SpawnProjectile("DCY_TripartiteTracer", 30, 0, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
			A_StartSound("PIS5FIRE", CHAN_WEAPON, attenuation: 0.85);
		}
		
		
		A_FaceTarget();
	}
  }
  
  States
  {
  Spawn:
	CSP3 A 10 DCY_Look();
	Loop;
  Look:
    CSP3 AABB 3 DCY_LookingForPlayer();
	CSP3 C 3
	{
		DCY_LookingForPlayer();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 CDDEE 3 DCY_LookingForPlayer();
	CSP3 F 3
	{
		DCY_LookingForPlayer();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 F 3 DCY_LookingForPlayer();
	Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
  See:
	CSP3 A 10;
	CSP3 AABB 3 A_Chase();
	CSP3 C 3
	{
		A_Chase();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 CDDEE 3 A_Chase();
	CSP3 F 3
	{
		A_Chase();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 0.82);
	}
	CSP3 F 3 A_Chase();
	Goto See+1;
  Missile:
    TNT1 A 0 A_Jump(75,"Attaching");
	CSP3 A 30
	{
		A_FaceTarget();
		
		if (attackmode == 2)
			tics = 40;
	}
	CSP3 GH 3
	{
		A_Tripartite();
		Return A_MonsterRefire(128, "See");
	}
	Goto Missile+2;
  Attaching:
    TNT1 A 0 { bFRIGHTENED = true; }
	CSP3 AABB 1 A_Chase(null,null);
	CSP3 C 2
	{
	  if(target && Distance3D(target) > 900)
	    SetStateLabel("Attach");
		
		A_Chase();
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 1.25);
	}
	CSP3 CDDEE 1 A_Chase(null,null);
	CSP3 F 2
	{
	  if(runnin >= 15)
	    SetStateLabel("Attach");
		
		A_Chase(null,null);
		A_StartSound("Aracgunner/Walk", CHAN_BODY, pitch: 1.25);
		runnin++;
	}
	CSP3 F 1 A_Chase(null,null); 
	Loop;
  Attach:
    TNT1 A 0 A_StartSound("Tripartite/Attach",11,CHANF_DEFAULT,attenuation: 0.5);
	CSP3 A 10 
	{
	A_FaceTarget();
	A_TripartiteAttach();
	bFRIGHTENED = false;
	}
  AttachLoop:
    CSP3 A 5 
	{
	A_Chase(null,"AttachMissile");
	A_FaceTarget();
	}
	TNT1 A 0 A_Jump(3,"AttachEnd");
	TNT1 A 0 A_CheckSight("AttachEnd");
	Loop;
  AttachEnd:
    TNT1 A 0 A_TripartiteDetach();
	Goto See;
  AttachMisEnd:
    CSP3 A 20 
	{
	A_FaceTarget();
	A_StartSound("Tripartite/AttachReady");
	}
	Goto AttachLoop;
  AttachMissile:
    CSP3 A 14 
	{
	A_FaceTarget();
	A_StartSound("Tripartite/AttachReady");
	}
    TNT1 A 0 A_Jump(65,"AttachDouble");
	TNT1 A 0 
	{
	if(health < 300 && random(1,5) == 1)
	 SetStateLabel("AttachTriple");
	}
  AttachSingle: //continuous
    TNT1 A 0 { attackloop = random(20,28); }
    CSP3 GH 3 Bright
	{
	  if(attackloop < 0)
	   SetStateLabel("AttachMisEnd");
	A_SpawnProjectile("DCY_TripartiteTracer", 30, 0, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
	A_StartSound("PIS5FIRE", CHAN_6, attenuation: 0.85);
	attackloop--;
	A_FaceTarget();
	}
	Goto AttachSingle+1;
  AttachDouble: //burst
    TNT1 A 0 { attackloop = random(6,8); }
    CSP3 QRQRQRQRQR 1 Bright
	{
	A_SpawnProjectile("DCY_TripartiteTracer", 16, -10, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
	A_SpawnProjectile("DCY_TripartiteTracer", 16, 10, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
	A_StartSound("PIS5FIRE", CHAN_6, attenuation: 0.85);
	A_FaceTarget();
	}
	CSP3 A 15 
	{
	  if(attackloop < 0)
	   SetStateLabel("AttachMisEnd");
	A_FaceTarget();
	attackloop--;
	}
	Goto AttachDouble+1;
  AttachTriple: //MAXIMUM FIREPOWERRRRRRRRRRRRR
   TNT1 A 0 
   {
   A_StartSound("Tripartite/Warning",11,CHANF_DEFAULT,attenuation: 0.5);
   attackloop = 80; 
   }
   CSP3 A 20 Bright;
   CSP3 TU 2 Bright
   {
	  if(attackloop < 0)
	   SetStateLabel("AttachMisEnd");
	 for (int i = -20; i < 21; i += 40)
	A_SpawnProjectile("DCY_TripartiteTracer", 16, i, frandom(-3.0, 3.0), CMF_TRACKOWNER|CMF_OFFSETPITCH, frandom(-1.5, 1.5));
	A_StartSound("PIS5FIRE", CHAN_6, attenuation: 0.85);
	A_FaceTarget();
	attackloop--;
  }
  Goto AttachTriple+2;
  Pain:
	CSP3 I 3 { bFRIGHTENED = false; }
	CSP3 I 4 A_Pain();
	Goto See;
  Death:
    CSP3 I 10;
	CSP3 I 5
	{
		A_NoBlocking();
		A_Scream();
	}
	CSP3 J 9 DCY_ArachnoExplosion();
	CSP3 KLMNO 9;
	CSP3 P -1 A_BossDeath2();
	Stop;
  XDeath:
	CSP3 I 10;
	CSP3 I 5
	{
		A_NoBlocking();
		A_Scream();
		A_StartSound("BGSpider/Scream", CHAN_VOICE, CHANF_OVERLAP, 1.0, 0.35);
	}
	CSP3 J 9 DCY_ArachnoExplosion();
	CSP3 KLMNO 9;
	CSP3 P -1 A_BossDeath2();
	Stop;
  Raise:
	CSP3 ONMLKJI 3 { attackmode = 0; }
	Goto See;
  }
}