Class DCY_IonArachnotron : DCY_ArachnoParent
{
  Default
  {
  Health 900;
  Speed 22;
  FastSpeed 24;
  SeeSound "SPI2SIT";
  ActiveSound "SPI2ACT";
  PainSound "SPI2PAIN";
  DeathSound "SPI2DTH";
  Obituary "%o was castrated by a Ion Arachnotron.";
  Tag "\c[c1]Ion Arachnotron\c-";
  +ALWAYSFAST
  +DONTGIB
  +MISSILEEVENMORE
  +NOTARGET
  +QUICKTORETALIATE
  +NOFEAR
  +DONTHARMCLASS
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  BloodColor "40 AE FF";
  Scale 1.25;
  Translation "13:15=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "232:235=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "160:167=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "208:223=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "48:79=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "128:151=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "16:47=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "168:191=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  }
  
  void A_IonArachnoDash()
  {
	A_Chase();
	if (DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_IonSpiderTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
  }
  
  int missile;
  States
  {
  Spawn:
    CSPI A 10 A_Look();
	Loop;
  SeePlusOne:
    Goto See+1;
  See:
    CSPI A 10;
	CSPI A 2 A_BabyMetal();
	CSPI ABBCC 2 A_Chase();
	CSPI D 2 A_BabyMetal();
	CSPI DEEFF 2 A_Chase();
	TNT1 A 0 A_Jump(90, "Dash");
	Goto See+1;
  Dash:
    CSPI AABBCCDDEEFF 1 A_IonArachnoDash();
	TNT1 A 0 A_Jump(90, "SeePlusOne");
	Loop;
  Missile:
    TNT1 A 0 A_Jump(60, "QuadShots");
	Goto FireDaShits;
  FireDaShits:
    CSPI A 10
	{
		A_FaceTarget();
		missile = random(8, 6);
	}
	CSPI G 3
	{
		if (missile == 0)
			Return ResolveState("See");
		
		A_SpawnProjectile("DCY_IPlasma", 25, 0, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-1, 1));
		A_FaceTarget();
		
		missile -= 1;
		Return ResolveState(null);
	}
	CSPI H 3 A_FaceTarget();
	Goto FireDaShits+1;
  QuadShots:
    CSPI A 35
	{
		A_FaceTarget();
		missile = random(1, 2);
	}
	CSPI  GH 20
	{
		if (missile == 0)
			Return ResolveState("See");
		
		for (int i = 4; i > 0; i--)
			A_SpawnProjectile("DCY_IPlasma", 25, 0, random(-5, 5), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
		
		A_FaceTarget();
		missile -= 1;
		Return ResolveState(null);
	}
	Goto QuadShots+1;
  Pain:
    CSPI I 4;
	CSPI I 4 A_Pain();
	TNT1 A 0 A_Jump(150, "Missile");
	Goto See;
  Death:
    CSPI I 1;
	CSPI I 13
	{
		A_NoBlocking();
		A_StartSound("SPI2PAIN", CHAN_VOICE, CHANF_DEFAULT, 1.0, ATTN_NONE, frandom(0.8, 0.75));
	}
	CSPI JKLMNO 9;
	CSPI P -1 A_BossDeath2();
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_IonSpiderTrail : DCY_GTrail
{
  Default
  {
  Scale 1.25;
  }
  States
  {
  Spawn:
    CSPI "#" 2;
	CSPI "#" 1 A_FadeIn(1.0, 0);
    CSPI "###" 1 Bright A_FadeOut(0.35, 0);
	Stop;
  }
}

Class DCY_IPlasma : FastProjectile
{
  Default
  {
  Speed 30.5;
  Damage 2;
  Scale 1.0;
  RenderStyle "Add";
  Height 20;
  Radius 20;
  SeeSound "FASTLASR";
  DeathSound "ball/death";
  Translation "0:255=%[0.00,0.00,0.00]:[0.67,1.34,2.00]";
  +SEEKERMISSILE
  +BRIGHT
  +NOEXTREMEDEATH
  }
  States
  {
  Spawn:
    TNT1 A 0 A_Jump(100, "Seeker");
    BL8R AB 1 A_SpawnItemEx("DCY_IPlasmaTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Loop;
  Seeker:
    TNT1 A 0 A_Jump(192, "Spawn");
	TNT1 A 0 A_SeekerMissile(4, 15);
    BL8R AB 1 A_SpawnItemEx("DCY_IPlasmaTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Loop;
  Death:
    TNT1 A 0;
	TNT1 AAAAAAAAA 0 A_SpawnItemEx("DCY_IPlasmaTrail2", 0, 0, 0, random(-10, 10), random(-10, 10), random(-10, 10), 0, SXF_CLIENTSIDE|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    BL8R CDE 5;
	Stop;
  }
}

Class DCY_IPlasmaTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Alpha 0.85;
  RenderStyle "Add";
  Scale 1.3;
  Translation "0:255=%[0.00,0.00,0.00]:[0.67,1.34,2.00]";
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  }
  States
  {
  Spawn:
    BL8R ABABA 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_IPlasmaTrail2 : DCY_IPlasmaTrail
{
  States
  {
  Spawn:
    BL8R AABBABA 1
	{
		A_FadeOut(0.1, 0);
		A_ChangeVelocity(0, 0, -2);
	}
	Stop;
  }
}