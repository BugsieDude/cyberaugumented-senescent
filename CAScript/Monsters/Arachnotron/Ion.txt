Class DCY_IonArachnotron : DCY_ArachnoParent
{
  int missile;
  int beamcharge;
  int cooldown;
  bool crippled;
  Default
  {
  Health 900;
  Speed 22;
  FastSpeed 24;
  SeeSound "SPI2SIT";
  ActiveSound "SPI2ACT";
  PainSound "SPI2PAIN";
  DeathSound "SPI2DTH";
  Obituary "%o was obliterated by an Ionizer.";
  Tag "\c[c1]Ionizer\c-";
  +DONTGIB
  +NOTARGET
  +QUICKTORETALIATE
  +NOFEAR
  +DONTHARMCLASS
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  DropItem "Cell", 120;
  BloodColor "Blue";
  Scale 1.25;
  Translation "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.85,0.85,0.85]", "5:8=%[0.00,0.00,0.00]:[0.85,0.85,0.85]", "9:15=%[0.00,0.00,0.00]:[0.85,0.85,0.85]", "48:79=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "128:159=%[0.00,0.00,0.00]:[1.03,1.03,1.03]", "16:47=%[0.00,0.00,0.50]:[0.00,1.01,2.00]", "160:191=%[0.00,0.00,0.50]:[0.00,1.01,2.00]", "208:235=%[0.00,0.00,0.50]:[0.67,1.34,2.00]", "248:255=%[0.00,0.00,0.50]:[0.67,1.35,2.00]";
  }
  
  void A_IonRailPrepare()
  {
    if  (health < 500)
	    SetStateLabel("Crippled");
	if  (beamcharge >= 150)
	    SetStateLabel("MissileGo");

	A_FaceTarget(0.8,0.8);
	beamcharge++;
  }
  
  void A_IonRailBeam()
  {
      if(health < 500)
	    SetStateLabel("Crippled");
	  if(missile >= 80)
	   SetStateLabel("CooldownGiver"); 
	A_FaceTarget(0.4,0.4);
	A_CustomRailgun(1, 0, 0, 0, RGF_FULLBRIGHT|RGF_SILENT, 0, 0, "DCY_NeonicRing", 0, 0, 0, 0, 80, 0, "DCY_NeonicWandTrail");
	missile++;
  }
  
  
  void A_IonArachnoDash()
  {
	A_Chase();
	if (DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_IonSpiderTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
  }
  States
  {
  Spawn:
    CSPI A 10 DCY_Look();
	Loop;
  Look:
	CSPI A 2 A_BabyMetal();
	CSPI ABBCC 2 DCY_LookingForPlayer();
	CSPI D 2 A_BabyMetal();
	CSPI DEEFF 2 DCY_LookingForPlayer();
    Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
  SeePlusOne:
    Goto See+1;
  See:
    CSPI A 10;
	CSPI A 2 A_BabyMetal();
	CSPI ABBCC 2 
	{
    if  (health < 500)
	    SetStateLabel("Crippled");
	A_Chase();
	}
	CSPI D 2 A_BabyMetal();
	CSPI DEEFF 2 
	{
    if  (health < 500)
	    SetStateLabel("Crippled");
	A_Chase();
	}
	TNT1 A 0 A_Jump(90, "Dash");
	Goto See+1;
  Dash:
    CSPI AABBCCDDEEFF 1 A_IonArachnoDash();
	TNT1 A 0 A_Jump(90, "SeePlusOne");
	Loop;
  Crippled:
    CSPI I 5
	{
	A_Recoil(5);
	A_StartSound("Ionizer/Crippled",11,attenuation: 0.5);
	bFRIGHTENED = true;
	bALWAYSFAST = true;
	bMISSILEMORE = true;
	bNOPAIN = false;
	bNODROPOFF = true;
	crippled = true;
	}
	CSPI I 3 A_Pain();
	Goto PanicSee;
  PanicSee:
	CSPI A 2 
	{
	A_StartSound("dcyarachno/walk");
	A_Chase(null,"PanicMissile");
	}
	CSPI ABBCC 2 A_Chase(null,"PanicMissile",CHF_FASTCHASE);
	CSPI D 2
	{
	A_StartSound("dcyarachno/walk");
	A_Chase(null,"PanicMissile");
	}
	CSPI DEEFF 2 A_Chase(null,"PanicMissile",CHF_FASTCHASE);
	Loop;
  Missile:
    TNT1 A 0 
	{
	beamcharge = 0;
	bNOPAIN = true;
	}
    CSPI G 15;
	TNT1 A 0 A_StartSound("Ionizer/Charge",11,CHANF_DEFAULT,attenuation: 0.5,pitch:frandom(0.95,1.25));
  MissileLoop:
    CSPI GH 1 Bright A_IonRailPrepare();
	Loop;
  MissileGo:
    TNT1 A 0 A_StartSound("Ionizer/Beam",11,CHANF_DEFAULT,attenuation: 0.5);
	TNT1 A 0 { missile = 0; }
  MissileGo2:
    CSPI GH 1 Bright A_IonRailBeam();
	Loop;
  CooldownGiver:
    TNT1 A 0 { bNOPAIN = false; }
	Goto See;
  PanicMissile:
    TNT1 A 0 A_Jump(60, "QuadShots");
	Goto FireDaShits;
  FireDaShits:
    CSPI A 10
	{
		A_FaceTarget();
		missile = random(13, 6);
	}
	CSPI G 3
	{
		if (missile == 0)
			SetStateLabel("PanicSee");
		
		A_SpawnProjectile("DCY_IPlasma", 25, 0, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-1, 1));
		A_FaceTarget();
		
		missile -= 1;
	}
	CSPI H 3 A_FaceTarget();
	Goto FireDaShits+1;
  QuadShots:
    CSPI A 15
	{
		A_FaceTarget();
		missile = random(1, 2);
	}
	CSPI  GH 20
	{
		if (missile == 0)
			SetStateLabel("PanicSee");
		
		for (int i = 4; i > 0; i--)
			A_SpawnProjectile("DCY_IPlasma", 25, 0, random(-5, 5), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
		
		A_FaceTarget();
		missile -= 1;
	}
	Goto QuadShots+1;
  Pain:
    CSPI I 4;
	TNT1 A 0
	{
	if(crippled == true)
	  SetStateLabel("PanicSee");
	}
	CSPI I 4 A_Pain();
	TNT1 A 0 A_Jump(150, "Missile");
	Goto See;
  Death:
    CSPI I 1;
	CSPI I 13
	{
		A_NoBlocking();
		A_StartSound("SPI2PAIN", CHAN_VOICE, CHANF_DEFAULT, 1.0, ATTN_NONE, frandom(0.8, 0.75));
	}
	CSPI J 9 DCY_ArachnoExplosion();
	CSPI KLMNO 9;
	CSPI P -1 A_BossDeath2();
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_IonSpiderTrail : DCY_GTrail
{
  Default
  {
  Scale 1.25;
  }
  States
  {
  Spawn:
    CSPI "#" 2;
	CSPI "#" 1 A_FadeIn(1.0, 0);
    CSPI "###" 1 Bright A_FadeOut(0.35, 0);
	Stop;
  }
}

Class DCY_IPlasma : DCY_FastProjectile
{
  Default
  {
  Speed 25.5;
  Damage 3;
  Scale 1.3;
  RenderStyle "Add";
  Height 10;
  Radius 20;
  SeeSound "FASTLASR";
  DeathSound "ball/death";
  Translation "0:255=%[0.00,0.00,0.50]:[0.67,1.35,2.00]";
  +SEEKERMISSILE
  +BRIGHT
  +NOEXTREMEDEATH
  }
  
  bool seek;
  
  States
  {
  Spawn:
    BL8R AB 1
	{
		if (!random(0, 10)) seek = !seek;
		if (seek) A_SeekerMissile(1, 1);
		if (DCY_EffectDistance(1000))
		{
			A_SpawnParticleEx(0x1351e8, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), vel.x / 4, vel.y / 4, vel.z / 4, fadestepf: -2);
			A_SpawnParticleEx(0x1e3a7d, TexMan.CheckForTexture("SUE1B0"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 20, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), fadestepf: 0.1, sizestep: -4.25);
		}
	}
	Loop;
  Death:
    BL8R CDE 5;
	Stop;
  }
}