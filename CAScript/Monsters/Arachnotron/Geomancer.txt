Class DCY_BattlemindGeomancer : DCY_SmallEnemySpider
{
  Default
  {
  Health 1100;
  Speed 14;
  FastSpeed 18;
  FloatSpeed 18;
  DamageFactor 0.8;
  Scale 1.3;
  Obituary "%o was outsmarted by a Battlemind Geomancer in a godly way.";
  Tag "\c[Sapphire]Battlemind Geomancer\c-";
  SeeSound "BGSpider/Sight";
  ActiveSound "BGSpider/Active";
  DeathSound "BGSpider/Death";
  PainSound "BGSpider/Pain";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  GibHealth -1000;
  Translation "80:111=[160,160,160]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "5:15=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "128:159=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "16:47=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "160:235=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "48:79=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "248:249=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  +MISSILEMORE
  +QUICKTORETALIATE
  +DONTFALL
  +NOGRAVITY
  +FLOAT
  }
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen() && DCY_EffectDistance(sfxdistance))
	{
		A_SpawnItemEx("DCY_LunisJetFlareSmall", frandom(33, 31), frandom(33, 31),45,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_LunisJetFlareSmall", frandom(-33, -31), frandom(-33, -31),45,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_LunisJetFlareSmall", frandom(-33, -31), frandom(33, 31),45,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_LunisJetFlareSmall", frandom(33, 31), frandom(-33, -31),45,0,0,frandom(-4,-6),0,128);
	}
  }
  
  States
  {
  Spawn:
	C8RP AB 3 DCY_Look();
	Loop;
  Look:
    C8RP ABABAB 2 DCY_LookingForPlayer();
    Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	C8RP ABABAB 3 A_Chase();
	C8RP A 0 A_Jump(68, "Dodge");
	Loop;
  Dodge:
	C8RP AB 3 A_Chase(null, null);
	C8RP A 0 A_Dodging(20);
	C8RP ABABAB 3;
	C8RP A 0 A_Stop();
	Goto See;
  Missile:
	C8RP ABABABA 3 A_FaceTarget();
	C8RP KL 4
	{
		for (int i = -13; i < 14; i += 26)
			A_SpawnProjectile("DCY_BigwigPlasma", 20, i, frandom(-1.25, 1.25), CMF_OFFSETPITCH, frandom(-1.25, 1.25));
			
		A_FaceTarget();
	}
	C8RP D 0 Bright A_MonsterRefire(40, "See");
    Goto Missile+7;
  Pain:
    C8RP A 3;
	C8RP B 3 A_Pain();
	Goto See;
  Death:
	C8RP E 1;
	C8RP E 17 A_StartSound("BGSpider/Death", CHAN_VOICE, 0, 1.0, 0.75);
	C8RP F 7 A_NoBlocking();
    C8RP G 0 A_Stop();
	C8RP GGGGG 1 A_SpawnItemEx("DCY_AndroidExplosion", random(-25, 25), random(-25, 25), 25, frandom(1.0, -1.0), frandom(1.0, -1.0), frandom(1.0, -1.0), random(0, 360), SXF_NOCHECKPOSITION);
	C8RP HIJ 7;
    TNT1 A 1 A_BossDeath2();
	Stop;
  XDeath:
	C8RP E 1;
	C8RP E 4 A_StartSound("BGSpider/Scream", CHAN_VOICE, 0, 1.0, 0.75);
	C8RP E 4 A_NoBlocking();
	C8RP EEEEEEFFFF 2 A_SpawnItemEx("DCY_AndroidExplosion", random(-25, 25), random(-25, 25), 25, frandom(1.0, -1.0), frandom(1.0, -1.0), frandom(1.0, -1.0), random(0, 360), SXF_NOCHECKPOSITION);
	C8RP G 0 A_Stop();
	C8RP HIJ 7;
    TNT1 A 10 A_BossDeath2();
	Stop;
  }
}