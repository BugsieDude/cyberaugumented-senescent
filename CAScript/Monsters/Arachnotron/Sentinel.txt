Class DCY_FluorescentSentinel : DCY_SmallEnemySpider
{
  Default
  {
  Health 1222;
  Translation "16:47=%[0.00,0.00,0.00]:[0.31,0.42,1.10]", "55:55=%[0.00,0.00,0.00]:[0.34,0.28,0.28]", "208:208=%[0.00,0.00,0.00]:[0.34,0.28,0.28]", "224:228=%[0.00,0.00,0.00]:[0.34,0.28,0.28]", "236:237=%[0.00,0.00,0.00]:[0.34,0.28,0.28]", "250:254=%[0.00,0.00,0.00]:[0.34,0.28,0.28]", "168:191=%[0.00,0.00,0.50]:[1.02,1.61,2.00]", "128:159=%[0.00,0.00,0.00]:[0.56,0.68,0.99]", "160:167=%[0.00,0.00,0.50]:[1.02,1.63,2.00]", "208:235=%[0.00,0.00,0.50]:[1.02,1.62,2.00]", "236:239=%[0.00,0.00,0.00]:[0.56,0.68,0.99]", "48:79=%[0.00,0.00,0.00]:[0.56,0.68,0.99]", "248:249=%[0.00,0.00,0.50]:[1.02,1.62,2.00]", "13:15=%[0.00,0.00,0.00]:[0.56,0.68,0.99]", "255:255=%[0.00,0.00,0.00]:[0.56,0.68,0.99]", "80:111=%[0.00,0.00,0.00]:[0.60,0.60,0.60]", "5:8=%[0.00,0.00,0.00]:[0.60,0.60,0.60]", "1:2=%[0.00,0.00,0.00]:[0.56,0.69,0.99]";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  BloodColor "53 9A BB";
  Speed 12;
  FastSpeed 18;
  +NOGRAVITY
  +FLOAT
  +MISSILEEVENMORE
  +NOTARGET
  +DONTHARMSPECIES
  +FIRERESIST
  +MISSILEMORE
  +DONTFALL
  +AVOIDMELEE
  +BRIGHT
  Scale 1.25;
  PainChance 25;
  DamageFactor "Ice", 0.8;
  DamageFactor "Neonic", 0.6;
  DropItem "DCY_Palisade", 60;
  Tag "\c[Cyan]Fluorescent \c[Sapphire]Sentinel\c-";
  SeeSound "FSentinel/Sight";
  ActiveSound "FSentinel/Active";
  DeathSound "FSentinel/Death";
  PainSound "FSentinel/Pain";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  }
  
  void A_Orb(double angle, double direct)
  {
	A_StartSound("PsiPistol/Laser", CHAN_WEAPON, 0, 1.0, 0.6);
	A_SpawnProjectile("DCY_FluorescentOrb", 19, direct, angle);
	A_FaceTarget();
  }

  int side;
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen() && DCY_EffectDistance(sfxdistance))
	{
		A_SpawnItemEx("DCY_FluorescentJetFlareSmall", frandom(35, 32), frandom(35, 32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlareSmall", frandom(-35, -32), frandom(-35, -32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlareSmall", frandom(-35, -32), frandom(35, 32),35,0,0,frandom(-4,-6),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlareSmall", frandom(35, 32), frandom(-35, -32),35,0,0,frandom(-4,-6),0,128);
	}
  }
  
  States
  {
  Spawn:
	FL0O AB 2 A_Look();
	Loop;
  See:
	FL0O AB 2 A_Chase();
	Loop;
  Missile:
	FL0O A 0
	{
		side = 1;
		Return A_Jump(130, "Orbs");
	}
	FL0O ABABABABA 2 A_FaceTarget();
	FL0O CD 6
	{
		A_SpawnProjectile("DCY_FSBeam", 19, 8 * side, frandom(-3.5, 3.5));
		A_FaceTarget();
		side = !side;
		Return A_Jump(6, "Orbs");
	}
	FL0O D 0 A_MonsterRefire(66, "Cooldown");
	Goto Missile+10;
  Cooldown:
	FL0O ABABABABABAB 2;
	Goto See;
  Orbs:
	FL0O ABABABABABAB 2 A_FaceTarget();
	FL0O A 1
	{
		If (Health <= 445)
			Return ResolveState("OrbsMode3");
		If (Health <= 866)
			Return ResolveState("OrbsMode2");
		
		Return ResolveState(null);
	}
	FL0O C 10 A_Orb(8, 0);
	FL0O D 10 A_Orb(-8, 0);
	FL0O C 10 A_Orb(8, 0);
	FL0O D 10 A_Orb(-8, 0);
	FL0O ABABA 2;
	Goto See;
  OrbsMode2:
	FL0O C 1 A_Orb(8, random(-10, 10));
	FL0O D 12 A_Orb(-8, random(-10, 10));
	FL0O D 1 A_Orb(8, random(-10, 10));
	FL0O C 12 A_Orb(-8, random(-10, 10));
	FL0O ABABA 2;
	Goto See;
  OrbsMode3:
	FL0O C 1 A_Orb(8, random(-10, 10));
	FL0O D 17 A_Orb(-8, random(-10, 10));
	FL0O D 1 A_Orb(8, random(-10, 10));
	FL0O C 17 A_Orb(-8, random(-10, 10));
	FL0O C 1 A_Orb(8, random(-10, 10));
	FL0O D 17 A_Orb(-8, random(-10, 10));
	FL0O D 1 A_Orb(8, random(-10, 10));
	FL0O C 17 A_Orb(-8, random(-10, 10));
	FL0O ABABA 2;
	Goto See;
  Pain:
	FL0O AB 2;
	FL0O AB 2 A_Pain();
	Goto See;
  Death:
	FL0O F 1;
	FL0O F 13 A_StartSound("FSentinel/Death", CHAN_VOICE, 0, 1.0, 0.35);
	FL0O G 3
	{
		A_NoBlocking();
		bDontFall = false;
		bNoGravity = false;
	}
	FL0O GGGG 1 A_SpawnItemEx("DCY_AndroidExplosion", random(-25, 25), random(-25, 25), 25, frandom(1.0, -1.0), frandom(1.0, -1.0), frandom(1.0, -1.0), random(0, 360), SXF_NOCHECKPOSITION);
	FL0O HIJKL 5;
	FL0O L -1 A_BossDeath2();
	Stop;
  XDeath:
	FL0O F 1;
	FL0O F 13 A_StartSound("FSentinel/Death", CHAN_VOICE, 0, 1.0, 0.35);
	FL0O G 3
	{
		A_NoBlocking();
		bDontFall = false;
		bNoGravity = false;
	}
	FL0O GGGGGGGGGGG 2 A_SpawnItemEx("DCY_AndroidExplosion", random(-25, 25), random(-25, 25), 25, frandom(1.0, -1.0), frandom(1.0, -1.0), frandom(1.0, -1.0), random(0, 360), SXF_NOCHECKPOSITION);
	FL0O HIJKL 5;
	FL0O L -1 A_BossDeath2();
	Stop;
  Death.Massacre:
	FL0O F 1;
	FL0O F 13 A_StartSound("FSentinel/Death", CHAN_VOICE, 0, 1.0, 0.35);
	FL0O G 3
	{
		A_NoBlocking();
		bDontFall = false;
		bNoGravity = false;
	}
	FL0O GGGGGGG 2;
	FL0O HIJKL 5;
	FL0O L -1 A_BossDeath2();
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_FSBeam : FastProjectile
{
  Default
  {
  Radius 15;
  Height 10;
  Speed 23.5;
  FastSpeed 25.5;
  SeeSound "LASER";
  RenderStyle "Add";
  Alpha 0.85;
  Scale 0.6;
  +RIPPER
  +BLOODLESSIMPACT
  +NOEXTREMEDEATH
  +FORCERADIUSDMG
  +BRIGHT
  +SEEKERMISSILE
  -RANDOMIZE
  DamageFunction (1);
  Decal "ImpScorch";
  DamageType "Fluorescent";
  Translation "192:196=[255,255,255]:[128,255,255]", "197:201=[117,255,255]:[0,128,255]", "80:111=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "202:207=[0,117,234]:[0,0,64]", "240:246=[0,0,64]:[0,0,0]";
  }
  States
  {
  Spawn:
	MPB_ A 1
	{
		A_Weave(frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5));
		
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_FluorescentSparkle", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), flags: SXF_NOCHECKPOSITION, failchance: 100);
	}
	Loop;
  Death:
	MPB_ H 3 A_Explode(25, 50);
	MPB_ IJKL 3;
	Stop;
  }
}

Class DCY_FluorescentSparkle : Actor
{
  Default
  {
  +NOCLIP
  +BRIGHT
  +THRUACTORS
  +NOBLOCKMAP
  +ROLLSPRITE
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  Scale 1.0;
  Gravity 0.25;
  }
  
  States
  {
  Spawn:
	SP4K A 0 NoDelay A_SetRoll(frandom(0, 360));
	SP4K ABCDEFG 2;
	Stop;
  }
}

Class DCY_FluorescentAura : Actor
{
  Default
  {
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  +THRUACTORS
  +DONTSPLASH
  RenderStyle "Add";
  Alpha 0.4;
  Scale 0.35;
  Radius 0;
  Height 0;
  }
  
  States
  {
  Spawn:
	EF1_ C 1
	{
		if (Scale.X <= 0.0001 || Alpha <= 0.0001)
			Destroy();
		
		A_FadeOut(0.05, 0);
		A_SetScale(Scale.X-0.025, Scale.Y-0.025);
	}
	Loop;
  }
}

Class DCY_FluorescentOrb : CacodemonBall
{
  Default
  {
  Speed 23;
  FastSpeed 24;
  Radius 18;
  Height 18;
  Damage (3);
  +RIPPER
  +BLOODLESSIMPACT
  +BOUNCEONCEILINGS
  +BOUNCEONWALLS
  +BOUNCEONFLOORS
  +CANBOUNCEWATER
  +BRIGHT
  +FORCERADIUSDMG
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.75;
  SeeSound "";
  BounceSound "PsiPistol/Laser";
  DeathSound "DSPLASM4";
  BounceCount 4;
  BounceFactor 10;
  BounceType "Hexen";
  }
  States
  {
  Spawn:
	C3AL A 1 A_SpawnItemEx("DCY_FluorescentOrbT", flags: SXF_NOCHECKPOSITION);
	Loop;
  Death:
	C3AL A 1
	{
		A_SpawnItemEx("DCY_AndroidExplosion", random(-1, 1), random(-1, 1), random(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_Explode(55, 50, XF_NOTMISSILE, 1);
		A_FadeOut(0.2, 0);
	}
	BL48 ABAB 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_FluorescentOrbT : Actor
{
  Default
  {
  RenderStyle "Add";
  +NOCLIP
  +NOINTERACTION
  Scale 1.75;
  Alpha 1.0;
  +BRIGHT
  }
  
  States
  {
  Spawn:
	TNT1 A 1;
	C3AL ABCDE 2;
	Stop;
  }
}