Class DCY_Receptacle : DCY_ArachnoParent
{
  Default
  {
  Translation "5:15=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "48:111=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "128:167=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "236:239=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "1:3=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "208:231=%[0.00,0.00,0.00]:[2.00,1.06,1.06]", "250:254=%[0.00,0.00,0.00]:[2.00,0.49,0.49]";
  Health 666;
  Speed 13;
  FastSpeed 17;
  SeeSound "DemonicSpider/Sight";
  ActiveSound "DemonicSpider/Active";
  PainSound "DemonicSpider/Pain";
  DeathSound "DemonicSpider/Death";
  Tag "Receptacle";
  GibHealth -500;
  }
  
  void A_Receptacle()
  {
	if (target)
	{
	  A_FaceTarget();
	  SpawnMissile(target, "DCY_ReceptaclePlasma");
	}
  }
  
  States
  {
  Spawn:
	1R1C A 10 A_Look();
	Loop;
  See:
	1R1C A 30;
	1R1C AABB 2 A_Chase();
	1R1C C 2
	{
		A_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C CDDEE 2 A_Chase();
	1R1C F 2
	{
		A_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C F 2 A_Chase();
	Goto See+1;
  Lunge:
	1R1C A 20
	{
		A_FaceTarget();
		
		if (target && distance3D(target) < 340)
			Return ResolveState("MissileContinue");
		
		Return ResolveState(null);
	}
	1R1C G 1
	{
		A_StartSound("DemonicSpider/Jump", CHAN_VOICE);
		A_Hop(13);
		A_Recoil(-20);
		A_FaceTarget();
	}
	1R1C G 1
	{
		if(pos.z <= floorz)
		{
			A_StartSound("HOPPLAND", CHAN_BODY);
			A_QuakeEx(0, 0, 2, 15, 0, 400, "", QF_3D|QF_SCALEDOWN);
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	Goto Lunge+2;
  SmallLunge:
	1R1C A 20 A_FaceTarget();
	1R1C G 1
	{
		A_StartSound("DemonicSpider/Jump", CHAN_VOICE);
		A_Hop(10);
		A_Recoil(-10);
		A_FaceTarget();
	}
	1R1C G 1
	{
		if(pos.z <= floorz)
		{
			A_StartSound("HOPPLAND", CHAN_BODY);
			A_QuakeEx(0, 0, 2, 15, 0, 400, "", QF_3D|QF_SCALEDOWN);
			Return ResolveState("See");
		}
		
		Return ResolveState(null);
	}
	Goto SmallLunge+2;
  Missile:
	1R1C A 1 Bright
	{
		A_FaceTarget();
		If (target && distance3D(target) < 200)
			Return ResolveState("SmallLunge");
		If (target && distance2D(target) < 500)
			Return ResolveState("Lunge");
		Return A_Jump(69, "BigPlasma");
	}
	Goto MissileContinue;
  MissileContinue:
	1R1C A 20 Bright A_FaceTarget();
	1R1C G 4 Bright A_Receptacle();
	1R1C H 4 Bright;
	1R1C H 1 Bright A_SpidRefire();
	Goto MissileContinue+1;
  BigPlasma:
	1R1C A 30 Bright A_FaceTarget();
	1R1C G 3 Bright A_FaceTarget();
	1R1C H 3
	{
		A_StartSound("CONVFIRE", attenuation: 0.85);
		
		if (target)
		{
			A_FaceTarget();
			A_SpawnProjectile("DCY_ReceptacleBigPlasma", 25);
		}
	}
	1R1C G 10 Bright;
	Goto See;
  Pain:
	1R1C I 3;
	1R1C I 3 A_Pain();
	Goto See+1;
  Death:
	1R1C J 20 A_Scream();
	1R1C K 7 A_NoBlocking();
	1R1C LMNO 7;
	1R1C P -1 A_BossDeath2;
	Stop;
  Raise:
	1R1C P 5;
	1R1C ONMLKJ 5;
	Goto See+1;
  }
}

Class DCY_ReceptaclePlasma : DCY_AnathemaProjectile
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.83,0.76,0.64]";
  -SEEKERMISSILE;
  +NEVERFAST;
  Radius 13;
  Height 8;
  Speed 25;
  Damage 5;
  Scale 1.0;
  +RANDOMIZE
  SeeSound "UNHLYSHT";
  DeathSound "CONVFIRX";
  }
  
  States
  {
  Spawn:
	SUE2 A 0 NoDelay A_SetRoll(frandom(0.00, 360.00));
	SUE2 AAAABBBB 1
	{
		A_SetRoll(Roll+10, SPF_INTERPOLATE);
		A_SpawnItemEx("DCY_AnathemaAura", xvel: -1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
	}
	Goto Spawn+1;
  }
}

Class DCY_ReceptacleBigPlasma : DCY_ReceptaclePlasma
{
  Default
  {
  Scale 1.75;
  Speed 15;
  Radius 20;
  Height 14;
  DeathSound "HOPPEXPL";
  SeeSound "CONVFIRE";
  }
  States
  {
  Spawn:
	SUE1 A 0 NoDelay A_SetRoll(frandom(0.00, 360.00));
	SUE1 AAAABBBB 1
	{
		A_SetRoll(Roll+25, SPF_INTERPOLATE);
		A_SpawnItemEx("DCY_AnathemaAura", xvel: -1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION);
	}
	Goto Spawn+1;
  Death:
	SUE1 C 1
	{
		A_Explode(30, 128);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Stop;
  }
}