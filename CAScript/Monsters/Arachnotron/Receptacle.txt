Class DCY_Receptacle : DCY_ArachnoParent
{
  Default
  {
  Health 666;
  Speed 13;
  FastSpeed 17;
  SeeSound "DemonicSpider/Sight";
  ActiveSound "DemonicSpider/Active";
  PainSound "DemonicSpider/Pain";
  DeathSound "DemonicSpider/Death";
  Tag "Receptacle";
  BloodColor "Blue";
  GibHealth -500;
  DCYMonster.ScientificName "Diabolicus ignis fatuus";
  }
  
  void A_Receptacle()
  {
	if (target)
	{
	  A_FaceTarget();
	  if (target && (distance3D(target) < 350)) A_StartSound("PhantomRev/Breath", CHAN_WEAPON, CHANF_LOOPING, pitch: frandom(2.1, 2.3));
	  else A_StopSound(CHAN_WEAPON);
	  A_SpawnProjectile((distance3D(target) < 350) ? "DCY_ReceptacleThrower" : "DCY_ReceptaclePlasma", 30, 0, (distance3D(target) < 350) ? frandom(-1, 1) : frandom(-8, 8));
	}
  }
  
  States
  {
  Spawn:
	2R2C A 10 DCY_Look();
	Loop;
  Look:
	2R2C AABB 2 DCY_LookingForPlayer();
	2R2C C 2
	{
		DCY_LookingForPlayer();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	2R2C CDDEE 2 DCY_LookingForPlayer();
	2R2C F 2
	{
		DCY_LookingForPlayer();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	2R2C F 2 DCY_LookingForPlayer();
	Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	2R2C A 30;
	2R2C AABB 2 DCY_Chase();
	2R2C C 2
	{
		DCY_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	2R2C CDDEE 2 DCY_Chase();
	2R2C F 2
	{
		DCY_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	2R2C F 2 DCY_Chase();
	Goto See+1;
  Lunge:
	2R2C A 20
	{
		if (target && distance3D(target) < 200) return;
		A_FaceTarget();
		
		if (target && distance3D(target) < 340)
			SetStateLabel("MissileContinue");
	}
	2R2C G 1
	{
		A_StartSound("DemonicSpider/Jump", CHAN_VOICE);
		
		if (target)
		{
			A_Hop((distance3D(target) < 200) ? 10 : 13);
			A_Recoil((distance3D(target) < 200) ? -10 : -20);
		}
		
		A_FaceTarget();
	}
	2R2C G 1
	{
		if(pos.z <= floorz)
		{
			A_StartSound("HOPPLAND", CHAN_BODY);
			A_QuakeEx(0, 0, 2, 15, 0, 400, "", QF_3D|QF_SCALEDOWN);
			SetStateLabel("See");
		}
	}
	Goto Lunge+2;
  Missile:
	2R2C A 1
	{
		A_FaceTarget();
		If (!random(0, 1) && (target && distance2D(target) < 500))
			SetStateLabel("Lunge");
	}
	Goto MissileContinue;
  MissileContinue:
	2R2C A 5 A_FaceTarget();
	2R2C G 2
	{
		if (DCY_EffectDistance(1000))
		{
			for (int i = random(10, 20); i > 0; i--)
			{
				A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(1, 6), 0, frandom(-1, 1) + 25, frandom(-1, 1), frandom(-1, 1) + 25, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
				A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(4, 18), 0, frandom(-1, 1) + 25, frandom(-1, 1), frandom(-1, 1) + 25, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
			}
		}
		
		A_StartSound("DemonicSpider/Ready", CHAN_WEAPON);
	}
	2R2C GGGGGGGGGGGGGGG 2
	{
		if (DCY_EffectDistance(1000))
			A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(1, 6), 0, frandom(-1, 1) + 25, frandom(-1, 1), frandom(-1, 1) + 25, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
		
		A_FaceTarget();
	}
	2R2C H 2 A_Receptacle();
	2R2C G random(3, 7)
	{
		if (target && (distance3D(target) < 350)) tics = 0;
		else tics = random(3, 7);
	}
	2R2C G 1 A_MonsterRefire(40, "Cooldown");
	Goto MissileContinue+17;
  Cooldown:
	2R2C G 0 A_StopSound(CHAN_WEAPON);
	Goto See;
  Pain:
	2R2C I 3 A_StopSound(CHAN_WEAPON);
	2R2C I 3 A_Pain();
	Goto See+1;
  Death:
	2R2C J 0;
	2R2C J 5 A_StopSound(CHAN_WEAPON);
	2R2C J 20 A_Scream();
	2R2C K 7 { DCY_ArachnoExplosion(); A_NoBlocking(); }
	2R2C LMNO 7;
	2R2C P -1 A_BossDeath2;
	Stop;
  Raise:
	2R2C P 5;
	2R2C ONMLKJ 5;
	Goto See+1;
  }
}

Class DCY_ReceptaclePlasma : DCY_RehauledPlasma
{
  Default
  {
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.20,0.00]";
  Radius 13;
  Height 8;
  Speed 15;
  FastSpeed 18;
  Damage 3;
  +RANDOMIZE
  +SEEKERMISSILE
  SeeSound "SpearGun";
  DeathSound "ArgentBall";
  DCY_RehauledPlasma.AuraColor "b83700";
  }
  
  bool home;
  
  States
  {
  Spawn:
	C3AL AABB 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnItemEx("DCY_Arc", flags: SXF_TRANSFERTRANSLATION, failchance: 240);
			vel.x += frandom(-0.5, 0.5);
			vel.y += frandom(-0.5, 0.5);
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.085, sizestep: 0.825);
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), vel.x / 4, vel.y / 4, vel.z / 4, fadestepf: -4);
		}
		
		if (tracer && CheckSight(tracer) && distance3D(tracer) < 250 && !home) { A_Tracer2(155); home = true; }
	}
	Loop;
  Death:
	MPB_ A 3
	{
		A_SetScale(1.0, 1.0);
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_SetRoll(frandom(0.00, 360.00));
		for (int i = 7; i > 0; i--)
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	PLSE BCDE 2;
	Stop;
  }
}

Class DCY_ReceptacleThrower : DCY_KranionBall
{
  Default
  {
  DamageFunction (3);
  DamageType "Fire";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.20,0.00]";
  Speed 25;
  +NEVERFAST
  SeeSound "";
  DeathSound "";
  }
  States
  {
  Spawn:
	SMOL EEEFFFGGGHHHIIIJJJKKKLlL 1
	{
		roll = frandom(0, 360);
		A_Weave(1, 1, 1, 1);
		A_Weave(3, 0, frandom(1, 3), 0);
		for (int i = 2; i > 0; i--) A_SpawnItemEx("DCY_KranionBallTrail", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERROLL);
		A_SpawnParticleEx(0xb83700, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 128, 25, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0065, sizestep: 1.525);
	
	}
	Stop;
  }
}