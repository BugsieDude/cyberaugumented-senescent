Class DCY_Receptacle : DCY_ArachnoParent
{
  Default
  {
  Translation "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.51,0.51,0.51]", "5:12=%[0.00,0.00,0.00]:[0.51,0.51,0.51]", "16:47=%[0.40,0.00,0.00]:[2.00,0.00,0.00]", "250:254=%[0.50,0.00,0.00]:[2.00,1.01,0.00]", "128:159=%[0.00,0.00,0.00]:[0.72,0.61,0.54]", "48:79=%[0.00,0.00,0.00]:[0.72,0.61,0.54]", "13:15=%[0.00,0.00,0.00]:[0.72,0.61,0.54]", "236:239=%[0.00,0.00,0.00]:[0.72,0.61,0.54]";
  Health 666;
  Speed 13;
  FastSpeed 17;
  SeeSound "DemonicSpider/Sight";
  ActiveSound "DemonicSpider/Active";
  PainSound "DemonicSpider/Pain";
  DeathSound "DemonicSpider/Death";
  Tag "Receptacle";
  GibHealth -500;
  DCYMonster.ScientificName "Diabolicus ignis fatuus";
  }
  
  void A_Receptacle()
  {
	if (target)
	{
	  A_FaceTarget();
	  A_SpawnProjectile("DCY_ReceptaclePlasma", 30, 0, frandom(-8, 8));
	}
  }
  
  States
  {
  Spawn:
	1R1C A 10 DCY_Look();
	Loop;
  Look:
	1R1C AABB 2 DCY_LookingForPlayer();
	1R1C C 2
	{
		DCY_LookingForPlayer();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C CDDEE 2 DCY_LookingForPlayer();
	1R1C F 2
	{
		DCY_LookingForPlayer();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C F 2 DCY_LookingForPlayer();
	Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	1R1C A 30;
	1R1C AABB 2 A_Chase();
	1R1C C 2
	{
		A_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C CDDEE 2 A_Chase();
	1R1C F 2
	{
		A_Chase();
		A_StartSound("DemonicSpider/Step", CHAN_BODY);
	}
	1R1C F 2 A_Chase();
	Goto See+1;
  Lunge:
	1R1C A 20
	{
		if (target && distance3D(target) < 200) return;
		A_FaceTarget();
		
		if (target && distance3D(target) < 340)
			SetStateLabel("MissileContinue");
	}
	1R1C G 1
	{
		A_StartSound("DemonicSpider/Jump", CHAN_VOICE);
		
		if (target)
		{
			A_Hop((distance3D(target) < 200) ? 10 : 13);
			A_Recoil((distance3D(target) < 200) ? -10 : -20);
		}
		
		A_FaceTarget();
	}
	1R1C G 1
	{
		if(pos.z <= floorz)
		{
			A_StartSound("HOPPLAND", CHAN_BODY);
			A_QuakeEx(0, 0, 2, 15, 0, 400, "", QF_3D|QF_SCALEDOWN);
			SetStateLabel("See");
		}
	}
	Goto Lunge+2;
  Missile:
	1R1C A 1
	{
		A_FaceTarget();
		If (!random(0, 1) && (target && distance2D(target) < 500))
			SetStateLabel("Lunge");
	}
	Goto MissileContinue;
  MissileContinue:
	1R1C A 5 A_FaceTarget();
	1R1C G 2 A_StartSound("DemonicSpider/Ready", CHAN_WEAPON);
	1R1C GGGGGGGGGGGGGGG 2 A_FaceTarget();
	1R1C G 2 A_Receptacle();
	1R1C H 3;
	1R1C H 1 A_SpidRefire();
	Goto MissileContinue+17;
  Pain:
	1R1C I 3;
	1R1C I 3 A_Pain();
	Goto See+1;
  Death:
	1R1C J 20 A_Scream();
	1R1C K 7 { DCY_ArachnoExplosion(); A_NoBlocking(); }
	1R1C LMNO 7;
	1R1C P -1 A_BossDeath2;
	Stop;
  Raise:
	1R1C P 5;
	1R1C ONMLKJ 5;
	Goto See+1;
  }
}

Class DCY_ReceptaclePlasma : DCY_RehauledPlasma
{
  Default
  {
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.20,0.00]";
  Radius 13;
  Height 8;
  Speed 25;
  FastSpeed 28;
  Damage 3;
  Scale 1.1;
  +RANDOMIZE
  SeeSound "UNHLYSHT";
  DCY_RehauledPlasma.AuraColor "b83700";
  }
  
  States
  {
  Spawn:
	MPB_ A 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 200);
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), vel.x / 4, vel.y / 4, vel.z / 4, fadestepf: -2);
			A_SpawnItemEx("DCY_CWFXTrail", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL);
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 20, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.085, sizestep: -1.225);
		}
	}
	Loop;
  Death:
	MPB_ A 3
	{
		A_SetScale(1.0, 1.0);
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_SetRoll(frandom(0.00, 360.00));
		for (int i = 7; i > 0; i--)
			A_SpawnParticleEx(ac, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	PLSE BCDE 2;
	Stop;
  }
}