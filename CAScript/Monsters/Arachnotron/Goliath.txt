Class DCY_Goliath : DCY_ArachnoParent
{
  Default
  {
  Tag "\c[Red]Goliath\c- [Abyssal Polypheme]";
  Translation "48:79=%[0.00,0.00,0.00]:[0.65,0.29,0.29]", "13:15=%[0.00,0.00,0.00]:[0.65,0.29,0.29]", "160:167=%[0.00,0.00,0.00]:[0.65,0.29,0.29]", "208:249=%[0.00,0.00,0.00]:[0.65,0.29,0.29]", "128:159=%[0.00,0.00,0.00]:[0.65,0.29,0.29]";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +BOSS
  SeeSound "roboarachno/sight";
  ActiveSound "roboarachno/active";
  PainSound "roboarachno/pain";
  DeathSound "roboarachno/death";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  Alpha 1.0;
  Scale 1.3;
  Health 1300;
  Speed 6;
  FastSpeed 10;
  Obituary "%o felt the might of a Goliath.";
  }
  
  States
  {
  Spawn:
	G___ A 10 DCY_Look();
	Loop;
  Look:
    G___ BBCCDDEEFF 4 DCY_LookingForPlayer();
    G___ G 4
    {
        DCY_LookingForPlayer();
		A_StartSound("roboarachno/step", CHAN_BODY);
	}
    G___ GHHIIHHGG 4 DCY_LookingForPlayer();
	G___ B 0 A_StartSound("roboarachno/step", CHAN_BODY);
	Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
    G___ BBCCDDEEFF 4 A_Chase();
    G___ G 4
    {
        A_Chase();
		A_StartSound("roboarachno/step", CHAN_BODY);
	}
    G___ GHHIIHHGG 4 A_Chase();
	G___ B 0 A_StartSound("roboarachno/step", CHAN_BODY);
	Loop;
  MissileStahp:
	Goto Missile+1;
  Missile:
	G___ J 10
	{
		If (target && Distance3D(target) < 340)
			Return ResolveState("Dash");
		
		A_FaceTarget();
		Return ResolveState(null);
	}
	G___ JK 10 A_FaceTarget();
	G___ KKKKKKKK 7
	{
		A_StartSound("DSSUPPLS", CHAN_WEAPON);
		A_SpawnProjectile("DCY_GoliathBall", 40, -10, frandom(-2.0, 2.0));
		A_SpawnProjectile("DCY_GoliathBall", 40, 10, frandom(-2.0, 2.0));
		A_FaceTarget();
	}
	G___ JA 4;
	Goto See;
  Dash:
	G___ J 1 A_StartSound("roboarachno/active", CHAN_VOICE, CHANF_OVERLAP|CHANF_DEFAULT);
	G___ JKL 4 A_FaceTarget();
	G___ MMMM 3
	{
		A_SpawnItemEx("DCY_GoliathDash", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_Recoil(-4);
	}
	G___ M 3
	{
		A_SpawnItemEx("DCY_GoliathDash", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_Recoil(-4);
		A_FaceTarget();
	}
	G___ MMMM 3
	{
		A_SpawnItemEx("DCY_GoliathDash", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		A_Recoil(-4);
	}
	G___ M 0 A_Stop();
	Goto MissileStahp;
  Pain:
	G___ A 4;
	G___ A 4 A_Pain();
	Goto See;
  Death:
	G___ A 10;
	G___ A 50 A_Scream();
	G___ A 0
	{
		if (damagetype == "Massacre")
		{
			A_BossDeath2();
			Destroy();
		}
	}
	G___ U 10
	{
		A_NoBlocking();
		A_StartSound("weapons/rocklx", CHAN_5, 0, 1.0, 0.95);
		A_StartSound("IABIGEXP", CHAN_BODY, 0, 1.0, 0.5, 0.85);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", 0, 0, 50);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", 0, 0, 50);
	}
	G___ U 1 A_StopSound(CHAN_BODY);
	G___ U 1
	{
		A_StartSound("weapons/rocklx", CHAN_6, 0, 1.0, 1.1);
		A_StartSound("UCKABEWM", CHAN_BODY, 0, 1.0, 0.5, 0.95);
		A_BossDeath2();
	}
	Stop;
  }
}

Class DCY_GoliathDash : DCY_HexodenicanTrail { Default { StencilColor "72 78 AD"; Alpha 0.65; } }

Class DCY_GoliathBall : CacodemonBall
{
  Default
  {
  Radius 13;
  Height 13;
  Scale 0.9115;
  Speed 25;
  FastSpeed 30;
  DamageType "Cybernetical";
  RenderStyle "Add";
  Alpha 1.0;
  +ROLLSPRITE
  +BRIGHT
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  SeeSound "DSSUPPLS";
  DeathSound "MBALEXPL";
  }
  
  States
  {
  Spawn:
	SUE1 A 1
	{
		A_SpawnItemEx("DCY_GoliathBallTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERROLL);
		A_Weave(1, 1, 1, 1);
		A_SetRoll(Roll+5);
	}
	Loop;
  Death:
	SUE2 FGHIJKLM 1 A_SetScale(Scale.X+0.034, Scale.Y+0.034);
	Stop;
  }
}

Class DCY_GoliathBallTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Scale 0.8855;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,0.80,2.00]";
  Alpha 0.95;
  +ROLLSPRITE
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  }
  
  States
  {
  Spawn:
	SUE1 ABCDEF 3 A_FadeOut(0.2, 0);
	Stop;
  }
}