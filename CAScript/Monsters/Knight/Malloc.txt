Class DCY_Malloc : DCY_KnightHell
{
  Default
  {
  Health 625;
  Tag "Malloc";
  Speed 10;
  FastSpeed 13;
  Translation "80:111=%[0.00,0.00,0.00]:[0.50,0.50,0.50]", "1:3=%[0.00,0.00,0.00]:[0.59,0.59,0.59]", "48:79=%[0.00,0.00,0.00]:[1.30,0.24,0.24]", "128:159=%[0.00,0.00,0.00]:[1.30,0.24,0.24]", "236:239=%[0.00,0.00,0.00]:[1.30,0.24,0.24]", "13:15=%[0.00,0.00,0.00]:[1.30,0.24,0.24]";
  Obituary "%o got slithered by the Malloc's mastery.";
  HitObituary "%o got slithered by the Malloc's mastery.";
  SeeSound "Malloc/Sight";
  ActiveSound "Malloc/Active";
  PainSound "Malloc/Pain";
  DeathSound "Malloc/Death";
  MeleeDamage 15;
  Damage 15;
  +DONTHARMCLASS
  +MISSILEMORE
  BloodColor "Red";
  DCYMonster.ScientificName "Brutis eruca";
  DCYMonster.FootstepSound "dcyabater/walk";
  }
  
  void A_Malloc()
  {
	if (target)
	{
		A_FaceTarget();
		A_StartSound("skeleton/attack", CHAN_WEAPON);
		A_SpawnProjectile("DCY_MallocMissile", 34, 5, random(-2, 2));
	}
  }
  
  int missile;
  
  States
  {
  Spawn:
    BRUS A 10 DCY_Look();
	Loop;
  Look:
	BRUS AAABBBCCCDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	BRUS AAABBBCCCDDD 3 DCY_Chase();
	Loop;
  Missile:
	BRUS E 0 A_Jump(200, "Ball");
	BRUS E 20
	{
		missile = 0;
		A_FaceTarget();
	}
	BRUS F 5 A_Malloc();
	BRUS E 10
	{
		if (missile >= 2)
			SetStateLabel("See");
		
		A_FaceTarget();
		missile++;
	}
    Goto Missile+2;
  Ball:
	BRUS GGGGGGGGGGGGGGGG 1
	{
		A_SpawnParticleEx(0xfc4903, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 45, 0, 5 + frandom(-2.5, 2.5), -35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -2.325);
		A_FaceTarget();
	}
	BRUS H 8 A_FaceTarget();
	BRUS I 10 A_SpawnProjectile("DCY_MallocFireball", 35);
	Goto See;
  Melee:
    BRUS GH 6 A_FaceTarget();
	BRUS I 6 A_CustomMeleeAttack(15, "baron/melee");
	Goto See;
  Pain:
    BRUS J 4;
	BRUS J 4 A_Pain();
	Goto See;
  Death:
	BRUS K 0
	{
		switch (random(0, 2))
		{
			case 0: return ResolveState("Death2"); break;
			case 1: return ResolveState("Death3"); break;
			case 2: return ResolveState(null); break;
		}
		
		return ResolveState(null);
	}
	Goto Death1;
  Death1:
    BRUS K 1;
	BRUS K 6 A_Scream();
	BRUS LM 5
	{
		if (damagetype != "Massacre")
		{
			for (int i = 2; i > 0; i--)
				A_SpawnItemEx("DCY_Explosion_1", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(25.0, 35.0), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
	}
	BRUS M 1 A_NoBlocking();
	BRUS OPQRS 5;
	BRUS T -1;
	Stop;
  Death2:
	BRUS K 1;
	BRUS K 6 A_Scream();
	BD1_ A 6 A_NoBlocking();
	BD1_ BCDE 6;
	BD1_ F -1;
	Stop;
  Death3:
	BD2_ A 1;
	BD2_ A 6 A_Scream();
	BD2_ B 6 A_NoBlocking();
	BD2_ CDD 6;
	BD2_ EF 5
	{
		if (damagetype != "Massacre")
		{
			for (int i = 2; i > 0; i--)
				A_SpawnItemEx("DCY_Explosion_1", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(10.0, 20.0), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		}
	}
	BD2_ G 5;
	BD2_ H -1;
	Stop;
  Raise:
    BRUS S 4;
	BRUS RQPOLMK 4;
	Goto See;
  }
}

Class DCY_MallocMissile : DCY_CyberMicroMissile { Default { Damage 5; SeeSound "skeleton/attack"; } }
Class DCY_MallocFireball : DCY_BaronFireball
{
  Default
  {
  DamageType "Fire";
  Speed 12;
  FastSpeed 22;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.61,0.00]";
  }
  
  States
  {
  Spawn:
	BAL7 AABB 1
	{
		A_SpawnFlames("fc4903", 15, 40, 0, false, frandom(-1.5, 1.5), thrust: 4);
		A_SpawnItemEx("DCY_BaronKnightFlame", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
	}
	Loop;
  Death:
	BAL7 CCCDDDEEE 1
	{
		A_FadeOut(0.1, 0);
		A_SetScale(Scale.X+0.1, Scale.Y+0.1);
	}
	Stop;
  }
}