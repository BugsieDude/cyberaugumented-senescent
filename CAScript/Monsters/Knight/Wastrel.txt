Class DCY_Wastrel : DCY_KnightHell
{
  Default
  {
  Translation "168:173=[255,255,255]:[255,255,128]", "174:180=[255,255,128]:[255,121,0]", "181:191=[255,121,0]:[64,9,0]", "224:231=[255,255,255]:[255,255,128]", "208:216=[255,255,255]:[255,255,128]", "217:223=[255,255,128]:[255,121,0]", "232:235=%[0.00,0.00,0.00]:[2.00,0.95,0.00]", "236:239=%[0.00,0.00,0.00]:[0.48,0.48,0.48]", "48:79=%[0.00,0.00,0.00]:[1.13,0.35,0.35]", "128:159=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "13:15=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "160:167=[255,255,128]:[64,9,0]", "16:23=[255,255,255]:[255,255,128]", "24:31=[255,255,128]:[255,121,0]", "32:47=[255,121,0]:[64,9,0]", "192:195=[255,255,255]:[255,255,128]", "196:201=[255,255,128]:[255,121,0]", "202:207=[255,121,0]:[64,9,0]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.50,0.07,0.00]:[2.00,2.00,1.01]";
  Health 900;
  SeeSound "Wastrel/Sight";
  ActiveSound "Wastrel/Active";
  PainSound "Wastrel/Pain";
  DeathSound "Wastrel/Death";
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  Speed 12;
  FastSpeed 18;
  Scale 1.2;
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.2;
  Tag "\c[l4]Wastrel\c-";
  }
  
  States
  {
  Spawn:
	WAWA A 10 DCY_Look();
	Loop;
  Look:
	WAWA AAAABBBBCCCCDDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	WAWA FGFE 7 A_FaceTarget();
	Goto See;
  See:
	WAWA AAAAABBBBBCCCCCDDDDD 2 A_Chase();
	Loop;
  Melee:
	WAWA EFGF 4 A_FaceTarget();
	WAWA E 4 A_CustomMeleeAttack(10 * random(3, 7), "BigHit");
	Goto See;
  Missile:
	WAWA E 0
	{
		if (target && distance3D(target) < 1200)
			Return A_Jump(100, "Leap");
		
		Return ResolveState(null);
	}
	Goto MissileContinue;
  MissileContinue:
	WAWA EFG 4 A_FaceTarget();
	WAWA G 10
	{
		A_FaceTarget();
		
		for (int i = -30; i < 31; i += 60)
		{
			A_SpawnProjectile("DCY_WastrelBall", 55, i);
			A_SpawnProjectile("DCY_WastrelBall", 25, i);
		}
	}
	WAWA F 5;
	Goto See;
  Leap:
	WAWA A 1
	{
		A_FaceTarget();
		Return A_Jump(80, "MissileContinue");
	}
	WAWA AAAAAABBBBBBCCCCCCDDDDDD 1 A_Recoil(-2);
	Loop;
  Pain:
	WAWA H 4;
	WAWA H 4 A_Pain();
	Goto See;
  Death:
	WAWA H 6;
	WAWA I 6 A_Scream();
	WAWA J 5 A_NoBlocking();
	WAWA KLM 4;
	WAWA M -1;
	Stop;
  Raise:
	WAWA MLKJIH 5;
	Goto See;
  }
}

Class DCY_WastrelBall : CacodemonBall
{
  Default
  {
  Speed 20;
  FastSpeed 30;
  Damage 3;
  Scale 0.75;
  DamageType "Fire";
  Translation "0:255=%[0.00,0.00,0.00]:[1.21,0.80,0.13]";
  }
  
  States
  {
  Spawn:
	B1L9 AABBCC 1
	{
		A_SpawnItemEx("DCY_RocketSmonk", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_RocketFlare", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 100);
	}
	Loop;
  Death:
	M_TR CDEFGH 3;
	Stop;
  }
}