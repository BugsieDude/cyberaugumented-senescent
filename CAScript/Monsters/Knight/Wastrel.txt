Class DCY_Wastrel : DCY_KnightHell
{
  Default
  {
  Translation "128:159=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "48:79=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "1:3=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "13:15=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "236:239=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "168:178=[255,255,0]:[255,128,0]", "179:191=[255,128,0]:[72,0,0]", "160:167=%[0.00,0.00,0.00]:[0.58,0.42,0.42]", "80:111=%[0.00,0.00,0.00]:[0.34,0.34,0.34]", "16:47=%[0.00,0.00,0.00]:[2.00,1.01,0.00]", "208:231=%[0.00,0.00,0.00]:[2.00,1.01,0.00]", "255:255=%[0.00,0.00,0.00]:[0.34,0.34,0.34]";
  Health 900;
  SeeSound "Wastrel/Sight";
  ActiveSound "Wastrel/Active";
  PainSound "Wastrel/Pain";
  DeathSound "Wastrel/Death";
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  Speed 12;
  FastSpeed 18;
  Scale 1.2;
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.2;
  Tag "\c[l4]Wastrel\c-";
  DCYMonster.ScientificName "Ignis serpentis";
  DCYMonster.FootstepSound "";
  }
  
  States
  {
  Spawn:
	WAWA A 10 DCY_Look();
	Loop;
  Look:
	WAWA AAAABBBBCCCCDDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	WAWA FGFE 7 A_FaceTarget();
	Goto See;
  See:
	WAWA AAAAABBBBBCCCCCDDDDD 2 DCY_Chase();
	Loop;
  Melee:
	WAWA E 0 A_StartSound("Wastrel/Swing", CHAN_BODY);
	WAWA EFGF 4 A_FaceTarget();
	WAWA E 4 A_CustomMeleeAttack(10 * random(3, 7), "BigHit");
	Goto See;
  Missile:
	WAWA E 0
	{
		if (target && distance3D(target) < 1200)
			Return A_Jump(100, "Leap");
		
		Return ResolveState(null);
	}
	Goto MissileContinue;
  MissileContinue:
	WAWA E 0 A_StartSound("Wastrel/Swing", CHAN_BODY);
	WAWA EFG 4 A_FaceTarget();
	WAWA G 10
	{
		A_FaceTarget();
		
		for (int i = -30; i < 31; i += 60)
		{
			A_SpawnProjectile("DCY_WastrelBall", 55, i);
			A_SpawnProjectile("DCY_WastrelBall", 25, i);
		}
	}
	WAWA F 5;
	Goto See;
  Leap:
	WAWA A 1
	{
		A_FaceTarget();
		Return A_Jump(80, "MissileContinue");
	}
	WAWA AAAAAABBBBBBCCCCCCDDDDDD 1 A_Recoil(-1.65);
	Loop;
  Pain:
	WAWA H 4;
	WAWA H 4 A_Pain();
	Goto See;
  Death:
	WAWA H 6;
	WAWA I 6 A_Scream();
	WAWA J 5 A_NoBlocking();
	WAWA KLM 4;
	WAWA M -1;
	Stop;
  Raise:
	WAWA MLKJIH 5;
	Goto See;
  }
}

Class DCY_WastrelBall : DCY_MeatballElecball
{
  Default
  {
  Speed 20;
  FastSpeed 30;
  Damage 3;
  Scale 0.75;
  DamageType "Fire";
  Translation "0:255=%[0.00,0.00,0.00]:[1.21,0.80,0.13]";
  }
  
  States
  {
  Spawn:
	B1L9 AABBCC 1
	{
		A_SpawnItemEx("DCY_RocketSmonk", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_RocketFlare", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 100);
	}
	Loop;
  Death:
	M_TR CDEFGH 3;
	Stop;
  }
}