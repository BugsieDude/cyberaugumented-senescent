Class DCY_CalamityTsar : DCY_KnightHell
{
  Default
  {
  Health 850;
  Translation "16:47=%[0.00,0.00,0.00]:[1.29,0.57,2.00]", "255:255=%[0.00,0.00,0.00]:[1.01,0.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "0:15=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "48:79=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "128:159=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "236:239=%[0.00,0.00,0.00]:[0.36,0.36,0.48]", "112:116=[255,255,255]:[255,255,128]", "117:121=[255,255,128]:[255,174,64]", "122:127=[255,169,53]:[81,40,0]", "160:235=%[0.00,0.00,0.00]:[2.00,1.37,0.50]", "240:254=%[0.00,0.00,0.00]:[2.00,1.37,0.50]";
  Tag "Calamity Tsar";
  Obituary "%o vanished before the Calamity Tsar.";
  HitObituary "%o got the slash of the Calamity Tsar.";
  +MISSILEMORE
  +DONTGIB
  +QUICKTORETALIATE
  PainChance 65;
  BloodColor "Yellow";
  SeeSound "Tsar/Sight";
  ActiveSound "Tsar/Active";
  PainSound "Tsar/Pain";
  DeathSound "Tsar/Death";
  DCYMonster.WalkFlames "DCY_YellowWalkFlames";
  DCYMonster.ScientificName "Brutis ductor";
  Speed 8;
  FastSpeed 10;
  }
  
  States
  {
  Spawn:
	HL59 A 10 DCY_Look();
	Loop;
  Look:
	HL59 AABBCCDD 3 { if (!random(0, 3)) A_WalkFlame(); DCY_LookingForPlayer(); }
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	HL59 AABBCCDD 3 { if (!random(0, 3)) A_WalkFlame(); DCY_Chase(); }
	Loop;
  Missile:
	HL59 A 0 A_Jump(128, "Ball");
	HL59 A 0 A_Jump(70, "SuperDash");
	Goto Ball;
  Ball:
	HL59 EF 7 A_FaceTarget();
	HL59 G 5
	{
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(-2.0, -4.0));
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(2.0, 4.0));
	}
	HL59 F 5;
	HL59 ST 7 A_FaceTarget();
	HL59 U 5
	{
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(-2.0, -4.0));
		A_SpawnProjectile("DCY_TsarBall", 34, 0, frandom(2.0, 4.0));
	}
	HL59 T 5;
	Goto See;
  SuperDash:
	HL59 AAAA 7;
	TNT1 AAAAAA 1
	{
		for (int i = 10; i > 0; i--)
		{
			A_Chase(null, null, CHF_NOPLAYACTIVE|CHF_NIGHTMAREFAST);
			A_SpawnItemEx("DCY_CalamityTsarSuperAura");
		}
	}
	TNT1 A 0
	{
		If (target && Distance3D(target) < 256)
			Return ResolveState("Ball");
			
		Return A_Jump(25, "Ball");
	}
	Goto SuperDash+4;
  Melee:
    HL59 EF 5 A_FaceTarget();
	HL59 G 5 A_CustomMeleeAttack(6 * random(1, 10), "knight/melee", "");
	HL59 F 5 A_FaceTarget();
	HL59 ST 5 A_FaceTarget();
	HL59 U 5 A_CustomMeleeAttack(6 * random(1, 10), "knight/melee", "");
	HL59 T 5 A_FaceTarget();
	Goto See;
  Pain:
	HL59 K 3;
	HL59 K 3 A_Pain();
	Goto See;
  Death:
	HL59 M 1;
	HL59 M 10 A_ScreamAndUnblock();
	HL59 NOPQ 4;
	HL59 R -1;
	Stop;
  Raise:
	HL59 QP 5;
	HL59 ONMK 3;
	Goto See;
  }
}

Class DCY_CalamityTsarSuperAura : Actor
{
  Default
  {
  RenderStyle "AddStencil";
  StencilColor "FF FF 99";
  Alpha 0.75;
  Scale 1.0;
  Radius 0;
  Height 0;
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	HL59 "#" 1
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.035, Scale.Y+0.035);
	}
	HL59 "#" 0 A_JumpIf(Alpha <= 0.0000000000000, "Stahp");
	Loop;
  Stahp:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_TsarBall : DCY_BaronFireball
{
  Default
  {
  Damage 4;
  Speed 12.5;
  FastSpeed 22.5;
  +BRIGHT
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  States
  {
  Spawn:
	BAL7 AABB 1
	{
		A_Weave(2, 0, 2, 0);
		A_SpawnFlames("ffe66b", 15, 40, 0, false, frandom(-1.5, 1.5), thrust: 4);
		A_SpawnItemEx("DCY_BaronKnightFlame", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
	}
	Loop;
  Death:
	BAL7 CCCDDDEEE 1
	{
		A_FadeOut(0.1, 0);
		A_SetScale(Scale.X+0.1, Scale.Y+0.1);
	}
	Stop;
  }
}