Class DCY_HellCustodian : DCY_FlyingMeatballOutOfTouch
{
  Default
  {
  Health 900;
  Tag "\c[X1]Hell Custodian\c-";
  Translation "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "5:12=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "13:15=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "48:79=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "128:159=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "236:239=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "160:191=%[0.50,0.00,0.00]:[2.00,1.10,0.10]", "208:235=%[0.50,0.00,0.00]:[2.00,1.11,0.10]", "16:47=%[0.50,0.00,0.00]:[2.00,1.12,0.10]";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "Orange";
  SeeSound "hellcaco/sight";
  PainSound "hellcaco/pain";
  DeathSound "hellcaco/death";
  ActiveSound "hellcaco/active";
  Obituary "%o got infernalized by a Hell Custodian.";
  HitObituary "%o got crunched in a hot af way by a Hell Custodian.";
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.3;
  DamageType "Fire";
  Speed 8;
  FastSpeed 12;
  }
  
  States
  {
  Spawn:
	HELE A 10 DCY_Look();
    Loop;
  Look:
	HELE A 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	HELE A 3 A_Chase();
	Loop;
  Melee:
	HELE BC 4 A_FaceTarget();
	HELE D 4 A_CustomMeleeAttack(10 * random(3, 6), "caco/melee");
	HELE C 4;
	Goto See;
  Missile:
	HELE E 0 A_Stop();
	HELE EF 6 A_FaceTarget();
	HELE G 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, -20);
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, 20);
	}
	HELE G 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, 0);
	}
	HELE FBA 3;
	Goto See;
  Bomb:
    TNT1 A 0 A_StartSound(SeeSound, CHAN_VOICE);
	HELE EEEEEFF 4 A_FaceTarget();
	HELE G 8
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyeBomb", 53, 0, 0);
	}
	HELE FBA 3;
	Goto See;  
  Pain:
	HELE H 3;
	HELE H 3 A_Pain();
	Goto See;
  Death:
	HELE H 10;
	HELE H 10 A_Scream();
	HELE I 6 A_NoBlocking();
	HELE J 1
	{
		A_SpawnItemEx("DCY_Explosion_1_Biggie", 0, 0, 35, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_StartSound("Archon/Explode", CHAN_BODY, attenuation: 0.5);
	}
	HELE JKLMNOPQRSTUV 3;
	HELE V -1;
	Stop;
  Death.Massacre:
	HELE H 10;
	HELE H 10 A_Scream();
	HELE I 6 A_NoBlocking();
	HELE J 1;
	HELE JKLMNOPQRSTUV 3;
	HELE V -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_CustodianEyefire : CacodemonBall
{
  Default
  {
  Damage 4;
  Speed 14;
  FastSpeed 24;
  DamageType "Fire";
  SeeSound "FIRE001";
  DeathSound "FLAME001";
  Translation "0:255=%[0.37,0.00,0.00]:[2.00,2.00,1.01]";
  +ROLLSPRITE
  -RANDOMIZE
  +SEEKERMISSILE
  }
  States
  {
  Spawn:
	SUE2 FG 1
	{
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERROLL|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
		A_SetRoll(Roll+5, SPF_INTERPOLATE);
		A_SeekerMissile(1, 1);
	}
	Loop;
  Death:
	SUE2 GHIJKLM 2;
	Stop;
  }
}

Class DCY_CustodianEyeBomb : CacodemonBall
{
  Default
  {
  Damage 10;
  Speed 23;
  FastSpeed 31;
  DamageType "Fire";
  SeeSound "BLAST002";
  DeathSound "FLAME002";
  Translation "0:255=%[0.37,0.00,0.00]:[2.00,2.00,1.01]";
  +ROLLSPRITE
  -RANDOMIZE
  }
  States
  {
  Spawn:
	SUE2 FG 1
	{
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERROLL|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
		A_SetRoll(Roll+5, SPF_INTERPOLATE);
	}
	Loop;
  Death:
    TNT1 A 0 A_Explode(45, 200, XF_NOTMISSILE);
    TNT1 A 0 A_SpawnItemEx("DCY_Explosion_1",random(-7,7),random(-7,7),random(-7,7),0,0,0,0,SXF_NOCHECKPOSITION);
	SUE2 GHIJKLM 2;
	Stop;
  }
}

Class DCY_CustodianBall : DCY_CustodianEyefire
{
  Default
  {
  Damage 5;
  Speed 20;
  }
}