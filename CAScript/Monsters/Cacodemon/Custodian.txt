Class DCY_HellCustodian : DCY_FlyingMeatballOutOfTouch
{
  Default
  {
  Health 900;
  Tag "\c[X1]Hell Custodian\c-";
  Translation "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "5:12=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "13:15=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "48:79=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "128:159=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "236:239=%[0.00,0.00,0.00]:[1.00,1.00,1.00]", "160:191=%[0.50,0.00,0.00]:[2.00,1.10,0.10]", "208:235=%[0.50,0.00,0.00]:[2.00,1.11,0.10]", "16:47=%[0.50,0.00,0.00]:[2.00,1.12,0.10]";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "ad3002";
  SeeSound "hellcaco/sight";
  PainSound "hellcaco/pain";
  DeathSound "hellcaco/death";
  ActiveSound "hellcaco/active";
  Obituary "%o got infernalized by a Hell Custodian.";
  HitObituary "%o got crunched by a Hell Custodian.";
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.3;
  DamageType "Fire";
  Speed 8;
  FastSpeed 12;
  DCYMonster.GibType "DCY_GibletsLava";
  DCYMonster.ScientificName "Praefervidus globus";
  }
  
  States
  {
  Spawn:
	HELE A 10 DCY_Look();
    Loop;
  Look:
	HELE A 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	HELE A 3 DCY_Chase();
	Loop;
  Melee:
	HELE BC 4 A_FaceTarget();
	HELE D 4 A_CustomMeleeAttack(10 * random(3, 6), "caco/melee");
	HELE C 4;
	Goto See;
  Missile:
	HELE E 0
	{
		A_Stop();
		
		if (!random(0, 5)) SetStateLabel("Bomb");
	}
	HELE EF 6 A_FaceTarget();
	HELE G 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, -20);
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, 20);
	}
	HELE G 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyefire", 53, 0, 0);
	}
	HELE FBA 3;
	Goto See;
  Bomb:
    TNT1 A 0 A_StartSound(SeeSound, CHAN_VOICE);
	HELE EEEEEFF 4 A_FaceTarget();
	HELE G 8
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_CustodianEyeBomb", 32, 0, 0);
	}
	HELE FBA 3;
	Goto See;  
  Pain:
	HELE H 3;
	HELE H 3 A_Pain();
	Goto See;
  Death:
	HELE H 10;
	HELE H 10 A_Scream();
	HELE I 6 A_NoBlocking();
	HELE J 1
	{
		DCY_Gibs(false);
		A_SpawnItemEx("DCY_Explosion_1_Biggie", 0, 0, 35, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_StartSound("Archon/Explode", CHAN_BODY, attenuation: 0.5);
	}
	HELE JKLMNOPQRSTUV 3;
	HELE V -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_CustodianEyefire : DCY_ImpFIreball
{
  Default
  {
  Damage 4;
  Speed 14;
  FastSpeed 24;
  DamageType "Fire";
  SeeSound "FIRE001";
  DeathSound "FLAME001";
  Translation "0:255=%[0.00,0.00,0.00]:[1.00,0.50,0.00]";
  +ROLLSPRITE
  -RANDOMIZE
  +SEEKERMISSILE
  }
  States
  {
  Spawn:
	SUE2 FG 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnItemEx("DCY_ImpBigFlame", frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(-10.0, 10.0), zvel: frandom(1, 2.5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 210);
			A_SpawnItemEx("DCY_ImpFlame1", frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(-10.0, 10.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
			A_SpawnFlames("bd3c00", 20, 45, 0, false);
			A_SpawnFlames("ff9838", 15, 35, 0, false);
		}
		
		A_SetRoll(Roll+5, SPF_INTERPOLATE);
		A_SeekerMissile(1, 1);
	}
	Loop;
  Death:
	TNT1 A 0
    {
		for (int i = 10; i > 0; i--)
			A_SpawnParticleEx(0xbd3c00, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	SUE2 GHIJKLM 2;
	Stop;
  }
}

Class DCY_CustodianEyeBomb : DCY_CustodianEyefire
{
  Default
  {
  Damage 10;
  Speed 23;
  FastSpeed 31;
  DamageType "Fire";
  SeeSound "LAVABUBL";
  DeathSound "DCYLavaImpact";
  }
  States
  {
  Spawn:
	SUE2 FG 1
	{
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERROLL|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
		A_SpawnParticleEx(0xff7b00, Texman.CheckforTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(1.5, 7.4), accelz: -0.55);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		A_SpawnFlames("ff7b00", 40, 50, allsmoke: true);
		A_SpawnFlames("ffffff", 25, 35, 0, false);
		A_SetRoll(Roll+15, SPF_INTERPOLATE);
	}
	Loop;
  Death:
    TNT1 A 0
	{
		for (int i = 25; i > 0; i--)
			A_SpawnParticleEx(0xff7b00, Texman.CheckforTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		A_Explode(50, 140, XF_NOTMISSILE);
		
		for (int i = 4; i > 0; i--) A_SpawnItemEx("DCY_Explosion_1",random(-7,7),random(-7,7),random(-7,7), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), flags: SXF_NOCHECKPOSITION);
		for (int i = 5; i > 0; i--)
		{
			let ember = Spawn("DCY_NapalmEmber", (pos.x, pos.y, pos.z));
			
			if (ember)
			{
				ember.vel.x = frandom(-7, 7);
				ember.vel.y = frandom(-7, 7);
				ember.vel.z = frandom(1, 10);
			}
		}
	}
	SUE2 GHIJKLM 2;
	Stop;
  }
}

Class DCY_CustodianBall : DCY_CustodianEyefire
{
  Default
  {
  Damage 5;
  Speed 20;
  }
}