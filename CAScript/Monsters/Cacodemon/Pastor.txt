Class DCY_Inscrutable : DCY_CacoParent
{
  Default
  {
  Health 350;
  PainChance 140;
  Speed 11;
  Scale 1.0;
  Radius 20;
  Height 56;
  Mass 320;
  Monster;
  +FLOORCLIP
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MISSILEMORE
  +FLOAT
  +NOGRAVITY
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  Obituary "%o was smote by the Inscrutable.";
  Tag "\c[v2]Inscrutable\c-";
  SeeSound "dcy/dissit";
  PainSound "dcy/dispai";
  DeathSound "dcy/disdth";
  ActiveSound "dcy/disact";
  DropItem "DCY_Eminence", 128;
  DropItem "DCY_Eminence", 50;
  BloodColor "White";
  }
  
  void A_PastorDash()
  {
	A_Chase(null, null);
	A_SpawnItemEx("DCY_DFX3", flags: SXF_NOCHECKPOSITION);
  }
  
  int missile;
  
  States
  {
  Spawn:
    1DSC AB 10 DCY_Look();
	Loop;
  Look:
	1DSC AAABBB 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	1DSC G 15 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    1DSC AAABBB 3 A_Chase();
	TNT1 A 0 A_Jump(80, "Blur", "Dodge");
	Loop;
  Blur:
    1DSC A 1
	{
		A_StartSound("dcy/blur");
		A_Chase(null, null);
	}
    1DSC AAAAAAAAAAAAAA 1 A_PastorDash();
    1DSC A 0 A_Jump(96, "See");
	Goto Blur+2;
  Dodge:
    1DSC A 1;
    1DSC A 1
	{
		A_StartSound("dcy/blur");
		A_Dodging(random(15, 25));
	}
    1DSC AAAAAAAAAAAAAA 1 A_SpawnItemEx("DCY_DFX3", flags: SXF_NOCHECKPOSITION);
    1DSC A 0 A_Stop();
	Goto See;
  Missile:
    TNT1 A 0
	{
		missile = random(0, 6);
		return A_Jump(90, "Missile1A", "Blur", "Missile2", "Missile2A");
	}
	Goto Missile1;
  Missile1:
	1DSC AAC 5 A_FaceTarget();
    1DSC C 7
	{
		A_StartSound("MAGI2SHT", CHAN_WEAPON);
		
		for (int i = -20; i < 21; i += 40)
			A_SpawnProjectile("DCY_PastorShot1", 70, i);
		
		A_FaceTarget();
		
		if (missile > 10)
			SetStateLabel("See");
		
		missile++;
	}
	Goto Missile1+3;
  Missile1A:
	1DSC CC 5 A_FaceTarget();
    1DSC D 15
	{
		for (int i = -30; i < 31; i += 15)
		{
			A_SpawnProjectile("DCY_PastorShotA", 30, 0, i, CMF_OFFSETPITCH, 0);
			A_SpawnProjectile("DCY_PastorShotA", 30, 0, i, CMF_OFFSETPITCH, -10);
			A_SpawnProjectile("DCY_PastorShotA", 30, 0, i, CMF_OFFSETPITCH, 10);
		}
	}
	TNT1 A 0 A_Jump(64, "Missile2", "Missile2A");
	Goto See;
  Missile2:
    1DSC EE 5 A_FaceTarget();
	1DSC F 0 A_SpawnProjectile("DCY_PastorShot2",30,8,0,0);
    1DSC F 10 A_SpawnProjectile("DCY_PastorShot2",30,-8,0,0);
	Goto See;
  Missile2A:
    1DSC EE 5 A_FaceTarget();
	1DSC F 5 A_SpawnProjectile("DCY_PastorShot2",30,8,0,0);
    1DSC FF 5 A_FaceTarget();
    1DSC F 5 A_SpawnProjectile("DCY_PastorShot2",30,-8,0,0);
    1DSC FF 5 A_FaceTarget();
	1DSC F 5 A_SpawnProjectile("DCY_PastorShot2",30,8,0,0);
	Goto See;
  Pain:
    1DSC G 2;
	1DSC G 0 A_StopSound(CHAN_7);
	1DSC G 2 A_Pain();
	Goto See;
  Death: 
	1DSC H 1;
	1DSC H 0 A_StopSound(CHAN_7);
    1DSC H 7 A_Scream();
    1DSC I 5;
    1DSC J 5;
    1DSC K 5 A_NoBlocking();
    1DSC LMN 4;
    1DSC O -1;
    Stop;
  Raise: 
    1DSC ONMLKJIH 5;
    Goto See;
  }
}

Class DCY_DFX3 : DCY_Effect
{
   Default
   {
   Radius 1;
   Height 1;
   Speed 0;
   Scale 1.0;
   Alpha 0.95;
   RenderStyle "AddStencil";
   StencilColor "ffffff";
   +NOCLIP
   -SOLID
   +NOGRAVITY
   +CLIENTSIDEONLY
   +BRIGHT
   }
   States
   {
   Spawn:
      NULL A 3;
	  NULL A 1 A_Jump(128, "Velocity");
	  Goto FadeOut;
   Velocity:
      NULL A 0;
	  NULL A 0 A_ChangeVelocity(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), CVF_RELATIVE|CVF_REPLACE);
	  Goto FadeOut;
   FadeOut:
      1DSC A 1 A_FadeOut(0.1);
      Wait;
   }
}

Class DCY_DFX1 : DCY_DFX3
{
  Default
  {
  RenderStyle "Add";
  Alpha 0.55;
  Scale 1.05;
  Scale 0.6;
  +BRIGHT;
  +ROLLSPRITE
  +NOGRAVITY;
  +NOCLIP;
  }
  
  States
  {
  Spawn:
    SUE2 ABCDE 2;
	Stop;
  }
}

Class DCY_PastorShot1 : DCY_Projectile
{
  Default
  {
   Radius 8;
   Height 16;
   Speed 15;
   FastSpeed 16;
   Damage 2;
   RenderStyle "Add";
   Alpha 0.67;
   Scale 0.65;
   Projectile;
   +THRUGHOST
   +SEEKERMISSILE
   DeathSound "dcy/disht1";
   Translation "0:255=%[0.00,0.00,0.00]:[1.51,1.51,1.51]";
   Decal "WizardScorch";
  }
   States
   {
   Spawn:
      AV_3 AAABBB 1
	  {
		if (bSEEKERMISSILE)
			A_Tracer2(1.5);
		
		A_SpawnItemEx("DCY_DFX1", frandom(-0.65, -0.65), frandom(-0.65, -0.65), frandom(-0.65, -0.65), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	  }
      Loop;
   Death:
      AV_3 CDEFG 3;
      Stop;
   }
}

Class DCY_PastorShotA : DCY_PastorShot1 { Default { -SEEKERMISSILE Speed 10; FastSpeed 13; } }

Class DCY_PastorShot2 : DCY_PastorShot1
{
  Default
  {
	Radius 8;
	Height 16;
	Speed 12;
	FastSpeed 15;
	Damage 10;
	RenderStyle "Subtract";
	Alpha 0.67;
	Projectile;
	+THRUGHOST
	+SEEKERMISSILE
	Translation "0:255=%[0.00,0.00,0.00]:[1.51,1.51,1.51]";
	DeathSound "dcy/disht2";
	SeeSound "ASXSHT1";
	Decal "PlasmaScorchLower";
  }
  
  int timer;
  
  States
  {
  Spawn:
	RBL2 AAABBBCCCDDD 1
	{
		if (timer < 20)
		{
			A_Tracer2(7);
			timer++;
		}
		
		A_SpawnItemEx("DCY_DFX1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
		
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_Smonk", frandom(-0.65, -0.65), frandom(-0.65, -0.65), frandom(-0.65, -0.65), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE);
	}
	Loop;
  Death:
	RBL2 E 2 A_SpawnItemEx("DCY_MonochromaticExplosionSmaller", flags: SXF_TRANSFERRENDERSTYLE);
	RBL2 FGHIJKL 2;
	Stop;
 }
}