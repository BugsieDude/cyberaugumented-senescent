Class DCY_Malwrath : DCY_FlyingMeatballOutOfTouch
{
  Default
  {
  Health 350;
  Tag "Malwrath";
  Translation "112:127=%[0.00,0.00,0.00]:[0.57,1.87,2.00]", "128:159=%[0.00,0.00,0.00]:[0.23,0.23,0.23]", "48:79=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "1:3=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "5:15=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "80:111=%[0.00,0.00,0.00]:[0.20,0.20,0.20]", "161:167=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "160:160=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "248:249=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "208:235=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "16:47=%[0.00,0.00,0.00]:[0.23,0.23,0.23]", "168:191=%[0.00,0.00,0.00]:[0.23,0.23,0.23]";
  Obituary "%o's eyes are now blinded due to the Malwrath.";
  HitObituary "%o got way too close to a Malwrath.";
  BloodColor "0394fc";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  SeeSound "Malwrath/Sight";
  ActiveSound "Malwrath/Active";
  PainSound "Malwrath/Pain";
  DeathSound "Malwrath/Death";
  DCYMonster.ScientificName "Monogrammos globus";
  +QUICKTORETALIATE
  Speed 6;
  FastSpeed 9;
  }
  
  States
  {
  Spawn:
    CALI A 10 DCY_Look();
    Loop;
  Look:
	CALI A 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    CALI A 3
	{
		if (!random(0, 80)) A_ChangeVelocity(random(-4, 4), random(-4, 4), random(-4, 4), CVF_REPLACE|CVF_RELATIVE);
		DCY_Chase();
	}
	Loop;
  Missile:
	CALI B 0
	{
		if (!random(0, 2) && (target && Distance3D(target) < 256))
			SetStateLabel("Lunge");
		if (!random(0, 2)) SetStateLabel("Charge");
	}
	CALI BBBCCC 2 A_FaceTarget();
	CALI D 2
	{
		for (int i = random(4, 10); i > 0; i--)
			A_SpawnProjectile("DCY_MalwrathBall", 40, 0, frandom(-6, 6), CMF_OFFSETPITCH, pitch + frandom(-6, 6));
	}
	CALI DDCCC 2 A_FaceTarget();
	CALI C 2 A_Stop();
	Goto See;
  Charge:
	CALI EFG 5 A_FaceTarget();
	CALI G 0 A_StartSound(SeeSound, CHAN_VOICE);
	CALI GGGGGGGGGGGGGGG 2
	{
		A_FaceTarget();
		
		if (DCY_EffectDistance(1000))
		{
		A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(1, 12), 0, frandom(-1, 1) + 20, frandom(-1, 1), frandom(-1, 1) + 23, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
		A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("graphics/particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(4, 18), 0, frandom(-1, 1) + 20, frandom(-1, 1), frandom(-1, 1) + 23, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
		}
	}
	CALI G 10 A_SpawnProjectile("DCY_MalwrathSeeker", 23);
	CALI FE 10;
	Goto See;
  Melee:
    CALI GF 8 A_FaceTarget();
	CALI G 0 A_CustomMeleeAttack(10*random(1, 6), "caco/melee");
	CALI EF 8;
	Goto See;
  Lunge:
	CALI E 1 A_Pain();
	CALI EEFF 3 A_FaceTarget();
	CALI F 0 A_FaceTarget();
	CALI F 0 A_Recoil(-15);
	CALI G 15;
	CALI GGFFEE 1 A_Stop();
	Goto Missile+1;
  Pain:
    CALI H 3;
	CALI H 2 A_Pain();
	Goto See;
  Death:
	CALI J 1;
	CALI J 1 A_SetFloorClip();
	CALI J 4 A_Scream();
	CALI KLMNOP 6;
	CALI Q 6 A_NoBlocking();
	CALI R 6;
	CALI S -1;
	Stop;
  Raise:
	CALI RQPONMLKJ 3;
	Goto See;
  }
}

Class DCY_MalwrathBall : DCY_MeatballElecball
{
  Default
  {
  Speed 15;
  FastSpeed 22;
  Translation "0:255=%[0.00,0.09,0.22]:[1.01,1.69,2.00]";
  SeeSound "Malwrath/Ball";
  DeathSound "Malwrath/BallX";
  Damage 3;
  }
  
  int t;
  
  override void PostBeginPlay()
  {
	t = 1;
  }
  
  States
  {
  Spawn:
	LMBB AAAABBBB 1 Bright
	{
		A_Weave(1, 1, 1, 1);
		A_SetRoll(frandom(0.00, 360.00));
		if (DCY_EffectDistance(1200)) A_SpawnFlames("0394fc", 30, 30, 0, false, thrust: 2.25);
		if (!(level.time % 15))
		{
			t *= -1;
			vel.x += frandom(0, 3) * t;
			vel.y += frandom(0, 3) * t;
			if (!random(0, 1)) vel.z += frandom(0, 3) * t;
		}
	}
	Loop;
  Death:
	LMBB C 3 Bright;
	LMBB DEFG 3 Bright;
	Stop;
  }
}

Class DCY_MalwrathSeeker : DCY_MalwrathBall
{
  Default
  {
  Speed 7;
  FastSpeed 12;
  +SEEKERMISSILE
  Damage 10;
  SeeSound "Malwrath/HomingBall";
  Scale 1.2;
  }
  
  int count;
  
  States
  {
  Spawn:
	NBKL AABBCC 1
	{
		A_SetRoll(frandom(0.00, 360.00));
		if (DCY_EffectDistance(1200)) A_SpawnFlames("03669c", 30, 50, 0, false, thrust: 1.65);
		if (!(GetAge() % 35) && count < 3)
		{
			for (int i = 2; i > 0; i--) A_SeekerMissile(9999, 9999, SMF_PRECISE|SMF_LOOK);
			A_StartSound("Malwrath/HomingBallSeek", CHAN_BODY);
			A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("C28YA0"), STYLE_ADD, SPF_FULLBRIGHT, 30, 2, sizestep: 30);
			A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("C28YA0"), STYLE_ADD, SPF_FULLBRIGHT, 30, 2, sizestep: 10);
			for (int i = random(10, 20); i > 0; i--) A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
			count++;
			vel.x *= 1.25;
			vel.y *= 1.25;
		}
	}
	Loop;
  Death:
	NBKL A 1
	{
		A_SpawnParticleEx(0x03669c, TexMan.CheckForTexture("C28YA0"), STYLE_ADD, SPF_FULLBRIGHT, 70, 2, fadestepf: -12, sizestep: 20);
		for (int i = 0; i < 360; i += 45) A_SpawnItemEx("DCY_Explosion_1", xvel: 10, angle: i, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_RadiusThrust(4000, 110, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE);
		A_RadiusThrust(70, 110, RTF_NOTMISSILE|RTF_NOIMPACTDAMAGE|RTF_THRUSTZ);
	}
	Stop;
  }
}