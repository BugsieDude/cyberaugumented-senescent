Class DCY_Malwrath : DCY_FlyingMeatballOutOfTouch
{
  Default
  {
  Health 350;
  Tag "Malwrath";
  Translation "112:127=%[0.00,0.00,0.00]:[2.00,1.20,0.00]", "128:159=%[0.00,0.00,0.00]:[0.23,0.23,0.23]", "48:79=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "1:3=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "5:15=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "80:111=%[0.00,0.00,0.00]:[0.20,0.20,0.20]", "161:167=%[0.00,0.00,0.00]:[0.30,0.30,0.30]", "16:47=%[0.00,0.00,0.00]:[0.23,0.23,0.23]", "168:191=%[0.00,0.00,0.00]:[0.23,0.23,0.23]";
  Obituary "%o's eyes are now blinded due to the Malwrath.";
  HitObituary "%o got way too close to a Malwrath.";
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  SeeSound "Malwrath/Sight";
  ActiveSound "Malwrath/Active";
  PainSound "Malwrath/Pain";
  DeathSound "Malwrath/Death";
  +QUICKTORETALIATE
  Speed 6;
  FastSpeed 9;
  }
  
  States
  {
  Spawn:
    CALI A 10 DCY_Look();
    Loop;
  Look:
	CALI A 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    CALI A 3
	{
		if (!random(0, 80)) A_ChangeVelocity(random(-4, 4), random(-4, 4), random(-4, 4), CVF_REPLACE|CVF_RELATIVE);
		A_Chase();
	}
	Loop;
  Missile:
	CALI B 0
	{
		if (!random(0, 2) && (target && Distance3D(target) < 256))
			SetStateLabel("Lunge");
	}
	CALI BBBCCC 2 A_FaceTarget();
	CALI D 2
	{
		for (int i = random(4, 10); i > 0; i--)
			A_SpawnProjectile("DCY_MalwrathBall", 40, 0, frandom(-6, 6), CMF_OFFSETPITCH, pitch + frandom(-6, 6));
	}
	CALI DDCCC 2 A_FaceTarget();
	CALI C 2 A_Stop();
	Goto See;
  Melee:
    CALI GF 8 A_FaceTarget();
	CALI G 0 A_CustomMeleeAttack(10*random(1, 6), "caco/melee");
	CALI EF 8;
	Goto See;
  Lunge:
	CALI E 1 A_Pain();
	CALI EEFF 3 A_FaceTarget();
	CALI F 0 A_FaceTarget();
	CALI F 0 A_Recoil(-15);
	CALI G 15;
	CALI GGFFEE 1 A_Stop();
	Goto Missile+1;
  Pain:
    CALI H 3;
	CALI H 2 A_Pain();
	Goto See;
  Death:
	CALI J 1;
	CALI J 1 A_SetFloorClip();
	CALI J 4 A_Scream();
	CALI KLMNOP 6;
	CALI Q 6 A_NoBlocking();
	CALI R 6;
	CALI S -1;
	Stop;
  Raise:
	CALI RQPONMLKJ 3;
	Goto See;
  }
}

Class DCY_MalwrathBall : DCY_MeatballElecball
{
  Default
  {
  Speed 15;
  FastSpeed 22;
  Translation "0:255=%[0.30,0.10,0.00]:[2.00,1.76,0.20]";
  SeeSound "Malwrath/Ball";
  Damage 3;
  }
  
  int t;
  
  override void PostBeginPlay()
  {
	t = 1;
  }
  
  States
  {
  Spawn:
	LMBB AAAABBBB 1 Bright
	{
		A_Weave(1, 1, 1, 1);
		A_SetRoll(frandom(0.00, 360.00));
		if (DCY_EffectDistance(1200)) A_SpawnFlames("cc5f00", 30, 25, 0, false, thrust: 2.25);
		if (!(level.time % 15))
		{
			t *= -1;
			vel.x += frandom(0, 3) * t;
			vel.y += frandom(0, 3) * t;
			if (!random(0, 1)) vel.z += frandom(0, 3) * t;
		}
	}
	Loop;
  Death:
	LMBB C 3 Bright;
	LMBB DEFG 3 Bright;
	Stop;
  }
}