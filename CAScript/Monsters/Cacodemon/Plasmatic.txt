Class DCY_Deluder : DCY_CacoParent
{
  Default
  {
  Health 650;
  Radius 20;
  Height 56;
  Speed 10;
  FastSpeed 13;
  PainChance 55;
  +QUICKTORETALIATE
  +MISSILEMORE
  +NOFEAR
  +NOGRAVITY
  +FLOAT
  +DONTHARMSPECIES
  +DONTHARMCLASS
  DamageType "PlasmaW";
  DamageFactor "PlasmaW", 0.00;
  BloodColor "Green";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  SeeSound "PWizard/Sight";
  PainSound "PWizard/Pain";
  DeathSound "PWizard/Death";
  ActiveSound "PWizard/Active";
  MeleeSound "wizard/melee";
  Obituary "%o was wrong as the Deluder manipulated %h's mind.";
  Tag "\c[a6]Deluder\c-";
  DCYMonster.ScientificName "NO DATA";
  }
  
  int missile;
  
  state A_DeluderDecide()
  {	
	switch (random(0, 3))
	{
		case 0: Return ResolveState("Missile1"); break;
		case 1: Return ResolveState("Missile2"); break;
		case 2: Return ResolveState("Missile3"); break;
		case 3: Return ResolveState("Melee"); break;
	}
	
	Return ResolveState(null);
  }
  
  void A_PlasmaticDash()
  {
	A_SpawnItemEx("DCY_PWZFX1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	DCY_Chase();
  }
  
  void A_PlasmaticAttack()
  {
	if (target)
	{
		for (int i = 2; i > 0; i--)
			A_Wander();
		A_FaceTarget();
		
		if ((level.time % 2) == 0)
			for (int i = -10; i < 11; i += 20) A_SpawnProjectile("DCY_WizardPlasma", 30, i, random(-4, 4), 0, random(-4, 4));
	}
  }
  
  States
  {
  Spawn:
    PWIZ AB 10 DCY_Look();
    Loop;
  Look:
	PWIZ AAABBB 2 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    PWIZ A 0 A_SetTranslucent(1, 0);
	TNT1 A 0 A_Jump(90, "Dash");
    PWIZ AAABBB 2 DCY_Chase();
    Loop;
  Dash:
    PWIZ A 1 A_StartSound("dcy/blur");
    PWIZ AAAAAAAAAAAAAAA 1 A_PlasmaticDash();
    PWIZ A 0 A_Jump(96,"See");
    Goto Dash+1;
  Melee: //AKA Plasmatic Slap
    PWIZ N 7 A_FaceTarget();
	PWIZ O 0 { A_StartSound("dcy/blur"); A_Recoil(-20); }
	PWIZ OOOOOOOOO 1 A_SpawnItemEx("DCY_PWZFX1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	PWIZ P 0 A_Stop();
	PWIZ P 9 A_CustomMeleeAttack(random(10, 100), "");
	Goto See;
  Missile:
    PWIZ C 0 A_DeluderDecide();
	Goto Missile1;
  Missile1:
    PWIZ C 12 A_FaceTarget();
    PWIZ D 12
	{
		A_FaceTarget();
		for (int i = -10; i < 11; i += 20) A_SpawnProjectile("DCY_WizardPlasma", 30, i, random(-4, 4), 0, random(-4, 4));
	}
    Goto See;
  Missile2:
    PWIZ C 12 { missile = 0; A_FaceTarget(); }
    PWIZ D 3
	{
		if (missile > 10)
			SetStateLabel("See");
		
		missile++;
		A_PlasmaticAttack();
	}
    Goto Missile2+1;
  Missile3:
	PWIZ N 0 A_Jump(80, "Missile1");
    PWIZ NO 10 A_FaceTarget();
    PWIZ P 10
	{
		A_StartSound("DCY/Lotus", attenuation: 0.5);
		for (int i = 15; i > 0; i--)
			A_SpawnProjectile("DCY_WizardBlast", 30, 0, frandom(-20, 20), CMF_OFFSETPITCH, pitch + frandom(5, -20));
	}
	PWIZ A 10;
    Goto See;
  Pain:
    PWIZ E 0 A_SetTranslucent(1,0);
    PWIZ E 4;
    PWIZ E 4 A_Pain();
    Goto See;
  Death:
    PWIZ E 1;
    PWIZ E 5 A_SetTranslucent(1,0);
    PWIZ F 6;
    PWIZ G 6 A_Scream();
    PWIZ H 6;
    PWIZ I 6 A_Fall();
    PWIZ JKL 6;
    PWIZ M -1;
    Stop;
  Raise:
    PWIZ ML 8;
    PWIZ KJI 6;
    Goto See;
  }
}

Class DCY_PWZFX1 : DCY_Effect
{
   Default
   {
   Radius 1;
   Height 1;
   Speed 0;
   Scale 1.0;
   Alpha 0.95;
   RenderStyle "AddStencil";
   StencilColor "Green";
   +NOCLIP
   -SOLID
   +NOGRAVITY
   +CLIENTSIDEONLY
   }
   States
   {
   Spawn:
      NULL A 3;
	  Goto FadeOut;
   FadeOut:
      PWIZ # 1 A_FadeOut(0.1);
      Loop;
   }
}

Class DCY_WizardPlasma : DCY_Projectile
{
  Default
  {
  Speed 20;
  Damage 3;
  Radius 12;
  Height 12;
  Alpha 1.0;
  Scale 1;
  RenderStyle "Add";
  SeeSound "Deluder/Plasma";
  +BRIGHT
  +RANDOMIZE
  +ROLLSPRITE
  Translation "0:255=%[0.00,0.00,0.00]:[0.92,2.00,0.92]";
  }
  States
  {
  Spawn:
    PLSS ABCD 1
	{
		if (random(0, 2) == 0)
		{
			actor plasma = Spawn("DCY_FluorescentSparkle", pos + (frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(-8.0, 8.0)));
			if (plasma) { plasma.translation = translation; plasma.bNOGRAVITY = true; }
		}
		
		roll += 3;
		A_SpawnItemEx("DCY_CWFXTrail", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL);
		A_SpawnParticleEx(0x006100, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: 0.725);
	}
	Loop;
  Death:
	PLSE A 3
	{
		A_SetScale(1.0, 1.0);
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_SetRoll(frandom(0.00, 360.00));
		for (int i = 8; i > 0; i--)
			A_SpawnParticleEx(0x006100, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	PLSE BCDE 2;
	Stop;
  }
}

Class DCY_WizardBlast : DCY_WizardPlasma
{
  Default
  {
  Radius 6;
  Height 8;
  Scale 0.8;
  Speed 15;
  FastSpeed 20;
  -NOGRAVITY
  Gravity 0.2;
  SeeSound "";
  Damage 5;
  }
  
  States
  {
  Spawn:
    PLSS ABCD 1
	{
		roll += 3;
		A_SpawnItemEx("DCY_CWFXTrail", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL);
		A_SpawnParticleEx(0x006100, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 30, 20, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: -0.725);
	}
	Loop;
  }
}