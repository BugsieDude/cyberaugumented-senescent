Class DCY_Monsoon : DCY_CacoParent
{
  Default
  {
  Health 1280;
  Translation "80:111=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "5:12=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "1:3=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "16:47=%[0.13,0.09,0.28]:[0.64,0.82,1.44]", "232:235=%[0.13,0.09,0.28]:[0.64,0.82,1.45]", "168:191=%[0.13,0.09,0.28]:[0.64,0.82,1.46]";
  BloodType "AndroidNeonFluid";
  BloodColor "66 8f d1";
  SeeSound "Monsoon/Sight";
  ActiveSound "Monsoon/Active";
  PainSound "Monsoon/Pain";
  DeathSound "Monsoon/Death";
  DamageType "Electric";
  DamageFactor "Electric", 0.00;
  +MISSILEMORE
  +MISSILEEVENMORE
  +BRIGHT
  +NOINFIGHTING
  +QUICKTORETALIATE
  Tag "\c[x5]Monsoon\c-";
  }
  
  States
  {
  Spawn:
	8RVV A 10 DCY_Look();
    Loop;
  Look:
	8RVV A 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	8RVV A 2
	{
		A_Chase();
		Return A_Jump(20, "Dodge");
	}
	Loop;
  Melee:
	8RVV BC 3 A_FaceTarget();
	8RVV D 2 A_CustomMeleeAttack(10 * random(3, 6), "caco/melee");
	8RVV C 2;
	Goto See;
  Dodge:
	8RVV A 2 A_Dodging(-20);
	8RVV A 10 A_Chase(flags: CHF_DONTMOVE);
	Goto See;
  Missile:
	8RVV E 0 A_Stop();
	8RVV E 0 A_Jump(100, "Laser", "DoubleThunder");
	8RVV E 0 A_Jump(30, "Thunder");
	8RVV EF 8 A_FaceTarget();
	8RVV GGGGGG 3
	{
		A_SpawnProjectile("DCY_MonsoonElec", 30, 0, frandom(-8, 8), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-4, 4));
		A_FaceTarget();
	}
	8RVV FE 6 A_FaceTarget();
	8RVV E 0 A_Stop();
	Goto See;
  Laser:
	8RVV EF 9 A_FaceTarget();
	8RVV G 1 A_StartSound("ROBOLAS5", CHAN_WEAPON, attenuation: 0.85);
	8RVV GGGGGGGGG 2
	{
		A_SpawnProjectile("DCY_MonsoonLaser", 50);
		A_FaceTarget();
	}
	8RVV FE 6 A_FaceTarget();
	8RVV E 0 A_Stop();
	Goto See;
  DoubleThunder:
	8RVV EF 9 A_FaceTarget();
	8RVV G 1
	{
		for(int i = -20; i < 21; i += 40)
			A_SpawnProjectile("DCY_MonsoonThunder", 1, 0, i);
	}
	8RVV G 8 A_Stop();
	8RVV FE 9;
	Goto See;
  Thunder:
	8RVV EF 9 A_FaceTarget();
	8RVV GG 9 A_StartSound("Monsoon/Sight", CHAN_WEAPON, attenuation: 0.85);
	8RVV G 1
	{
		A_QuakeEx(3, 3, 3, 50, 0, 300, "", QF_SCALEDOWN|QF_3D);
		
		for(int i = 0; i < 360; i += 60)
			A_SpawnProjectile("DCY_MonsoonThunder", 1, 0, i);
	}
	8RVV G 8 A_Stop();
	8RVV FE 9;
	Goto See;
  Pain:
	8RVV H 3;
	8RVV H 3
	{
		A_Stop();
		A_Pain();
	}
	Goto See;
  Death:
	8RVV H 3;
	8RVV I 7 A_Scream();
	8RVV J 6;
	8RVV K 6
	{
		A_SpawnItemEx("DCY_VamchnormExplode", flags: SXF_NOCHECKPOSITION);
		A_StartSound("Archon/Explode", CHAN_6, attenuation: 0.85);
		A_NoBlocking();
	}
	8RVV LMNOPQRSTU 5;
	8RVV V -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_MonsoonElec : CacodemonBall
{
  Default
  {
  Speed 20;
  FastSpeed 30;
  Translation "0:255=%[0.24,0.17,0.35]:[1.10,1.46,1.91]";
  DamageType "Electric";
  SeeSound "ElectricBall/Sight";
  DeathSound "ElectricBall/Death";
  +ROLLSPRITE
  Scale 0.65;
  }
  
  States
  {
  Spawn:
	DLIT DEFGH 1
	{
		A_SetRoll(Roll+20);
		A_SpawnItemEx("DCY_MonsoonTrailE", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	Loop;
  Death:
	TSAR A 1
	{
		A_SetRoll(frandom(0, 360));
		
		for (int i = 4; i > 0; i--)
		{
			A_SpawnParticle(0xacddfc, SPF_FULLBRIGHT, random(10, 20), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0));
			A_SpawnItemEx("DCY_MonsoonTrailED", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		}
	}
	TSAR EFGHIJKLMNO 1;
	Stop;
  }
}

Class DCY_MonsoonTrailE : DCY_ElecTrailBase { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.51,0.54,1.39]"; } }
Class DCY_MonsoonTrailED : DCY_ElecDeathBase { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.51,0.54,1.39]"; } }

Class DCY_MonsoonLaser : FastProjectile
{
  Default
  {
  DamageFunction 1;
  +RIPPER
  MissileType "DCY_MonsoonLaserTrail";
  MissileHeight 6;
  Speed 1;
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		vel = vel + vel.Unit() * 5;
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_SetRoll(frandom(0, 360));
		
		for (int i = 2; i > 0; i--)
		{
			A_SpawnParticle(0xacddfc, SPF_FULLBRIGHT, random(10, 20), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0));
			A_SpawnItemEx("DCY_MonsoonTrailED", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		}
	}
	TNT1 A 1;
	Stop;
  }
}

Class DCY_MonsoonLaserTrail : Actor
{
  Default
  {
  +NOGRAVITY
  +BRIGHT
  +THRUACTORS
  +NOCLIP
  +ROLLSPRITE
  +NOINTERACTION
  -SOLID
  RenderStyle "Add";
  Alpha 1.0;
  Scale 0.5;
  Translation "0:255=%[0.00,0.00,0.00]:[0.52,0.61,1.32]";
  }
  
  int destroychance;
  
  States
  {
  Spawn:
	ASZ0 A 0 NoDelay
	{
		destroychance = random(0, 15);
	}
	ASZ0 ABCABCA 1
	{
		if (destroychance == 10)
			Destroy();
			
		A_FadeOut(0.15);
		A_SetRoll(frandom(0, 360));
	}
	Stop;
  }
}

Class DCY_MonsoonThunder : CacodemonBall
{
  override void Tick()
  {
	super.Tick();
	Scale.Y = 1.0 * (CeilingZ - FloorZ) / 160;
  }
  
  Default
  {
	+FLOORHUGGER
	BounceType "Hexen";
	BounceFactor 1.0;
	Speed 15;
	+NEVERFAST
	+DONTSPLASH
	SeeSound "";
	DeathSound "";
	DamageType "Electric";
  }
  
  int timer;
  
  States
  {
  Spawn:
	B2LZ A 0 NoDelay
	{ 
		timer = random(0, 100); 
		A_StartSound("Monsoon/ThunderLoop", CHAN_BODY, CHANF_LOOPING, pitch: frandom(0.8, 1.2), startTime: frandom(0, 0.35));
	}
	B2LZ ABEF 1
	{
		if (timer > 150)
		{
			A_StopSound(CHAN_BODY);
			Destroy();
		}
			
		timer++;
		
		A_Explode(5, 32, damagetype: "Electric");
		A_SpawnItemEx("DCY_MonsoonTTrail", frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME, failchance: 50);
		A_SpawnItemEx("DCY_MonsoonTrailED", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION, failchance: 150);
		A_SpawnItemEx("DCY_MonsoonTrailED", frandom(-1, 1), frandom(-1, 1), CeilingZ + frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION, failchance: 150);
		A_SetScale(randompick(Scale.X, -Scale.X), Scale.Y);
	}
	Goto Spawn+1;
  Death:
	Stop;
  }
}

Class DCY_MonsoonTTrail : DCY_ProjectileBaseTrail { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.52,0.61,1.32]"; } }