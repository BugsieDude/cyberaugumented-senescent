Class DCY_Wretched : DCY_CacoParent
{
  Default
  {
  Health 500;
  Tag "Wretched";
  BloodType "AndroidNeonFluid";
  BloodColor "Cyan";
  Speed 14;
  FastSpeed 18;
  Mass 600;
  PainChance 96;
  SeeSound "Wretched/Sight";
  ActiveSound "Wretched/Active";
  PainSound "Wretched/Pain";
  DeathSound "Wretched/Death";
  +MISSILEMORE
  +QUICKTORETALIATE
  +DONTHARMCLASS
  +BRIGHT
  }
  
  States
  {
  Spawn:
    SCD8 ABCD 3 DCY_Look();
    Loop;
  Look:
	SCD8 ABCD 2 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	SCD8 A 0 A_Jump(50, "Dodge");
    SCD8 ABCDABCD 3 A_Chase("Flame", "Missile");
	Loop;
  Dodge:
	SCD8 A 0 A_Dodging(12);
	SCD8 AAAA 5 A_Chase("Flame", "Missile", CHF_DONTMOVE);
	Goto See;
  Missile:
	SCD8 B 0
	{
		If(target && Distance3D(target) < 256)
			return resolvestate("Flame");
		
		A_FaceTarget();
		A_Stop();
		return A_Jump(128, "Dual", "TripleAtARow", "DualAtARow");
	}
	SCD8 B 0 A_Jump(100, "Comets");
	Goto MissileNormal;
  MissileNormal:
	SCD8 EEEEEEEEEEFFFFFFFF 1 A_FaceTarget();
	SCD8 G 1 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFFF 1;
	Goto See;
  Dual:
	SCD8 EEEEEEEEEEEEEFFFFFFFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24, -10);
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24, 10);
	SCD8 GGGGGGGGGGFFFFFFFFFF 1;
	Goto See;
  TripleAtARow:
	SCD8 EEEEEEFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFF 1;
	Goto See;
  DualAtARow:
	SCD8 EEEEEEFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFF 1 A_FaceTarget();
	SCD8 G 0 A_SpawnProjectile("DCY_WretchedBall", 24);
	SCD8 GGGGGGFFFFFF 1;
	Goto See;
  Comets:
	SCD8 HHHHHIIIII 3 A_FaceTarget();
	SCD8 JJJJ 7
	{
		A_SpawnProjectile("DCY_WretchedComet", 24, 0, random(-15, 5));
		A_FaceTarget();
	}
	SCD8 JJJJJFFFFFF 2;
	Goto See;
  Flame:
	SCD8 H 0 A_Jump(60, "MissileContinue");
	SCD8 L 0 A_StartSound("Wretched/Sight", CHAN_VOICE);
	SCD8 HHHHHHHHIIIIIII 1 A_FaceTarget();
	SCD8 J 0 A_StartSound("FLAMTHRW", CHAN_WEAPON);
	SCD8 JJJJJJJIIIIII 2
	{
		A_SpawnProjectile("DCY_WretchedFlame", 24, 0, random(-1, 1));
		A_FaceTarget();
	}
	SCD8 IIIIIIIIIIFFFFFFFFF 1;
	Goto See;
  Pain:
    SCD8 K 3;
	SCD8 K 2 A_Pain();
	Goto See;
  Death:
    SCD8 L 1;
	SCD8 L 8 A_Scream();
	SCD8 M 8
	{
		A_StartSound("Wretched/DeathExplosion", CHAN_BODY);
		
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_AndroidExplosion", frandom(-4.0, 4.0), frandom(-4.0, 4.0), 40 + frandom(-4.0, 4.0), flags: SXF_NOCHECKPOSITION);
	}
	SCD8 N 6 A_NoBlocking();
	SCD8 OPQ 5;
	TNT1 A -1;
	Stop;
  Death.Massacre:
    SCD8 L 1;
	SCD8 L 8 A_Scream();
	SCD8 M 8 A_StartSound("Wretched/DeathExplosion", CHAN_BODY);
	SCD8 N 6 A_NoBlocking();
	SCD8 OPQ 5;
	TNT1 A -1;
	Stop;
  Raise:
    SCD8 QP 4;
	SCD8 ONMLK 4;
	Goto See;
  }
}

Class DCY_WretchedBall : DCY_IFlame { Default { Damage 4; Speed 11; FastSpeed 22; } }

Class DCY_WretchedComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.38,2.00]";
  Speed 12.5;
  FastSpeed 20;
  }
}