Class DCY_ColonialWizard : DCY_CacoParent
{
  int bobstate;
  
  Default
  {
  Health 695;
  Radius 22;
  Height 62;
  Speed 11;
  PainChance 89;
  +FLOAT
  +NOGRAVITY
  +NOBLOOD
  +NOTELEFRAG
  +DONTOVERLAP
  +NEVERRESPAWN
  +NOTARGETSWITCH
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MISSILEMORE
  DropItem "DCY_Eminence", 128;
  DropItem "DCY_Eminence", 128;
  SeeSound "dcy/acosit";
  ActiveSound "dcy/acoact";
  PainSound "dcy/acopai";
  HowlSound "dcy/acopai";
  DeathSound "dcy/acodth";
  Tag "\c[g3]Colonial \c[l5]Hell Pastor\c-";
  Obituary "%o got hexed by a Colonial Hell Pastor's suprapower.";
  }
  
  void A_BishopChase()
  {
	double newz = pos.z - BobSin(bobstate) / 2.;
	bobstate = (bobstate + 4) & 63;
	newz += BobSin(bobstate) / 2.;
	SetZ(newz);
  }
  
  void A_ColonialChase() { A_Chase(); A_Chase(); }
  
  void A_ColonialDash() { A_SpawnItemEx("DCY_CWTrail"); A_Chase(); A_BishopChase(); }
	
  States
  {
  Spawn:
    DD9S A 10 DCY_Look();
    Loop;
  Look:
	PWIZ AAABBB 4
	{
		A_BishopChase();
		A_Chase("See", null, CHF_DONTMOVE|CHF_NOPLAYACTIVE);
		DCY_LookingForPlayer();
	}
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    TNT1 A 0 A_Jump(15, "Rush", "Teleport");
    DD9S A 3 A_ColonialChase();
	DD9S A 3 A_BishopChase();
	DD9S B 3 A_ColonialChase();
	DD9S B 3 A_BishopChase();
	Loop;
  Teleport:
	TNT1 A 0 A_UnSetShootable();
    DD9S AAAAAAAAA 1 A_FadeOut(0.1, 0);
	TNT1 A 0 A_Jump(128,6);
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1);
    TNT1 A 0 A_Jump(128,6);
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_ExtChase(0,0,1,1);
	DD9S AAAAAAAAA 1 A_FadeIn(0.1);
	TNT1 A 0 A_SetShootable();
	Goto See;
  Rush:
    TNT1 A 0 A_StartSound("dcy/blur", CHAN_7);
    DD9S AAAABBBBAAAABBBB 1 A_ColonialDash();
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(64, "Charge");
    TNT1 A 0 A_Jump(128, "Missile2");
    DD9S DE 3 A_FaceTarget();
	DD9S F 3
	{
		A_SpawnProjectile("DCY_CWFX", 36, 0, 0);
		A_SpawnProjectile("DCY_CWFX", 36, 0, 10);
		A_SpawnProjectile("DCY_CWFX", 36, 0, -10);
	}
	Goto See;
  Missile2:
    DD9S DE 4 A_FaceTarget();
  Missile2Loop:
	DD9S F 3
	{
		A_SpawnProjectile("DCY_CWFX2", 36, 0, random(-5, 5), 0, random(-5, 5));
		A_FaceTarget();
	}
	TNT1 A 0 A_Jump(20, "See");
	Loop;
  Charge:
    DD9S DDDDDDDDDDDDEEEEEEEEEEEEE 2 A_FaceTarget();
	DD9S F 10
	{
		A_SpawnItemEx("DCY_CWFXDeathTrail2", 6, 0, 36);
		A_SpawnProjectile("DCY_CWFX3", 36);
	}
	Goto See;
  Pain:
	DD9S C 4;
    DD9S C 4 A_Pain();
	Goto See;
  Death:
    DD9S G 6;
	DD9S H 6 Bright A_ScreamAndUnblock();
	DD9S IJKLM 5 Bright;
	DD9S N 4 A_SpawnItemEx("DCY_CWPuff", 0, 0, 40, 0, 0, 0.5);
	DD9S O 4;
	DD9S P -1;
	Stop;
  }
}

Class DCY_CWTrail : DCY_Effect
{
   Default
   {
   Radius 1;
   Height 1;
   Speed 0;
   Scale 1.0;
   Alpha 0.95;
   RenderStyle "AddStencil";
   StencilColor "White";
   +NOCLIP
   -SOLID
   +NOGRAVITY
   +CLIENTSIDEONLY
   }
   States
   {
   Spawn:
      NULL A 3;
	  Goto FadeOut;
   FadeOut:
      DD9S A 1 A_FadeOut(0.1);
      Loop;
   }
}

Class DCY_CWPuff : BishopPuff
{
  States
  {
  Spawn:
    DD9S QRSTUVW 5;
	Stop;
  }
}

Class DCY_CWFX : FastProjectile
{
  Default
  {
  Speed 15;
  Damage 5;
  Radius 12;
  Height 12;
  Alpha 1.0;
  Scale 1.0;
  RenderStyle "Add";
  SeeSound "MBALFIRE";
  DeathSound "MBALEXPL";
  +BRIGHT
  +RANDOMIZE
  }
  States
  {
  Spawn:
    D9PJ AABBCCDD 1 A_SpawnItemEx("DCY_CWFXTrail");
	Loop;
  Death:
    TNT1 A 0;
	TNT1 A 0 A_StartSound("DCY_CWExplode", CHAN_WEAPON);
	TNT1 A 0 A_SpawnItemEx("DCY_CWFXDeathTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
    D9PJ EFGHIJK 3 Bright;
	Stop;
  }
}

Class DCY_CWFX2 : DCY_CWFX
{
  Default
  {
  Speed 40;
  Damage 1;
  }
}

Class DCY_CWFX3 : DCY_CWFX
{
  Default
  {
  Speed 10;
  Damage 35;
  Scale 2.5;
  SeeSound "MBALFIR2";
  DeathSound "MBALEXP2";
  }
  States
  {
  Spawn:
    D9PJ AAABBBCCCDDD 1 A_SpawnItemEx("DCY_CWFXTrail3", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	Loop;
  Death:
	TNT1 A 1
	{
		A_QuakeEx(5, 5, 5, 30, 0, 700, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 1100);
		A_Explode(40, 100, 0, 0);
		A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_CWFXDeathTrail2", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
    TNT1 AAAAAAA 5 Bright A_Explode(8, 100, 0, 0);
	Stop;
  }
}

Class DCY_CWFXTrail : DCY_Effect
{
  Default
  {
  Alpha 0.55;
  Scale 0.55;
  Radius 0;
  Height 0;
  +NOINTERACTION
  +DONTSPLASH
  +NOCLIP
  +BRIGHT
  RenderStyle "Add";
  }
  States
  {
  Spawn:
    EF1_ A 0;
	EF1_ A 1;
	Stop;
  }
}

Class DCY_CWFXDeathTrail : DCY_CWFXTrail 
{
  Default
  {
  Scale 0.675;
  Alpha 1.0;
  }
  States
  {
  Spawn:
    KABE A 1 A_SetScale(frandompick(0.7, -0.7), 0.7);
	KABE BCDEFGHIJKLMNOPQRSTUVW 1;
	Stop;
  }
}

Class DCY_CWFXTrail3 : DCY_CWFXTrail
{
  Default
  {
  Scale 1.5;
  Alpha 0.4;
  }
  States
  {
  Spawn:
    EF1_ A 0;
	EF1_ A 1;
	Stop;
  }
}

Class DCY_CWFXDeathTrail2 : DCY_CWFXDeathTrail
{
  Default
  {
  Scale 1.3;
  }
  States
  {
  Spawn:
    KAB1 A 0;
	KAB1 ABCDEFGHIJKLMNOPQRSTUVWXY 1;
	Stop;
  }
}
Class DCY_CWFXDeathTrail3 : DCY_CWFXDeathTrail
{
  Default
  {
  Scale 1.35;
  Alpha 1.0;
  +ROLLSPRITE
  +FLATSPRITE
  }
  States
  {
  Spawn:
    KAB3 A 0;
	KAB3 A 1
	{
		A_SetRoll(Roll+frandom(0.0, 360.0));
		A_SetPitch(Pitch+frandom(0.0, 360.0));
		A_SetAngle(Angle+frandom(0.0, 360.0));
	}
	KAB3 A 1;
	KAB3 BCDEFGHIJKLMN 2;
	Stop;
  }
}