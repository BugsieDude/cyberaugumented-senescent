Class DCY_Tagati : DCY_CacoParent
{
  int bobstate;
  
  Default
  {
  Health 695;
  Radius 22;
  Height 62;
  Speed 11;
  PainChance 89;
  +FLOAT
  +NOGRAVITY
  +NOTELEFRAG
  +DONTOVERLAP
  +NEVERRESPAWN
  +NOTARGETSWITCH
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MISSILEMORE
  DropItem "DCY_Eminence", 128;
  DropItem "DCY_Eminence", 128;
  SeeSound "tagati/sight";
  ActiveSound "tagati/idle";
  PainSound "tagati/pain";
  HowlSound "tagati/pain";
  DeathSound "tagati/death";
  Tag "\c[f1]Tagati\c-";
  Obituary "%o got hexed by a Tagati.";
  BloodColor "4a0b23";
  BloodType "DCY_SpiritBlood";
  }
  
  void A_BishopChase() //borrowed
  {
	double newz = pos.z - BobSin(bobstate) / 2.;
	bobstate = (bobstate + 4) & 63;
	newz += BobSin(bobstate) / 2.;
	SetZ(newz);
  }
  
  void A_TagatiChase() { A_Chase(); A_Chase(); }
  void A_TagatiDash() { A_SpawnItemEx("DCY_CWTrail"); A_Chase(); A_BishopChase(); }
	
  void A_TagatiFire(int speed, int xangle)
  {
	if (target)
	{
		A_FaceTarget();
		A_StartSound("MBALFIRE", CHAN_WEAPON, CHANF_OVERLAP);
		
		let tagatifx = Spawn("DCY_CWFX", pos + (0, 0, 36));
		
		if (tagatifx)
		{
			tagatifx.target = self;
			tagatifx.angle = angle;
			tagatifx.Vel3DFromAngle((!speed) ? GetDefaultSpeed("DCY_CWFX") : speed, tagatifx.angle + frandom(-xangle, xangle), BulletSlope());
		}
	}
	
	if (random(0, 30) == 10)
		SetStateLabel("See");
  }
  
  States
  {
  Spawn:
    DD9S A 10 DCY_Look();
    Loop;
  Look:
	DD9S AAABBB 4
	{
		A_BishopChase();
		A_Chase("See", null, CHF_DONTMOVE|CHF_NOPLAYACTIVE);
		DCY_LookingForPlayer();
	}
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    TNT1 A 0 A_Jump(15, "Rush", "Teleport");
    DD9S A 3 A_TagatiChase();
	DD9S A 3 A_BishopChase();
	DD9S B 3 A_TagatiChase();
	DD9S B 3 A_BishopChase();
	Loop;
  Teleport:
	TNT1 A 0 { bSHOOTABLE = false; A_SpawnItemEx("DCY_CWTrail"); }
    DD9S AAAAAAAAA 1 A_FadeOut(0.1, 0);
	TNT1 A 0 A_Jump(128,6);
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0
	{
		A_Chase(null, null, CHF_NOPLAYACTIVE);
		A_Wander();
	}
    TNT1 A 0 A_Jump(128,6);
    TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0
	{
		A_Chase(null, null, CHF_NOPLAYACTIVE);
		A_Wander();
	}
	DD9S AAAAAAAAA 1 A_FadeIn(0.1);
	TNT1 A 0 { bSHOOTABLE = true; }
	Goto See;
  Rush:
    TNT1 A 0 A_StartSound("dcy/blur", CHAN_7, volume: 0.5);
    DD9S AAAABBBBAAAABBBB 1 A_TagatiDash();
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(64, "Charge");
    DD9S DE 7 Bright A_FaceTarget();
	DD9S F 6 Bright
	{
		A_TagatiFire((Distance3D(target) < 500) ? 15 : 25, (Distance3D(target) < 300) ? 5 : 15);
		Return A_MonsterRefire(40, "See");
	}
	Goto Missile+3;
  Charge:
	DD9S D 0 Bright A_StartSound(SeeSound, CHAN_VOICE);
    DD9S DDDDDDDDDDDDEEEEEEEEEEEEE 2 Bright A_FaceTarget();
	DD9S F 10 Bright
	{
		A_SpawnItemEx("DCY_CWFXDeathTrail2X", 6, 0, 36);
		A_SpawnProjectile("DCY_CWFX3", 36);
	}
	Goto See;
  Pain:
	DD9S C 4;
    DD9S C 4 A_Pain();
	Goto See;
  Death:
    DD9S G 6;
	DD9S H 6 Bright A_ScreamAndUnblock();
	DD9S IJKLM 5 Bright;
	DD9S N 4
	{
		if (damagetype != "Massacre")
			Spawn("DCY_CWPuff", pos + (0, 0, 40));
	}
	DD9S O 4;
	DD9S P -1;
	Stop;
  }
}

Class DCY_CWTrail : DCY_Effect
{
   Default
   {
   Radius 1;
   Height 1;
   Speed 0;
   Scale 1.0;
   Alpha 0.95;
   RenderStyle "AddStencil";
   StencilColor "da3287";
   +NOCLIP
   -SOLID
   +NOGRAVITY
   +CLIENTSIDEONLY
   }
   States
   {
   Spawn:
      NULL A 3;
	  Goto FadeOut;
   FadeOut:
      DD9S A 1 A_FadeOut(0.1);
      Loop;
   }
}

Class DCY_CWPuff : BishopPuff
{
  Default { RenderStyle "Add"; }
  
  States
  {
  Spawn:
    DD9S QRSTUVW 5 Bright
	{
		vel.z += 0.2;
	}
	Stop;
  }
}

Class DCY_CWFX : DCY_FastProjectile
{
  Default
  {
  Speed 15;
  Damage 5;
  Radius 12;
  Height 12;
  Alpha 1.0;
  Scale 0.8;
  RenderStyle "Add";
  SeeSound "MBALFIRE";
  DeathSound "MBALEXPL";
  +BRIGHT
  +RANDOMIZE
  +ROLLSPRITE
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.79,0.94]";
  }
  States
  {
  Spawn:
    SUE1 B 1
	{
		roll += 3;
		A_SpawnItemEx("DCY_CWFXTrail", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL);
		A_SpawnParticleEx(0xff2679, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: 0.725);
	}
	Loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("DCY_CWFXDeathTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    D9PJ EFGHIJK 3 Bright;
	Stop;
  }
}

Class DCY_CWFX3 : DCY_CWFX
{
  Default
  {
  Speed 10;
  Damage 35;
  Scale 2.5;
  SeeSound "MBALFIR2";
  DeathSound "MBALEXP2";
  }
  States
  {
  Spawn:
    D9PJ AAABBBCCCDDD 1
	{
		for (int i = 2; i > 0; i--)
			A_SpawnParticleEx(0x4a0b23, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 130, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.015, sizestep: 0.55);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_QuakeEx(5, 5, 5, 30, 0, 700, "none", QF_RELATIVE|QF_SCALEDOWN, 0, 0, 0, 1100);
		A_Explode(55, 100, 0, 0);
		
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_CWFXDeathTrail3", random(-2, 2), random(-2, 2), random(-2, 2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			
		A_SpawnItemEx("DCY_CWFXDeathTrail2", random(-2, 2), random(-2, 2), random(-2, 2), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    TNT1 AAAAAAA 5 Bright A_Explode(8, 100, 0, 0);
	Stop;
  }
}

Class DCY_CWFXTrail : DCY_Effect
{
  Default
  {
  Alpha 0.55;
  Scale 0.55;
  Radius 0;
  Height 0;
  +NOINTERACTION
  +DONTSPLASH
  +NOCLIP
  +BRIGHT
  RenderStyle "Add";
  }
  States
  {
  Spawn:
    SUE1 CCCCDDDDEEEEFFFF 1
	{
		A_FadeOut(0.1, 0);
		roll += 3;
	}
	Stop;
  }
}

Class DCY_CWFXDeathTrail : DCY_CWFXTrail 
{
  Default
  {
  Scale 0.675;
  Alpha 1.0;
  }
  States
  {
  Spawn:
    KABE A 1 A_SetScale(frandompick(0.7, -0.7), 0.7);
	KABE BCDEFGHIJKLMNOPQRSTUVW 1;
	Stop;
  }
}

Class DCY_CWFXTrail3 : DCY_CWFXTrail
{
  Default
  {
  Scale 1.5;
  Alpha 0.4;
  }
  States
  {
  Spawn:
    EF1_ A 0;
	EF1_ A 1;
	Stop;
  }
}

Class DCY_CWFXDeathTrail2 : DCY_CWFXDeathTrail
{
  Default
  {
  Scale 1.3;
  }
  States
  {
  Spawn:
    KAB1 A 0;
	KAB1 ABCDEFGHIJKLMNOPQRSTUVWXY 1;
	Stop;
  }
}
Class DCY_CWFXDeathTrail3 : DCY_CWFXDeathTrail
{
  Default
  {
  Scale 1.35;
  Alpha 1.0;
  +ROLLSPRITE
  +FLATSPRITE
  }
  States
  {
  Spawn:
    KAB3 A 0;
	KAB3 A 1
	{
		A_SetRoll(Roll+frandom(0.0, 360.0));
		A_SetPitch(Pitch+frandom(0.0, 360.0));
		A_SetAngle(Angle+frandom(0.0, 360.0));
	}
	KAB3 A 1;
	KAB3 BCDEFGHIJKLMN 2;
	Stop;
  }
}

Class DCY_CWFXDeathTrail2X : DCY_CWFXDeathTrail2 { Default { Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.79,0.94]"; } }