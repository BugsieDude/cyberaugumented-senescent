Class DCY_Chrysalis : DCY_CacoParent
{
  Default
  {
  Health 800;
  Speed 8;
  FastSpeed 10;
  Radius 25;
  Height 55;
  Tag "\c[w4]Chrysalis\c-";
  Translation "0:255=%[0.07,0.00,0.00]:[1.41,0.50,0.00]", "0:3=[0,0,0]:[36,36,36]", "5:15=[0,0,0]:[17,17,17]", "70:79=[65,65,65]:[0,0,0]", "80:111=[192,192,192]:[0,0,0]", "128:143=[129,129,129]:[25,25,25]", "144:151=[77,77,77]:[17,17,17]", "13:15=%[0.00,0.00,0.00]:[0.40,0.40,0.40]", "236:239=%[0.00,0.00,0.00]:[0.21,0.21,0.21]";
  SeeSound "ATurret2/Sight";
  ActiveSound "ATurret/Sight";
  PainSound "ATurret/Pain";
  DeathSound "ATurret2/Death";
  DamageType "Arcane";
  DamageFactor "Arcane", 0.000;
  DropItem "Blursphere", 40;
  Obituary "A Chrysalis tricked %o.";
  DCYMonster.ScientificName "NO DATA";
  -MISSILEMORE
  +FLOAT
  +NOBLOOD
  +DONTFALL
  FloatSpeed 0;
  RenderStyle "Normal";
  Alpha 1.0;
  }
  
  void A_CLaser()
  {
	A_StartSound("Chrysalis/Laser");
	A_SpawnProjectile("DCY_ChrysalisBall", 32, -7, -15);
	A_SpawnProjectile("DCY_ChrysalisBall", 32, 7, 15);
  }
  
  States
  {
  Spawn:
	GARD ABCDEF 3 DCY_Look();
    Loop;
  Look:
	GARD ABCDEF 3 DCY_LookingForPlayer();
    Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	GARD ABCDEF 3
	{
		If(Health <= 100)
		{
			A_Chase();
			A_SetSpeed(15);
			Return ResolveState(null);
		}
		Else If(Health <= 45)
		{
			A_Chase();
			A_SetTics(2);
			Return ResolveState(null);
		}
		Else
		{
			A_Chase();
			Return ResolveState(null);
		}
	}
	Loop;
  Missile:
	GARD ABCDEF 2 A_FaceTarget();
	GARD G 10 A_FaceTarget();
	GARD H 3 A_CLaser();
	GARD G 9 A_FaceTarget();
	GARD H 3 A_CLaser();
	GARD G 9 A_FaceTarget();
	GARD H 3 A_CLaser();
	GARD G 9 A_FaceTarget();
	GARD H 3 A_CLaser();
	GARD G 9 A_FaceTarget();
	Goto See;
  Pain:
	GARD AB 1;
	GARD C 1 A_Pain();
	GARD DEF 1;
	Goto See;
  Death:
	GARD A 10;
	GARD B 3 A_Scream();
	GARD CDEFABCDEF 2;
	GARD K 0
	{
		A_Gravity();
		bNOGRAVITY = false;
		bFLOORCLIP = true;
		A_NoBlocking();
	}
	GARD KLM 6;
	GARD NNNNNN 1
	{
		if (damagetype != "Massacre")
			A_SpawnItemEx("DCY_Explosion_1", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(20, 30));
	}
	GARD OPQR 5;
	GARD S -1;
	Stop;
  Raise:
	GARD RQPONMLK 4;
	GARD K 0
	{
		A_NoGravity();
		bNOGRAVITY = true;
		bFLOORCLIP = false;
	}
	Goto See;
  }
}

Class DCY_ChrysalisBall : DCY_MeatballElecball
{
  Default
  {
  Speed 15;
  +NEVERFAST
  +SEEKERMISSILE
  Scale 0.7;
  Alpha 1;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.40,0.00]";
  SeeSound "Chrysalis/Laser";
  DeathSound "Chrysalis/LaserXplode";
  }
  
  int timer;
  
  void A_SpawnTrail()
  {
	A_SpawnParticleEx(0xff5500, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, 34, random(15, 20), 0, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), fadestepf: 0.1, sizestep: -1.15);
	
	if (random(0, 2) == 0)
	{
		actor plasma = Spawn("DCY_FluorescentSparkle", pos + (frandom(-8.0, 8.0), frandom(-8.0, 8.0), frandom(-8.0, 8.0)));
		if (plasma) { plasma.translation = translation; plasma.bNOGRAVITY = true; }
	}
	
	A_SpawnParticleEx(0xff5500, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 10, 25, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: -2.225);
  }
  
  States
  {
  Spawn:
	RBL2 AABBCCDD 1
	{
		if (timer > 35)
			SetStateLabel("Seek");
		
		A_SpawnTrail();
		
		vel -= vel.Unit() * 0.5;
		timer++;
	}
	Loop;
  Seek:
	RBL2 A 0 { for (int i = 2; i > 0; i--) A_SeekerMissile(99, 99, SMF_PRECISE); }
	RBL2 AABBCCDD 1
	{
		A_SpawnTrail();
		
		if (timer < 45)
		{
			timer++;
			vel += vel.Unit() * 0.65;
		}
	}
	Goto Seek+1;
  Death:
	RFX1 G 2
	{
		for (int i = 7; i > 0; i--)
			A_SpawnParticleEx(0xff5500, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, random(10, 15), 0, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), fadestepf: 0.2, sizestep: 1.15);
	}
	RFX1 GHIJKLMNO 2;
	Stop;
  }
}