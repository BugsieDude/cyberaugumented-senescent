Class DCY_NightmareSoul : DCY_SkullParent
{
  Default
  {
  Translation "80:159=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "112:127=%[0.00,0.00,0.00]:[0.48,0.66,1.53]", "1:3=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "5:15=%[0.00,0.00,0.00]:[0.54,0.54,0.54]";
  Health 70;
  Tag "Nightmare";
  AttackSound "SOULATK3";
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOINFIGHTING
  }
  
  override void Tick()
  {
	Super.Tick();
	
	if (Health > 0 && !IsFrozen() && DCY_EffectDistance(sfxdistance))
	{
		A_SpawnItemEx("DCY_NFLAME1", frandom(-3.0, -10.0), frandom(-5.0, 5.0), frandom(12.0, 19.0), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 210);
		A_SpawnItemEx("DCY_NFLAME2", frandom(-3.0, -10.0), frandom(-5.0, 5.0), frandom(12.0, 19.0), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 120);
	}
  }
  
  int chargenum;
  
  States
  {
  Spawn:
	STLK A 2 DCY_Look();
	Loop;
  Look:
	STLK A 2 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	STLK A 2 A_Chase();
	Loop;
  Missile:
	STLK A 2
	{
		A_FaceTarget();
		chargenum = 0;
	}
	STLK AAAAA 2 A_FaceTarget();
	STLK B 1
	{
		A_Weave(3, 3, 1, 1);
		A_SkullAttack(random(20, 25));
		chargenum += 1;
	}
	STLK BBBBBBBBBBBBBBBBBBBBBBB 1 A_Weave(3, 3, 1, 1);
	STLK A 2 A_Stop();
	STLK A 0 A_JumpIf(chargenum > 3, "See");
	Goto Missile + 5;
  Pain:
	STLK C 3 Bright;
    STLK C 3 Bright A_Pain();
    Goto See;
  Death:
    STLK F 3 Bright;
    STLK G 3 Bright A_ScreamAndUnblock();
    STLK H 3 Bright
	{
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_RemoveChildren(true, flags: RMVF_EVERYTHING);
		A_Stop();
		A_SpawnItemEx("DCY_Explosion_1_Nightmare", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
    STLK I 3 Bright;
    Stop;
  }
}

Class DCY_NFlame1 : DCY_ImpBigFlame { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.57,0.57,2.00]"; } }
Class DCY_NFlame2 : DCY_ImpFlame1 { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.57,0.57,2.00]"; } }
Class DCY_Explosion_1_Nightmare : DCY_Explosion_1_Small { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.57,0.57,2.00]"; } }