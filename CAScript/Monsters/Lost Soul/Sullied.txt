Class DCY_Sullied : DCY_SkullParent
{
  Default
  {
  Alpha 1.0;
  RenderStyle "Normal";
  DamageType "Fire";
  Health 128;
  DamageFactor "Fire", 0.000;
  Tag "Sullied";
  SeeSound "Sullied/Sight";
  AttackSound "Sullied/Sight";
  ActiveSound "Sullied/Active";
  PainSound "Sullied/Pain";
  DeathSound "Sullied/Death";
  Translation "0:255=%[0.00,0.00,0.00]:[0.52,0.52,2.00]", "1:3=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "5:15=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "80:111=[255,255,255]:[0,0,0]", "128:159=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "236:239=%[0.00,0.00,0.00]:[0.00,0.50,2.00]";
  Obituary "A Sullied caused harrow to %o.";
  DCYMonster.ScientificName "Occare animam";
  Speed 10;
  FastSpeed 12;
  Scale 0.8;
  -ZDOOMTRANS
  -BRIGHT
  +NOGRAVITY
  -DONTFALL
  +FIRERESIST
  -MISSILEMORE
  +NOBLOOD
  }
  
  void A_Sullied()
  {
	A_FaceTarget();
	A_SpawnProjectile("DCY_SulliedFireball", 26, 0, frandom(-4.0, 4.0), CMF_OFFSETPITCH, pitch + frandom(-4, 4));
  }
  
  override void Tick()
  {
	Super.Tick();
	if (DCY_EffectDistance(sfxdistance) && !level.isFrozen() && health > 0)
	{
		A_SpawnItemEx("DCY_SulliedTrail", -0.5, xvel: vel.x / 8, yvel: vel.y / 8, zvel: vel.z / 8, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(0x3e32a8, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 60, random(3, 9), frandom(0, 360), frandom(-20.0, 20.0), frandom(-20.0, 20.0), 40 + frandom(-4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.05, 0.2), fadestepf: 0, sizestep: -0.15);
	}
  }
  
  States
  {
  Spawn:
	FALN AABBCCDD 10 DCY_Look();
	Loop;
  Look:
	FALN AAABBBCCCDDD 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	FALN AAABBBCCCDDD 3
	{
		A_Chase();
		
		if (!random(0, 3))
		{
			vel.x += frandom(-1, 1);
			vel.y += frandom(-1, 1);
			vel.z += frandom(-1, 1);
		}
	}
  Melee:
  Missile:
	FALN E 1
	{
		if (!random(0, 2) && (target && Distance3D(target) < 256)) SetStateLabel("FireThrower");
	}
	FALN E 7 A_FaceTarget();
	FALN FFF 7 A_Sullied();
	FALN E 14 A_FaceTarget();
	FALN FFF 7 A_Sullied();
	FALN E 14 A_FaceTarget();
	Goto See;
  Firethrower:
	FALN E 5 { A_StartSound("DCYDeadlyBreath", CHAN_WEAPON); A_FaceTarget(); }
	FALN FEFEFEFEFEFEFEF 3
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_SulliedFirethrower", 26, 0, frandom(-1.0, 1.0));
	}
	FALN A 10 A_FaceTarget();
	Goto See;
  Pain:
	FALN G 4;
	FALN G 4 A_Pain();
	Goto See;
  Death:
	FALN H 3;
	FALN H 3 A_ScreamAndUnblock();
	FALN IJK 5;
	FALN LM 4;
	FALN M -1;
	Stop;
  Raise:
	FALN MLK 6;
	FALN JIHG 6;
	Goto See;
  }
}

Class DCY_SulliedTrail : DCY_HexodenicanTrail2 { Default { Alpha 0.2; StencilColor "0b0052"; } }

Class DCY_SulliedFirethrower : DCY_Projectile
{
  Default
  {
  Projectile;
  Radius 10;
  Height 10;
  +RIPPER
  +MTHRUSPECIES
  DamageFunction 1;
  DamageType "Fire";
  Speed 15;
  +NEVERFAST;
  Translation "0:255=%[0.00,0.00,0.00]:[0.55,0.55,1.64]";
  }
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAAAAA 1
	{
		A_SpawnFlames("382bb5", 50, 60, 0.04, false, frandom(-10, 10));
		A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
		A_ChangeVelocity(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
	}
	Stop;
  Death:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_SulliedFireball : DCY_ImpFireball
{
  Default
  {
  Damage 4;
  RenderStyle "Add";
  DamageType "Fire";
  +BRIGHT;
  Speed 13;
  FastSpeed 18;
  Translation "0:255=%[0.00,0.00,0.00]:[0.55,0.55,1.64]";
  }
  
  States
  {
  Spawn:
	NBKL AABBCC 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnFlames("382bb5", 20, 32, 0, true);
			A_SpawnFlames("a196ff", 15, 20, 0, false);
		}
		
		A_Weave(1, 0, 1, 0);
	}
	Loop;
  Death:
	SMOL F 2 { A_SetScale(1.2); A_SpawnItemEx("DCY_Explosion_1_Small", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION); }
	SMOL GHIJL 2;
	Stop;
  }
}