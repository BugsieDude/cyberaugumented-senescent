Class DCY_LunarChiller : DCY_CacoParent
{
  Default
  {
  Health 200;
  Tag "\c[Sapphire]The Anesthesized\c-";
  Obituary "%o got the cold shoulder.";
  HitObituary "%o got the cold shoulder.";
  DamageType "Ice";
  DamageFactor "Ice", 0.00;
  DamageFactor "Fire", 1.25;
  +NOICEDEATH
  +MISSILEMORE
  +NEVERFAST
  +QUICKTORETALIATE
  +NOTARGET
  +BRIGHT
  +NOBLOOD
  +GHOST
  FloatSpeed 8;
  Speed 8;
  RenderStyle "Translucent";
  Alpha 0.7;
  SeeSound "LunarChiller/Sight";
  ActiveSound "LunarChiller/Sight";
  PainSound "LunarChiller/Pain";
  DeathSound "LunarChiller/Death";
  Translation "192:195=[255,255,255]:[128,255,255]", "196:198=[81,255,255]:[0,128,255]", "199:204=[0,124,249]:[0,0,160]";
  DCYMonster.ScientificName "Exspiravit eques";
  }
  
  override void Tick()
  {
	if (!level.isfrozen() && DCY_EffectDistance(sfxdistance) && tics != -1)
	{
		if (!(level.time % 2)) A_SpawnParticleEx(0x222222, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_SUBTRACT, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.2, fadestepf: 0.0035, sizestep: 1.525);
		A_SpawnItemEx("DCY_ManesTrail", xvel: frandom(-1, 1), yvel: frandom(-1, 1), zvel: frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		A_SpawnItemEx("DCY_ProjectileBaseTrail", 0.1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		let shadow = spawn("DCY_ManesTrail2", pos);
		if (shadow)
		{
			shadow.vel.x = frandom(-0.5, 0.5);
			shadow.vel.y = frandom(-0.5, 0.5);
			shadow.vel.z = frandom(-0.5, 0.5);
			shadow.scale = scale;
			shadow.frame = frame;
			shadow.sprite = GetSpriteIndex("LCHL");
			shadow.angle = angle;
		}
	}
	
	Super.Tick();
  }
  
  States
  {
  Spawn:
	LCHL ABCD 6 DCY_Look();
	Loop;
  Look:
	LCHL AAABBBCCCDDD 2 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	LCHL AAABBBCCCDDD 2 A_Chase();
	Loop;
  Melee:
	LCHL E 5 A_FaceTarget();
	LCHL F 5 A_CustomMeleeAttack(random(3, 8)*22, "LunarChiller/Pain", "none", "Ice");
	LCHL E 3;
    Goto See;
  Missile:
	LCHL AAABBBCCCDDD 2
	{
		If(target && Distance3D(target) < 120)
			Return ResolveState("ChillMan");
		
		A_Chase();
		Return ResolveState(null);
	}
    Goto See;
  ChillMan:
	LCHL E 5 A_FaceTarget();
	LCHL F 5 A_SpawnProjectile("DCY_LunarSilhouette", 0, 0, -10);
	LCHL EAA 3;
    Goto See;
  Pain:
	LCHL H 3;
	LCHL H 4 A_Pain();
	Goto See;
  Death:
	LCHL H 3;
	LCHL I 5 A_ScreamAndUnblock();
	LCHL JKLMNOPQ 3;
	LCHL R -1;
	Stop;
  Raise:
	LCHL QPONMLKJIH 3;
	Goto See;
  }
}

Class DCY_LunarSilhouette : CacodemonBall
{
  Default
  {
  SeeSound "LunarChiller/Pain";
  DeathSound "";
  Scale 0.9;
  RenderStyle "Add";
  Alpha 0.7;
  Speed 9;
  FastSpeed 14;
  +RIPPER
  +BRIGHT
  DamageType "Ice";
  }
  States
  {
  Spawn:
	LCHL EEEEEFFFFFEEEE 1 A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
	LCHL FFFFFEEEEE 1
	{
		A_FadeOut(0.1, 0);
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERALPHA);
	}
	Stop;
  Death:
	TNT1 A 1;
	Stop;
  }
}