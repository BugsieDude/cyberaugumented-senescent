Class DCY_Auteur : DCY_VileParent
{
  Default
  {
  Health 700;
  Tag "Auteur";
  Obituary "Due to the Auteur, %o is now a pile of nothingness.";
  Scale 1.2;
  SeeSound "Auteur/Sight";
  ActiveSound "Auteur/Idle";
  PainSound "Auteur/Pain";
  DeathSound "Auteur/Death";
  BloodColor "00 50 AA";
  BloodType "DCY_SpiritBlood";
  Speed 15;
  +NEVERFAST
  +MISSILEMORE
  +DONTHARMSPECIES
  +DONTHARMCLASS
  DCYMonster.FootStepSound "";
  }
  
  bool xdirection;
  
  void A_TracerCheck()
  {
	if (target && tracer)
	{
		target.A_StopSound(10);
		tracer.A_StopSound(10);
		A_Remove(AAPTR_TRACER, RMVF_EVERYTHING, "DCY_AuteurVile");
	}
  }
  
  States
  {
  Spawn:
    A_1_ A 10 DCY_Look();
	Loop;
  Look:
	#### A 3 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
    A_1_ A 3 DCY_Chase("Missile", "Missile", CHF_RESURRECT);
	Loop;
  Heal:
	A_1_ E 40;
	Goto See;
  Missile:
	A_1_ B 0
	{
		switch(random(0, 2))
		{
			case 0: Return ResolveState("Wave"); break;
			case 1: Return ResolveState("Balls"); break;
			case 2: Return ResolveState("Vile"); break;
		}
		
		Return ResolveState(null);
	}
	Goto See;
  Wave:
	A_1_ B 2 A_StartSound("Auteur/Sight", CHAN_VOICE);
	A_1_ BBBCCC 4 A_FaceTarget();
	A_1_ D 10
	{
		A_SpawnProjectile("DCY_AuteurWave", 30, flags: CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_FaceTarget();
		A_StartSound("Auteur/WaveAtk");
	}
	A_1_ A 10;
	Goto See;
  Balls:
	A_1_ EEEE 5 A_FaceTarget();
	A_1_ BBBB 10
	{
		int ballangle = 90;
		xdirection = !xdirection;
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_AuteurBall", 45, 0, ballangle * xdirection - 45, CMF_OFFSETPITCH, -10);
	}
	A_1_ CD 5 A_FaceTarget();
	Goto See;
  Vile:
    A_1_ B 5 { A_FaceTarget(); A_StartSound("Auteur/Idle", CHAN_VOICE); }
    A_1_ B 1 A_VileTarget("DCY_AuteurVile");
    A_1_ BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBCCCCCCCCCC 1 A_FaceTarget();
    A_1_ D 9 A_VileAttack();
    A_1_ A 20;
    Goto See;
  Pain:
	A_1_ F 4 A_TracerCheck();
	A_1_ F 4 A_Pain();
	Goto See;
  Death:
	A_1_ F 10 A_TracerCheck();
	A_2_ A 6 A_Scream();
	A_2_ B 6 A_NoBlocking();
	A_2_ CDEF 5;
	A_2_ G -1;
	Stop;
  }
}

Class DCY_AuteurWave : DCY_CrusaderSlash
{
  Default
  {
  Damage 15;
  Radius 10;
  Height 56;
  Speed 30;
  SeeSound "Auteur/WaveAtk";
  XScale 0.15;
  YScale 9.0;
  Species "Viles";
  MissileType "DCY_AuteurWaveTrail";
  +NEVERFAST
  }
  
  States
  {
  Spawn:
	TRAC A 1
	{
		A_SpawnItemEx("DCY_AuteurFire", frandom(-10, 10), frandom(-10, 10), zvel: -1000, flags: SXF_NOCHECKPOSITION);
		if (timer < 20) A_ScaleVelocity(1.045); else Destroy();
		timer += (timer < 20) ? 1 : 0;
	}
	Loop;
  }
}

Class DCY_AuteurWaveTrail : DCY_CrusaderSlashTrail
{
  Default
  {
  XScale 0.15;
  YScale 9.0;
  Alpha 0.135;
  Translation "0:255=%[0.00,0.00,0.00]:[0.43,0.75,1.58]";
  }
}

Class DCY_AuteurFire : DCY_NapalmFire
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.40]:[0.62,1.20,1.79]";
  Scale 2.0;
  +FLOORHUGGER
  }
  
  static const string ParticleColors[] = { "4ad5ff", "149dff", "0048ff" };
  
  States
  {
  Spawn:
	123E "#" 0 NoDelay { fireloop = random(0, 20); A_StartSound("FIR3LOOPBIG", CHAN_BODY, CHANF_LOOPING, vol, pitch: frandom(1.5, 1.9)); A_SetScale(frandom(1.55, 2.0) * randompick(-1, 1), frandom(1.0, 1.6)); }
	123E "##########" 1 A_FadeIn(0.1);
	123E "#" 1
	{
		if (fireloop > 35)
			SetStateLabel("Gone");
		
		if ((level.time % 3) == 0)
		{
			fireloop++;
			A_Explode(5, 60, 0);
		}
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 35, random(9, 12), frandom(0, 360), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.2, fadestepf: 0, sizestep: -0.45);
	}
	Goto Spawn+11;
  Gone:
	123E "##########" 1
	{
		A_SoundVolume(CHAN_BODY, vol);
		vol -= 0.1;
		A_FadeOut(0.1);
	}
	Stop;
  }
}

Class DCY_AuteurBall : DCY_ImpFireball
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.40]:[0.62,1.20,1.79]";
  +SEEKERMISSILE
  Speed 15;
  +NEVERFAST;
  SeeSound "Auteur/Ball";
  DeathSound "Auteur/BallX";
  }
  
  int timer;
  
  States
  {
  Spawn:
	C3AL A 1
	{
		timer++;
		
		if (timer < 20) A_ScaleVelocity(0.9);
		if (timer > 45 && timer < 70) { A_ScaleVelocity(1.065); if (timer < 48) A_SeekerMissile(999, 999); }
		A_SpawnItemEx("DCY_FluorescentOrbT", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnParticleEx(0x0048ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 45, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.015, sizestep: 0.55);
	}
	Loop;
  Death:
	C3AL A 1
	{
		actor ring = Spawn("DCY_NeonicRing", pos);
		if (ring) ring.scale *= 1.5;
		
		A_SpawnItemEx("DCY_AndroidExplosion", random(-1, 1), random(-1, 1), random(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Explode(60, 50);
		A_FadeOut(0.2, 0);
	}
	C3AL ABAB 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_AuteurVile : ArchvileFire
{
  Default
  {
  Alpha 0;
  Obituary "$OB_VILE";
  Translation "0:255=%[0.00,0.00,0.00]:[0.02,0.50,1.79]";
  +DONTSPLASH
  }
  
  static const string ParticleColors[] = { "4ad5ff", "149dff", "0048ff" };
  
  override void Tick()
  {
	if (level.isFrozen()) return;
	timer++;
	
	if (timer > 80)
	{
		for (float i = 0; i < 5; i += 0.25)
		{
			A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 45, random(9, 12), frandom(0, 360), frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.3 + i, fadestepf: 0.05, sizestep: -0.65);
			A_SpawnParticleEx(0x149dff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 87, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5) + i, startalphaf: 0.35, fadestepf: 0.015, sizestep: -0.3);
		}
		
		for (int i = 10; i > 0; i--)
		{
			A_SpawnParticleEx(0x0048ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0025, sizestep: 1.525);
			actor beamx = Spawn("DCY_AuteurBeam", pos);
			if (beamx) beamx.translation = translation;
		}
		
		A_StopSound(10);
		if (tracer) tracer.A_StopSound(10);
		for (int i = 2; i > 0; i--) A_StartSound("le/laser", 11, CHANF_OVERLAP, 3.0, pitch: 2.54);
		Destroy();
	}
	
	actor beam = Spawn("DCY_AuteurBeam", pos);
	A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 35, random(9, 12), frandom(0, 360), frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.3, fadestepf: 0, sizestep: -0.45);
	A_SpawnParticleEx(0x0048ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 87, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5) + 1.5, startalphaf: 0.35, fadestepf: 0.0035, sizestep: -0.5);
	A_Fire();
  }
  
  override void PostBeginPlay()
  {
	float randompitch = frandom(0.2, 0.6);
	
	A_StartSound("Auteur/VileAtk");
	for (int i = 3; i > 0; i--)
		A_StartSound("Supremacy/IdleNoise", 10, CHANF_OVERLAP, 3.0, pitch: randompitch);
	if (tracer) tracer.A_StartSound("Supremacy/IdleNoise", 10, CHANF_OVERLAP, 3.0, pitch: randompitch);
  }
  
  int timer;
  
  States
  {
  Spawn:
    TNT1 A -1;
	Stop;
  }
}

Class DCY_AuteurBeam : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  Alpha 0.25;
  YScale 512.256;
  XScale 5.7;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.02,0.20,1.09]";
  +FORCEYBILLBOARD
  +BRIGHT
  -SOLID
  +FLOORHUGGER
  +THRUACTORS
  }
  
  States
  {
  Spawn:
	L2ZO C 1
	{
		A_FadeOut(0.025);
		A_SetScale(Scale.X+0.25, 512.256);
	}
	Wait;
  }
}