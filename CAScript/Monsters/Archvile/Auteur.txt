Class DCY_Auteur : DCY_VileParent
{
  Default
  {
  Health 800;
  Tag "Auteur";
  Obituary "Due to the Auteur, %o is now a pile of nothingness.";
  Scale 1.2;
  SeeSound "Auteur/Sight";
  ActiveSound "Auteur/Idle";
  PainSound "Auteur/Pain";
  DeathSound "Auteur/Death";
  BloodColor "00 50 AA";
  BloodType "DCY_SpiritBlood";
  Speed 15;
  +NEVERFAST
  +MISSILEMORE
  +DONTHARMSPECIES
  +DONTHARMCLASS
  }
  
  bool xdirection;
  
  States
  {
  Spawn:
    A_1_ A 10 DCY_Look();
	Loop;
  Look:
	#### A 3 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
    A_1_ A 3 A_Chase("Missile", "Missile", CHF_RESURRECT);
	Loop;
  Raise:
	A_1_ E 20;
	Goto See;
  Missile:
	A_1_ B 0
	{
		switch(random(0, 1))
		{
			case 0: Return ResolveState("Wave"); break;
			case 1: Return ResolveState("Balls"); break;
		}
		
		Return ResolveState(null);
	}
	Goto See;
  Wave:
	A_1_ B 2 A_StartSound("Auteur/Sight", CHAN_VOICE);
	A_1_ BBBCCC 4 A_FaceTarget();
	A_1_ D 10
	{
		A_SpawnProjectile("DCY_AuteurWave", 30, flags: CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_FaceTarget();
		A_StartSound("Auteur/WaveAtk");
	}
	A_1_ A 10;
	Goto See;
  Balls:
	A_1_ EEEE 5 A_FaceTarget();
	A_1_ BBBB 10
	{
		int ballangle = 90;
		xdirection = !xdirection;
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_AuteurBall", 45, ballangle / 2.5, ballangle * xdirection, CMF_OFFSETPITCH, -10);
	}
	A_1_ CD 5 A_FaceTarget();
	Goto See;
  Pain:
	A_1_ F 4;
	A_1_ F 4 A_Pain();
	Goto See;
  Death:
	A_1_ F 10;
	A_2_ A 6 A_Scream();
	A_2_ B 6 A_NoBlocking();
	A_2_ CDEF 5;
	A_2_ G -1;
	Stop;
  }
}

Class DCY_AuteurWave : DCY_CrusaderSlash
{
  Default
  {
  Damage 15;
  Radius 10;
  Height 56;
  Speed 30;
  SeeSound "Auteur/WaveAtk";
  XScale 0.15;
  YScale 9.0;
  Species "Viles";
  MissileType "DCY_AuteurWaveTrail";
  +NEVERFAST
  }
  
  States
  {
  Spawn:
	TRAC A 1
	{
		A_SpawnItemEx("DCY_AuteurFire", frandom(-10, 10), frandom(-10, 10), zvel: -1000, flags: SXF_NOCHECKPOSITION);
		if (timer < 20) A_ScaleVelocity(1.045); else Destroy();
		timer += (timer < 20) ? 1 : 0;
	}
	Loop;
  }
}

Class DCY_AuteurWaveTrail : DCY_CrusaderSlashTrail
{
  Default
  {
  XScale 0.15;
  YScale 9.0;
  Alpha 0.135;
  Translation "0:255=%[0.00,0.00,0.00]:[0.43,0.75,1.58]";
  }
}

Class DCY_AuteurFire : DCY_NapalmFire
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.40]:[0.62,1.20,1.79]";
  Scale 2.0;
  +FLOORHUGGER
  }
  
  static const string ParticleColors[] = { "4ad5ff", "149dff", "0048ff" };
  
  States
  {
  Spawn:
	123E "#" 0 NoDelay { fireloop = random(0, 20); A_StartSound("FIR3LOOP", CHAN_BODY, CHANF_LOOPING, pitch: frandom(1.5, 1.9)); A_SetScale(frandom(1.55, 2.0) * randompick(-1, 1), frandom(1.0, 1.6)); }
	123E "##########" 1 A_FadeIn(0.1);
	123E "#" 1
	{
		if (fireloop > 35)
			SetStateLabel("Gone");
		
		if ((level.time % 3) == 0)
		{
			fireloop++;
			A_Explode(5, 80);
		}
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 35, random(9, 12), frandom(0, 360), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.2, fadestepf: 0, sizestep: -0.45);
	}
	Goto Spawn+11;
  Gone:
	123E "##########" 1 A_FadeOut(0.1);
	Stop;
  }
}

Class DCY_AuteurBall : DCY_ImpFireball
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.40]:[0.62,1.20,1.79]";
  +SEEKERMISSILE
  Speed 15;
  +NEVERFAST;
  }
  
  int timer;
  
  States
  {
  Spawn:
	C3AL A 1
	{
		timer++;
		
		if (timer < 20) A_ScaleVelocity(0.9);
		if (timer > 45 && timer < 70) { A_ScaleVelocity(1.065); if (timer < 48) A_SeekerMissile(999, 999); }
		A_SpawnItemEx("DCY_FluorescentOrbT", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnParticleEx(0x0048ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 45, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.015, sizestep: 0.55);
	}
	Loop;
  Death:
	C3AL A 1
	{
		actor ring = Spawn("DCY_NeonicRing", pos);
		if (ring) ring.scale *= 1.5;
		
		A_SpawnItemEx("DCY_AndroidExplosion", random(-1, 1), random(-1, 1), random(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Explode(60, 50);
		A_FadeOut(0.2, 0);
	}
	C3AL ABAB 1 A_FadeOut(0.2, 0);
	Stop;
  }
}