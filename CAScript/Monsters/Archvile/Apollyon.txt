Class DCY_Apollyon : DCY_VileParent
{
  Default
  {
  Health 843;
  Tag "Apollyon";
  BloodColor "Black";
  Speed 10;
  FastSpeed 14;
  +MISSILEMORE
  +FIRERESIST
  +QUICKTORETALIATE
  +DONTHARMCLASS
  Translation "16:47=%[0.00,0.00,0.00]:[1.80,0.54,0.39]", "168:191=%[0.00,0.00,0.00]:[1.80,0.54,0.39]", "51:79=%[0.00,0.00,0.00]:[0.70,0.70,0.70]", "128:159=%[0.00,0.00,0.00]:[0.70,0.70,0.70]", "48:50=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[0.70,0.70,0.70]";
  DamageFactor "Fire", 0.1;
  DamageFactor "Ice", 1.5;
  SeeSound "Apollyon/Sight";
  ActiveSound "Apollyon/Active";
  PainSound "Apollyon/Pain";
  DeathSound "Apollyon/Death";
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  HitObituary "%o was turned into scarlet dust by an Apollyon.";
  }
  
  States
  {
  Spawn:
	DIAB A 10 A_Look();
	Loop;
  See:
	DIAB AABBCCDDEEFF 2 A_Chase("Pillars", "Missile");
	Loop;
  Pillars:
	DIAB A 0 Bright A_Jump(50, "MissileContinue");
	DIA2 A 0 Bright A_FaceTarget();
	DIA2 ABCABCABC 2 Bright;
	DIA2 A 2 Bright
	{
		A_StartSound("Apollyon/Pillar", CHAN_WEAPON);
		A_SpawnItemEx("DCY_ApollyonPillar", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar2", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar3", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar4", flags: SXF_SETMASTER);
	}
	DIA2 BCABCABCABCABCABC 2 Bright;
	Goto See;
  CometBlast2:
	DIAB K 0 A_Jump(144, "MissileContinue");
	Goto CometBlast;
  CometBlast:
	DIAB K 0 A_StartSound("Apollyon/Spell", CHAN_VOICE);
	DIAB KLKLKLKL 2 Bright A_FaceTarget();
	DIAB LKJ 4 Bright;
	DIAB IIIIII 0 Bright A_SpawnProjectile("DCY_ApollyonCometGravity2", 95, 0, random(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, random(-60, -80));
	DIAB IHGHIHGH 2 Bright A_SpawnProjectile("DCY_ApollyonCometGravity", 95, 0, random(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, random(-10, -70));
	DIAB IHGH 1 Bright;
	DIAB IHGHIHGHIHGHIHGHIHGHI 1 Bright;
	Goto See;
  Missile:
	DIAB A 0 Bright A_JumpIf(target && Distance3D(target) < 200, "Pillars");
	DIAB A 0 Bright A_JumpIf(target && Distance3D(target) < 400, "CometBlast2");
	Goto MissileContinue;
  MissileContinue:
	DIAB A 0 Bright A_Jump(128, "Firewall", "Fireground");
	DIAB A 0 Bright A_Jump(20, "CometBlast");
	DIAB HIJKLMN 3 Bright A_FaceTarget();
	DIAB OOO 0 Bright A_SpawnProjectile("DCY_ApollyonComet", 35, 0, random(-3, 3), CMF_TRACKOWNER|CMF_OFFSETPITCH, random(-3, 3));
	DIAB OP 5 Bright A_FaceTarget();
	TNT1 A 0 Bright A_Jump(80, "AnotherTime");
	Goto See;
  AnotherTime:
	DIAB A 0 Bright A_Jump(75, "Firewall");
    DIAB HIJKLMN 3 Bright A_FaceTarget();
	DIAB OOO 0 Bright A_SpawnProjectile("DCY_ApollyonComet", 35, 0, random(-10, 10), CMF_TRACKOWNER|CMF_OFFSETPITCH, random(-3, 3));
	DIAB OP 5 Bright A_FaceTarget();
	Goto See;
  Firewall:
    DIAB JLMLM 2 Bright A_FaceTarget();
	DIAB N 4 Bright A_FaceTarget();
	DIAB N 1 Bright
	{
		A_StartSound("vile/flamewall", CHAN_WEAPON);
		for (int i = 80; i > -81; i -= 40)
			A_SpawnProjectile("DCY_ArchvileFlamewall", 5, i);
	}
	DIAB NNOOOOOPPP 1 Bright A_FaceTarget();
	Goto See;
  Fireground:
	DIA2 ABCAB 4 Bright A_FaceTarget();
	DIA2 C 0 Bright A_SpawnProjectile("DCY_ApollyonTracer", 0, -40, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER);
    DIA2 C 4 Bright A_SpawnProjectile("DCY_ApollyonTracer", 0, 40, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER);
	DIA2 ABC 4 Bright;
    Goto See;
  Pain:
    DIAB Q 5;
    DIAB Q 5 A_Pain();
    Goto See;
  Death:
    DIAB Q 7;
    DIAB R 7 A_Scream();
    DIAB S 7 A_NoBlocking();
    DIAB TUVW 7;
    DIAB XY 5;
    DIAB Z -1;
    Stop;
  Death.Massacre:
    DIAB Q 7;
    DIAB R 7 A_Scream();
    DIAB S 7 A_NoBlocking();
    DIAB TUVW 7;
    DIAB XY 5;
    DIAB Z -1;
    Stop;
  }
}

Class DCY_ApollyonComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.71,1.01]";
  Radius 12;
  Height 12;
  Speed 15;
  +NEVERFAST
  +EXTREMEDEATH
  DamageType "Fire";
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  Damage 2;
  Scale 0.9;
  RenderStyle "Normal";
  Alpha 1.0;
  Projectile;
  Decal "Scorch";
  }
}

Class DCY_ApollyonCometGravity : DCY_ApollyonComet
{
  Default
  {
  Speed 50;
  }
  
  States
  {
  Spawn:
    CGMT AABBCC 1 Bright
	{
		A_ChangeVelocity(0, 0, -2);
		A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    Loop;
  }
}

Class DCY_ApollyonCometGravity2 : DCY_ApollyonCometGravity { Default { Speed 30; } }

Class DCY_ApollyonPillar : Actor
{
  Default
  {
  Damage 10;
  +MISSILE
  +NOINTERACTION
  RenderStyle "Add";
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  DamageType "Fire";
  }
  
  int cangle;
  int swooce;
  
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
	{
		A_Warp(AAPTR_TARGET, 125, 0, 5, cangle, WARPF_INTERPOLATE);
	}
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (swooce >= 40)
			return resolvestate("Null");
		
		cangle += 10;
		swooce += 1;
		
		for (int i = 9; i > 0; i--)
			A_SpawnItemEx("DCY_ArchvileFlame",random(-10, 10), random(-10, 10), random(2, 40), 0, 0, random(5, 15), 0, SXF_NOCHECKPOSITION, 100);
		
		Return ResolveState(null);
	}
	Loop;
  }
}

Class DCY_ApollyonPillar2 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
	{
		A_Warp(AAPTR_TARGET, -125, 0, 5, cangle, WARPF_INTERPOLATE);
	}
  }
}

Class DCY_ApollyonPillar3 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
	{
		A_Warp(AAPTR_TARGET, 0, 125, 5, cangle, WARPF_INTERPOLATE);
	}
  }
}

Class DCY_ApollyonPillar4 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
	{
		A_Warp(AAPTR_TARGET, 0, -125, 5, cangle, WARPF_INTERPOLATE);
	}
  }
}

Class DCY_ApollyonTracer : Actor
{
  Default
  {
  Radius 5;
  Height 5;
  Speed 30;
  ReactionTime 175;
  Damage 10;
  DamageType "Fire";
  RenderStyle "Add";
  Alpha 0.5;
  Projectile;
  +SEEKERMISSILE
  +FLOORHUGGER
  +NEVERFAST
  +THRUGHOST
  -NOGRAVITY
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  Seesound "Apollyon/Tracer";
  DeathSound "Apollyon/TracerExplode";
  }
  
  int looping;
  
  States
  {
  Spawn:
    TNT1 A 1 Bright
	{
		if (looping >= 175)
			Destroy();
		
		A_SeekerMissile(1, 2, SMF_LOOK);
		looping += 1;
		
		Spawn("DCY_ApollyonFire", pos);
	}
    Loop;
  Death:
	PXPL AAA 0 Bright A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION);
    FTRA K 3 Bright;
    FTRA L 2 Bright A_Explode(64, 64);
    FTRA MNO 2 Bright;
    Stop;
  }
}