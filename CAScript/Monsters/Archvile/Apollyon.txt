Class DCY_Apollyon : DCY_VileParent
{
  Default
  {
  Health 843;
  Tag "Apollyon";
  BloodColor "Black";
  Speed 10;
  FastSpeed 14;
  +MISSILEMORE
  +FIRERESIST
  +QUICKTORETALIATE
  +DONTHARMCLASS
  Translation "0:225=%[0.00,0.00,0.00]:[0.61,0.61,0.61]", "168:191=[116,63,63]:[0,0,0]", "16:47=[116,63,63]:[0,0,0]", "208:235=%[0.18,0.00,0.00]:[2.00,0.99,0.25]", "248:255=%[0.18,0.00,0.00]:[2.00,0.85,0.00]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "0:3=%[0.00,0.00,0.00]:[0.50,0.00,0.00]", "49:49=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "160:167=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  DamageFactor "Fire", 0.1;
  DamageFactor "Ice", 1.5;
  SeeSound "Apollyon/Sight";
  ActiveSound "Apollyon/Active";
  PainSound "Apollyon/Pain";
  DeathSound "Apollyon/Death";
  Obituary "%o got turned into scarlet dust by an Apollyon.";
  HitObituary "%o got turned into scarlet dust by an Apollyon.";
  MeleeThreshold 200;
  DCYMonster.ScientificName "Magus immolationis";
  }
  
  int cooldown;
  bool firebuff;
  
  States
  {
  Spawn:
	A_PB A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	A_PB AABBCCDDEEFF 2 DCY_Chase("Pillars", "Missile");
	Loop;
  Pillars:
	A_PB A 0 A_Jump(50, "MissileContinue");
	A_P2 A 0 A_FaceTarget();
	A_P2 ABCABCABC 2;
	A_P2 A 2
	{
		A_StartSound("Apollyon/Pillar", CHAN_WEAPON);
		A_SpawnItemEx("DCY_ApollyonPillar", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar2", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar3", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_ApollyonPillar4", flags: SXF_SETMASTER);
	}
	A_PB IHGHIHGHIHGHIHGHI 2;
	Goto See;
  CometBlast2:
	A_PB K 0 A_Jump(144, "MissileContinue");
	Goto CometBlast;
  CometBlast:
	A_PB K 0 A_StartSound("Apollyon/Spell", CHAN_VOICE);
	A_PB KLKLKLKL 2 A_FaceTarget();
	A_PB LKJ 4;
	A_PB IIIIII 0 A_SpawnProjectile("DCY_ApollyonCometGravity2", 95, 0, random(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, random(-60, -80));
	A_PB IHGHIHGH 2 A_SpawnProjectile("DCY_ApollyonCometGravity", 95, 0, random(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, random(-10, -70));
	A_PB IHGH 1;
	A_PB IHGHIHGHIHGHIHGHIHGHI 1;
	Goto See;
  Missile:
	A_PB A 0 A_JumpIf(target && Distance3D(target) < 200, "Pillars");
	A_PB A 0 A_JumpIf(target && Distance3D(target) < 400, "CometBlast2");
	Goto MissileContinue;
  MissileContinue:
	A_PB A 0 A_Jump(128, "Firewall", "Fireground");
	A_PB A 0 A_Jump(20, "CometBlast");
	A_PB HIJKLMN 3 A_FaceTarget();
	A_PB O 5 { A_FaceTarget();  A_SpawnProjectile("DCY_ApollyonComet", 35); }
	A_PB P 5 A_FaceTarget();
	TNT1 A 0 A_Jump(80, "AnotherTime");
	Goto See;
  AnotherTime:
	A_PB A 0 A_Jump(75, "Firewall");
    A_PB HIJKLMN 3 A_FaceTarget();
	A_PB O 5 { A_FaceTarget(); A_SpawnProjectile("DCY_ApollyonComet", 35, 0, random(-10, 10), CMF_TRACKOWNER|CMF_OFFSETPITCH, random(-3, 3)); }
	A_PB P 5 A_FaceTarget();
	Goto See;
  Firewall:
    A_PB JLMLM 2 A_FaceTarget();
	A_PB N 4 A_FaceTarget();
	A_PB N 1
	{
		A_StartSound("vile/flamewall", CHAN_WEAPON);
		for (int i = 80; i > -81; i -= 40)
			A_SpawnProjectile("DCY_ArchvileFlamewall", 5, i);
	}
	A_PB NNOOOOOPPP 1 A_FaceTarget();
	Goto See;
  Fireground:
	A_P2 ABCAB 4 A_FaceTarget();
	A_P2 C 0 A_SpawnProjectile("DCY_ApollyonTracer", 0, -40, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER);
    A_P2 C 4 A_SpawnProjectile("DCY_ApollyonTracer", 0, 40, 0, CMF_AIMDIRECTION|CMF_TRACKOWNER);
	A_P2 ABC 4;
    Goto See;
  Pain:
    A_PB Q 5;
    A_PB Q 5 A_Pain();
    Goto See;
  Death:
    A_PB Q 7 DCY_Gibs(false);
    A_PB R 7 A_Scream();
    A_PB S 7 A_NoBlocking();
    A_PB TUVW 7;
    A_PB XY 5;
    A_PB Z -1;
    Stop;
  }
}

Class DCY_ApollyonComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.71,1.01]";
  Radius 12;
  Height 12;
  Speed 15;
  +NEVERFAST
  +EXTREMEDEATH
  DamageType "Fire";
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  Damage 4;
  Scale 0.9;
  RenderStyle "Normal";
  Alpha 1.0;
  Projectile;
  Decal "Scorch";
  }
}

Class DCY_ApollyonCometGravity : DCY_ApollyonComet
{
  Default
  {
  Speed 50;
  }
  
  States
  {
  Spawn:
    CGMT AABBCC 1
	{
		A_ChangeVelocity(0, 0, -2);
		A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    Loop;
  }
}

Class DCY_ApollyonCometGravity2 : DCY_ApollyonCometGravity { Default { Speed 30; } }

Class DCY_ApollyonPillar : Actor
{
  Default
  {
  Damage 3;
  +MISSILE
  +NOINTERACTION
  RenderStyle "Add";
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  DamageType "Fire";
  }
  
  int cangle;
  int swooce;
  
  Mixin DCY_Effects;
  
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
		A_Warp(AAPTR_TARGET, 125, 0, 5, cangle, WARPF_INTERPOLATE);
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (GetAge() >= 40) Destroy();
		cangle += 10;
		if (DCY_EffectDistance(1000))
		{
			for (int i = 9; i > 0; i--)
				A_SpawnParticleEx(0xff5219, TexMan.CheckForTexture("graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 20, 100, angle, frandom(-10, 10), frandom(-10, 10), frandom(2, 40), velz: frandom(5, 15), startalphaf: 1.0, fadestepf: 0.1, sizestep: -5, startroll: frandom(0, 360), rollvel: random(0, 1) ? 2 : -2);
		}
	}
	Loop;
  }
}

Class DCY_ApollyonPillar2 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
		A_Warp(AAPTR_TARGET, -125, 0, 5, cangle, WARPF_INTERPOLATE);
  }
}

Class DCY_ApollyonPillar3 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
		A_Warp(AAPTR_TARGET, 0, 125, 5, cangle, WARPF_INTERPOLATE);
  }
}

Class DCY_ApollyonPillar4 : DCY_ApollyonPillar
{
  override void Tick()
  {
	super.tick();
	
	if (!IsFrozen())
		A_Warp(AAPTR_TARGET, 0, -125, 5, cangle, WARPF_INTERPOLATE);
  }
}

Class DCY_ApollyonTracer : Actor
{
  Default
  {
  Radius 5;
  Height 5;
  Speed 30;
  ReactionTime 175;
  Damage 10;
  DamageType "Fire";
  RenderStyle "Add";
  Alpha 0.5;
  Projectile;
  +SEEKERMISSILE
  +FLOORHUGGER
  +NEVERFAST
  +THRUGHOST
  -NOGRAVITY
  Obituary "%o was turned into scarlet dust by an Apollyon.";
  Seesound "Apollyon/Tracer";
  DeathSound "Apollyon/TracerExplode";
  }
  
  int looping;
  
  States
  {
  Spawn:
    TNT1 A 1
	{
		if (looping >= 175)
			Destroy();
		
		A_SeekerMissile(1, 2, SMF_LOOK);
		looping += 1;
		
		Spawn("DCY_ApollyonFire", pos);
	}
    Loop;
  Death:
	PXPL AAA 0 A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION);
    FTRA K 3;
    FTRA L 2 A_Explode(64, 64);
    FTRA MNO 2;
    Stop;
  }
}