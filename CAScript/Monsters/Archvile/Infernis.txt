Class DCY_Infernis : DCY_VileParent //Yet again a Doom RPG enemy
{
  Default
  {
  Health 700;
  Tag "Infernis";
  Obituary "%o is now a pile of nothingness, thanks Infernis.";
  Scale 1.2;
  Translation "16:47=%[0.00,0.00,0.00]:[0.54,1.13,1.79]", "208:255=%[0.00,0.00,0.00]:[0.54,1.13,1.79]", "168:191=%[0.00,0.00,0.00]:[0.54,1.13,1.79]", "160:167=%[0.00,0.00,0.00]:[0.54,1.13,1.79]", "48:79=%[0.00,0.00,0.00]:[0.54,1.13,1.79]", "112:159=%[0.00,0.00,0.00]:[0.54,1.13,1.79]";
  SeeSound "Infernis/Sight";
  ActiveSound "Infernis/Active";
  PainSound "Infernis/Pain";
  DeathSound "Infernis/Death";
  BloodColor "Blue";
  Speed 22.75;
  +NEVERFAST
  +MISSILEEVENMORE
  +DONTHARMSPECIES
  +DONTHARMCLASS
  }
  
  int directionf;
  int firetimer;
  
  override void tick()
  {
	super.tick();
	
	if (firetimer > 0)
		firetimer--;
  }
  
  States
  {
  Spawn:
    A_1_ A 10 DCY_Look();
	Loop;
  Look:
	#### A 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
    A_1_ A 2 A_Chase("Missile", "Missile", CHF_RESURRECT);
	Loop;
  Missile:
	H7LN G 0 A_Jump(128, "Fireballs");
	H7LN G 0 A_Jump(100, "OriginalVile");
	Goto Flame;
  Flame:
	H7LN G 0
	{
		if (firetimer > 0)
			SetStateLabel("Fireballs");
		
		A_FaceTarget();
	}
	H7LN GGGGHHGGHGHGHGHGGH 1 A_FaceTarget();
	H7LN IJK 8 A_FaceTarget();
	H7LN L 10 { A_SpawnProjectile("DCY_InfernisFire", 5, 0, 0, CMF_TRACKOWNER|CMF_AIMDIRECTION); firetimer = 172; }
	H7LN L 0 A_Jump(25, "FireAgain");
	Goto See;
  FireAgain:
    H7LN IJK 10 A_FaceTarget();
	H7LN L 0 A_SpawnProjectile("DCY_InfernisFire", 5, 0, 0, CMF_TRACKOWNER|CMF_AIMDIRECTION);
	H7LN L 15;
	H7LN L 0 A_Jump(25, "FireAgain");
	Goto See;
  Fireballs:
	H7LN J 3
	{
		A_FaceTarget();
		directionf = randompick(-1, 1);
	}
	H7LN JJIIHHGH 3 A_FaceTarget();
	H7LN H 0 A_SpawnProjectile("DCY_IFlame", 79, -40 * directionf, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
	H7LN HGH 3 A_FaceTarget();
	H7LN H 0 A_SpawnProjectile("DCY_IFlame", 79, 40 * directionf, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
	H7LN HGH 3 A_FaceTarget();
	H7LN H 0 A_SpawnProjectile("DCY_IFlame", 79, -40 * directionf, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
	H7LN HGH 3 A_FaceTarget();
	H7LN H 0 A_SpawnProjectile("DCY_IFlame", 79, 40 * directionf, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
	H7LN HGH 3 A_FaceTarget();
	H7LN H 0 A_SpawnProjectile("DCY_IFlame", 79, -40 * directionf, random(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-2, 2));
	H7LN HGHGHGHGHGHHGGGG 3 A_FaceTarget();
	Goto See;
  OriginalVile:
    H7LN G 0 Bright A_VileStart();
    H7LN GGGHH 1 Bright A_FaceTarget();
    H7LN H 1 Bright A_VileTarget("DCY_InfernisBlaze");
    H7LN GGGGHHHHIIIIIIIJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJ 1 Bright A_FaceTarget();
    H7LN K 9 Bright A_VileAttack();
    H7LN L 20 Bright;
    Goto See;
  Pain:
    H7LN M 3;
	H7LN M 3 A_Pain();
	Goto See;
  Death:
    H7LN N 1;
	H7LN N 7 A_Scream();
	H7LN O 8;
	H7LN P 8 A_NoBlocking();
	H7LN QRS 6;
	H7LN T -1;
	Stop;
  Heal:
    H7LN GHGHGH 4 Bright;
	Goto See;
  }
}

Class DCY_InfernisBlaze : ArchvileFire
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.32,0.61,1.06]";
  }
  
  int fx;
  
  States
  {
  Spawn:
    FIRE A 1 Bright NoDelay
	{
		fx = 200;
		A_StartFire();
	}
	FIRE ABBAABBCCBBCCBBCCDDCCDDCCDDEEDDEEDDEEDDEEFFEEFFEEFFGGHHGGHHGGHH 1 Bright
	{
		A_Fire();
		A_SpawnItemEx("DCY_InfernisFlame5", random(-20, 20), random(-20, 20), random(-2, 20), 0, 0, random(5, 12), 0, SXF_NOCHECKPOSITION, 128);
		
		if (fx == 150 || fx == 10)
			A_FireCrackle();
		
		fx -= 5;
	}
	TNT1 AA 1 A_Fire();
	TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("DCY_InfernisFlame4", random(-15, 15), random(-15, 15), random(8, 36), random(-10, 10), random(-10, 10), random(6, 2), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 100);
    Stop;
  }
}

Class DCY_IFlame : BaronBall
{
  Default
  {
  Speed 18;
  FastSpeed 24;
  SeeSound "InfernoGallant/Ball";
  DeathSound "InfernoGallant/BallExplode";
  +BRIGHT
  +NOEXTREMEDEATH
  Scale 1.35;
  Alpha 0.5;
  Translation "0:255=%[0.00,0.00,0.00]:[0.62,1.24,2.00]";
  }
  
  States
  {
  Spawn:
	BAL1 AABB 1
	{
		A_SpawnItemEx("DCY_InfernisFlame2", random(-3, 3), random(-3, 3), random(-3, 3));
	}
	Loop;
  Death:
    TNT1 A 0 A_SpawnItemEx("DCY_InfernisFlame4", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	BAL1 CCDDEE 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_InfernisFire : DCY_IFlame
{
  Default
  {
  Speed 50;
  FastSpeed 50;
  SeeSound "Apollyon/Pillars";
  DeathSound "";
  Scale 1.5;
  Alpha 0.01;
  +FLOORHUGGER
  +FLOORCLIP
  +NEVERFAST
  +THRUACTORS
  +THRUSPECIES
  +DONTSPLASH
  +THRUGHOST
  }
  
  int fireloop;
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (fireloop >= 125)
			Return ResolveState("Death");
		
		fireloop += random(1, 3);
		A_Explode(3, 75, XF_NOTMISSILE, 1);
		A_SpawnItemEx("DCY_InfernisFlame4Spawner", random(-3, 3), random(-3, 3), 3, flags: SXF_NOCHECKPOSITION);
		Return ResolveState(null);
	}
	Loop;
  Death:
    TNT1 A 1;
	Stop;
  }
}
