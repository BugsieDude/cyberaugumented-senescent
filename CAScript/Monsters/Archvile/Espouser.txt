Class DCY_Espouser : DCY_VileParent
{
  Default
  {
  Health 1111;
  Speed 9;
  FastSpeed 12;
  FloatSpeed 9;
  +MISSILEMORE
  +FLOAT
  +NOGRAVITY
  DamageType "Ghost";
  DamageFactor "Ghost", 0.00;
  Tag "\c[w2]Espouser\c-";
  SeeSound "Espouser/Sight";
  ActiveSound "Espouser/Active";
  PainSound "Espouser/Pain";
  DeathSound "Espouser/Death";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  BloodColor "White";
  }
  
  override void Tick()
  {
	super.tick();
	
	if (!InStateSequence(CurState, ResolveState("Dash")))
		A_SetRenderStyle(1, STYLE_NORMAL);
  }
  
  int dash;
  bool summonedbrats;
  
  States
  {
  Spawn:
	HR1E AC 20 DCY_Look();
	Loop;
  Look:
	#### AABBCCDD 3 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	HR1E AABBCCDD 2
	{
		A_Chase();
		Return A_Jump(5, "Dash");
	}
	Loop;
  Dash:
	HR1E E 10;
	HR2E P 1
	{
		dash = random(0, 10);
		
		if(target)
			A_FaceTarget();
			
		bGhost = true;
		bThruSpecies = true;
		A_StartSound("Espouser/Missile", CHAN_BODY);
		A_SetRenderStyle(1, STYLE_ADD);
		
		for (int i = 15; i > 0; i--)
			A_SpawnItemEx("DCY_Smonk", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25) + 30, frandom(10, -10), frandom(10, -10), frandom(10, -10), frandom(0, 360), SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
	}
	HR2E P 1
	{
		if (dash > 40)
		{
			bGhost = false;
			bThruSpecies = false;
			A_StartSound("Espouser/Missile", CHAN_BODY);
			A_SetRenderStyle(1, STYLE_NORMAL);
			A_Stop();
			
			if(target)
				A_FaceTarget(1.5, 1.5);
			
			for (int i = 15; i > 0; i--)
				A_SpawnItemEx("DCY_Smonk", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25) + 30, frandom(10, -10), frandom(10, -10), frandom(10, -10), frandom(0, 360), SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
			
			SetStateLabel("See");
		}
		
		for (int i = 2; i > 0; i--)
		{
			A_Recoil(-0.5);
			A_SpawnItemEx("DCY_Smonk", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25) + 30, flags: SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
		}
		
		dash++;
	}
	Goto Dash+2;
  Missile:
	HR1E H 0
	{
		if (health < 666)
			Return A_Jump(80, "BigBoyBall", "Souls", "SummonAllies");
		else
			Return A_Jump(80, "BigBoyBall", "Souls");
	}
	HR1E HI 8 A_FaceTarget();
	HR1E J 8
	{
		for (int i = -20; i < 21; i += 10)
			A_SpawnProjectile("DCY_EspouserFire", 30 + (i / 2), 0, i);
		
		A_FaceTarget();
	}
	HR1E F 6;
	Goto See;
  BigBoyBall:
	HR1E KKKKKKKKKKKKKKKKK 1
	{
		for (int i = 30; i > -31; i -= 60)
			A_SpawnItemEx("DCY_Smonk", frandom(-2.0, 2.0), frandom(-2.0, 2.0) + i, frandom(-2.0, 2.0) + 15, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0, 360), SXF_NOCHECKPOSITION);
		A_FaceTarget();
		
	}
	HR1E LMO 4 A_FaceTarget();
	HR1E N 4 A_SpawnProjectile("DCY_EspouserBall", 30);
	HR1E OM 4;
	Goto See;
  Souls:
	HR1E ML 4 A_FaceTarget();
	HR1E KKKKKKKKKKKKKKKKK 3
	{
		tics = random(1, 4);
		
		for (int i = 30; i > -31; i -= 60)
		{
			A_SpawnProjectile("DCY_EspouserShriekingSoul", 15, i, frandom(-12, 12), CMF_OFFSETPITCH, pitch);
			A_SpawnItemEx("DCY_Smonk", frandom(-2.0, 2.0), frandom(-2.0, 2.0) + i, frandom(-2.0, 2.0) + 15, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(0, 360), SXF_NOCHECKPOSITION);
		}
		
		A_FaceTarget();
		
	}
	HR1E L 4 A_FaceTarget();
	Goto See;
  SummonAllies:
	HR1E K 10
	{
		if (summonedbrats)
			SetStateLabel("BigBoyBall");
		
		A_Stop();
		bNoPain = true;
		A_FaceTarget();
	}
	HR1E Z 40
	{
		A_FaceTarget();
		A_StartSound("PENTASUM", CHAN_BODY, attenuation: 0.45);
		A_SpawnItemEx("DCY_ServerPentagram", 0, 0, 1, flags: SXF_NOCHECKPOSITION);
	}
	HR1E KLM 8 A_FaceTarget();
	HR1E M 10
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_MonochromaticExplosion", 0, 0, FloorZ + 30, flags: SXF_NOCHECKPOSITION);
		
		for (int i = 0; i < 360; i += 75)
		{
			let spirit = Spawn("DCY_EspouserServer", pos + (0, 0, floorz + 1));
			if (spirit)
			{
				spirit.A_ChangeVelocity(frandom(20.0, -20.0), frandom(20.0, -20.0), frandom(1, 5));
				spirit.tics *= 0.5;
				spirit.angle = i;
				spirit.master = self;
				spirit.bThruActors = true;
				spirit.bThruSpecies = true;
				
				if (bFriendly == true)
					spirit.bFriendly = true;
			}
		}
		
		summonedbrats = true;
	}
	Goto See;
  Pain:
	HR1E P 4;
	HR1E P 4 A_Pain();
	Goto See;
  Death:
	HR1E P 1;
	HR1E P 1 A_KillChildren("EspouserDeath", KILS_KILLMISSILES|KILS_FOILINVUL, "DCY_EspouserServer", "Ghostly", AAPTR_MASTER);
	HR1E P 7 A_SetFloorClip();
	HR1E Q 6 { bNoGravity = false; A_Fall(); A_Scream(); }
	HR1E RSTUVW 5;
	HR1E X -1;
	Stop;
  }
}

Class DCY_EspouserFire : CacodemonBall
{
  Default
  {
  DamageType "Ghost";
  Scale 0.5;
  Translation "0:255=%[0.06,0.06,0.06]:[1.31,1.31,1.31]";
  Damage 2;
  Speed 15;
  FastSpeed 25;
  SeeSound "Espouser/Melee";
  }
  
  States
  {
  Spawn:
	FHED AABBCCDD 1 A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
	Loop;
  Death:
	RIP1 DCEFGH 3;
	Stop;
  }
}

Class DCY_EspouserSoul : CacodemonBall
{
  Default
  {
  +SEEKERMISSILE
  Radius 7;
  Height 7;
  DamageType "Ghost";
  SeeSound "Espouser/Missile";
  Speed 23;
  FastSpeed 28;
  }
  
  States
  {
  Spawn:
	S0UX L 1
	{
		A_Weave(random(1, -1), random(1, -1), random(1, -1), random(1, -1));
		A_SeekerMissile(0, 1);
		A_SpawnItemEx("DCY_Smonk", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(1, -1), frandom(1, -1), frandom(1, -1), frandom(0, 360), SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	SPRQ ABCDEFG 2;
	Stop;
  }
}

Class DCY_EspouserBall : DCY_EspouserSoul
{
  Default
  {
  Scale 1.5;
  Radius 20;
  Height 20;
  Speed 13;
  FastSpeed 16;
  SeeSound "";
  DeathSound "PAIN001";
  }
  
  bool smissile;
  
  States
  {
  Spawn:
	S0U2 ABCDE 1
	{
		A_SpawnItemEx("DCY_Smonk", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(1, -1), frandom(1, -1), frandom(1, -1), frandom(0, 360), SXF_TRANSFERSCALE|SXF_TRANSFERALPHA|SXF_NOCHECKPOSITION);
		
		smissile = random(0, 1);
		
		if (smissile)
			A_SpawnProjectile("DCY_EspouserSoul", 0, 0, frandom(0, 360), CMF_OFFSETPITCH, frandom(-20, 20));
	}
	Loop;
  Death:
	S0UX A 1
	{
		A_Explode(50, 128);
		
		for (int i = 9; i > 0; i--)
			A_SpawnItemEx("DCY_Smonk", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25) + 30, frandom(10, -10), frandom(10, -10), frandom(10, -10), frandom(0, 360), SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
		
		A_QuakeEx(3, 3, 3, 50, 0, 600, "", QF_SCALEDOWN, rollintensity: 0.5, rollwave: 1);
		
		A_SpawnItemEx("DCY_MonochromaticExplosion", flags: SXF_NOCHECKPOSITION);
	}
	S0UX ABCDEFGHI 3;
	TNT1 A 20;
	Stop;
  }
}

Class DCY_EspouserShriekingSoul : DCY_EspouserSoul
{
  Default
  {
  Speed 18;
  FastSpeed 24;
  SeeSound "DTHSOUL";
  DeathSound "DTHSOULP";
  }
  
  States
  {
  Spawn:
	A33R AAABBB 1
	{
		A_Weave(random(-6, 6), random(-6, 6), random(1, 3), random(1, 3));
		A_ChangeVelocity(0, frandom(-2, 2), frandom(-2, 2), CVF_RELATIVE);
		A_SpawnItemEx("DCY_RocketSmonk", flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  }
}

Class DCY_EspouserServer : DCY_Impe
{
  Default
  {
  Health 100;
  RenderStyle "Add";
  Speed 3;
  +NEVERFAST
  +GHOST
  +NOINFIGHTING
  DamageFactor "Ghost", 0.00;
  Species "Ghostly";
  Scale 1.1;
  Translation "0:255=%[0.04,0.02,0.09]:[1.51,1.51,1.51]";
  Tag "Failed Experiment";
  SeeSound "Manes/Chatter";
  ActiveSound "Manes/Chatter";
  PainSound "Manes/Pain";
  DeathSound "Manes/Death";
  -COUNTKILL
  }
  
  protected bool dief;
  override void Tick()
  {
	super.tick();
	
	if (!dief && (!master || master.health <= 0))
    {
      dief = true;
      SetStateLabel("Death");
	}
	
	if (!level.isFrozen() && health > 0)
		A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 0, frandom(-8, 8), frandom(-8, 8), frandom(0, 8), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
  }
  
  States
  {
  Spawn:
	DHUI A 10 A_Look();
	Loop;
  See:
	DHUI AAABBBCCCDDD 3
	{
		A_Chase();
		Return A_Jump(5, "Teleport");
	}
	Loop;
  Teleport:
	DHUI A 10;
	TNT1 A 10
	{
		for (int i = 360; i > 0; i--)
			A_Chase(null, null, CHF_NOPLAYACTIVE);
	}
	DHUI A 10
	{
		A_FaceTarget();
		Return A_Jump(90, "See");
	}
	Loop;
  Melee:
  Missile:
	DHUI EFGHIJK 3 A_FaceTarget();
	DHUI LLLLLLLL 3
	{
		A_SpawnProjectile("DCY_ServerBall", 30, 0, frandom(-5.0, 5.0));
		A_FaceTarget();
	}
	DHUI M 3;
	Goto See;
  Pain:
	DHUI N 3;
	DHUI N 3 A_Pain();
	Goto See;
  Death:
  XDeath:
	DHUI N 2;
	DHUI N 2 A_ScreamAndUnblock();
	DHUI OPQRSTUVW 3;
	DHUI W -1;
	Stop;
  XDeath.EspouserDeath:
  Death.EspouserDeath:
	DHUI NNNNN 2 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_ServerBall : DCY_ExoBall
{
  Default
  {
  Scale 0.75;
  Speed 20;
  Damage 3;
  DamageType "Ghost";
  SeeSound "Espouser/Melee";
  DeathSound "";
  +NEVERFAST
  -NOTIMEFREEZE
  Translation "0:255=%[0.06,0.06,0.06]:[1.31,1.31,1.31]";
  }
  
  States
  {
  Spawn: 
	BAL7 A 1 Bright
	{
		A_SpawnItemEx("DCY_ViolationBallTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Weave(3, 3, 0.35, 0.35);
	}
	Loop;
  }
}

Class DCY_ServerPentagram : Actor
{
  Default
  {
  Scale 1.2;
  RenderStyle "Add";
  Alpha 0.00;
  +FLATSPRITE
  +NOGRAVITY
  +FLOORHUGGER
  }
  
  override void Tick()
  {
	super.tick();
	A_SetAngle(Angle + 10);
	A_SpawnItemEx("DCY_ServerPentagramTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	
	for (int i = 2; i > 0; i--)
		A_SpawnParticle(0xffffff, SPF_FULLBRIGHT, random(5, 20), random(13, 18), xoff: frandom(-50, 50), yoff: frandom(-50, 50), velz: frandom(0.5, 1.0), accelz: 1);
  }
  
  States
  {
  Spawn:
	JXSI AAAAAAAAAA 1
	{
		A_FadeIn(0.1);
	}
	JXSI A 30;
	JXSI AAAAAAAAAA 1 A_FadeOut(0.1, 0);
	Stop;
  }
}
	
Class DCY_ServerPentagramTrail : DCY_ProjectileBaseTrail { Default { +FLATSPRITE; +NOTIMEFREEZE Alpha 0.24; } }