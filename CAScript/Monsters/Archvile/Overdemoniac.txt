Class DCY_Overdemoniac : DCY_VileParent
{
  Default
  {
  Health 1900;
  BloodType "DCY_ElecDeathBase";
  BloodColor "Red";
  Tag "\c[w8]Overdemoniac\c-";
  SeeSound "Overdemoniac/Sight";
  ActiveSound "Overdemoniac/Active";
  PainSound "Overdemoniac/Pain";
  DeathSound "Overdemoniac/Death";
  Obituary "%o got mindfucked by the Overdemoniac.";
  +DONTHARMSPECIES
  +BOSS
  +DONTHARMCLASS
  +QUICKTORETALIATE
  +MISSILEMORE
  +FLOORCLIP
  +FIRERESIST
  +NOFEAR
  Scale 1.15;
  PainChance 50;
  Speed 7;
  FastSpeed 17;
  DamageFactor "Ice", 0.5;
  DCYMonster.GibType "DCY_GibletsLava";
  DCYMonster.ScientificName "Purissimum dominum argenteum";
  +DCYMONSTER.DCYBOSS;
  DCYMonster.FootStepSound "dcybaron/walk";
  }
  
  int phasecooldown;
  int phasetimer;
  int lasercount;
  
  States
  {
  Spawn:
	DSJ1 A 10 DCY_Look();
	Loop;
  Look:
	#### AAABBBCCCDDDEEEFFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	DSJ1 AAABBBCCCDDDEEEFFF 2
	{
		if (phasecooldown <= 0 && !random(0, 20)) SetStateLabel("Teleportation");
		DCY_Chase(null, "Missile");
		
		if (phasecooldown > 0) phasecooldown--;
	}
	Loop;
  Teleportation:
	DSJ1 GHIJKI 3
	{
		if (phasecooldown > 0) SetStateLabel("See");
		phasetimer = random(15, 35);
	}
	DSJ1 H 0 { if (DCY_EffectDistance(sfxdistance)) Spawn("DCY_WretchedTeleport", pos + (0, 0, 30)); }
	DSJ1 H 1
	{
		A_HideThing();
		bFLOAT = true;
		bNOGRAVITY = true;
		bSHOOTABLE = false;
		
		phasetimer--;
		
		for (int i = 4; i > 0; i--)
		{
			if (target && target.pos.z != self.pos.z) vel.z += frandom(-8, 8);
			vel.y += (target && target.pos.z != self.pos.z) ? frandom(-15, 15) : frandom(-8, 8);
			vel.x += (target && target.pos.z != self.pos.z) ? frandom(-15, 15) : frandom(-8, 8);
			
			if (DCY_EffectDistance(sfxdistance))
				for (int i = 2; i > 0; i--) A_SpawnItemEx("DCY_ArgentPhase", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		}
		
		if (phasetimer <= 0) SetStateLabel("Cool");
	}
	Goto Teleportation+7;
  Cool:
	DSJ1 H 3
	{
		A_UnhideThing();
		A_Stop();
		if (DCY_EffectDistance(sfxdistance)) Spawn("DCY_WretchedTeleport", pos + (0, 0, 30));
		bSHOOTABLE = true;
		bFLOAT = false;
		bNOGRAVITY = false;
		phasecooldown = 100;
	}
	DSJ1 IJKIJ 3;
	Goto See;
  Missile:
	DSJ1 G 0;
	DSJ1 G 0 A_Jump(128, "DeadlyRailgun", "BigLazer", "Archvile");
	Goto Balls;
  Archvile:
	DSJ1 A 20 
	{
		A_FaceTarget();
		A_StartSound(ActiveSound, CHAN_VOICE);
	}
	DSJ1 GHIJKLMO 3 A_FaceTarget();
	DSJ1 O 10 A_VileTarget("DCY_OverdemoniacVile");
	DSJ1 POPOPOPOPP 5 { A_FaceTarget(); }
	Goto BigLazer+6;
  Balls:
	DSJ1 GHI 3 A_FaceTarget();
	DSJ1 IJKJIHJHIJKHJKIJHJIKHKIJIJIHKJK 2
	{
		A_StartSound("ALASER", CHAN_WEAPON, attenuation: 0.5);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 35, 0, Angle, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 55, 0, Angle-180, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 25, 0, Angle+180, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 45, 0, Angle-90, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SetAngle(Angle+random(40, 60));
	}
	DSJ1 A 20
	{
		A_FaceTarget();
		Return A_Jump(20, "DeadlyRailgun");
	}
	Goto See;
  DeadlyRailgun:
	DSJ1 G 10 A_FaceTarget();
	DSJ1 H 3;
	DSJ1 IJKLMO 4 A_FaceTarget();
	DSJ1 O 10
	{
		A_StartSound("THNDSHCK", CHAN_WEAPON, attenuation: 0.35);
		A_CustomRailgun(200, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, 2, 0, "DCY_OverdemoniacPuff2", 0, 0, 0, 0, 50, 0, "DCY_OverdemoniacLaser2", 24);
	}
	DSJ1 P 10;
	DSJ1 A 10 A_Jump(30, "Balls", "BigLazer");
	Goto See;
  BigLazer:
	DSJ1 IJKLMO 4 { lasercount = 0; A_FaceTarget(); }
	DSJ1 O 10
	{
		if (lasercount > 2)
		{
			frame = 0;
			tics = 10;
			SetStateLabel("See");
			return A_Jump(30, "Balls", "DeadlyRailgun");
		}
		else
		{
			A_StartSound("BLASER", CHAN_WEAPON, attenuation: 0.35);
			A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, 0);
		}
		
		return ResolveState(null);
	}
	DSJ1 P 10 { lasercount++; A_FaceTarget(); }
	Goto BigLazer+6;
  Pain:
	DSJ1 Q 4;
	DSJ1 Q 4
	{
		if (!random(0, 5)) SetStateLabel("Teleportation");
		A_Pain();
	}
	Goto See;
  Death:
	DSJ1 Q 2;
	DSJ1 R 20 A_Pain();
	DSJ1 S 6
	{
		for (int i = 2; i > 0; i--) { DCY_Gibs(false); A_SpawnItemEx("DCY_WretchedExplosion", zofs: 30); }
		A_ScreamAndUnblock();
	}
	DSJ1 TUVWXYZ 5;
	DSJ1 Z -1;
	Stop;
  }
}

Class DCY_OverdemoniacOrb : CacodemonBall
{
  Mixin DCY_Effects;
  
  Default
  {
  Damage 5;
  Speed 22;
  FastSpeed 28;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  SeeSound "ALASER";
  DeathSound "ALASERX";
  +BRIGHT
  }
  
  States
  {
  Spawn:
	ETV6 AB 1 { if (DCY_EffectDistance(1000)) { actor l = spawn("DCY_ElecTrailBase", pos + (frandom(-5, 5), frandom(-5, 5), frandom(-5, 5))); if (l) { l.translation = translation; l.alpha *= 0.45; } } }
	Loop;
  Death:
	TNT1 A 10
	{
		if (DCY_EffectDistance(1500)) for (int i = 2; i > 0; i--)  A_SpawnItemEx("DCY_ElecDeathBase", xvel: frandom(-2, 2), yvel: frandom(-2, 2), zvel: frandom(-2, 2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_MonochromaticExplosionSmaller", flags: SXF_TRANSFERTRANSLATION);
	}
	Stop;
  }
}

Class DCY_OverdemoniacVile : ArchvileFire
{
  Default
  {
  Alpha 0;
  Obituary "Thy prayed for death.";
  Translation "0:255=%[0.25,0.00,0.00]:[2.00,0.17,0.17]";
  +DONTSPLASH
  +FLOORHUGGER
  Scale 3.0;
  }
  
  static const string ParticleColors[] = { "ff0000", "ff2222", "ff1111" };
  
  override void Tick()
  {
	if (level.isFrozen()) return;
	timer++;
	
	if (timer > 44)
	{
		for (int i = random(10, 20); i > 0; i--)
				A_SpawnProjectile("DCY_OverdemoniacBall", frandom(-30, 30), frandom(-30, 30), frandom(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, frandom(-10, -80));
		
		A_SpawnItemEx("DCY_WretchedExplosion", flags: SXF_NOCHECKPOSITION);
		A_Explode(200, 150);
		A_StopSound(10);
		if (tracer) tracer.A_StopSound(10);
		for (int i = 2; i > 0; i--) A_StartSound("Intone/Explode", 11, CHANF_OVERLAP, 3.0, pitch: 2.54);
		Destroy();
	}
	
	let beam = Spawn("DCY_AuteurBeam", pos).translation = translation;
	if (timer > 22) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	A_SpawnParticleEx(0xff0000, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 287, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(30, 60), startalphaf: 0.35, fadestepf: 0.0035, sizestep: -0.5);
	for (int i = 1; i > -2; i -= 2)
	{
		A_SpawnItemEx("DCY_VLine", frandom(-90, 90), frandom(-90, 90), (i) ? 0 : CeilingZ, zvel: random(40, 100) * ((i) ? 1 : -1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE, failchance: 30);
		A_SpawnParticleEx(0xff0000, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), random(87, 207), 0, zoff: (i == -1) ? CeilingZ : 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(20, 60) * ((i == -1) ? -1 : 1), startalphaf: 0.35, fadestepf: 0.0035, sizestep: -0.5);
	}
	if (timer == 1) A_Fire();
  }
  
  override void PostBeginPlay()
  {
	A_StartSound("Auteur/VileAtk");
	for (int i = 4; i > 0; i--)
		A_StartSound("Supremacy/IdleNoise", 10, CHANF_OVERLAP, 3.0, pitch: frandom(0.2, 2.6));
	if (tracer) tracer.A_StartSound("Supremacy/IdleNoise", 10, CHANF_OVERLAP, 3.0, pitch: frandom(0.2, 2.6));
  }
  
  int timer;
  
  States
  {
  Spawn:
    TNT1 A -1;
	Stop;
  }
}

Class DCY_OverdemoniacBall : DCY_WretchedBall { Default { -SEEKERMISSILE -NOGRAVITY Damage 2; Speed 20; } }