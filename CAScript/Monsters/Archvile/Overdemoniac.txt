Class DCY_Overdemoniac : DCY_VileParent
{
  Default
  {
  Health 1900;
  Translation "168:191=%[0.24,0.24,0.24]:[2.00,2.00,2.00]", "208:231=%[0.00,0.00,0.00]:[1.01,1.01,1.01]", "236:239=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "16:47=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "1:2=%[0.00,0.00,0.00]:[0.69,0.69,0.69]", "232:235=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  BloodType "DCY_SpiritBlood";
  BloodColor "Gray";
  Tag "\c[w2]Overdemoniac\c-";
  SeeSound "Overdemoniac/Sight";
  ActiveSound "Overdemoniac/Active";
  PainSound "Overdemoniac/Pain";
  DeathSound "Overdemoniac/Death";
  Obituary "%o got mindfucked by the Overdemoniac.";
  +DONTHARMSPECIES
  +BOSS
  +DONTHARMCLASS
  +QUICKTORETALIATE
  +MISSILEMORE
  +FLOORCLIP
  +FIRERESIST
  +NOFEAR
  Scale 1.15;
  PainChance 50;
  Speed 7;
  FastSpeed 17;
  DamageFactor "Ice", 0.5;
  }
  
  States
  {
  Spawn:
	DSJ1 A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	DSJ1 AAABBBCCCDDDEEEFFF 2 A_Chase(null, "Missile");
	Loop;
  Missile:
	DSJ1 G 0;
	DSJ1 G 0 A_Jump(128, "DeadlyRailgun", "BigLazer");
	Goto Balls;
  Balls:
	DSJ1 GHI 3 A_FaceTarget();
	DSJ1 IJKJIHGHIJKHIGIGHJIGHGIJIGIHGJG 2
	{
		A_StartSound("ALASER", CHAN_WEAPON, attenuation: 0.5);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 35, 0, Angle, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 55, 0, Angle-180, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 25, 0, Angle+180, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SpawnProjectile("DCY_OverdemoniacOrb", 45, 0, Angle-90, CMF_AIMDIRECTION|CMF_SAVEPITCH);
		A_SetAngle(Angle+random(40, 60));
	}
	DSJ1 A 20
	{
		A_FaceTarget();
		Return A_Jump(20, "DeadlyRailgun");
	}
	Goto See;
  DeadlyRailgun:
	DSJ1 G 10 A_FaceTarget();
	DSJ1 H 3;
	DSJ1 IJKLMO 4 A_FaceTarget();
	DSJ1 O 10
	{
		A_StartSound("THNDSHCK", CHAN_WEAPON, attenuation: 0.35);
		A_CustomRailgun(200, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, 2, 0, "DCY_OverdemoniacPuff2", 0, 0, 0, 0, 50, 0, "DCY_OverdemoniacLaser2", 24);
	}
	DSJ1 P 10;
	DSJ1 A 10 A_Jump(30, "Balls", "Big Lazer");
	Goto See;
  BigLazer:
	DSJ1 IJKLMO 4 A_FaceTarget();
	DSJ1 O 10
	{
		A_StartSound("BLASER", CHAN_WEAPON, attenuation: 0.35);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, 0);
	}
	DSJ1 P 10 A_FaceTarget();
	DSJ1 O 10
	{
		A_StartSound("BLASER", CHAN_WEAPON, attenuation: 0.35);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, -8);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, 8);
	}
	DSJ1 P 10 A_FaceTarget();
	DSJ1 O 10
	{
		A_StartSound("BLASER", CHAN_WEAPON, attenuation: 0.35);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, 0);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, -13);
		A_SpawnProjectile("DCY_OverdemoniacBeam", 65, 0, 13);
	}
	DSJ1 P 10 A_FaceTarget();
	DSJ1 A 10 A_Jump(30, "Balls", "DeadlyRailgun");
	Goto See;
  Pain:
	DSJ1 Q 4;
	DSJ1 Q 4 A_Pain();
	Goto See;
  Death:
	DSJ1 Q 2;
	DSJ1 R 20 A_Pain();
	DSJ1 S 1 A_ScreamAndUnblock();
	DSJ1 STUVWXYZ 5;
	DSJ1 Z -1;
	Stop;
  }
}

Class DCY_OverdemoniacOrb : CacodemonBall
{
  Default
  {
  Damage 5;
  Speed 22;
  FastSpeed 28;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  SeeSound "ALASER";
  DeathSound "ALASERX";
  +BRIGHT
  }
  
  States
  {
  Spawn:
	ETV6 AB 1;
	Loop;
  Death:
	ETV6 A 0 A_SpawnItemEx("DCY_MonochromaticExplosionSmaller");
	TNT1 A 10;
	Stop;
  }
}