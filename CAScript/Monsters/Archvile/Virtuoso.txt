Class DCY_FulminationVirtuoso : DCY_VileParent
{
  Default
  {
  Health 1150;
  Translation "0:255=%[0.00,0.00,0.00]:[1.39,1.50,1.72]", "128:159=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "13:15=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "1:2=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "236:239=%[0.00,0.00,0.00]:[0.35,0.35,0.35]";
  +FLOAT
  +NOGRAVITY
  GibHealth -1000;
  Speed 8;
  FastSpeed 8;
  Scale 0.945;
  BloodType "DCY_ElecDeathBase", "DCY_ElecDeathBase", "DCY_ElecDeathBase";
  BloodColor "56618f";
  Tag "\c[e3]Fulmination Virtuoso\c-";
  Obituary "%o has been decimated by a Fulmination Virtuoso.";
  SeeSound "virtuoso/sight";
  PainSound "virtuoso/pain";
  DeathSound "virtuoso/death";
  ActiveSound "virtuoso/active";
  DCYMonster.ScientificName "Iratus tonitrui";
  DCYMonster.FootStepSound "";
  }
  
  override void Tick()
  {
	super.Tick();
	if (health > 0 && !level.isFrozen() && DCY_EffectDistance(900))
	{
		if (random(0, 3) == 0) A_SpawnParticleEx(0xd4dbff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 100, frandom(160, 195), 0, frandom(-40.0, 40.0), frandom(-40.0, 40.0), frandom(10, 50), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(1.0, 3.0), startalphaf: 0.1, fadestepf: 0.008, sizestep: 0.825);
		if (random(0, 20) == 0) A_SpawnItemEx("DCY_SiriusArc", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20) + 30, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	}
  }
  
  States
  {
  Spawn:
	ALTR A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 3 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	ALTR AABBCCDDEEFF 3 A_Chase();
	Loop;
  Heal:
	ALRR ABCABC 2;
	Goto See;
  Missile:
	ALRR A 0 A_Jump(111, "Balls");
	ALRR ABC 2 A_FaceTarget();
	ALRR A 2
	{
		A_SpawnItemEx("DCY_VirtuosoBall", 0, -68, 40, 0, 0, 0, 0);
		A_SpawnItemEx("DCY_VirtuosoBall", 0, 68, 40, 0, 0, 0, 0);
		A_FaceTarget();
	}
	ALRR BCABCABCABCABCABC 2 A_SetTics(random(1, 4));
	ALRR C 2 A_SpawnItemEx("DCY_VirtuosoRing", zofs: 30, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	ALRR CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 0 A_Wander();
	ALRR C 2
	{
		A_StartSound("BALL002", CHAN_BODY, attenuation: 1.25);
		A_SpawnItemEx("DCY_VirtuosoRing", zofs: 30, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Goto See;
  Balls:
	ALTR LMKJI 4 A_FaceTarget();
	ALTR H 4
	{
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23);
		A_FaceTarget();
	}
	ALTR IG 4 A_FaceTarget();
	ALTR H 4
	{
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23, -20);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23, 20);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23, -20);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23, 20);
		A_FaceTarget();
	}
	ALTR IG 4 A_FaceTarget();
	ALTR H 4
	{
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23, -30);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23, 30);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, -23);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23, -30);
		A_SpawnProjectile("DCY_VirtuosoProjectile", 80, 23, 30);
		A_FaceTarget();
	}
	ALTR IG 4 A_FaceTarget();
	Goto See;
  Pain:
	ALTR Q 4;
	ALTR Q 4 A_Pain();
	Goto See;
  Death:
  Death.Massacre:
	ALTR R 1;
	ALTR R 7
	{
		bNoGravity = false;
		A_Scream();
	}
	ALTR STUVWXYZ 6;
	ALTR Z -1;
	Stop;
  XDeath:
	ALTR Q 1;
	ALTR Q 40
	{
		bNoGravity = true;
		bDontFall = true;
		A_Scream();
		A_Stop();
	}
	ALTR A 25 A_FaceTarget();
	ALTR LMKJ 4;
	ALTR I 4 A_StartSound("virtuoso/sight", CHAN_VOICE, attenuation: 0.4);
	ALTR HGHIHGIHGI 3;
	ALTR R 7
	{
		A_NoBlocking();
		A_SpawnItemEx("DCY_BigAssLightning", zofs: 5, flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);
		A_Hop(20);
		bNoGravity = false;
		bDontFall = false;
	}
	ALTR STUVWXYZ 6;
	ALTR Z -1;
	Stop;
  }
}

Class DCY_VirtuosoRing : DCY_SpiritRing { Default { Scale 0.5; } }

Class DCY_VirtuosoProjectile : CacodemonBall
{
  Default
  {
  Speed 24.5;
  FastSpeed 28.5;
  Translation "0:255=%[0.00,0.00,0.00]:[1.39,1.50,1.72]";
  Damage (5);
  Alpha 1.0;
  Scale 1.25;
  Radius 15;
  Height 15;
  RenderStyle "Add";
  SeeSound "PLSORB2";
  BounceSound "PLSORB2";
  DeathSound "PLSBFIRE";
  +BOUNCEONWALLS
  +BRIGHT
  +BOUNCEONCEILINGS
  +BOUNCEONFLOORS
  +CANBOUNCEWATER
  BounceType "Doom";
  BounceCount 3;
  }
  
  States
  {
  Spawn:
	B1L9 AAAAABBBBBCCCCC 1 A_Weave(2, 0, 3, 0);
	Loop;
  Death:
	TSAR A 1
	{
		A_SetRoll(frandom(0, 360));
		A_SpawnItemEx("DCY_Explosion_1_Smaller", flags: SXF_TRANSFERTRANSLATION);
		
		for (int i = 4; i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	}
	TSAR EFGHIJKLMNO 1
	{
		A_Explode(4, 100, damagetype: "lightning");
		A_SpawnItemEx("DCY_FallingSpark", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-8, 8), frandom(-8, 8), frandom(-8, 16), frandom(0, 360), SXF_NOCHECKPOSITION, 200);
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	TNT1 AAAAAAAAA 3
	{
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	Stop;
  }
}

Class DCY_VirtuosoBall : LostSoul
{
  Default
  {
  Health 0x7FFFFFFF;
  Radius 6;
  Height 6;
  SeeSound "";
  ActiveSound "";
  PainSound "";
  DeathSound "";
  AttackSound "";
  Scale 0.5;
  PainChance 0;
  Speed 0;
  FastSpeed 0;
  FloatSpeed 0;
  VSpeed 0;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[1.39,1.50,1.72]";
  Alpha 1.0;
  +PAINLESS
  +NOPAIN
  +NOGRAVITY
  +DROPOFF
  +FLOAT
  +NEVERRESPAWN
  +NOBLOOD
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  +THRUSPECIES
  +THRUGHOST
  -SOLID
  +NOINFIGHTING
  +BRIGHT
  +ROLLSPRITE
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +FLOATBOB
  +THRUACTORS
  +DONTHARMCLASS
  FloatBobPhase 10;
  FloatBobStrength 2;
  DamageFactor 0;
  Tag "\c[l7]Virtuoso Vessel\c-";
  Obituary "bruh";
  HitObituary "bruh";
  }
  
  override void Tick()
  {
    super.tick();
	
	if (!isFrozen())
	{
		A_SetRoll(Roll+random(-360, 360), SPF_INTERPOLATE);
		A_SpawnItemEx("DCY_ProjectileBaseTrail", 0, 0, 0, frandom(-1.3, 1.3), frandom(-1.3, 1.3), frandom(-1.3, 1.3), frandom(-360.0, 360.0), SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_TRANSFERROLL|SXF_TRANSFERSCALE);
	}
  }
  
  States
  {
  Spawn:
	LFX2 K 0;
	LFX2 K 0 A_StartSound("BALL001", CHAN_BODY, attenuation: 0.5);
	LFX2 KMNLOLKOLMNLOKMNLOLKOLMNLO 1;
	LFX2 KMNLOLKOLMNLO 1 A_Chase(null, "Missile", CHF_DONTMOVE);
	Goto Spawn+20;
  Missile:
  Pain:
  Death:
	LFX2 KMNLOLKOLMNLOKMNLOLKK 1;
	LFX2 K 1 A_StartSound("B3LLCHRG", CHAN_BODY, attenuation: 0.75);
	LFX2 KMNLOLKOLMNLOKMNLOLKKMNLOLKOLMNLOKMNLKMNLOLKOLMNLOKMNLOLK 1
	{
		A_FaceTarget();
		A_SpawnItemEx("DCY_SiriusArc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	LFX2 K 1
	{
		//A_StartSound("EXPLD001", CHAN_BODY, CHANF_OVERLAP, 0.5, 0.4);
		A_StartSound("B3LLFIRE", CHAN_BODY, CHANF_OVERLAP, attenuation: 0.4);
		A_CustomRailgun(100, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, 1, frandom(4, 1), "DCY_LightningExplode", frandom(-3.0, 3.0), frandom(-3.0, 3.0), 0, 0, 90, 0, "DCY_VirtuosoTrail");
		A_QuakeEx(4, 4, 4, 40, 0, 200, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 800);
		
		for (int i = 4; i > 0; i--)
		{
			A_SpawnItemEx("DCY_BigSpark", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(0, 360), SXF_NOCHECKPOSITION, 100);
			A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		}
	}
	Stop;
  }
}