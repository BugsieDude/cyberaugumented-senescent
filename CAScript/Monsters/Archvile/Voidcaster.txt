Class DCY_VoidCaster : DCY_VileParent
{
  Default
  {
  Health 1400;
  Tag "\c[Blue]Voidcaster\c-";
  Translation "178:191=[0,0,255]:[0,0,0]", "236:239=[0,0,0]:[0,0,0]", "224:231=[255,255,255]:[45,45,45]", "1:2=[0,0,0]:[0,0,0]", "160:167=[133,133,133]:[0,0,0]", "16:31=[170,170,255]:[0,0,255]", "32:47=[0,0,255]:[0,0,0]", "168:177=[255,255,255]:[0,0,255]", "48:79=[128,128,128]:[0,0,0]", "13:15=[30,30,30]:[0,0,0]", "128:143=[128,128,128]:[0,0,0]", "144:151=[68,68,68]:[0,0,0]", "208:223=[128,128,128]:[0,0,0]";
  BloodColor "Blue";
  BloodType "AndroidNeonFluid";
  Speed 14;
  FastSpeed 18;
  SeeSound "Voidcaster/Sight";
  ActiveSound "Voidcaster/Active";
  PainSound "Voidcaster/Pain";
  DeathSound "Voidcaster/Death";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  DamageFactor "V", 0.00;
  +MISSILEMORE
  +MISSILEEVENMORE
  +NODROPOFF
  StencilColor "Blue";
  Obituary "%o was extirpated by a Voidcaster.";
  DCYMonster.ScientificName "Beatus inanis";
  }
  
  int teleloop;
  
  States
  {
  Spawn:
    EX1E A 12 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
    EX1E A 0 A_StartSound("Voidcaster/Sight", CHAN_VOICE, attenuation: 0.25);
	Goto See;
  See:
    EX1E AABBCCDDEEFF 2
	{
		DCY_Chase(null, "Missile", CHF_RESURRECT);
		Return A_Jump(25, "TeleportStart");
	}
	Loop;
  Heal:
	EX12 A 0 A_RadiusGive("DCY_MassResurrection", 150, RGF_KILLED);
	EX12 ABCABC 4;
	Goto See;
  TeleportFlee:
    TNT1 A 0 { bFrightened = true; }
	Goto TeleportStart;
  TeleportStart:
    EX12 A 3 Bright
	{
		bThruSpecies = true;
		bShootable = false;
		teleloop = 0;
	}
	EX12 BC 3;
	TNT1 A 0 A_StartSound("Voidcaster/Teleport", CHAN_7);
	Goto TeleportLoop;
  TeleportLoop:
	NULL A 1
	{
		if (teleloop >= 35)
			Return ResolveState("TeleportEnd");
		
		DCY_Chase(null, null, CHF_NIGHTMAREFAST|CHF_NOPLAYACTIVE);
		DCY_Chase(null, null, CHF_NIGHTMAREFAST|CHF_NOPLAYACTIVE);
		DCY_Chase(null, null, CHF_NOPLAYACTIVE);
		
		for (int i = 12; i > 0; i--)
			A_SpawnItemEx("DCY_VTele", frandom(-18, 18), frandom(-18, 18), frandom(0, 75), 0, 0, 0, Angle, SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
		
		teleloop += 1;
		Return ResolveState(null);
	}
	Loop;
  TeleportEnd:
	EX12 AB 3
	{
		bFrightened = false;
		A_StartSound("Voidcaster/Teleport", CHAN_7);
		bThruSpecies = false;
		bShootable = true;
	}
    EX12 A 3 Bright;
	Goto SeeNoTele;
  SeeNoTele:
    EX1E AABBCCDDEEFF 3 DCY_Chase(null, "Missile", CHF_RESURRECT);
	Goto See;
  Missile:
	EX1E G 0 A_JumpIf(target && Distance3D(target) < 256, "PushPlayerAway");
	EX1E G 0 A_Jump(128, "Balls");
	EX1E GGHHIIHHGGHH 2 Bright A_FaceTarget();
	EX1E II 45 Bright
	{
		A_FaceTarget();
		A_SetTics(random(15, 45));
		A_VileTarget("DCY_VoidcasterAbyss");
	}
	EX1E G 0 A_Jump(75, "Balls");
	EX1E II 45 Bright
	{
		A_FaceTarget();
		A_SetTics(random(15, 45));
		A_VileTarget("DCY_VoidcasterAbyss");
	}
	EX1E G 0 A_Jump(90, "Balls");
	Goto See;
  Balls:
	EX12 ABCABCABCABC 3 Bright A_FaceTarget();
	EX12 AABBCCA 6 Bright
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_VoidcasterSpherule", 30, -45, frandom(-30, 30));
		A_SpawnProjectile("DCY_VoidcasterSpherule", 30, 45, frandom(-30, 30));
	}
	Goto Missile;
  PushPlayerAway:
	EX1E GHIJKLM 5 Bright A_FaceTarget();
	EX1E N 5 Bright
	{
		A_RadiusThrust(12550, 800, RTF_NOTMISSILE);
		A_StartSound("ABDUCT", CHAN_BODY, attenuation: ATTN_IDLE);
		A_StartSound("CASTAB", CHAN_BODY, attenuation: ATTN_IDLE);
		A_SpawnItemEx("DCY_VoidcasterVortex2", 0, 0, 45, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		A_SpawnItemEx("DCY_VoidcasterVortex2", 0, 0, 45, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		A_SpawnItemEx("DCY_VoidcasterVortex2", 0, 0, 45, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	EX1E NNNOOOOOOOPPPP 1 Bright
	{
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 130);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 240);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 95);
	}
	Goto See;
  Pain:
	EX1E Q 3;
	EX1E Q 3 A_Pain();
	EX1E Q 1 A_Jump(45, "TeleportFlee");
	Goto See;
  Death:
	EX1E Q 1;
	EX1E Q 10
	{
		bNoGravity = true;
		A_ChangeVelocity(0, 0, 5);
		A_SetRenderStyle(1.0, STYLE_ADDSTENCIL);
		A_Scream();
	}
	EX1E Q 1 A_StartSound("PUSLD", CHAN_BODY, attenuation: 0.45);
	EX1E QQQQQQQQQQQQQQQQQQQQQQQQQQQ 1
	{
		A_SetAngle(Angle + 20);
		A_SpawnItemEx("DCY_VoidcasterVortex2", 0, 0, 45, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERALPHA);
		A_SpawnItemEx("DCY_VoidcasterVortex2", 0, 0, 45, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERALPHA);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, 130);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, 240);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, 95);
		A_FadeOut(0.01, 0);
	}
	Stop;
  Death.Massacre:
	EX1E Q 1;
	EX1E Q 10 A_Scream();
	Stop;
  }
}

Class DCY_VLine : DCY_Effect
{
  Default
  {
  RenderStyle "Add";
  Alpha 0.25;
  Radius 0;
  Height 0;
  +NOINTERACTION
  -SOLID
  +NOCLIP
  +BRIGHT
  Scale 2.0;
  VSpeed 1.75;
  }
  
  States
  {
  Spawn:
	VCSL AAAAAA 1
	{
		A_ChangeVelocity(0, 0, 1);
		A_FadeIn(0.15, 0);
	}
	VCSL AAAAAAAAAAA 1
	{
		A_FadeOut(0.1, 0);
	}
	Stop;
  }
}

Class DCY_VoidcasterVortex : DCY_VLine
{
  Default
  {
  VSpeed 0;
  Alpha 0.1;
  Scale 1.25;
  +FLATSPRITE
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,0.00,2.00]";
  }
  
  States
  {
  Spawn:
	VOIP AAAAAAAAAAA 1
	{
		A_SetAngle(Angle + 15, SPF_INTERPOLATE);
		A_FadeIn(0.1, 0);
	}
	VOIP AAAAAAAAAAA 1
	{
		A_SetAngle(Angle + 15, SPF_INTERPOLATE);
		A_FadeOut(0.1, 0);
	}
	Stop;
  }
}

Class DCY_VoidcasterVortex2 : DCY_VoidcasterVortex
{
  Default
  {
  -FLATSPRITE
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	VOIP A 0 NoDelay A_SetRoll(random(0, 360));
	VOIP AAA 1
	{
		A_SetRoll(Roll + 15, SPF_INTERPOLATE);
		A_FadeIn(0.3, 0);
	}
	VOIP AAAAAAAAAAA 1
	{
		A_SetRoll(Roll + 15, SPF_INTERPOLATE);
		A_FadeOut(0.1, 0);
	}
	Stop;
  }
}

Class DCY_VoidcasterVoid : DCY_VoidcasterVortex2
{
  Default
  {
  Alpha 0;
  Radius 0;
  Height 0;
  DamageType "V";
  +NOGRAVITY
  -NOINTERACTION
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_ChangeVelocity(0, 0, 15);
		A_SpawnItemEx("DCY_VoidcasterVortex2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	TNT1 AAAAAAAAAAAAAAA 1
	{
		A_ChangeVelocity(0, 0, -1);
		A_SpawnItemEx("DCY_VoidcasterVortex2", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	TNT1 A 1
	{
		A_SpawnItemEx("DCY_Explosion_1_Biggie", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Biggie", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		A_Explode(150, 256, XF_NOTMISSILE, 0);
		A_StartSound("PUSTF", CHAN_BODY, attenuation: 0.5);
	}
	Stop;
  }
}

Class DCY_VTele : DCY_VLine
{
  Default
  {
  Alpha 0.5;
  +ROLLSPRITE
  +FLATSPRITE
  XScale 12.5;
  YScale 14.85;
  VSpeed 0;
  }
  
  States
  {
  Spawn:
	VCSL AAAA 1
	{
		A_ChangeVelocity(0, 0, 1);
		A_SetAngle(Angle + 23, SPF_INTERPOLATE);
		A_FadeIn(0.15, 0);
	}
	VCSL AAAAAAAAAAA 1
	{
		A_SetAngle(Angle + 23, SPF_INTERPOLATE);
		A_FadeOut(0.1, 0);
	}
	Stop;
  }
}

Class DCY_VoidcasterAbyss : ArchvileFire
{
  Default
  {
  +BRIGHT
  +THRUACTORS
  +FORCEYBILLBOARD
  +FLOORHUGGER
  Alpha 0.00;
  }
  
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 1
	{
		A_Fire();
		A_StartSound("PORTALO", CHAN_BODY, attenuation: 0.75);
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		A_SpawnItemEx("DCY_VoidcasterVortex", random(-1, 1), random(-1, 1), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 10);
		A_SpawnItemEx("DCY_VoidcasterVortex", random(-1, 1), random(-1, 1), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 220);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 130);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 10);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 60);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 240);
		A_SpawnItemEx("DCY_VLine", random(-25, 25), random(-25, 25), 1, 0, 0, 0, frandom(0, 360), SXF_NOCHECKPOSITION, 95);
	}
	TNT1 A 1 A_SpawnItemEx("DCY_VoidcasterVoid", 0, 0, 10, 0, 0, 15, 0, SXF_NOCHECKPOSITION);
	Stop;
  }
}

Class DCY_VoidcasterSpherule : DCY_MeatballElecball
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.29,0.43,1.72]";
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.2;
  Speed 22;
  FastSpeed 33;
  BounceFactor 1.0;
  BounceCount 4;
  Damage 1;
  +BOUNCEONWALLS;
  +RIPPER;
  +BRIGHT;
  SeeSound "ABDUCT";
  DeathSound "CASTAB";
  BounceSound "STARPRE";
  }
  
  States
  {
  Spawn:
	P_FX AB 1 {
		if (DCY_EffectDistance(900)) A_SpawnParticleEx(0x1d2399, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 45, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.025, sizestep: 0.55);
	}
	Loop;
  Death:
	P_FX A 1
	{
		A_SpawnItemEx("DCY_VoidcasterVortex2", flags: SXF_NOCHECKPOSITION);
		A_Explode(50, 150, XF_NOTMISSILE, 0);
	}
	P_FX CDEFGH 5;
	Stop;
  }
}