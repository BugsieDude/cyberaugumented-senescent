Class DCY_HTrail : DCY_GTrail
{
  Default
  {
  Alpha 1;
  Scale 1;
  -BRIGHT
  RenderStyle "Translucent";
  }
  
  int tat;
  
  override void Tick()
  {
	super.Tick();
	alpha = tat;
  }
  
  States
  {
  Spawn:
	#### # 1
	{
		tat = !tat;
		if (getAge() > 25) Destroy();
		A_FaceTracer();
	}
	Loop;
  }
}
	
Class DCY_Hexodenican : DCY_VileParent
{
  Default
  {
  SeeSound "HX/Sight";
  ActiveSound "HX/Active";
  PainSound "HX/Pain";
  DeathSound "HX/Death";
  Health 1000;
  Tag "\c[n9]Hexodenican\c-";
  +MISSILEMORE
  +NOTARGET
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOINFIGHTING
  +FIRERESIST
  +NORADIUSDMG
  DamageFactor "Hex", 0.000;
  BloodColor "Black";
  Obituary "%o was hexed by the Hexodenican's mastery.";
  HitObituary "%o was hexed by the Hexodenican's mastery.";
  Scale 1.245;
  Speed 10;
  +NEVERFAST;
  PainChance 20;
  DCYMonster.ScientificName "Hei aliena";
  }
  
  int timer;
  
  States
  {
  Spawn:
	H3X_ A 10  DCY_Look();
	Loop;
  Look:
	#### AAAABBBBCCCCDDDDEEEEFFFF 2
	{
		DCY_LookingForPlayer();
		if (!sv_effectchoker && DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_HexodenicanTrail2", -13, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	H3X_ AAAAABBBBBCCCCCDDDDDEEEEEFFFFF 1
	{
		A_VileChase();
		if (!sv_effectchoker && DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_HexodenicanTrail2", -13, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	Loop;
  Heal:
	H3X_ J 4 A_RadiusGive("DCY_MassResurrection", 128, RGF_KILLED);
	H3X_ IHHH 4;
	Goto See;
  Missile:
	H3X_ A 0
	{
		switch(random(0, 3))
		{
			case 1: SetStateLabel("ThunderBolts"); break;
			case 2: SetStateLabel("BigBall"); break;
			case 3: SetStateLabel("Tele"); break;
		}
	}
	Goto Lunge;
  Tele:
	H3X_ A 10 A_FaceTarget();
	H3X_ A 1
	{
		timer = 0;
		A_StartSound("ysteleport", CHAN_BODY);
		bNODAMAGE = true;
		bNOPAIN = true;
		bSHOOTABLE = false;
		bINVULNERABLE = true;
		bSOLID = false;
		bSLIDESONWALLS = true;
		A_HideThing();
		angle = frandom(0, 360);
		A_SetSpeed(15);
	}
	H3X_ A 1
	{
		A_Wander(CHF_DONTTURN|CHF_STOPIFBLOCKED);
		if (getAge() % 2)
		{
			DCY_HTrail t = DCY_HTrail(Spawn("DCY_HTrail", pos + (0, 0, 5)));
			if (t) { t.tracer = self.target; t.angle = angle; t.sprite = sprite; t.frame = frame; t.scale = scale; t.translation = translation; }
		}
		timer++;
		A_FaceTarget();
	}
	H3X_ A 0
	{
		if (timer > 50)
		{
			bNODAMAGE = false;
			bNOPAIN = false;
			bSOLID = true;
			bSHOOTABLE = true;
			bINVULNERABLE = false;
			BSLIDESONWALLS = false;
			A_UnHideThing();
			A_SetSpeed(10);
			SetStateLabel("See");
		}
	}
	Goto Tele+2;
  ThunderBolts:
	H3X_ H 2 A_FaceTarget();
	H3X_ H 0 A_StartSound("CYBRLASC", CHAN_WEAPON);
	H3X_ HHHHHHHHHHHHHHHHHHHHHHHHH 2
	{
		A_FaceTarget(5, 5);
		if (DCY_EffectDistance(sfxdistance))
			for (int i = -30; i < 31; i += 60)
				A_SpawnItemEx("DCY_LightningPhaseElec", -10, i, 90, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
	}
	H3X_ IJ 3 A_FaceTarget();
	H3X_ K 15
	{
		A_FaceTarget(5, 5);
		A_StartSound("LIGTFIR1", CHAN_WEAPON);
		A_SpawnProjectile("DCY_LightningPhaseDischarge", 32, 0, 0, CMF_AIMDIRECTION, pitch);
	}
	H3X_ KA 10 A_FaceTarget();
	Goto See;
  BigBall:
	H3X_ H 0 A_StartSound("ElectricBall/Charge", CHAN_WEAPON);
	H3X_ HHHHHHHHHHHHHHHHHHHHHHHHH 2
	{
		A_FaceTarget(5, 5);
		if (DCY_EffectDistance(sfxdistance))
			for (int i = -30; i < 31; i += 60)
			{
				A_SpawnItemEx("DCY_HexArc", -10, i, 90, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION, failchance: 200);
				A_SpawnItemEx("DCY_HexElec", -10, i, 90, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
			}
	}
	H3X_ HIJ 10 A_FaceTarget();
	H3X_ K 30
	{
		bool s;
		actor e;
		A_StartSound("DCYHex/Lightning", CHAN_WEAPON);
		[s, e] = A_SpawnItemEx("DCY_HexBall", 10, 0, 40, 10, flags: SXF_SETTRACER);
		if (s && e != null) e.tracer = self.target;
		A_FaceTarget();
	}
	Goto See;
  Lunge:
	H3X_ HHHHHHHHIIIIIIIIIIJJJJJJJJJJ 1
	{
		A_FaceTarget();
		
		if (!sv_effectchoker && DCY_EffectDistance(1200))
		{
			A_SpawnItemEx("DCY_HexodenicanTrail2", -0.5, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_HexodenicanTrail", -0.5, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnParticleEx(0x194d05, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(50, 55), 130, 0, frandom(-20, 20), frandom(-20, 20), frandom(10, 80), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), fadestepf: 0.065, sizestep: 0.55);
			for (int i = 0; i < 2; i++)	A_SpawnItemEx("DCY_HexArc", frandom(-40, 40), frandom(-40, 40), frandom(10, 70), flags: SXF_NOCHECKPOSITION, failchance: 100);
		}
	}
	H3X_ K 0
	{
		A_FaceTarget();
		A_Recoil(-29);
		A_QuakeEx(3, 3, 3, 50, 0, 200, "", QF_3D|QF_RELATIVE|QF_SCALEDOWN);
		A_StartSound("ExplosiveDash", CHAN_BODY, attenuation: 0.5);
	}
	H3X_ KKKKKKKKKKKKKKKKKKKK 1
	{
		if ((level.time % 2) == 1)
		{
			A_Explode(25, 64, XF_NOTMISSILE, damagetype: "Hex");
			A_Recoil(-4);
		}
		
		if (!sv_effectchoker && DCY_EffectDistance(1200))
		{
			A_SpawnItemEx("DCY_HexodenicanTrail2", -11, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			A_SpawnItemEx("DCY_HexodenicanTrail", -13, 0, 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
			
			for (int i = 2; i > 0; i--)
			{
				for (int i = 2; i > 0; i--) A_SpawnItemEx("DCY_HexArc", frandom(-40, 40), frandom(-40, 40), frandom(10, 70), flags: SXF_NOCHECKPOSITION, failchance: 100);
				A_SpawnItemEx("DCY_HexElec", frandom(-40, 40), frandom(-40, 40), frandom(10, 70), flags: SXF_NOCHECKPOSITION, failchance: 100);
				A_SpawnParticleEx(0x145700, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 130, 0, frandom(-10, 10), frandom(-10, 10), frandom(10, 70), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), fadestepf: 0.015, sizestep: 0.55);
			}
		}
	}
	H3X_ LA 10;
	Goto See;
  Pain:
	H3X_ M 4;
	H3X_ M 2 A_Pain();
	Goto See;
  Death:
	H3X_ M 1;
	H3X_ M 1 A_Scream();
	H3X_ MMMMMMMMMMMMMMMMMMMM 2 A_SpawnItemEx("DCY_HexodenicanTrail", zofs: 6, zvel: 6, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	H3X_ UVWX 5 DCY_Gibs(false);
	H3X_ YZ 4 A_NoBlocking();
	H4X_ ABC 2;
	H4X_ C -1;
	Stop;
  }
}

Class DCY_HexArc : DCY_Arc { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.76,1.52,0.00]"; } }
Class DCY_HexElec : DCY_ElecDeathBase { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.26,1.52,0.00]"; Scale 1.5;} }

Class DCY_HexBall : DCY_MeatballElecball
{
  Default
  {
  Height 40;
  Radius 20;
  Speed 10;
  +NEVERFAST
  Scale 1.6;
  SeeSound "DCYHex/Lightning";
  DeathSound "LightningPulse/Explode";
  Translation "0:255=%[0.00,0.00,0.00]:[0.89,2.00,0.89]"; 
  }
  
  States
  {
  Spawn:
	LFX1 STUVW 1
	{
		for (int i = 2; i > 0; i--)
		{
			A_SpawnItemEx("DCY_HexElec", frandom(-40, 40), frandom(-40, 40), frandom(-40, 40), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE, failchance: 100);
			A_SpawnParticleEx(0x0a3300, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 70, 0, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.7, fadestepf: 0.015, sizestep: 0.55);
		}
		
		if (!(getAge() % 8) && (tracer && CheckSight(tracer) && distance2D(tracer) < 500))
		{
			A_FaceTracer();
			A_SpawnProjectile("DCY_HexBolt", 0, flags: CMF_TRACKOWNER|CMF_AIMDIRECTION|CMF_OFFSETPITCH, pitch);
		}
	}
	Loop;
  Death:
	SFE1 A 1
	{
		A_QuakeEx(2, 2, 2, 38, 0, 1000, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 45, 0, 1250, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 35, 0, 1250, "", QF_SCALEDOWN, 0, 0, 0, 0, 0, 0.7, frandom(0.7, -0.7));
		A_Explode(150, 200, XF_NOTMISSILE, True);
		
		if (DCY_EffectDistance(2000))
		{
			A_SpawnItemEx("DCY_Explosion_1_Biggie", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			A_SpawnParticleEx(0x0a3300, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
			for (int i = 20; i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
		}
	}
    SFE1 AAAABBBCCDEF 1;
	Stop;
  }
}

Class DCY_HexBolt : DCY_LightningVulgarShot
{
  Default
  {
  Scale 0.8;
  Translation "0:255=%[0.00,0.00,0.00]:[0.89,2.00,0.89]";
  Speed 23;
  }
  
  States
  {
  Spawn:
	P_FX AAABBB 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnParticleEx(0x0a3300, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 45, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.02, sizestep: 0.35);
			A_SpawnItemEx("DCY_ElecTrailBase", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION);
			if (!(level.time % random(1, 4))) A_SpawnParticleEx(0x0a3300, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 100, random(3, 9), 0, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.2, 0.2), frandom(-0.2, 0.2), frandom(-0.2, 0.2), fadestepf: 0, sizestep: -0.025);	
		}
	}
	Loop;
  }
}