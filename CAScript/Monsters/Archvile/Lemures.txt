Class DCY_Lemures : DCY_VileParent
{
  Default
  {
  Health 1400;
  BloodColor "ffaa00";
  RenderStyle "Add";
  SeeSound "Lemures/Sight";
  ActiveSound "";
  PainSound "Lemures/Pain";
  DeathSound "Lemures/Death";
  Tag "\c[b6]Lemures\cL";
  DamageType "Lemures";
  BloodType "DCY_ElecDeathBase";
  Translation "0:255=%[0.76,0.21,0.08]:[0.00,0.00,0.00]";
  DamageFactor "Lemures", 0.00;
  Alpha 1.5;
  Scale 1.12;
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +QUICKTORETALIATE
  Speed 10;
  FastSpeed 15;
  DCYMonster.ScientificName "NO DATA";
  }
  
  int ballangle;
  
  override void Tick()
  {
	super.Tick();
	
	if (!level.isFrozen() && DCY_EffectDistance(1500))
	{
		A_SpawnItemEx("DCY_LemuresAura", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), (!random(0, 1)) ? 0 : frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME, 20);
		A_SpawnItemEx("DCY_Arc", frandom(-self.radius, self.radius), frandom(-self.radius, self.radius), frandom(0, self.height), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 70);
		alpha = frandom(1.4, 3);
		A_SetScale(frandom(1.12, 1.13), frandom(1.12, 1.13));
	}
  }
  
  States
  {
  Spawn:
	L3ML A 10 DCY_Look();
	Loop;
  Look:
	#### AABBCCDDEEFF 2 DCY_LookingForPlayer();
	#### # 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	#### # 0 DCY_AlertSound();
	Goto See;
  See:
	L3ML AAABBBCCCDDD 2
	{
		A_Chase(null, "Missile", CHF_RESURRECT);
		Return A_Jump(20, "Dash");
	}
	Loop;
  Heal:
	L3M2 A 0 A_RadiusGive("DCY_MassResurrection", 400, RGF_KILLED);
	L3M2 ABCABCABC 6;
	Goto See;
  Dash:
	L3ML AAABBBCCCDDD 1
	{
		for (int i = 2; i > 0; i--)
		{
			A_Chase();
		
			if (DCY_EffectDistance(1500))
				A_SpawnItemEx("DCY_LemuresAura", zofs: 5, xvel: frandom(3, -3), yvel: frandom(-3, 13), zvel: frandom(-3, 3), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		}
		
		Return A_Jump(5, "See");
	}
	Loop;
  Missile:
  BouncingBalls:
	L3M2 ABCABC 6 A_FaceTarget();
	L3M2 ABCAB 6
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_LemuresGrenade", 35, -30, frandom(-10.0, 10.0));
		A_SpawnProjectile("DCY_LemuresGrenade", 35, 30, frandom(-10.0, 10.0));
	}
	L3M2 BCABCABC 6 A_FaceTarget();
	Goto See;
  Pain:
	L3ML Q 4;
	L3ML Q 3 A_Pain();
	Goto See;
  Death:
	L3ML Q 10;
	L3ML Q 1 A_ScreamAndUnblock();
	L3ML RSTUVWXY 6;
	L3ML Z 60;
	L3ML Z 0
	{
		for (int i = 3; i > 1; i--) A_StartSound("V3RYSMSH", i, CHANF_OVERLAP);
		A_SpawnItemEx("DCY_LemuresExplosion", zofs: 10, flags: SXF_NOCHECKPOSITION);
	}
	Stop;
  }
}

Class DCY_LemuresExplosion : DCY_WretchedExplosion { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.76,0.31,0.08]"; } }

Class DCY_LemuresAura : DCY_ProjectileBaseTrail
{
  Default { Alpha 0.2; }
  States
  {
  Spawn:
	#### # 1 { if (alpha < 0) destroy(); A_FadeOut(0.005, 0); }
	Wait;
  }
}

Class DCY_LemuresSoul : DCY_RZSpirit { Default { Translation "0:255=%[0.00,0.50,0.50]:[1.01,2.00,1.01]"; +DONTSPLASH +BRIGHT Alpha 1.0; } }

Class DCY_LemuresGrenade : DoomImpBall
{
  Default
  {
  Radius 13;
  Height 13;
  Speed 25;
  DamageFunction 25;
  BounceType "Hexen";
  Projectile;
  -NOGRAVITY
  +FORCEXYBILLBOARD
  +DONTHARMCLASS
  +CANBOUNCEWATER
  +BOUNCEONWALLS
  +BOUNCEONFLOORS
  +BOUNCEONCEILINGS
  BounceCount 4;
  BounceFactor 1.0;
  RenderStyle "Add";
  DamageType "Lemures";
  Alpha 1.0;
  SeeSound "LPISLASR";
  DeathSound "LIGTBALX";
  Translation "0:255=%[0.00,0.00,0.00]:[0.76,0.31,0.08]";
  }
  
  States
  {
  Spawn:
	C3AL AB 1
	{
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 180);
		A_SpawnItemEx("DCY_FluorescentOrbTS", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnParticleEx(0xff2200, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 45, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.025, sizestep: -0.55);
		A_SpawnItemEx("DCY_Arc", frandom(-self.radius, self.radius), frandom(-self.radius, self.radius), frandom(-self.height, self.height), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 70);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_Explode(25, 69, XF_NOTMISSILE, 0);
		
		for (int i = 14; i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-6, 6), frandom(-6, 6), frandom(-6, 6), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		for (int i = 3; i > 0; i--) A_SpawnItemEx("DCY_Explosion_1_Small", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			
		A_StartSound("ROBOEXPL", CHAN_BODY, 0, 1, 0.5);
	}
	Stop;
  }
}