Class DCY_Discerner : DCY_TheGiantEnemySpider
{
  Default
  {
  BloodType "DCY_ElecDeathBase";
  BloodColor "Red";
  StencilColor "ff0000";
  Health 6000;
  SeeSound "DCYDiscerner/Sight";
  ActiveSound "DCYDiscerner/Idle";
  PainSound "DCYDiscerner/Idle";
  DeathSound "DCYDiscerner/Death";
  Tag "\c[m6]Discerner\c-";
  Speed 13;
  FastSpeed 16;
  PainChance 20;
  Scale 1.2;
  DCYMonster.ScientificName "Sycophant MK-II";
  +DCYMONSTER.DCYBOSS;
  }
  
  double v;
  
  override void Tick()
  {
	super.Tick();
	A_SoundVolume(123, v);
	if (v > 0) v -= 0.1;
  }
  
  void A_DiscernerChase()
  {
	if (target && CheckSight(target))
	{
		A_Chase(flags: CHF_DONTMOVE);
		A_FaceTarget(3, 3);
		A_Recoil(-2);
	}
	else
		A_Chase();
  }
  
  void A_DiscernerStep(double p = 0)
  {
	A_QuakeEx(3, 3, 5, 8, 0, 400, "", QF_SCALEDOWN|QF_3D);
	A_StartSound("DCYDiscerner/Stomp", 123, pitch: frandom(0.7, 1.2) + p);
	v = 1;
  }
  
  States
  {
  Spawn:
	D3S_ A 10 DCY_Look();
	Loop;
  Look:
	D3S_ AABB 3 DCY_LookingForPlayer();
	D3S_ C 30 A_DiscernerStep();
	D3S_ CCDD 3 DCY_LookingForPlayer();
	D3S_ A 30 A_DiscernerStep();
	Loop;
  Alert:
	"####" E 0 DCY_AlertSound();
	D3S_ AAAAAAAAAEEEEEEEEEEEEEEE 1 A_FaceTarget();
	D3S_ A 1 A_DiscernerStep(-0.5);
	D3S_ AAAAAAA 1 A_FaceTarget();
	Goto See;
  See:
	D3S_ AABB 2 A_DiscernerChase();
	D3S_ C 10
	{
		if (target && checkSight(target)) tics = 2;
		A_DiscernerStep();
	}
	D3S_ CCDD 2 A_DiscernerChase();
	D3S_ A 10
	{
		if (target && checkSight(target)) tics = 2;
		A_DiscernerStep();
	}
	Loop;
  Missile:
	D3S_ A 0
	{
		if (!random(0, 1) && target && CheckSight(target) && Distance3D(Target) < 400) SetStateLabel("Lunge");
		if (target && Distance3D(target) < 250) SetStateLabel("Shockwave");
	}
	Goto Sniper;
  Lunge:
	D3S_ A 10
	{
		if (target && distance3D(target) < 250) return;
		A_FaceTarget();
	}
	D3S_ E 1
	{
		A_StartSound(ActiveSound, CHAN_VOICE);
		
		if (target)
		{
			A_Hop((distance3D(target) < 500) ? 15 : 20);
			A_Recoil((distance3D(target) < 200) ? -15 : -30);
		}
		
		A_FaceTarget();
	}
	D3S_ E 1
	{
		A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSTENCILCOL|SXF_NOCHECKPOSITION);
		if(pos.z <= floorz)
		{
			A_DiscernerStep(-0.6);
			A_QuakeEx(2, 2, 7, 15, 0, 400, "", QF_3D|QF_SCALEDOWN);
			SetStateLabel("See");
		}
	}
	Goto Lunge+2;
  Sniper:
	D3S_ A 10 A_FaceTarget();
	D3S_ A 1 A_StartSound("DCYDiscerner/Aim", CHAN_WEAPON, attenuation: 0.5);
	D3S_ AAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		if (DCY_EffectDistance(2000))
			A_SpawnItemEx("DCY_ElecDeathBase", 15, 0, 35, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR);
		
		A_FaceTarget(1.2, 1.2);
	}
	D3S_ E 20
	{
		A_FaceTarget(1.2, 1.2);
		A_SpawnProjectile("DCY_DiscernerBall", 35, 0, frandom(-1, 1), CMF_AIMDIRECTION, pitch);
		A_StartSound("DCYDiscerner/Ball", CHAN_WEAPON, CHANF_OVERLAP, attenuation: 0.5);
		A_Recoil(10);
	}
	D3S_ A 1 A_DiscernerStep(-0.5);
	Goto See;
  Shockwave:
	D3S_ A 10 A_FaceTarget();
	D3S_ E 40
	{
		A_Recoil(50);
		vel.z += 10;
		A_RadiusThrust(12000, 400, RTF_NOTMISSILE);
		A_Explode(128, 300, XF_NOTMISSILE);
		target.A_StartSound("MW/Shockwave", 15, attenuation: ATTN_IDLE);
		target.A_StartSound("DHG/Groundpound", 16, attenuation: ATTN_IDLE);
		A_QuakeEx(5, 5, 5, 40, 0, 2280, "", QF_3D|QF_SCALEDOWN);
		
		if (DCY_EffectDistance(2000))
		for (int i = 0; i < 360; i += frandom(5, 30))
		{
			A_SpawnParticleEx(0xaa0000, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 155, i, velx: frandom(-0.5, 0.5) + 10, vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelx: 0.3, fadestepf: 0.015, sizestep: 0.55);
			A_SpawnItemEx("DCY_ElecDeathBase", 15, 0, 30, frandom(-5, 5) + 20 + (i / 100), frandom(-5, 5), frandom(-5, 5), i, SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR, failchance: 100);
			A_SpawnItemEx("DCY_ElecDeathBase", 15, 0, 30, frandom(-5, 5) + 20, frandom(-5, 5), frandom(-5, 5), i, SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR|SXF_TRANSFERSCALE, failchance: 60);
		}
	}
	D3S_ A 1 A_DiscernerStep(-0.5);
	Goto See;
  Pain:
	D3S_ E 4;
	D3S_ E 4 A_Pain();
	D3S_ A 1 A_DiscernerStep(-0.5);
	Goto See;
  Death:
	D3S_ E 10 A_FaceTarget();
	D3S_ A 1 A_DiscernerStep(-0.5);
	D3S_ A 10 { A_FaceTarget(); A_ScreamAndUnblock(); }
	D3S_ FGHHHHHH 8;
	D3S_ H -1 A_BossDeath2();
	Stop;
  }
}

Class DCY_DiscernerBall : DCY_ImpFireball
{
  Default
  {
  Radius 15;
  Height 30;
  Scale 2;
  Damage 40;
  DamageType "Electric";
  StencilColor "ff0000";
  Translation "0:255=%[0.30,0.00,0.00]:[2.00,1.01,1.01]";
  }
  
  States
  {
  Spawn:
	C3AL BA 1
	{
		if (getAge() < 20) A_ScaleVelocity(1.1);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_FluorescentOrbT", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSTENCILCOL);
		A_SpawnParticleEx(0xaa0000, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 95, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), accelx: -0.21, fadestepf: 0.015, sizestep: 0.55);
	}
	Loop;
  Death:
	C3AL A 1
	{
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		A_SpawnItemEx("DCY_WretchedTeleport", flags: SXF_NOCHECKPOSITION);
		A_RadiusThrust(8000, 128, RTF_NOTMISSILE);
	}
	Stop;
  }
}