Class DCY_Mindmonarch : DCY_TheGiantEnemySpider
{
  Default
  {
  Health 5000;
  BloodColor "Blue";
  Tag "\c[w6]Mindmonarch\c-";
  SeeSound "MM/Sight";
  ActiveSound "MM/Active";
  PainSound "MM/Pain";
  DeathSound "MM/Death";
  PainChance 30;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  Obituary "%o carried the lethal weight of the Mindmonarch's discharge.";
  DCYMonster.ScientificName "Nulla personatus";
  }
  
  void A_BigLaser(string laser)
  {
	A_FaceTarget(0.5, 2.5);
	A_SpawnProjectile(laser, 55, flags: CMF_AIMDIRECTION, pitch: pitch);
  }
  
  void A_Run()
  {
	A_FaceTarget();
	A_Recoil(-2);
	
	if (target && distance3D(target) < 400) SetStateLabel("Lunge");
	if (l <= 0) SetStateLabel("See"); else l--;
  }
  
  void A_MindmonarchChase()
  {
	A_Chase();
	l++;
  }
	
  int laser, l;
  
  States
  {
  Spawn:
	NMBD A 10 DCY_Look();
	Loop;
  Look:
	NMBD AABBCC 4 DCY_LookingForPlayer();
	NMBD D 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	NMBD DDEEFF 4 DCY_LookingForPlayer();
	NMBD A 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	"####" A 1 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	NMBD AABBCC 4 A_MindmonarchChase();
	NMBD D 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	NMBD DDEEFF 4 A_MindmonarchChase();
	NMBD A 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	Loop;
  Missile:
	NMBD G 0
	{
		if (target && distance3D(target) < 1000 && !random(0, 2) && l > 30) SetStateLabel("ChargeAtPlayer");
	}
	Goto Laser;
  ChargeAtPlayer:
	NMBD AABBCC 3 A_Run();
	NMBD D 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	NMBD DDEEFF 3 A_Run();
	NMBD A 0 A_StartSound("DCYDictator/Step", CHAN_BODY, attenuation: 0.75);
	Loop;
  Lunge:
	NMBD G 1
	{
		A_StartSound(PainSound, CHAN_VOICE);
		
		if (target)
		{
			A_Hop(15);
			A_Recoil(-7);
		}
		
		A_FaceTarget();
	}
	NMBD G 1
	{
		if(pos.z <= floorz)
		{
			A_StartSound("EarthPound", CHAN_BODY, pitch: .8);
			A_PoundEffect(300);
			A_RadiusThrust(6000, 600, RTF_NOTMISSILE);
			A_Explode(200, 300, XF_NOTMISSILE);
			A_QuakeEx(1, 1, 3, 65, 0, 2000, "", QF_3D|QF_SCALEDOWN);
			tics = 20;
			l -= 10;
			SetStateLabel("See");
		}
	}
	Goto Lunge+1; 
  Laser:
	NMBD G 0 { laser = 80; A_StartSound("Minigun/WindUp", CHAN_6, volume: 0.2, pitch: 0.35675); A_StartSound("SPELL15", CHAN_WEAPON, pitch: 0.12); }
	NMBD TTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTT 1
	{
		A_FaceTarget();
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnParticleEx(0x6417ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 200, 0, frandom(100, 60), frandom(-100, 100), frandom(-40, 40) + 31, frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: 0.725);
			A_SpawnParticleEx(0x6417ff, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), random(10, 100), 0, frandom(120, 100), frandom(-200, 200), frandom(-30, 30) + 31, accelx: -2, fadestepf: 0.05, sizestep: -0.225);
		}
	}
	NMBD T 10;
	Goto LaserF;
  LaserF:
	NMBD U 1 A_StartSound("BIGASSLAZOR/FIRE", CHAN_WEAPON, attenuation: 0.5);
	NMBD U 1
	{
		if (laser < 0)
			SetStateLabel("Cooldown");
		
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnParticleEx(0x6417ff, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 200, 0, frandom(100, 60), frandom(-100, 100), frandom(-40, 40) + 31, velx: frandom(0, 12), accelx: 3, fadestepf: 0.05, sizestep: 0.725);
			A_SpawnParticleEx(0x6417ff, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), random(2, 15), 0, frandom(120, 100), frandom(-200, 200), frandom(-30, 30) + 31, accelx: 5, fadestepf: 0.05, sizestep: -0.225);
		}
		
		A_BigLaser("DCY_MindmasterLaser");
		laser--;
	}
	Goto LaserF+1;
  Cooldown:
	NMBD A 68;
	Goto See;
  Pain:
	NMBD I 4;
	NMBD I 4 A_Pain();
	Goto See;
  Death:
	NMBD I 5;
	NMBD I 10 A_Scream();
	NMBD JKLM 6;
	NMBD N 6
	{
		A_NoBlocking();
		DCY_Gibs(false);
		A_StartSound("Archon/Explode", CHAN_BODY, attenuation: 0.5);
		
		if (damagetype != "Massacre")
			A_SpawnItemEx("DCY_Explosion_1_Biggie", frandom(-40, 40), frandom(-40, 40), frandom(10, 55), flags: SXF_NOCHECKPOSITION);
	}
	NMBD OP 6 { DCY_Gibs(false); if (damagetype != "Massacre") A_SpawnItemEx("DCY_Explosion_1_Biggie", frandom(-40, 40), frandom(-40, 40), frandom(10, 55), flags: SXF_NOCHECKPOSITION); }
	NMBD QRS 5;
	NMBD S -1;
	Stop;
  }
}