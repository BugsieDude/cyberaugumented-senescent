Class DCY_Executioner : DCY_TheGiantEnemySpider
{
  Default
  {
  Health 5500;
  Tag "\c[l5]Executioner\c-";
  Speed 8;
  FastSpeed 10;
  Translation "168:173=[255,255,255]:[128,255,255]", "174:180=[121,255,255]:[0,128,255]", "181:191=[0,115,230]:[0,0,64]", "224:231=[255,255,255]:[128,255,255]", "208:216=[255,255,255]:[128,255,255]", "217:223=[119,255,255]:[0,128,192]", "232:235=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[0.85,0.91,1.16]", "48:79=%[0.00,0.00,0.00]:[0.85,0.91,1.16]", "128:159=%[0.00,0.00,0.00]:[0.85,0.91,1.16]", "13:15=%[0.00,0.00,0.00]:[0.85,0.91,1.16]", "160:163=[255,255,255]:[128,255,255]", "164:167=[106,255,255]:[0,128,255]", "16:23=[255,255,255]:[128,255,255]", "24:31=[111,255,255]:[0,128,255]", "32:47=[0,124,249]:[0,0,64]", "192:195=[255,255,255]:[128,255,255]", "196:201=[106,255,255]:[0,128,255]", "202:207=[0,116,232]:[0,0,64]", "112:119=[0,128,255]:[0,0,128]", "120:127=[0,0,119]:[0,0,64]", "9:12=%[0.00,0.00,0.00]:[0.76,0.76,0.76]", "248:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  Obituary "%o messed with the Executioner and got the quietus.";
  SeeSound "PSSpider/Sight";
  ActiveSound "PSSpider/Active";
  PainSound "PSSpider/Pain";
  DeathSound "PSSpider/Death";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  -FLOAT
  -NOGRAVITY
  +MISSILEMORE
  +DONTFALL
  FloatSpeed 5;
  DCYMonster.GibType "DCY_GibletsAdd";
  }
  
  int missile;
  
  override void Tick()
  {
	Super.Tick();
	
	If (Health > 0 && !IsFrozen())
	{
		A_SpawnItemEx("DCY_FluorescentJetFlare", frandom(55, 65), frandom(55, 65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlare", frandom(-55, -65), frandom(-55, -65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlare", frandom(-55, -65), frandom(55, 65),74,0,0,frandom(-6,-8),0,128);
		A_SpawnItemEx("DCY_FluorescentJetFlare", frandom(55, 65), frandom(-55, -65),74,0,0,frandom(-6,-8),0,128);
	}
  }
  
  States
  {
  Spawn:
	I8HJ AB 3 DCY_Look();
	Loop;
  Look:
	I8HJ AB 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 1 DCY_AlertSound();
	Goto See;
  See:
	I8HJ A 0
	{
		bNoGravity = true;
		bFloat = true;
		bFloorClip = true;
	}
	I8HJ AB 3 A_Chase();
	Loop;
  Missile:
	I8HJ ABABPPP 3 A_FaceTarget();
	Goto MissileFire;
  MissileFire:
	I8HJ E 0 A_Jump(60, "Bewm");
	I8HJ E 2 A_FaceTarget();
	I8HJ F 2
	{
		A_SpawnProjectile("DCY_ExecutionerBeam", 25, -25, frandom(-6.0, 6.0), CMF_OFFSETPITCH, frandom(-3.0, 3.0));
		A_SpawnProjectile("DCY_ExecutionerBeam", 25, 25, frandom(-6.0, 6.0), CMF_OFFSETPITCH, frandom(-3.0, 3.0));
		A_FaceTarget();
		A_MonsterRefire(3, "See");
		Return A_Jump(7, "Bewm");
	}
	Goto MissileFire+1;
  Bewm:
	I8HJ P 5 A_FaceTarget();
	I8HJ N 4 A_FaceTarget();
	I8HJ O 4
	{
		A_StartSound("PSIPistol/Laser", CHAN_WEAPON, 0, 1.0, 0.65);
		
		for (int i = 10; i > -11; i -= 5)
			A_SpawnProjectile("DCY_FluorescentOrb", 50, 0, i);
	}
	I8HJ NPP 5;
	Goto See;
  Pain:
	I8HJ A 3;
	I8HJ B 3 A_Pain();
	Goto See;
  Death:
	I8HJ H 1;
	I8HJ H 10
	{
		bFloorClip = true;
		bFloat = false;
		A_Scream();
		A_Stop();
	}
	I8HJ IJ 6;
	I8HJ K 6
	{
		if (damagetype != "Massacre")
		{
			for (int i = 3; i > 0; i--) {
				DCY_Gibs(false);
				A_SpawnItemEx("DCY_AndroidExplosionBiggie", frandom(-85.0, 85.0), frandom(-85.0, 85.0), frandom(15.0, 55.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), random(0, 360), SXF_NOCHECKPOSITION);
			}
		}
	}
	I8HJ LM 4;
	TNT1 A 30 A_NoBlocking();
	TNT1 A -1 A_BossDeath();
	Stop;
  }
}

Class DCY_ExecutionerBeam : FastProjectile
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,1.71,2.00]";
  Damage 4;
  DamageType "Sirius";
  Speed 30.5;
  Scale 1.3;
  RenderStyle "Add";
  +BRIGHT
  +NEVERFAST
  +BLOODLESSIMPACT
  -BLOODSPLATTER
  SeeSound "scout/laser";
  DeathSound "";
  }
  
  States
  {
  Spawn:
	THZ4 A 1
	{
		A_SpawnItemEx("DCY_ExecutionerBeamTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
		A_Weave(3, 0, frandom(-5.0, 5.0), 0);
	}
	Loop;
  Death:
	D8PJ E 2
	{
		A_FadeOut(0.1, 0);
		A_SetScale(1.0, 1.0);
	}
	D8PJ FGHIJK 2 A_FadeOut(0.1, 0);
	Stop;
  }
}

Class DCY_ExecutionerBeamTrail : DCY_ProjectileBaseTrail { Default { Alpha 0.5; Translation "0:255=%[0.00,0.00,0.00]:[0.00,0.55,2.00]"; } }