Class DCY_VariegationDemon : DCY_Befouler
{
  Default
  {
  Health 1200;
  Obituary "%o underestimated the swift power of the Variegation Trimental.";
  HitObituary "%o's life has been eradicated by the Variegation Trimental's eye.";
  Tag "\c[u3]Variegation Trimental\c-";
  SeeSound "VTrimental/Sight";
  ActiveSound "VTrimental/Active";
  PainSound "VTrimental/Pain";
  DeathSound "VTrimental/Death";
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NOTARGET
  +NOFEAR
  +DONTHARMCLASS
  +NOINFIGHTING
  +DONTHARMSPECIES
  +NOBLOOD
  +BOSS
  }
  
  float rainbowtag;
  string tagbase;
  
  States
  {
  Spawn:
    VL_N A 10 DCY_Look();
	Loop;
  Look:
    VL_N AABBCC 2 DCY_LookingForPlayer();
    Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	TNT1 A 0 A_Jump(40, "Teleporting");
    VL_N AABBCC 2 A_Chase();
	TNT1 A 0 A_Jump(65, "Avoid");
	Loop;
  Avoid:
	VL_N A 1
	{
		A_Chase(null, "Missile", CHF_DONTMOVE);
		A_Dodging(10);
	}
	VL_N AAAA 1 A_Chase(null, "Missile", CHF_DONTMOVE);
	TNT1 A 0 A_Jump(200, "See");
	Loop;
  Teleporting:
	VL_N AB 2;
	VL_N A 0
	{
		for (int i = 15; i > 0; i--)
		{
			A_SpawnItemEx("DCY_VariegationTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME);
			
			for (int i = 4; i > 0; i--)
				A_Wander();
		}
	}
	VL_N AB 2;
	Goto See;
  Missile:
	VL_N DN 7 A_FaceTarget();
	VL_N N 0 A_Jump(128, "Flames");
	Goto Flames;
  Flames:
	VL_3 PQ 5 Bright A_FaceTarget();
	VL_3 RQRQR 3 Bright
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_VariegationEyeMissile", 40, 0, frandom(-15.0, 15.0), CMF_OFFSETPITCH, frandom(-15.0, 15.0));
	}
	VL_3 QP 5 Bright A_FaceTarget();
	VL_N NDA 7 A_FaceTarget();
	Goto See;
  Pain:
	VL_N G 3;
	VL_N G 4 A_Pain();
	VL_N G 1 A_Jump(25, "Suicide");
	Goto See;
  Suicide:
    VL_N H 1;
	VL_N H 9
	{
		A_StartSound("pain/death", CHAN_VOICE, pitch: 0.65);
		bShootable = false;
	}
	VL_N IIIIJJJJKKKK 2 Bright A_SpawnItemEx("DCY_MonochromaticExplosion", random(-20, 20), random(-20, 20), random(10, 70), flags: SXF_NOCHECKPOSITION);
	VL_N L 10 Bright A_PainDie("DCY_VariegationSouls");
	VL_N M 10 Bright;
	TNT1 A 50
	{
		for (int i = 156; i > 0; i--)
			A_Chase(null, null, CHF_NOPLAYACTIVE);
	}
	VL_N M 5 { bShootable = true; }
	VL_N LKJIHA 5 Bright;
	Goto See;
  Death:
	VL_N G 3 A_Stop;
	VL_N G 4 A_ScreamAndUnblock();
	VL_N GGGGGGGGGGGGGGG 1
	{
		A_SetAngle(Angle += 15);
		A_SpawnItemEx("DCY_MonochromaticExplosion", random(-20, 20), random(-20, 20), random(10, 70), flags: SXF_NOCHECKPOSITION, 150);
	}
	VL_N IIIIJJJJKKKK 2 Bright A_SpawnItemEx("DCY_MonochromaticExplosion", random(-20, 20), random(-20, 20), random(10, 70), flags: SXF_NOCHECKPOSITION);
	VL_N L 10 Bright A_PainDie("DCY_VariegationSouls");
	VL_N M 10 Bright;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_VariegationSouls : RandomSpawner
{
  Default
  {
	DropItem "DCY_FlyingSkullWithFIRE", 256, 256;
	DropItem "DCY_PhantomSoul", 256, 150;
	DropItem "DCY_NightmareSoul", 256, 100;
  }
}

Class DCY_VariegationEyeMissile : CacodemonBall
{
  Default
  {
  SeeSound "vile/flamewall";
  Damage 4;
  Speed 25;
  FastSpeed 30;
  RenderStyle "Add";
  Radius 5;
  Height 5;
  Alpha 1.0;
  Scale 1.5;
  DamageType "Variegation";
  +SEEKERMISSILE;
  Translation "0:255=%[0.00,0.00,0.00]:[1.37,1.09,0.64]";
  }
  
  States
  {
  Spawn:
    BAL1 AAABBB 1
	{
		A_SeekerMissile(1, 0);
		A_SpawnItemEx("DCY_XBallTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 80);
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 80);
	}
	Loop;
  Death:
    BAL1 CCDDEE 2 A_SpawnItemEx("DCY_InfernisFlame4", frandom(-17.0, 17.0), frandom(-17.0, 17.0), frandom(-17.0, 17.0), zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 80);
	Stop;
  }
}