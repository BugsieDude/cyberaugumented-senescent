Class DCY_Combatant : DCY_Zombe
{
  Default
  {
  Health 45;
  Tag "Combatant";
  Obituary "%o got shot by a Combatant.";
  SeeSound "DCYCombatant/Sight";
  ActiveSound "DCYCombatant/Idle";
  PainSound "DCYCombatant/Pain";
  DeathSound "DCYCombatant/Death";
  Translation "128:159=%[0.00,0.00,0.00]:[0.20,0.20,0.20]", "48:79=%[0.00,0.00,0.00]:[1.18,0.72,0.53]", "16:47=%[0.00,0.00,0.00]:[1.57,0.00,0.00]", "80:111=[255,255,255]:[4,4,4]", "142:142=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "74:79=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "1:2=%[0.00,0.00,0.00]:[0.31,0.31,0.31]";
  }
  
  void A_Combatant()
  {
	A_FaceTarget();
	A_SpawnProjectile("DCY_ZombieTracer", 33, -3, frandom(-2.0, 2.0));
	A_StartSound("PIS5FIRE", CHAN_WEAPON);
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o ran and got the gun.",
		"%o got shot by a Combatant.",
		"%o angered the Combatant."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
	RR3_ A 10 DCY_Look();
	Loop;
  Look:
	RR3_ AABBCCDD 4
	{
		DCY_LookingForPlayer();
		
		if (random(0, 20) == 0)
			SetStateLabel("Stand");
	}
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	RR3_ AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	RR3_ AABBCCDD random(2, 3) DCY_Chase();
	Loop;
  Missile:
	RR3_ A 0
	{
		if (random(0, 5) == 0)
		{
			tics = 10;
			A_Dodging(10);
		}
		else
			tics = 0;
		
		A_FaceTarget();
	}
	RR3_ E 10 A_FaceTarget();
	RR3_ F 2 Bright A_Combatant();
	RR3_ E 2 A_FaceTarget();
	RR3_ F 2 Bright A_Combatant();
	RR3_ E 2 A_FaceTarget();
	RR3_ F 2 Bright A_Combatant();
	RR3_ E 10;
	Goto See;
  Pain:
	RR3_ G 4;
	RR3_ G 3 A_Pain();
	Goto See;
  Death:
	RR3_ H 0
	{
		if (random(0, 4) == 1) SetStateLabel("Death1");
		if (random(0, 4) == 2) SetStateLabel("Death2");
		if (random(0, 4) == 3) SetStateLabel("Death3");
	}
	RR3_ H 5 A_Flip();
	RR3_ I 5 A_ScreamAndUnblock();
	RR3_ JK 4;
	RR3_ L -1;
	Stop;
  Death1:
	R3_1 A 4;
	R3_1 A 2 A_Scream();
	R3_1 B 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ZombieTracer", 20, -20, -30, CMF_OFFSETPITCH, 50);
		A_StartSound("PIS5FIRE", CHAN_WEAPON);
	}
	R3_1 C 5 A_NoBlocking();
	R3_1 D 5;
	R3_1 E 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ZombieTracer", 42, 15, 30, CMF_OFFSETPITCH, -30);
		A_StartSound("PIS5FIRE", CHAN_WEAPON);
	}
	R3_1 FGH 4;
	R3_1 I -1;
	Stop;
  Death2:
	R3_2 A 5 A_Flip();
	R3_2 B 5 A_ScreamAndUnblock();
	R3_2 CDE 4;
	R3_2 F -1;
	Stop;
  Death3:
	R3_3 A 5;
	R3_3 B 5 A_ScreamAndUnblock();
	R3_3 CDE 4;
	R3_3 F -1;
	Stop;
  XDeath:
	RR3_ M 6;
	RR3_ N 4
	{
		A_NoBlocking();
		DCY_Gibs();
	}
	RR3_ OPQRS 4;
	RR3_ T -1;
	Stop;
  Raise:
	RR3_ NM 4;
	RR3_ LKJIH 3;
	RR3_ H 0 A_Restore();
	Goto See;
  }
}