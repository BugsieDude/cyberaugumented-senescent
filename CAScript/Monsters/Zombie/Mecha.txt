Class DCY_MechaZombie : DCY_ZombeParent
{
  Default
  {
  Health 100;
  Speed 10;
  PainChance 64;
  +DONTOVERLAP
  +MISSILEMORE
  +QUICKTORETALIATE
  +DONTHARMCLASS
  +NOINFIGHTING
  Tag "\c[l7]Infected Mechanoid\c-";
  Obituary "%o has meddled with a mechanoid.";
  SeeSound "MechaZombie/Sight";
  ActiveSound "MechaZombie/Idle";
  PainSound "MechaZombie/Pain";
  HowlSound "MechaZombie/Pain";
  DeathSound "MechaZombie/Death";
  DropItem "Clip";
  DropItem "Cell", 150;
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o has meddled with a mechanoid.",
		"That must've hurt, %o.",
		"Executed to dust."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void DCY_MechaLook()
  {
	DCY_LookingForPlayer();
		
	if (random(0, 20) == 0)
		SetStateLabel("Stand");
  }
	
  States
  {
  Spawn:
    CZ07 A 10 DCY_Look();
	Loop;
  Look:
	CZ07 AA 5 DCY_MechaLook();
	CZ07 A 0 A_StartSound("Monsters/NBWalk");
	CZ07 BBCC 5 DCY_MechaLook();
	CZ07 A 0 A_StartSound("Monsters/NBWalk");
	CZ07 DD 5 DCY_MechaLook();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	CZ07 AAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    CZ07 AA 4 A_Chase();
	CZ07 A 0 A_StartSound("Monsters/NBWalk");
	CZ07 BBCC 4 A_Chase();
	CZ07 A 0 A_StartSound("Monsters/NBWalk");
	CZ07 DD 4 A_Chase();
	Loop;
  Missile:
    CZ07 E 5 A_FaceTarget();
	CZ07 F 5 
	{
		A_StartSound("DSPLASM4", CHAN_WEAPON);
		A_SpawnProjectile("DCY_MechaZombiePlasma", 33, 1, random(-1, 1));
		A_FaceTarget();
		A_MonsterRefire(128, "See");
	}
	TNT1 A 0 A_Jump(128, "See");
	Loop;
  Pain:
    CZ07 G 5 A_Pain();
	Goto See;
  Death:
    CZ07 H 2;
	CZ07 H 2 A_Scream();
	CZ07 H 2 A_NoBlocking();
    CZ07 HIJKLM 6;
	CZ07 N -1;
	Stop;
  XDeath:
    CZ07 O 4;
	CZ07 P 4 { A_StartSound("Android/Gib", CHAN_BODY); A_XScream(); }
	TNT1 A 0 A_NoBlocking();
	CZ07 QRSTUV 6;
	CZ07 W -1;
	Stop;
  Death.Fire:
    TNT1 A 0;
	TNT1 A 0 A_StartSound("MechaZombie/Death", CHAN_VOICE, pitch: frandom(1.1, 1.3));
	TNT1 A 0 A_NoBlocking();
	TNT1 A 0 A_Fall();
    FUUU ABCEFGHIJKLMNOPQRSTU 3 Bright
	{
		A_SpawnItemEx("DCY_ImpBigFlame", random(-12, 12), random(-12, 12), random(10, 45), 0, 0, random(2, 6), 0, SXF_NOCHECKPOSITION, failchance: 100);
		A_SpawnItemEx("DCY_ImpFlame1", random(-12, 12), random(-12, 12), random(10, 45), 0, 0, random(2, 6), 0, SXF_NOCHECKPOSITION, failchance: 100);
	}
	FUUU V -1;
	Stop;
  Raise:
    CZ07 MLKJIH 5;
	Goto See;
  }
}