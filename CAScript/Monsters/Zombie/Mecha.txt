Class DCY_Huntsman : DCY_ZombeParent
{
  Default
  {
  Health 70;
  Speed 10;
  PainChance 64;
  +DONTOVERLAP
  +MISSILEMORE
  +QUICKTORETALIATE
  +DONTHARMCLASS
  +NOINFIGHTING
  +ROLLSPRITE
  Tag "Huntsman";
  Obituary "%o has meddled with a huntsman.";
  SeeSound "MechaZombie/Sight";
  ActiveSound "MechaZombie/Idle";
  PainSound "MechaZombie/Pain";
  HowlSound "MechaZombie/Pain";
  AttackSound "";
  DeathSound "MechaZombie/Death";
  Translation "48:79=%[0.00,0.00,0.00]:[0.72,0.44,0.34]", "112:127=%[0.00,0.00,0.00]:[1.01,0.50,0.50]", "80:111=[255,255,255]:[0,0,0]", "13:15=%[0.00,0.00,0.00]:[0.76,0.50,0.43]", "16:47=%[0.00,0.00,0.00]:[1.56,0.00,0.00]", "128:151=%[0.00,0.00,0.00]:[0.71,0.41,0.38]", "152:159=[148,35,35]:[0,0,0]";
  DropItem "Clip";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o has meddled with a huntsman.",
		"That must've hurt, %o.",
		"Executed to dust."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void DCY_MechaLook()
  {
	DCY_LookingForPlayer();
		
	if (random(0, 20) == 0)
		SetStateLabel("Stand");
  }
	
  States
  {
  LoadSprites:
	RFR0 AB 0;
  Spawn:
    RFTR A 10 DCY_Look();
	Loop;
  Look:
	RFTR AABBCCDD 5 DCY_MechaLook();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Stand:
	RFTR AAAAAAAAAAAAAAAAAAAAAA 1 A_Chase(null, "Alert", CHF_NOPLAYACTIVE|CHF_DONTMOVE);
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    RFTR AABBCCDD 4 A_Chase();
	Loop;
  Dodge:
    RFR0 # 0
	{
		A_FaceTarget();
		frame = randompick(0, 1);
		Vel3DFromAngle(12, Angle*128/256+((!frame) ? 64 : 192), 0);
	}
	RFR0 ################# 1 { roll += (!frame) ? -22 : 22; }
	RFR0 # 0 { roll = 0; A_FaceTarget(); }
	Goto Missile+1;
  Missile:
	RFTR E 0 { A_StartSound("MechaZombie/Attack"); if (!random(0, 6)) SetStateLabel("Dodge"); }
    RFTR E 12 A_FaceTarget();
	RFTR FFF 3 
	{
		A_SpawnProjectile("DCY_ZombieTracer", 33, -3, frandom(-1.0, 1.0));
		A_StartSound("PIS3FIR", CHAN_WEAPON);
		A_MonsterRefire(128, "See");
	}
	TNT1 A 0 A_Jump(20, "See");
	Goto Missile+1;
  Melee:
	RFTR V 10 A_FaceTarget();
	RFTR W 10 A_CustomMeleeAttack(random(10, 15), "Fist/PunchEntity", "");
	Goto Missile+1;
  Pain:
    RFTR G 5 { roll = 0; A_Pain(); }
	Goto See;
  Death:
    RFTR H 2 { roll = 0; if (!random(0, 1)) SetStateLabel("Death1"); }
	RFTR H 2 A_Scream();
	RFTR H 2 A_NoBlocking();
    RFTR HIJK 5;
	RFTR L -1;
	Stop;
  XDeath:
    RFTR M 6 { roll = 0; }
	RFTR N 5 A_XScream();
	TNT1 A 0 A_NoBlocking();
	RFTR OPQRST 4;
	RFTR U -1;
	Stop;
  Death1:
	02DG A 4;
	02DG A 2 A_Scream();
	02DG B 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ZombieTracer", 20, -17, -30, CMF_OFFSETPITCH, 50);
		A_StartSound("PIS3FIRE", CHAN_WEAPON);
	}
	02DG C 5 A_NoBlocking();
	02DG D 5;
	02DG E 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ZombieTracer", 42, 12, 30, CMF_OFFSETPITCH, -30);
		A_StartSound("PIS3FIRE", CHAN_WEAPON);
	}
	02DG FGH 4;
	02DG I -1;
	Stop;
  Death.Fire:
    TNT1 A 0 { roll = 0; }
	TNT1 A 0 A_StartSound("MechaZombie/Death", CHAN_VOICE, pitch: frandom(1.1, 1.3));
	TNT1 A 0 A_NoBlocking();
	TNT1 A 0 A_Fall();
    FUUU ABCEFGHIJKLMNOPQRSTU 3 Bright
	{
		A_SpawnItemEx("DCY_ImpBigFlame", random(-12, 12), random(-12, 12), random(10, 45), 0, 0, random(2, 6), 0, SXF_NOCHECKPOSITION, failchance: 100);
		A_SpawnItemEx("DCY_ImpFlame1", random(-12, 12), random(-12, 12), random(10, 45), 0, 0, random(2, 6), 0, SXF_NOCHECKPOSITION, failchance: 100);
	}
	FUUU V -1;
	Stop;
  Raise:
    RFTR KJIH 5;
	Goto See;
  }
}

Class DCY_HuntsmanX : DCY_Huntsman { Default { DropItem "DCY_CyberRifle"; } }