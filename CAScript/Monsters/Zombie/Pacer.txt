Class DCY_Pacer : DCY_Zombe
{
  Default
  {
  Translation "168:173=[255,255,255]:[128,255,255]", "0:0=[0,0,0]:[255,255,255]", "174:180=[121,255,255]:[0,128,255]", "181:191=[0,115,230]:[0,0,64]", "224:231=[255,255,255]:[128,255,255]", "208:216=[255,255,255]:[128,255,255]", "217:223=[119,255,255]:[0,128,192]", "232:235=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "236:239=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[128,255,255]:[0,0,128]", "16:23=[255,255,255]:[128,255,255]", "24:31=[111,255,255]:[0,128,255]", "32:47=[0,124,249]:[0,0,64]", "192:195=[255,255,255]:[128,255,255]", "196:201=[106,255,255]:[0,128,255]", "202:207=[0,116,232]:[0,0,64]", "112:119=[0,128,255]:[0,0,128]", "120:127=[0,0,119]:[0,0,64]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  Health 35;
  Tag "\c[Ice]Pacer\c-";
  Speed 12;
  FastSpeed 15;
  Obituary "%o's veins are castrated by the pacer's fast strategies.";
  SeeSound "pacerdcy/sight";
  ActiveSound "pacerdcy/idle";
  PainSound "pacerdcy/pain";
  DeathSound "pacerdcy/death";
  PainChance 128;
  BloodColor "2ba0ff";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NODROPOFF
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"Death by Pacer.",
		"%o's veins are castrated by the pacer's fast strategies.",
		"Blast! Death."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
    DEHU A 10 DCY_Look();
	Loop;
  Look:
	DEHU AABBCCDD 4 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(70, "Dodge");
    DEHU AAABBBCCCDDD 3 A_Chase();
	Loop;
  Dodge:
	DEHU A 5 A_ChangeVelocity(random(-10, 10), 0, 5, CVF_REPLACE);
	DEHU A 5 A_Chase("Missile", "Missile", CHF_DONTMOVE);
	DEHU A 1 A_Stop();
	Goto See;
  Missile:
    DEHU E 9 A_FaceTarget();
    DEHU F 5 Bright A_SpawnProjectile("DCY_PacerTracer",32,5);
	DEHU E 5;
    Goto See;
  Pain:
    DEHU G 1;
    DEHU G 7 A_Pain();
    Goto See;
  Death:
    DEHU G 1;
	DEHU G 9 A_Scream();
    DEHU H 5;
    DEHU I 5 A_NoBlocking();
    DEHU JKLMN 3;
    DEHU N -1;
    Stop;
  XDeath:
	DEHU O 1;
    DEHU O 5 A_XScream();
	DEHU P 4 A_NoBlocking();
	DEHU QRSTUV 4;
	DEHU W -1;
    Stop;
  Death.Fire:
	DEHU G 1
	{
		A_StartSound("pacerdcy/Burn", CHAN_VOICE);
		A_NoBlocking();
		A_Fall();
	}
    DEHU GGGGGGGGGGGGGGGGGGGGGGGGHHHHH 1 A_SpawnItemEx("DCY_ImpBigFlame", random(-12, 12), random(-12, 12), random(10, 45), 0, 0, random(2, 6), 0, SXF_NOCHECKPOSITION);
    DEHU H 5;
    DEHU I 5 A_NoBlocking();
    DEHU JKLMN 3;
    DEHU N -1;
    Stop;
  Raise:
    DEHU MLKJIH 3;
    Goto See;
  }
}

Class DCY_PacerTracer : Actor
{
  Default
  {
  Radius 5;
  Height 12;
  Speed 25;
  Damage 6;
  Projectile;
  +NOGRAVITY
  +BRIGHT
  RenderStyle "Add";
  Alpha 0.8;
  Scale 0.5;
  SeeSound "PLASMA7";
  DeathSound "PLASMX07";
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  }
  States
  {
  Spawn:
    H2P_ A 1 A_SpawnItemEx("DCY_PacerTracerFX", flags: SXF_NOCHECKPOSITION);
    Loop;
  Death:
    H2P_ F 3
	{
		for (int i = 7; i > 0; i--)
			A_SpawnItemEx("DCY_PacerTracerFX", 0, 0, 0, random(-5, 5), random(-5, 5), random(-1, 8), 0, SXF_NOCHECKPOSITION);
	}
    H2P_ FGHI 3;
	Stop;
  }
}

Class DCY_PacerTracerFX : Actor
{
  Default
  {
  +NOCLIP
  +NOBLOCKMAP
  +NOGRAVITY
  +BRIGHT
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  RenderStyle "Add";
  Alpha 1.0;
  }
  
  void A_PacerFade()
  {
	A_FadeOut(0.1);
	A_ChangeVelocity(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
  }
  
  States
  {
  Spawn:
	H2P_ A 1 A_PacerFade();
	H2P_ AA 1 A_FadeOut(0.05);
	H2P_ B 1 A_PacerFade();
	H2P_ BB 1 A_FadeOut(0.05);
	H2P_ C 1 A_PacerFade();
	H2P_ CC 1 A_FadeOut(0.05);
	H2P_ D 1 A_PacerFade();
	H2P_ DD 1 A_FadeOut(0.05);
	H2P_ E 1 A_PacerFade();
	H2P_ EE 1 A_FadeOut(0.05);
	Stop;
  }
}
