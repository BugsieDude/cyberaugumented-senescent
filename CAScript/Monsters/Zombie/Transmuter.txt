Class DCY_Transmuter : DCY_ShotgunZombe
{
  Default
  {
  Health 111;
  Translation "112:159=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "5:12=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "1:2=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "160:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "48:79=%[0.00,0.00,0.00]:[1.03,0.79,0.61]";
  Speed 4;
  FastSpeed 6;
  Tag "\c[Ultramarine]Transmuter\c-";
  PainChance 90;
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NODROPOFF
  +ALWAYSFAST
  +NEVERRESPAWN
  +DONTHARMCLASS
  BloodType "AndroidNeonFluid", "AndroidNeonFluid";
  SeeSound "commandant/sight";
  PainSound "commandant/pain";
  DeathSound "commandant/death";
  ActiveSound "commandant/active";
  DropItem "RocketAmmo";
  DropItem "RocketBox", 128;
  }
  
  int torpedos;
  
  void A_TransmuterDodge(double speed)
  {
	A_ChangeVelocity(frandom(-speed, speed), random(-speed, speed), 3.5, CVF_REPLACE);
  }
  
  void A_TransmuterTorpedo()
  {
	A_StartSound("ROBOMISL", CHAN_WEAPON, 0, 1.0, 0.5);
	A_FaceTarget();
	A_SpawnProjectile("DCY_TransmuterTorpedo", 30, 0, 0);
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"Blue rocket insanity!",
		"%o fell before the might of the Transmuter.",
		"%o didn't see it coming."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
    RZ22 A 10 DCY_Look();
	Loop;
  Look:
	RZ22 AABBCCDD 3 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" G 10 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(80, "Dodge");
    RZ22 AABBCCDD 2 A_Chase();
	Loop;
  Dodge:
    RZ22 A 0 A_TransmuterDodge(10);
	RZ22 AAAAAAAAAAAAAA 1 A_Chase(null, "RareMissile", CHF_DONTMOVE);
	Goto See;
  Dodge2:
	TNT1 A 0 A_TransmuterDodge(7);
	RZ22 AAAAAAAAAAAAAA 1 A_Chase(null, "MissileReturn", CHF_DONTMOVE);
	Goto See;
  RareMissile:
    TNT1 A 0 A_Jump(95, "MissileReturn");
	Goto See;
  Missile:
	TNT1 A 0 
	{
		torpedos = 0;
		Return A_Jump(128, "Dodge2");
	}
	Goto MissileReturn;
  MissileReturn:
	TNT1 A 0 A_Jump(128, "Torpedo", "2Torpedos", "3Torpedos", "TorpedoPlusDodge", "2TorpedosPlusDodge");
	Goto Torpedo;
  Torpedo:
	RZ22 EEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	Goto See;
  2Torpedos:
	RZ22 EEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 E 8;
	RZ22 EEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	Goto See;
  3Torpedos:
	RZ22 EEEEEEEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 E 8;
	RZ22 EEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 E 8;
	RZ22 EEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	Goto See;
  TorpedoPlusDodge:
	RZ22 EEEEEE 1 A_FaceTarget();
	TNT1 A 0 A_TransmuterDodge(9);
	RZ22 EEEEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	Goto See;
  2TorpedosPlusDodge:
    RZ22 EEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 E 8;
	TNT1 A 0 A_TransmuterDodge(9);
	RZ22 EEEEEEEEEEEEEEE 1 A_FaceTarget();
	RZ22 F 8 A_TransmuterTorpedo();
	RZ22 EAAA 8;
	Goto See;
  Pain:
    RZ22 G 3;
	RZ22 G 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
    RZ22 H 1;
	RZ22 H 4 A_Scream();
	RZ22 H 1 A_NoBlocking();
	RZ22 IJKL 6;
	RZ22 L -1;
	Stop;
  XDeath:
    RZ22 M 1;
	RZ22 M 3 A_StartSound("Android/Gib", CHAN_VOICE);
	RZ22 M 1 A_NoBlocking();
	RZ22 NOPQRSTU 5;
	RZ22 U -1;
	Stop;
  Raise:
    RZ22 LKJIH 6;
	Goto See;
  }
}

Class DCY_TransmuterTorpedo : Rocket
{
  Default
  {
  SeeSound "ROBOMISL";
  DeathSound "AOI/Explode";
  Damage 3;
  Speed 17;
  FastSpeed 27;
  -ROCKETTRAIL
  +BRIGHT
  +FORCERADIUSDMG
  +DONTHARMCLASS
  Alpha 1.0;
  RenderStyle "Normal";
  Translation "16:79=%[0.00,0.00,0.00]:[0.42,1.02,2.00]", "112:255=%[0.00,0.00,0.00]:[0.42,1.02,2.00]", "80:111=[255,255,255]:[0,0,0]";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"Blue rocket insanity!",
		"%o fell before the might of the unknown.",
		"%o didn't see it coming."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
	
  States
  {
  Spawn:
    HMIS A 1
	{
		A_SpawnParticle(0xabe9ff, SPF_FULLBRIGHT, random(20, 40), random(9, 13), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.1);
		A_SpawnItemEx("DCY_TransmuterFlare", frandom(-2.5, 2.5), frandom(-2.5, 2.5), frandom(-2.5, 2.5), flags: SXF_NOCHECKPOSITION, failchance: 150);
		A_SpawnItemEx("DCY_TransmuterFlame", frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 210);
	}
	Loop;
  Death:
    BAL3 C 1
	{
		A_SetRenderStyle(0.875, STYLE_ADD);
		
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_AndroidExplosionSmall", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1), random(0, 360), SXF_NOCHECKPOSITION);
		for (int i = random(10, 20); i > 0; i--)
			A_SpawnParticle(0xabe9ff, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(6.5, 12.4), accelz: -1);
		
		A_Explode(30, 85, XF_NOTMISSILE, 1);
	}
		
    BAL3 C 1 Bright A_Explode(30, 85, XF_NOTMISSILE, 1);
	BAL3 CCCDDDDEEEE 1 Bright
	{
	  A_FadeOut(0.085, 0);
	  A_SetScale(Scale.X+0.05, Scale.Y+0.05);
	}
	TNT1 A 1 Bright;
    Stop;
  }
}

Class DCY_TransmuterFlare : DCY_RocketFlare { Default { Translation "0:255=%[0.00,0.00,0.54]:[1.14,2.00,2.00]"; } }
Class DCY_TransmuterFlame : DCY_ImpFlame1 { Default { Translation "0:255=%[0.00,0.00,0.54]:[1.14,2.00,2.00]"; } }