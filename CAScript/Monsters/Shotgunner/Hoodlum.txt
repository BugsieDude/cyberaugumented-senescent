Class DCY_Hoodlum : DCY_SuperShotgunSoldier
{
  Default
  {
  Health 130;
  Speed 10;
  FastSpeed 14;
  SeeSound "HoodGuy/Sight";
  ActiveSound "HoodGuy/Active";
  PainSound "HoodGuy/Pain";
  DeathSound "HoodGuy/Death";
  Tag "Hoodlum Soldier";
  DropItem "SuperShotgun";
  DropItem "Shell";
  Translation "0:0=0:0";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o died in the hood.",
		"The Hoodlum castrated $o's face."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void A_HoodlumChase()
  {
	A_Chase();
		
	if (random(0, 10) == 0)
		A_Dodging(15);
		
	if (random(0, 5) == 0)
	{
		for (int i = 2; i > 0; i--)
			A_Chase();
			
		A_SpawnItemEx("DCY_GTrail", zofs: 4, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
  }
  
  States
  {
  Spawn:
	H00D A 10 DCY_Look();
	Loop;
  Look:
	H00D AABBCCDD 2 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	H00D AABBCCDD 2 A_HoodlumChase();
	Loop;
  Missile:
	H00D E 14
	{
		if (random(0, 1) == 1)
			A_Dodging(20);
		
		A_FaceTarget();
	}
	H00D F 5
	{
		for (int i = 14; i > 0; i--)
			A_SpawnProjectile("DCY_Zombie2Tracer", 33, 0, frandom(-4.5, 4.5), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-3.0, 3.0));
		
		A_FaceTarget();
		A_StartSound("SuperShotgun/Fire", CHAN_WEAPON);
	}
	H00D E 5;
	H00D E 10 A_StartSound("SuperShotgun/Reload", CHAN_WEAPON);
	Goto See;
  Pain:
	H00D G 4;
	H00D G 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
    H00D H 5 A_SetScale(frandompick(Scale.x, -Scale.x), Scale.y);
    H00D I 5 A_ScreamAndUnblock();
    H00D J 4;
    H00D K 4;
    H00D L -1;
    Stop;
  XDeath:
    H00D M 5;
    H00D N 5 A_XScream();
    H00D O 4 A_NoBlocking();
    H00D PQ 4;
    H00D R -1;
	Stop;
  Raise:
    H00D KJ 6;
	H00D IH 5;
	H00D H 0 A_SetScale(Scale.x, Scale.y);
    Goto See;
  }
}
  