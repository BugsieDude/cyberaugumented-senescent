Class DCY_Hoodlum : DCY_SuperShotgunSoldier
{
  Default
  {
  Health 130;
  Speed 10;
  FastSpeed 14;
  SeeSound "HoodGuy/Sight";
  ActiveSound "HoodGuy/Active";
  PainSound "HoodGuy/Pain";
  DeathSound "HoodGuy/Death";
  Tag "Hoodlum Soldier";
  DropItem "SuperShotgun";
  DropItem "Shell";
  Translation "116:127=[0,255,0]:[0,0,0]", "121:121=[0,255,0]:[0,255,0]", "112:116=[0,255,0]:[0,255,0]", "80:111=[255,255,255]:[13,13,13]", "5:8=%[0.00,0.00,0.00]:[0.43,0.43,0.43]", "48:79=%[0.00,0.00,0.00]:[1.11,0.74,0.53]", "16:47=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o died in the hood.",
		"The Hoodlum castrated $o's face."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void A_HoodlumChase()
  {
	bool rushing;
	A_Chase();
		
	if (random(0, 100) == 0)
		A_Dodging(10);
		
	if (random(0, 150) == 0) rushing = true;
	
	if (rushing)
	{
		A_Chase();	
		A_SpawnItemEx("DCY_GTrail", zofs: 4, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
  }
  
  States
  {
  Spawn:
	H00D A 10 DCY_Look();
	Loop;
  Look:
	H00D AABBCCDD 3 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	H00D AABBCCDD random(2, 3) A_HoodlumChase();
	Loop;
  Missile:
	H00D E 14
	{
		if (!random(0, 10))
			A_Dodging(10);
		
		A_FaceTarget();
	}
	H00D F 5
	{
		for (int i = 14; i > 0; i--)
			A_SpawnProjectile("DCY_Zombie2Tracer", 33, 0, frandom(-4.5, 4.5), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-3.0, 3.0));
		
		A_FaceTarget();
		A_StartSound("DCYSuperShotgun/Fire", CHAN_WEAPON);
	}
	H00D E 5;
	H00D E 10 A_StartSound("DCYSuperShotgun/Reload", CHAN_WEAPON, pitch: 1.4);
	Goto See;
  Pain:
	H00D G 4;
	H00D G 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
    H00D H 5 A_SetScale(frandompick(Scale.x, -Scale.x), Scale.y);
    H00D I 5 A_ScreamAndUnblock();
    H00D J 4;
    H00D K 4;
    H00D L -1;
    Stop;
  XDeath:
    H00D M 5;
    H00D N 5 DCY_Gibs();
    H00D O 4 A_NoBlocking();
    H00D PQ 4;
    H00D R -1;
	Stop;
  Raise:
    H00D KJ 6;
	H00D IH 5;
	H00D H 0 A_Restore();
    Goto See;
  }
}
  