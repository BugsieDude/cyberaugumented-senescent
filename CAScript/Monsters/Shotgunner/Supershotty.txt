Class DCY_SuperShotgunSoldier : DCY_ShotgunZombe
{
  Default
  {
  Health 50;
  DeathSound "misc/smallgibbed";
  Tag "Super Shotgun Hell Soldier";
  Obituary "$o got blasted by a Super Shotgun Soldier.";
  DropItem "SuperShotgun", 192;
  SeeSound "SSGZombie/Sight";
  PainSound "SSGZombie/Pain";
  Translation "152:159=%[0.00,0.00,0.00]:[0.98,0.98,0.98]", "9:12=%[0.00,0.00,0.00]:[0.98,0.98,0.98]", "112:127=%[0.00,0.00,0.00]:[0.98,0.98,0.98]";
  }
  States
  {
  Spawn:
	ZSDO A 10 DCY_Look();
	Loop;
  Look:
	ZSDO AABBCCDD 3 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	ZSDO AABBCCDD 4 A_Chase();
	Loop;
  Missile:
	ZSDO E 20 A_FaceTarget();
	ZSDO F 5
	{
		for (int i = 14; i > 0; i--)
			A_SpawnProjectile("DCY_Zombie2Tracer", 33, 0, frandom(-4.5, 4.5), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-3.0, 3.0));
		
		A_StartSound("SuperShotgun/Fire", CHAN_WEAPON);
	}
	ZSDO E 15;
	ZSDO E 30 A_StartSound("SuperShotgun/Reload", CHAN_WEAPON);
	Goto See;
  Pain:
	ZSDO G 4;
	ZSDO G 4 A_Pain();
	Goto See;
  Death:
    ZSDO H 5;
    ZSDO I 5
	{
		if (DCY_EffectDistance(sfxdistance) && dcy_gibbing)
		{
			for (int i = 2; i > 0; i--)
			{
				A_SpawnItemEx("De_Blood_Splat", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4) + 60, vel.x * 1.5 + frandom(-5, 5), vel.y * 1.5 + frandom(-5, 5), vel.z * 1.5 + frandom(1, 5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, 20);
				A_SpawnItemEx("DCY_AndroidGib", frandom(-4, 4), frandom(-4, 4), frandom(-4, 4) + 60, vel.x * 1.5 + frandom(-5, 5), vel.y * 1.5 + frandom(-5, 5), vel.z * 1.5 + frandom(1, 5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION, 20);
			}
		}
		A_ScreamAndUnblock();
	}
    ZSDO J 4;
    ZSDO KLM 4;
    ZSDO N -1;
    Stop;
  XDeath:
    ZSDO O 5;
    ZSDO P 5 DCY_Gibs();
    ZSDO Q 4 A_NoBlocking();
    ZSDO RSTUV 4;
    ZSDO W -1;
	Stop;
  Raise:
    ZSDO KJ 6;
	ZSDO IH 5;
    Goto See;
  }
}