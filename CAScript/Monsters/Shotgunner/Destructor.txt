Class DCY_Destructor : DCY_ShotgunZombe
{
  Default
  {
  Health 110;
  Translation "16:47=[255,0,0]:[0,0,0]", "168:191=[255,0,0]:[0,0,0]", "80:111=[255,255,255]:[0,0,0]", "48:79=%[0.00,0.00,0.00]:[0.95,0.78,0.65]", "9:12=%[0.00,0.00,0.00]:[0.50,0.00,0.00]";
  Tag "\c[Red]Destructor\c-";
  Obituary "%o saw the Destructor's strength.";
  Speed 8;
  FastSpeed 12;
  SeeSound "Destructor/Sight";
  ActiveSound "Destructor/Sight";
  PainSound "Destructor/Pain";
  DeathSound "Destructor/Death";
  +NOTARGET
  +NODROPOFF
  +DONTHARMCLASS
  DropItem "DCY_Eminence";
  }
  
  void A_Destructor()
  {
	A_ChangeVelocity(random(-5, 5), random(-5, 5), 0);
  }
  
  void A_DestructorFire()
  {
	A_FaceTarget();
	A_SpawnProjectile("DCY_DestructorMissile", 30, 0, random(-3, 3), CMF_OFFSETPITCH, pitch + frandom(-3, 3));
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o saw the Destructor's strength.",
		"You got all messed up.",
		"Something has gone wrong."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
	UUU_ A 10 DCY_Look();
	Loop;
  Look:
	UUU_ AABBCCDD 4 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(192, "Dodge");
	UUU_ AABBCCDD 3 A_Chase();
	Loop;
  Dodge:
	UUU_ A 0 A_Destructor();
	UUU_ AA 2 A_Chase(null, null, CHF_DONTMOVE);
	UUU_ A 0 A_Destructor();
	UUU_ BB 2 A_Chase("Missile", "Missile", CHF_DONTMOVE);
	UUU_ A 0 A_Destructor();
	UUU_ CC 2 A_Chase(null, null, CHF_DONTMOVE);
	UUU_ A 0 A_Destructor();
	UUU_ DD 2 A_Chase("Missile", "Missile", CHF_DONTMOVE);
	TNT1 A 0 A_Jump(128, "See");
	Loop;
  Missile:
    TNT1 A 0 A_Jump(80, "More", "Spread");
    UUU_ E 10 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 3 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 10 A_FaceTarget();
	Goto See;
  More:
    UUU_ E 10 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 3 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 3 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 3 A_FaceTarget();
	UUU_ F 3 A_DestructorFire();
	UUU_ E 10 A_FaceTarget();
	Goto See;
  Spread:
    UUU_ E 20 A_FaceTarget();
	UUU_ F 3
	{
		A_FaceTarget();
		A_StartSound("RVNSTAF2", CHAN_WEAPON);
		
		for (int i = 3; i > 0; i--)
			A_SpawnProjectile("DCY_DestructorMissile", 30, 0, random(-7, 7), CMF_OFFSETPITCH|CMF_TRACKOWNER, random(-5, 5));
	}
	UUU_ EEEEAA 3 A_FaceTarget();
	Goto See;
  Pain:
	UUU_ G 4;
	UUU_ G 4
	{
		A_Pain();
		Return A_Jump(80, "PainMissile");
	}
	Goto See;
  PainMissile:
	UUU_ G 2;
    UUU_ G 2
	{
		A_Pain();
		Return A_Jump(100, "More", "Spread", "Missile");
	}
	Goto Missile;
  Death:
  Death.Fire:
    UUU_ H 1;
	UUU_ H 1 A_Scream();
	UUU_ H 6 A_Fall();
	UUU_ IJKLM 6;
	UUU_ N -1;
	Stop;
  XDeath:
    UUU_ O 1;
	UUU_ O 1 A_XScream();
	UUU_ O 5 A_Fall();
	UUU_ PQRSTUV 5;
	UUU_ W -1;
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_DestructorMissile : DCY_ImpFireball
{
  Default
  {
  Damage 7;
  Radius 10;
  Height 10;
  Scale 0.8;
  Speed 20.5;
  FastSpeed 30.5;
  SeeSound "ArgentBall";
  DeathSound "ArgentImpact";
  MissileHeight 6;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.60,0.60]";
  +BRIGHT
  +SEEKERMISSILE
  }
  States
  {
  Spawn:
	ZT__ AAAABBBB 1
	{
		roll += 3;
		A_SpawnItemEx("DCY_Arc", flags: SXF_TRANSFERTRANSLATION, failchance: 120);
		if (!random(0, 60)) { A_SpawnItemEx("DCY_ElecDeathBase", flags: SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION); A_Tracer2(100); }
		A_SpawnParticleEx(0x943232, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.035, sizestep: 0.825);
	}
	Loop;
  Death:
	ZT__ CDEFGHIJKL 2 A_SpawnItemEx("DCY_ElecDeathBase", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_TRANSFERTRANSLATION, failchance: 20);
	Stop;
  }
}