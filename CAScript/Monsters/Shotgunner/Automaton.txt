Class DCY_AutomatonSupervisor : DCY_Zombe
{
  Default
  {
  Health 200;
  Tag "Automaton Supervisor";
  Translation "128:143=%[0.00,0.00,0.00]:[1.06,0.74,0.58]", "13:15=%[0.00,0.00,0.00]:[1.06,0.74,0.58]", "144:151=%[0.00,0.00,0.00]:[0.29,0.34,0.65]", "9:12=%[0.00,0.00,0.00]:[0.00,0.00,0.50]", "168:207=%[0.00,0.00,0.50]:[1.01,2.00,2.00]", "80:111=%[0.00,0.00,0.00]:[0.85,0.85,0.85]", "16:47=%[0.00,0.00,0.50]:[1.02,2.00,2.00]", "208:235=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "248:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "160:167=%[0.00,0.00,0.00]:[1.01,2.00,2.00]", "48:79=%[0.00,0.00,0.00]:[1.06,0.74,0.58]", "236:239=%[0.00,0.00,0.00]:[1.06,0.74,0.58]";
  BloodColor "2ba0ff";
  SeeSound "ZombieEliteSoldier/Sight";
  ActiveSound "ZombieEliteSoldier/Active";
  PainSound "ZombieEliteSoldier/Pain";
  DeathSound "ZombieEliteSoldier/Death";
  DropItem "DCY_UltramodernEnergy", 45;
  Speed 12;
  FastSpeed 14;
  Mass 1000;
  PainChance 100;
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  DamageFactor "Automaton", 0.000;
  Obituary "%o wasn't good enough for the Automaton's arsenal.";
  +MISSILEMORE;
  +QUICKTORETALIATE;
  DropItem "Pistol";
  }
  
  bool deploydroid;
  int missile;
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o wasn't good enough for the supervisor's arsenal.",
		"I guess you could say you we're an automaton opposer."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
    AJLS C 10 DCY_Look();
	Loop;
  Look:
	AJLS AAABBBCCCDDD 3 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	AJLS AAABBBCCCDDD 3 A_Chase();
	Loop;
  Missile:
	AJLS A 0
	{
		if (health < 100 && !deploydroid)
		{
			if ((random(0, 3) == 0))
				SetStateLabel("Deploy");
		}
	}
	Goto Laser;
  Laser:
    AJLS E 25
	{
		missile = 0;
		A_FaceTarget();
	}
	AJLS F 10
	{
		if (missile > 2)
			SetStateLabel("See");
		else
		{
			A_StartSound("AUTOLASR", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.5);
			A_SpawnProjectile("DCY_AutomatonLaser", 32, 0, random(-2, 2), 0, random(-2, 2));
			A_FaceTarget();
			
			missile++;
		}
	}
	AJLS E 10 A_FaceTarget();
	Goto Laser+1;
  Deploy:
	AJLS E 50
	{
		if (deploydroid)
			SetStateLabel("See");
		else
		{
			deploydroid = true;
			A_StartSound("ZombieEliteSoldier/Sight", CHAN_VOICE, CHANF_DEFAULT, 1.0, 0.5);
			A_FaceTarget();
		}
	}
	AJLS E 30
	{
		A_StartSound("Guard2/Hide", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.75);
		A_SpawnItemEx("DCY_AutomatonDroid", flags: SXF_SETMASTER);
		A_SpawnItemEx("DCY_AutomatonDroid2", flags: SXF_SETMASTER);
	}
	Goto See;
  Pain:
    AJLS G 4;
	AJLS G 3 A_Pain();
	Goto See;
  Death:
    AJLS H 1 { if (random(0, 1) == 0) SetStateLabel("DeathAlt"); }
	AJLS H 7 A_ScreamAndUnblock();
	AJLS I 7 A_KillChildren();
	AJLS JKL 7;
	AJLS L -1;
	Stop;
  DeathAlt:
	AJL1 A 1 A_Flip();
	AJL1 A 7 A_ScreamAndUnblock();
	AJL1 B 7 A_KillChildren();
	AJL1 CD 7;
	AJL1 E -1;
	Stop;
  XDeath:
	AJLS M 1;
	AJLS M 6 { A_StartSound("Android/Gib", CHAN_VOICE); A_NoBlocking(); }
	AJLS N 6 A_KillChildren();
	AJLS OPQRST 5;
	AJLS U -1;
	Stop;
  Ice:
    AJLS "#" 0;
	AJLS "#" 0 A_KillChildren("Ice");
	AJLS "#" 5 A_GenericFreezeDeath();
	AJLS "#" 0 A_KillChildren("Ice");
	AJLS "#" 1 A_FreezeDeathChunks();
	Wait;
  Raise:
	AJLS LKJIH 5;
	AJLS H 0 A_Restore();
	Goto See;
  }
}

Class DCY_AutomatonDroid : DCY_SkullParent
{
  Default
  {
  Health 200;
  Painchance 255;
  Speed 0;
  Radius 12;
  Height 26;
  Alpha 1.0;
  RenderStyle "Normal";
  Mass 300;
  -ZDOOMTRANS
  +NOGRAVITY
  +DROPOFF
  +FLOAT
  +DONTHARMSPECIES
  +MISSILEEVENMORE
  +LOOKALLAROUND
  +NEVERRESPAWN
  +FLOATBOB
  +NOBLOOD
  +NOCLIP
  -BRIGHT
  +NOINFIGHTING
  -COUNTKILL
  DamageFactor "Automaton", 0.000;
  Translation "112:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "48:79=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "16:47=%[0.00,0.00,0.00]:[0.00,1.01,2.00]", "80:111=%[0.00,0.00,0.00]:[0.85,0.89,1.16]", "0:15=%[0.00,0.00,0.00]:[0.85,0.89,1.16]";
  Tag "Automator Laser Droid";
  SeeSound "drone/see";
  DeathSound "drone/die";
  ActiveSound "drone/idle";
  AttackSound "";
  PainSound "drone/painI";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  Obituary "%o got fucked up by the Automator's laser droid.";
  }
  
  override void Tick()
  {
	Super.Tick();
	A_Warp(AAPTR_MASTER, 0, 40, 40, 0, WARPF_INTERPOLATE);
  }
	
  States
  {
  Spawn:
    MNDR A 1 A_Look();
	Loop;
  See:
	MNDR A 1 A_Chase();
	Loop;
  Missile:
	MNDR A 1
	{
		A_Chase();
		Return A_Jump(128, "MissileFuck");
	}
	Goto See;
  MissileFuck:
	MNDR AAAAAAAAAAAA 1 A_Chase(null, null);
	MNDR B 1
	{
		A_StartSound("AUTOLASR", CHAN_WEAPON, CHANF_DEFAULT, 1.0, 0.5);
		A_SpawnProjectile("DCY_AutomatonLaser", 10, 0, random(-2, 2), 0, random(-2, 2));
		A_FaceTarget();
	}
	MNDR BBA 1 A_FaceTarget();
	Goto See;
  Pain:
    MNDR A 4
	{
		bTHRUACTORS = false;
		bINVULNERABLE = false;
		bSHOOTABLE = true;
	}
	MNDR A 0 A_Pain();
	Goto See;
  Death:
	MNDR A 3
	{
		bFloatBob = false;
	}
	MNDR A 4 Bright A_Scream();
	MNDR C 5 Bright
	{
		A_SpawnItemEx("DCY_AndroidExplosionSmall", frandom(-1.0, 1.0), frandom(-1.0, 1.0), 17+frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_AndroidExplosionSmall", frandom(-1.0, 1.0), frandom(-1.0, 1.0), 17+frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), flags: SXF_NOCHECKPOSITION);
		A_Fall();
	}
	Stop;
  }
}

Class DCY_AutomatonDroid2 : DCY_AutomatonDroid
{
  override void Tick()
  {
	Super.Tick();
	A_Warp(AAPTR_MASTER, 0, -40, 40, 0, WARPF_INTERPOLATE);
  }
}

Class DCY_AutomatonLaser : DCY_FastProjectile
{
  Default
  {
  DamageFunction 1;
  Speed 32;
  Radius 15;
  Height 11;
  RenderStyle "Add";
  DamageType "Automaton";
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  SeeSound "";
  DeathSound "";
  MissileType "DCY_RipperLaserTrailLong";
  DCY_FastProjectile.CV true;
  DCY_FastProjectile.CR true;
  DCY_FastProjectile.CT true;
  MissileHeight 6;
  +NEVERFAST
  +BRIGHT
  +RIPPER
  +NOEXTREMEDEATH
  -RANDOMIZE
  }
  
  States
  {
  Spawn:
    TNT1 A 1
	{
		A_SpawnFlames("008ea1", 15, smonk: false);
		A_SpawnParticleEx(0x008ea1, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), fadestepf: -2);
	}
	Loop;
  Death:
	THZ4 D 2
	{
		for (int i = 13; i > 0; i--)
			A_SpawnParticleEx(0xaaffff, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 45), 5, frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(3.5, 6.4), accelz: -0.85, fadestepf: 0, sizestep: -0.125);
	}
	THZ4 EFGHIJKLM 2;
	Stop;
  }
}