Class DCY_PrimerialEmir : DCY_HeckBaron
{
  Default
  {
  Health 1300;
  Translation "112:229=%[0.00,0.00,0.00]:[1.69,0.35,0.65]", "16:47=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "48:79=%[0.00,0.00,0.00]:[0.86,0.68,0.48]";
  Speed 15.5;
  FastSpeed 17.5;
  Tag "\c[d7]Primerial Emir\c-";
  Obituary "%o was destroyed to shit by a Primerial Emir.";
  HitObituary "%o was destroyed to shit by a Primerial Emir.";
  SeeSound "Emir/Sight";
  DeathSound "Emir/Death";
  ActiveSound "Emir/Active";
  PainSound "Emir/Pain";
  BloodColor "EB 34 89";
  DamageFactor "EmirWave", 0.00;
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  }
  States
  {
  Spawn:
    LHS_ A 10 DCY_Look();
	Loop;
  Look:
    LHS_ AAABBBCCCDDD 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
    LHS_ AAABBBCCCDDD 3 A_Chase();
	Loop;
  Melee:
  Missile:
	LHS_ E 0 A_Jump(90, "Spread", "Triple");
	LHS_ E 0 A_Jump(87, "Wave", "Quad");
	LHS_ EF 8 A_FaceTarget();
	LHS_ G 8
	{
		A_SpawnProjectile("DCY_EBall", 34, 0, 10);
		A_SpawnProjectile("DCY_EBall", 34, 0, -10);
	}
	LHS_ F 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, 0);
	LHS_ G 8;
	Goto See;
  Triple:
    LHS_ EF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-2, 2));
	LHS_ GF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-2, 2));
	LHS_ GF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-2, 2));
	LHS_ G 12;
	Goto See;
  Quad:
    LHS_ EF 9 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-4, 4));
	LHS_ GF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-4, 4));
	LHS_ GF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-4, 4));
	LHS_ GF 8 A_FaceTarget();
	LHS_ G 0 A_SpawnProjectile("DCY_EBall", 34, 0, random(-4, 4));
	LHS_ G 15;
	Goto See;
  Spread:
    LHS_ EF 10 A_FaceTarget();
	LHS_ G 10
	{
		for (int i = -10; i < 11; i += 10)
			A_SpawnProjectile("DCY_EWave", 34, 0, i);
	}
	Goto See;
  Wave:
    LHS_ FEF 9 A_FaceTarget();
	LHS_ G 10
	{
		for (int i = -40; i < 41; i += 20)
			A_SpawnProjectile("DCY_EWave", 1, i, 0, CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, 0);
	}
	Goto See;
  Pain:
    LHS_ H 4;
	LHS_ H 4 A_Pain();
	TNT1 A 0 A_Jump(45, "Wave");
	Goto See;
  Death:
    LHS_ I 10;
	LHS_ J 8 A_ScreamAndUnblock();
	LHS_ KLMN 8;
	LHS_ O -1 A_BossDeath2();
	Stop;
  Raise:
    LHS_ NMLKJIH 8;
	Goto See;
  }
}

Class DCY_EBall : FastProjectile
{
  Default
  {
  Damage 6;
  Speed 14;
  FastSpeed 20;
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.0;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.12,1.06]";
  Radius 12;
  Height 12;
  +BRIGHT
  }
  
  States
  {
  Spawn:
    BAL7 AB 1
	{
		A_Weave(2, 0, 3, 0);
		A_SpawnItemEx("DCY_PlasmaTrail", frandom(-7.0, 7.0), frandom(-7.0, 7.0), frandom(-7.0, 7.0), zvel: randompick(1, -1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE, failchance: 90);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		for (int i = 10; i > 0; i--)
			A_SpawnParticle(0xfc5d85, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	Stop;
  }
}

Class DCY_EWave : CacodemonBall
{
  Default
  {
  Radius 21;
  Height 0;
  Speed 18;
  FastSpeed 23;
  RenderStyle "Add";
  DamageType "EmirWave";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,1.01]";
  Scale 1.0;
  Alpha 1.0;
  SeeSound "";
  DeathSound "";
  +BRIGHT
  +FLOORHUGGER
  +DONTSPLASH
  +FLOORCLIP
  +RIPPER
  }
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_Explode(3, 35, XF_NOTMISSILE, 1);
		A_SpawnItemEx("DCY_EWaveFire", flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
    TNT1 A 2;
	Stop;
  }
}

Class DCY_EWaveFire : DCY_FAura1 { Default { DamageType "EmirWave"; Scale 1.55; Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,1.01]"; } }