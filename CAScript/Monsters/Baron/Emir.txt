Class DCY_PrimerialEmir : DCY_HeckBaron
{
  Default
  {
  Health 1300;
  Translation "112:229=%[0.00,0.00,0.00]:[1.69,0.35,0.65]", "16:47=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "48:79=%[0.00,0.00,0.00]:[0.86,0.68,0.48]";
  Speed 10.5;
  FastSpeed 12.5;
  Tag "\c[d7]Primerial Emir\c-";
  Obituary "%o was not able to dethrone the Primerial Emir.";
  HitObituary "%o was not able to dethrone the Primerial Emir.";
  SeeSound "Emir/Sight";
  DeathSound "Emir/Death";
  ActiveSound "Emir/Active";
  PainSound "Emir/Pain";
  BloodColor "EB 34 89";
  DamageFactor "EmirWave", 0.00;
  DCYMonster.ScientificName "Se brutis";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  }
  States
  {
  Spawn:
    LHS_ A 10 DCY_Look();
	Loop;
  Look:
    LHS_ AAABBBCCCDDD 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 { if (!random(0, 1)) SetStateLabel("Roar"); A_StartSound(SeeSound, CHAN_VOICE); }
	Goto See;
  Roar:
	LHS_ P 4
	{
		DCY_AlertSound();
		A_FaceTarget();
	}
	LHS_ QPRPQPRPQ 4 A_FaceTarget();
	Goto See;
  See:
    LHS_ AAABBBCCCDDD 3 DCY_Chase();
	Loop;
  Melee:
  Missile:
	LHS_ E 0 A_Jump(67, "Wave");
	LHS_ EF 8 A_FaceTarget();
	LHS_ G 8
	{
		A_SpawnProjectile("DCY_EBall", 34, 0, 10);
		A_SpawnProjectile("DCY_EBall", 34, 0, -10);
	}
	LHS_ Y 8 A_FaceTarget();
	LHS_ Z 0 A_SpawnProjectile("DCY_EBall", 34, 0, 0);
	LHS_ Z 8;
	Goto See;
  Spread:
    LHS_ EF 10 A_FaceTarget();
	LHS_ G 10
	{
		for (int i = -10; i < 11; i += 10)
			A_SpawnProjectile("DCY_EWave", 34, 0, i);
	}
	Goto See;
  Wave:
    LHS_ F 4
	{
		A_StartSound("EmirCharge", CHAN_WEAPON);
		A_FaceTarget();
	}
	LHS_ EEEEEEEEEEEEEEEEEEEEEEEEEEEE 1
	{
		A_SpawnParticleEx(0xfc037f, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 55, 0, 5 + frandom(-2.5, 2.5), 35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -2.325);
		A_FaceTarget();
	}
	LHS_ EF 9 A_FaceTarget(2, 2);
	LHS_ G 10
	{
		A_StartSound("EmirExplode", CHAN_7);
		for (int i = -50; i < 51; i += 50)
			A_SpawnProjectile("DCY_EWave", 1, i, -i / 3, CMF_AIMDIRECTION|CMF_SAVEPITCH|CMF_OFFSETPITCH, 0);
	}
	Goto See;
  Pain:
    LHS_ H 4;
	LHS_ H 4 A_Pain();
	TNT1 A 0 A_Jump(45, "Wave");
	Goto See;
  Death:
    LHS_ I 10;
	LHS_ J 8 A_ScreamAndUnblock();
	LHS_ KLMN 8;
	LHS_ O -1 A_BossDeath2();
	Stop;
  Raise:
    LHS_ NMLKJIH 8;
	Goto See;
  }
}

Class DCY_EBall : DCY_BaronFireball
{
  Default
  {
  Damage 6;
  Speed 14;
  FastSpeed 20;
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.0;
  SeeSound "EmirBall";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.12,1.06]";
  Radius 12;
  Height 12;
  +BRIGHT
  }
  
  States
  {
  Spawn:
    LMBB AAAABBBB 1
	{
		A_Weave(1, 0, 1, 0);
		if (!random(0, 15)) A_SpawnItemEx("DCY_PlasmaTrail", frandom(-7.0, 7.0), frandom(-7.0, 7.0), frandom(-7.0, 7.0), zvel: randompick(1, -1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE, failchance: 90);
		for (int i = 4; i > 0; i--) A_SpawnParticleEx(0xfc037f, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), (Vel.x / 3.75), (Vel.y / 3.75), (Vel.z / 3.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.15);
		A_SpawnParticleEx(0xfc037f, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 26, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: 0.725);
	}
	Loop;
  Death:
	LMBB C 5 Bright A_SpawnParticleEx(0xfc037f, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
	LMBB DEFG 5 Bright;
	Stop;
  }
}

Class DCY_EWave : DCY_MeatballElecball
{
  Default
  {
  Radius 21;
  Height 0;
  Speed 18;
  FastSpeed 23;
  RenderStyle "Add";
  DamageType "EmirWave";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,1.01]";
  Scale 1.0;
  Alpha 1.0;
  SeeSound "EmirFloor";
  DeathSound "";
  +BRIGHT
  +FLOORHUGGER
  +DONTSPLASH
  +FLOORCLIP
  +RIPPER
  }
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_Explode(3, 35, XF_NOTMISSILE, 1);
		A_SpawnItemEx("DCY_EWaveFire", flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
    TNT1 A 2;
	Stop;
  }
}

Class DCY_EWaveFire : DCY_FAura1 { Default { DamageType "EmirWave"; Scale 1.4; Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.00,1.01]"; } }