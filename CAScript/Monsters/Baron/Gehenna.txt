Class DCY_GehennaChasseur : DCY_BaronParent
{
  Default
  {
  Health 1222;
  Tag "\c[e7]Gehenna Chasseur\c-";
  Obituary "%o got burned by a Gehenna Chasseur.";
  HitObituary "%o got burned by a Gehenna Chasseur.";
  SeeSound "Desperado/Sight";
  ActiveSound "Desperado/Active";
  PainSound "Desperado/Pain";
  DeathSound "Desperado/Death";
  +NOGRAVITY
  +FLOAT
  +MISSILEEVENMORE
  +FIRERESIST
  FloatSpeed 9;
  FastSpeed 13;
  Speed 9;
  BloodColor "ad3002";
  BloodType "AndroidNeonFluid";
  DCYMonster.ScientificName "Volans exstirpatio";
  DCYMonster.FootstepSound "";
  }
  
  int bombloop;
  
  void A_GehennaMissile()
  {
	A_FaceTarget();
	A_StartSound("DNRPGFIR", CHAN_WEAPON);
	
	for (int i = 18; i > -20; i -= 36)
		A_SpawnProjectile("DCY_GehennaMissile", 48, i, frandom(-2.0, 2.0));
  }
  
  override void Tick()
  {
    super.Tick();
	
	If (Health > 0 && !IsFrozen() && !sv_effectchoker && DCY_EffectDistance(sfxdistance))
	{
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-2.0, -5.0), flags: SXF_NOCHECKPOSITION, 150);
	  
		A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-1.0, -2.5), flags: SXF_NOCHECKPOSITION, 200);
	}
  }
  
  States
  {
  Spawn:
	HYCY AB 3 DCY_Look();
	Loop;
  Look:
    HYCY ABABAB 3 DCY_lookingForPlayer();
    TNT1 A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	HYCY ABABAB 3 DCY_Chase();
	Loop;
  Melee:
  Missile:
	HYCY C 1 A_Jump(192, "Minigunner", "Rockets", "LaserBombs");
	Goto Rockets;
  Minigunner:
	HYCY CCCCCCCCCCCCCCC 1 A_FaceTarget();
	HYCY C 1 A_StartSound("DSUHH", CHAN_WEAPON, CHANF_OVERLAP, 1.0, 0.45);
	HYCY CCCCCCCCCCCCCCCCCCCCCC 1
	{
		A_CustomRailgun(0, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING, 2, sparsity: 70, spawnclass: "DCY_GehennaTrack", spawnofs_z: 40);
		A_FaceTarget();
	}
	HYCY FCFCFCFCFCFCFCF 3
	{
		for (int i = 13; i > -14; i -= 26)
			A_SpawnProjectile("DCY_AlacritousGuyBullet", 70, i, frandom(-1.2, 1.2), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(1.2, -1.2));
		
		A_StartSound("PIS5FIRE", CHAN_WEAPON);
		A_FaceTarget();
	}
	Goto See;
  Rockets:
	HYCY CCCCCCCCCCCCCCC 1 A_FaceTarget();
	HYCY E 5 A_GehennaMissile();
	HYCY C 4 A_FaceTarget();
	HYCY E 5 A_GehennaMissile();
	HYCY C 4 A_FaceTarget();
	HYCY E 5 A_GehennaMissile();
	HYCY C 4 A_FaceTarget();
	HYCY E 5 A_GehennaMissile();
	HYCY C 15;
	Goto See;
  LaserBombs:
	HYCY C 1 { bombloop = random(2, 4); }
	HYCY CCCCCCCCCCCCCC 1 A_FaceTarget();
	HYCY F 3
	{
		for (int i = 14; i > -15; i -= 28)
			A_SpawnProjectile("DCY_GehennaBomb", 70, i, frandom(-1.8, 1.8), CMF_OFFSETPITCH|CMF_TRACKOWNER, -2 + frandom(1.2, -1.2));
		
		A_FaceTarget();
		bombloop--;
	}
	HYCY C 3
	{
		if (bombloop <= 0)
			SetStateLabel("See");
	}
	Goto LaserBombs+10;
  Pain:
	HYCY G 3;
	HYCY G 3 A_Pain();
	Goto See;
  Death:
	HYCD A 1;
	HYCD A 8 A_Scream();
	HYCD B 6;
	HYCD CD 4;
	HYCD E 4 A_SpawnItemEx("DCY_Explosion_1_Biggie", zofs: 30, flags: SXF_NOCHECKPOSITION);
	HYCD FG 4;
	HYCD H 4 A_BossDeath2();
	Stop;
  }
}

Class DCY_GehennaMissile : DCY_OverhauledRocket
{
  Default
  {
  Speed 20;
  Scale 0.9;
  Radius 8;
  Height 5;
  DamageFunction (0);
  DamageType "Fire";
  SeeSound "DNROCKET";
  Translation "0:255=%[0.18,0.00,0.00]:[2.00,1.01,0.00]", "48:159=%[0.00,0.00,0.00]:[1.51,1.51,1.51]", "5:15=%[0.00,0.00,0.00]:[1.51,1.51,1.51]", "236:239=%[0.00,0.00,0.00]:[1.51,1.51,1.51]";
  Obituary "%o got inferno burned by a Gehenna Chasseur.";
  +BRIGHT;
  }
  
  States
  {
  Spawn:
	HMIS A 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnFlames("c95a0a", 25, 32, 0, allsmoke: true);
			if ((level.time % random(1, 9)) == 0) A_SpawnParticleEx(0xc95a0a, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 80, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.2, 0.2), frandom(-0.2, 0.2), frandom(-0.2, 0.2), fadestepf: 0, sizestep: -0.125);			
			A_SpawnItemEx("DCY_RocketFlare", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.51, 0.51), frandom(-0.51, 0.51), frandom(-0.51, 0.51), random(0, 360), SXF_NOCHECKPOSITION);
		}
		
		A_Weave(1, 1, 1, 1);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, 100, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), startalphaf: 0.05, fadestepf: 0.0007, sizestep: 0.2, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
		A_SetTranslucent(0.865, 2);
		A_Explode(30, 80, XF_NOTMISSILE, 1);
	}
    Stop;
  }
}