Class DCY_CorruptInfiltrator : DCY_HeckBaron
{
  Default
  {
  Translation "112:127=%[0.00,0.00,0.00]:[1.71,1.45,0.00]", "16:79=%[0.00,0.00,0.00]:[2.00,1.52,0.50]", "128:191=%[0.00,0.00,0.00]:[2.00,1.52,0.50]", "208:239=%[0.00,0.00,0.00]:[1.51,1.51,1.51]";
  Health 2000;
  +BOSSDEATH
  +MISSILEMORE
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NODROPOFF
  +DONTRIP
  +BRIGHT
  Speed 12.35;
  Mass 550;
  DropItem "DCY_Palisade", 70;
  DropItem "DCY_Crosscut", 70;
  DropItem "DCY_Deterrence", 70;
  DropItem "Blursphere", 70;
  BloodColor "Gray";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  StencilColor "Yellow";
  Tag "\c[Gold]Corr\c[DarkGray]upt \c[z3]Infil\c[White]trator\c-";
  Obituary "%o predicted his outcome.";
  HitObituary "%o was brutalized by the Corrupt Infiltrator.";
  SeeSound "CI/Sight";
  ActiveSound "CI/Active";
  PainSound "CI/Pain";
  DeathSound "CI/Death";
  }
  
  int looping;
  int dash;
  
  void A_CorruptChase()
  {
	A_Chase();
	
	if (dash == 10)
		SetStateLabel("Dodge");
	
	dash = random(0, 60);
  }
	
  States
  {
  Spawn:
	ARHO A 10 DCY_Look();
    Loop;
  Look:
	ARHO A 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO AAA 2 DCY_LookingForPlayer();
	ARHO B 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO BBB 2 DCY_LookingForPlayer();
	ARHO C 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO CCC 2 DCY_LookingForPlayer();
	ARHO D 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO DDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	ARHO N 10
	{
		DCY_AlertSound();
		A_FaceTarget();
	}
	ARHO MMMMMMMMMMMMM 4
	{
		A_QuakeEx(3, 3, 1, 20, 0, 600, "", QF_3D|QF_SCALEDOWN);
		A_FaceTarget();
	}
	Goto See;
  See:
	ARHO A 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO AAA 2 A_CorruptChase();
	ARHO B 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO BBB 2 A_CorruptChase();
	ARHO C 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO CCC 2 A_CorruptChase();
	ARHO D 0 A_SpawnItemEx("DCY_WhiteWalkFlames", 0, 0, 0, 1, 0, 0, 0, SXF_NOCHECKPOSITION);
	ARHO DDD 2 A_CorruptChase();
	Loop;
  Dodge:
	ARHO M 7;
	ARHO M 7
	{
		A_StartSound("FF4AFADE", CHAN_BODY, attenuation: 0.75);
		A_Dodging(80);
		A_SetRenderStyle(1.0, STYLE_AddStencil);
	}
	Goto DodgeLoop;
  DodgeLoop:
	ARHO M 1 A_SpawnItemEx("DCY_CorruptInfiltratorTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	ARHO M 1
	{
		If(looping >= 15)
		{
		Return ResolveState("Stopp");
		}
		Else
		{
		A_SpawnItemEx("DCY_CorruptInfiltratorTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		ThrustThing(Angle, 60, 0, 0);
		looping++;
		Return ResolveState(null);
		}
	}
	Loop;
  Stopp:
	ARHO A 5
	{
		A_Stop();
		A_SetRenderStyle(1.0, STYLE_Normal);
		looping = 0;
	}
	Goto See;
  Melee:
	ARHO EF 4 Bright A_FaceTarget();
    ARHO G 3 Bright A_CustomMeleeAttack(randompick(40, 50), "baron/melee", "", "CorruptInf");
    ARHO H 2 Bright;
    ARHO IJ 4 Bright A_FaceTarget();
    ARHO K 3 Bright A_CustomMeleeAttack(randompick(40, 50), "baron/melee", "", "CorruptInf");
    ARHO L 2 Bright;
	Goto Missile;
  Missile:
	ARHO F 0 A_JumpIf(target && Distance3D(target) < 256, "Smash");
	ARHO FE 4 A_FaceTarget();
	ARHO EEEE 5 A_SpawnProjectile("DCY_CorruptInfiltratorBall", 48, 13, frandom(-1.5, 1.5));
	ARHO E 10
	{
		for (int i = 3; i > 0; i--)
			A_SpawnProjectile("DCY_CorruptInfiltratorBall", 48, 13, frandom(-10.5, 10.5));
	}
	ARHO F 6;
	Goto See;
  MissileI:
	Goto Missile+2;
  Smash:
	ARHO O 0 A_Jump(128, "MissileI");
	ARHO ON 5 A_FaceTarget();
	ARHO M 2
	{
		bNoGravity = true;
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_ChangeVelocity(0, 0, 40, CVF_RELATIVE|CVF_REPLACE);
	}
	ARHO MMMMM 1 A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	ARHO N 10 A_Stop();
	ARHO O 2
	{
		A_ChangeVelocity(15, 0, -29, CVF_RELATIVE|CVF_REPLACE);
		A_FaceTarget();
	}
	ARHO O 2
	{
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_ChangeVelocity(1, 0, -7, CVF_RELATIVE);
	}
	ARHO O 1
	{
		if (pos.z <= floorz)
			Return ResolveState("SLAMGOOO");
			
		A_ChangeVelocity(0, 0, -7, CVF_RELATIVE);
		Return ResolveState(null);
	}
	Goto Smash+12;
  SLAMGOOO:
	ARHO O 2
	{
		bNoGravity = false;
		A_Stop();
		A_QuakeEx(0, 0, 7, 60, 0, 500, "none", QF_SCALEDOWN, falloff: 1000);
		A_Explode(500, 128, XF_NOTMISSILE, 0);
		A_StartSound("Misc/BigSmash", CHAN_BODY, attenuation: 0.5);
		A_StartSound("Misc/BigSmash", CHAN_5, attenuation: 0.5);
	}
	ARHO OOOOOOOOOOOONN 2 A_JumpIf(target && Distance3D(target) < 128, "Yeet");
	Goto See;
  YEET:
	ARHO N 0 A_Stop();
	ARHO N 0 A_Jump(100, "MissileI");
	ARHO NM 5 A_FaceTarget();
	ARHO MMM 4
	{
		A_Recoil(-20);
	}
	ARHO M 0 A_Stop();
	Goto See;
  Pain:
	ARHO Q 4 A_SetRenderStyle(1.0, STYLE_Normal);
	ARHO Q 4
	{
		bNoGravity = false;
		A_Pain();
	}
	Goto See;
  Death:
	ARHO Q 2;
	ARHO Q 15
	{
		bNoGravity = false;
		A_Scream();
	}
	ARHO Q 0
	{
		bNoGravity = true;
		A_ChangeVelocity(0, 0, 0.5, CVF_RELATIVE);
	}
	ARHO RRRRRRRR 2 A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	ARHO R 2 A_Stop();
	ARHO RS 20 A_SpawnItemEx("DCY_CorruptInfiltratorTrail", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
	ARHO ST 3;
	ARHO U 3
	{
		A_NoBlocking();
		A_StartSound("Archon/Explode", CHAN_BODY, 0, 1.0, 0.45);
		A_SpawnItemEx("DCY_CorruptInfiltratorExplosion", 0, 0, 38, 0, 0, 0, SXF_NOCHECKPOSITION);
	}
	ARHO VWXYZ 3;
	ARHO "[" 3 A_BossDeath2();
	Stop;
  Death.Massacre:
	ARHO Q 2;
	ARHO Q 15
	{
		bNoGravity = false;
		A_Scream();
	}
	ARHO RS 20;
	ARHO ST 3;
	ARHO U 3;
	ARHO VWXYZ 3;
	ARHO "[" 3 A_BossDeath2();
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_CorruptInfiltratorTrail : DCY_CalamityTsarSuperAura
{
  Default
  {
  RenderStyle "AddStencil";
  StencilColor "9f 8a 3c";
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  }
  
  States
  {
  Spawn:
    ARHO "##########" 1 { A_SetScale(Scale.X+0.025, Scale.Y+0.025); A_FadeOut(0.1, 0); }
	Stop;
  }
}

Class DCY_CorruptInfiltratorBall : DCY_TsarBall
{
  Default
  {
  Speed 14.55;
  FastSpeed 18.55;
  SeeSound "Corrupt/Flame";
  BounceSound "Corrupt/Flame";
  DeathSound "FIREBOOM";
  BounceType "Hexen";
  +CANBOUNCEWATER
  +BOUNCEONWALLS
  +BOUNCEONCEILINGS
  +BOUNCEONFLOORS
  BounceCount 2;
  BounceFactor 21.55;
  Scale 0.8;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.43]";
  }
  
  States
  {
  Spawn:
	FHED AAABBBCCCDDD 1
	{
		A_SpawnItemEx("DCY_ProjectileBaseTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSPRITEFRAME);
		A_Weave(3, 3, 1, 1);
	}
	Loop;
  Death:
	HBAL E 1
	{
		for (int i = 10; i > 0; i--)
			A_SpawnParticle(0xfff9c9, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
	}
	HBAL E 2 A_SpawnItemEx("DCY_Explosion_1_Small", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERTRANSLATION);
	HBAL FGHI 2;
	Stop;
  }
}

Class DCY_CorruptInfiltratorComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.37]";
  DamageFunction 20;
  Speed 30;
  FastSpeed 35;
  +BOUNCEONWALLS
  +BOUNCEONCEILINGS
  +BOUNCEONFLOORS
  BounceType "Doom";
  BounceCount 2;
  BounceFactor 2;
  }
}
