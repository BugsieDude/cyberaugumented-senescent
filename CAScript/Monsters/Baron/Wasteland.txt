Class DCY_WastelandInfiltrator : DCY_BaronParent
{
  Default
  {
  Translation "168:173=[255,255,255]:[255,255,128]", "174:180=[255,255,128]:[255,121,0]", "181:191=[255,121,0]:[64,9,0]", "224:231=[255,255,255]:[255,255,128]", "208:216=[255,255,255]:[255,255,128]", "217:223=[255,255,128]:[255,121,0]", "232:235=%[0.00,0.00,0.00]:[2.00,0.95,0.00]", "236:239=%[0.00,0.00,0.00]:[0.48,0.48,0.48]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[255,255,128]:[64,9,0]", "16:23=[255,255,255]:[255,255,128]", "24:31=[255,255,128]:[255,121,0]", "32:47=[255,121,0]:[64,9,0]", "192:195=[255,255,255]:[255,255,128]", "196:201=[255,255,128]:[255,121,0]", "202:207=[255,121,0]:[64,9,0]", "112:119=[255,255,128]:[255,121,0]", "120:127=[255,121,0]:[64,9,0]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.50,0.07,0.00]:[2.00,2.00,1.01]";
  Health 1500;
  Speed 9;
  Radius 24;
  Height 64;
  PainChance 50;
  Mass 1000;
  Meleedamage 15;
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "ad3002";
  SeeSound "wastelandinfiltrator/sight";
  PainSound "wastelandinfiltrator/pain";
  DeathSound "wastelandinfiltrator/death";
  ActiveSound "wastelandinfiltrator/active";
  MeleeSound "knight/melee";
  DamageType "WastelandInfiltrator";
  DamageFactor "WastelandInfiltrator", 0.000000;
  +FLOORCLIP
  +NOBLOOD
  +NEVERRESPAWN
  +BOSSDEATH
  Tag "\c[Fire]FIRE-STORMER\c- ''Codeword - \c[z3]Wasteland \c[x1]Infiltrator\c-''";
  Obituary "%o got outsmarted by the Firestormer... in a infernal way.";
  HitObituary "It's the Firestormer's melee flaming move! And %o is dead.";
  DCYMonster.WalkFlames "DCY_WalkFlames";
  DCYMonster.GibType "DCY_GibletsLava";
  }
  
  void A_WastelandShotty()
  {
	A_StartSound("Underline/Fire", CHAN_WEAPON);
	
	for (int i = 7; i > 0; i--)
		A_SpawnProjectile("DCY_AlacritousGuyBullet", 45, 12, frandom(-3.2, 3.2), CMF_AIMOFFSET|CMF_TRACKOWNER, frandom(-2.5, 2.5));
		
	A_FaceTarget();
  }
  
  States
  {
  Spawn:
	ZRCY A 10 DCY_Look();
    Loop;
  Look:
	ZRCY AAABBB 3 DCY_LookingForPlayer();
	ZRCY C 0
	{
		A_WalkFlame();
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY, CHANF_DEFAULT);
	}
	ZRCY CCCDDD 3 DCY_LookingForPlayer();
	ZRCY A 0
	{
		A_WalkFlame();
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY, CHANF_DEFAULT);
	}
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	ZRCY AAABBB 3 A_Chase();
	ZRCY C 0
	{
		A_WalkFlame();
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY, CHANF_DEFAULT);
	}
	ZRCY CCCDDD 3 A_Chase();
	ZRCY A 0
	{
		A_WalkFlame();
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY, CHANF_DEFAULT);
	}
	Loop;
  Melee:
	ZRCY HI 5 A_FaceTarget();
	ZRCY J 5 A_CustomMeleeAttack(5 * random(5, 10), "baron/melee");
	ZRCY I 5 A_FaceTarget();
	Goto See;
  Missile:
	ZRCY H 0 A_Jump(144, "Autogun", "Grenades");
	Goto Comets;
  Comets:
	ZRCY H 0 A_JumpIf(target && Distance3D(target) < 220, "FlameThrower");
	ZRCY HI 5 A_FaceTarget();
	ZRCY J 5
	{
		A_SpawnProjectile("DCY_WastelandComet", 30);
		A_SpawnProjectile("DCY_WastelandComet", 30, 0, randompick(-10, 10), CMF_OFFSETPITCH, pitch + frandom(-2, 2));
		A_SpawnProjectile("DCY_WastelandComet", 30, 0, 0, CMF_OFFSETPITCH|CMF_TRACKOWNER, randompick(-2, -8));
	}
	ZRCY I 5 A_FaceTarget();
	Goto See;
  FlameThrower:
	ZRCY E 25 A_FaceTarget();
	ZRCY FFFFFFFFFFFFFF 3
	{
		A_StartSound("ROBOFIRE", CHAN_WEAPON, attenuation: 0.35);
		A_SpawnProjectile("DCY_WastelandFlame", 45, 12, 0);
		A_FaceTarget();
	}
	ZRCY EA 10;
	Goto See;
  Autogun:
	ZRCY E 0 A_JumpIf(target && Distance3D(target) < 500, "Comets");
	ZRCY E 30 A_FaceTarget();
	ZRCY F 3 A_WastelandShotty();
	ZRCY E 6 A_FaceTarget();
	ZRCY F 3 A_WastelandShotty();
	ZRCY E 6 A_FaceTarget();
	ZRCY F 3 A_WastelandShotty();
	ZRCY E 6 A_FaceTarget();
	ZRCY F 3 A_WastelandShotty();
	ZRCY E 6 A_FaceTarget();
	Goto See;
  Grenades:
	ZRCY A 1;
	ZRCY E 0 A_JumpIf(target && Distance3D(target) < 400, "GrenadesStart");
	Goto Comets;
  GrenadesStart:
	ZRCY E 30 A_FaceTarget();
	ZRCY FEFEFEFEFEF 3
	{
		A_StartSound("ROBOMISL", CHAN_WEAPON);
		A_SetTics(random(3, 6));
		A_SpawnProjectile("DCY_WastelandGrenade", 45, 12, frandom(-6.2, 6.2), CMF_OFFSETPITCH, frandom(-5.5, -43.5));
		A_FaceTarget();
	}
	ZRCY EAA 10;
	Goto See;
  Pain:
	ZRCY G 4;
	ZRCY G 4 A_Pain();
	Goto See;
  Death:
	ZRCY G 1;
	ZRCY G 38 A_Scream();
	ZRCY K 7;
	ZRCY K 2
	{
		DCY_Gibs(false);
		A_StartSound("Archon/Explode", CHAN_BODY, attenuation: 0.45);
		A_SpawnItemEx("DCY_Explosion_1_Biggie", zofs: 25, flags: SXF_NOCHECKPOSITION);
		A_NoBlocking();
	}
	ZRCY LMNOP 6;
	ZRCY P -1 A_BossDeath2();
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_WastelandGrenade : DoomImpBall
{
  Default
  {
  Radius 10;
  Height 10;
  Speed 25;
  DamageFunction 25;
  BounceType "Hexen";
  Projectile;
  -NOGRAVITY
  +FORCEXYBILLBOARD
  +DONTHARMCLASS
  +CANBOUNCEWATER
  +BOUNCEONWALLS
  +BOUNCEONFLOORS
  +BOUNCEONCEILINGS
  BounceCount 5;
  RenderStyle "Add";
  Alpha 1.0;
  SeeSound "";
  DeathSound "";
  Translation "0:255=%[0.45,0.17,0.05]:[2.00,1.85,1.02]";
  }
  
  States
  {
  Spawn:
	ETV6 AB 1 A_SpawnItemEx("DCY_WastelandBallTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Loop;
  Death:
	ETV6 B 1 A_SetRenderStyle(1, STYLE_ADD);
	ETV6 ABABABABABABABABABABABAB 1;
	TNT1 A 1
	{
		A_Explode(35, 69, XF_NOTMISSILE, 0);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION);
			
		A_StartSound("ROBOEXPL", CHAN_BODY, 0, 1, 0.5);
	}
	Stop;
  }
}

Class DCY_WastelandBallTrail : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 1.0;
  +NOINTERACTION
  -SOLID
  +BRIGHT
  }
  
  States
  {
  Spawn:
	ETV6 B 0 A_JumpIf(Scale.X < 0.0000001, "Stahp");
	ETV6 BA 1 A_SetScale(Scale.X - 0.075, Scale.Y - 0.075);
	Loop;
  Stahp:
	TNT1 A 1;
	Stop;
  }
}

Class DCY_WastelandComet : DCY_GeneralComet
{
  Default
  {
  DamageType "Fire";
  Speed 25;
  FastSpeed 34;
  Translation "0:255=%[0.24,0.00,0.00]:[2.00,1.22,0.35]";
  +SEEKERMISSILE
  }
  
  States
  {
  Spawn:
    CGMT AABBCC 1 Bright
	{
		if ((level.time % random(1, 5)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, 50, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 1), startalphaf: 0.25, fadestepf: 0.0014, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
		
		A_Tracer2(3);
		
		roll += r;
		A_SpawnItemEx("DCY_CometTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), vel.x / 4, vel.y / 4, vel.z / 4, flags: SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
	}
    Loop;
  Death:
    CGMT D 0 Bright;
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(42, 100, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, 0, 0, 0, 400);
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_SetTranslucent(1.0, 1);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_MonochromaticExplosion", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.25, Scale.Y+0.25);
	TNT1 A 25;
    Stop;
  }
}