Class DCY_Excoriator : DCY_BaronParent
{
  Default
  {
  Health 1500;
  Tag "\c[x1]Excoriator\c-";
  Translation "168:173=[255,255,255]:[255,255,128]", "174:180=[255,255,128]:[255,121,0]", "181:191=[255,121,0]:[64,9,0]", "224:231=[255,255,255]:[255,255,128]", "208:216=[255,255,255]:[255,255,128]", "217:223=[255,255,128]:[255,121,0]", "232:235=%[0.00,0.00,0.00]:[2.00,0.95,0.00]", "236:239=%[0.00,0.00,0.00]:[0.48,0.48,0.48]", "48:79=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "128:159=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "13:15=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "160:167=[255,255,128]:[64,9,0]", "16:23=[255,255,255]:[255,255,128]", "24:31=[255,255,128]:[255,121,0]", "32:47=[255,121,0]:[64,9,0]", "192:195=[255,255,255]:[255,255,128]", "196:201=[255,255,128]:[255,121,0]", "202:207=[255,121,0]:[64,9,0]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.50,0.07,0.00]:[2.00,2.00,1.01]";
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  Speed 12;
  FastSpeed 18;
  DamageType "Fire";
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.25;
  SeeSound "Excoriator/Sight";
  ActiveSound "Excoriator/Active";
  PainSound "Excoriator/Pain";
  DeathSound "Excoriator/Death";
  +BRIGHT
  +NOGRAVITY
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +FLOAT
  }
  
  void Napalm()
  {
	A_StartSound("Grenade/Launch", CHAN_WEAPON);
	A_SpawnProjectile("DCY_WeakNapalm", 30, 12, frandom(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, -10);
	A_FaceTarget();
  }
  
  void AcroComet()
  {
	A_SpawnProjectile("DCY_AComet", 27);
	A_FaceTarget();
  }
  
  void A_AMissile()
  {
	A_FaceTarget();
	A_StartSound("DNRPGFIR", CHAN_WEAPON);
	A_SpawnProjectile("DCY_GehennaMissile", 48, 12, frandom(-2.0, 2.0));
  }
  
  void A_MM()
  {
	A_FaceTarget();
	A_StartSound("BOSSMISL", CHAN_WEAPON);
	A_SpawnProjectile("DCY_AMissile", 48, 12, frandom(-40.0, 40.0), CMF_OFFSETPITCH, pitch + frandom(-30, 30));
  }
  
  override void Tick()
  {
    super.Tick();
	
	If (Health > 0 && !IsFrozen() && !sv_effectchoker)
	{
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-2.0, -5.0), flags: SXF_NOCHECKPOSITION, 140);
	  
		A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-1.0, -2.5), flags: SXF_NOCHECKPOSITION, 220);
	}
  }
  
  bool rushing;
  int mmissile;
  
  States
  {
  Spawn:
	FR6T AB 2 A_Look();
	Loop;
  See:
	FR6T AB 2
	{
		if (rushing == true)
		{
			tics = 1;
			A_SpawnItemEx("DCY_ProjectileBaseTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE);
		}
		else
			tics = 2;
		
		A_Chase();
			
		Return A_Jump(1, "Rush");
	}
	Loop;
  Rush:
	FR6T A 0 { return (rushing == 0) ? true : false; }
	Goto See;
  Melee:
	FR6T EF 6 A_FaceTarget();
	FR6T G 5 A_CustomMeleeAttack(10 * random(3, 7), "Baron/Melee");
	FR6T F 6;
	Goto See;
  Missile:
	FR6T C 0
	{
		if (target && distance3D(target) > 400)
			Return A_Jump(80, "Napalm", "Comet", "MiniMissile");
		
		Return A_Jump(80, "Comet", "MiniMissile");
	}
	Goto Rockets;
  MiniMissile:
	FR6T C 20 { mmissile = 0; A_FaceTarget(); }
	FR6T D 3 Bright A_MM();
	FR6T C 2 Bright
	{
		if (mmissile > 15)
			SetStateLabel("See");
		
		mmissile++;
		A_FaceTarget();
	}
	Goto MiniMissile+1;
  Rockets:
	FR6T C 15 A_FaceTarget();
	FR6T D 5 Bright A_AMissile();
	FR6T C 4 Bright A_FaceTarget();
	FR6T D 5 Bright A_AMissile();
	FR6T C 4 Bright A_FaceTarget();
	FR6T D 5 Bright A_AMissile();
	FR6T C 4 Bright A_FaceTarget();
	FR6T D 5 Bright A_AMissile();
	FR6T C 15 Bright;
	Goto See;
  Napalm:
	FR6T C 15 A_FaceTarget();
	FR6T D 5 Napalm();
	FR6T C 12;
	Goto See;
  Comet:
	FR6T EF 6 A_FaceTarget();
	FR6T G 6 AcroComet();
	FR6T F 6 A_FaceTarget();
	Goto See;
  Pain:
	FR6T H 4;
	FR6T H 4 A_Pain();
	Goto See;
  Death:
	FR6T I 2;
	FR6T I 4 A_Scream();
	FR6T JK 5;
	FR6T L 5
	{
		A_SpawnItemEx("DCY_Explosion_1_Biggie", zofs: 30, flags: SXF_NOCHECKPOSITION);
		A_NoBlocking();
		A_StartSound("Archon/Explode", CHAN_BODY);
	}
	FR6T MNO 5;
	Stop;
  Death.Massacre:
	FR6T I 2;
	FR6T I 4 A_Scream();
	FR6T JK 5;
	FR6T L 5 A_NoBlocking();
	FR6T MNO 5;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_AComet : DCY_GComet
{
  Default
  {
  DamageType "Fire";
  +SEEKERMISSILE
  Translation "112:255=%[0.37,0.00,0.00]:[2.00,1.53,0.54]";
  Speed 15;
  +NEVERFAST
  Scale 0.76;
  SeeSound "TORPMISL";
  DeathSound "";
  }
  
  States
  {
  Spawn: 
    AC_Z AAAABBBBCCCC 1 Bright
	{
		A_Tracer2(3);
		A_SpawnItemEx("DCY_ACometTrail", flags: SXF_NOCHECKPOSITION);
	}
    Loop;
  Death:
	TNT1 A 2
	{
		A_Explode(77, 100, XF_NOTMISSILE, 1);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, falloff: 400);
		A_SetTranslucent(1.0, 1);
		
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_NOCHECKPOSITION);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
	}
	TNT1 A 25;
    Stop;
  }
}

Class DCY_ACometTrail : DCY_GCTrail { Default { Translation "0:255=%[0.37,0.00,0.00]:[2.00,1.53,0.54]"; Scale 0.76; } }

Class DCY_AMissile : DCY_CyberMicromissile
{
  Default
  {
  SeeSound "BOSSMISL";
  Speed 4;
  +NEVERFAST
  Damage 6;
  Scale 0.8;
  Translation "192:207=%[0.50,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  int timer;
  
  States
  {
  Spawn:
	MMIS A 1
	{
		if (timer == 40)
		{
			A_SeekerMissile(999, 999);
			A_ScaleVelocity(8);
		}
		
		A_SpawnItemEx("DCY_MastermindJetFlareSmall", flags: SXF_NOCHECKPOSITION);
		timer++;
	}
	Loop;
  }
}