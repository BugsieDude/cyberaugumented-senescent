Class DCY_Excoriator : DCY_BaronParent
{
  Default
  {
  Health 1500;
  Tag "\c[x1]Excoriator\c-";
  Translation "80:111=[255,255,255]:[0,0,0]", "1:15=%[0.00,0.00,0.00]:[0.09,0.09,0.09]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "160:255=%[0.37,0.00,0.00]:[1.99,0.59,0.00]", "16:79=%[0.00,0.00,0.00]:[2.00,0.80,0.00]";  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  DCYMonster.GibType "DCY_GibletsLava";
  BloodColor "ad3002";
  Speed 6;
  FastSpeed 12;
  DamageType "Fire";
  DamageFactor "Fire", 0.00;
  DamageFactor "Ice", 1.25;
  SeeSound "Excoriator/Sight";
  ActiveSound "Excoriator/Active";
  PainSound "Excoriator/Pain";
  DeathSound "Excoriator/Death";
  DCYMonster.FootstepSound "";
  +NOGRAVITY
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +FLOAT
  DCYMonster.ScientificName "Volans eversor";
  }
  
  void Napalm()
  {
	A_StartSound("Grenade/Launch", CHAN_WEAPON);
	A_SpawnProjectile("DCY_Napalm", 30, 12, frandom(-2, 2), CMF_OFFSETPITCH|CMF_TRACKOWNER, -10);
	A_FaceTarget();
  }
  
  void AcroComet()
  {
	A_SpawnProjectile("DCY_AComet", 27);
	A_FaceTarget();
  }
  
  void A_AMissile()
  {
	A_FaceTarget();
	A_StartSound("DNRPGFIR", CHAN_WEAPON);
	A_SpawnProjectile("DCY_GRocket", 48, 12, frandom(-2.0, 2.0));
  }
  
  void A_MM()
  {
	A_FaceTarget();
	A_StartSound("BOSSMISL", CHAN_WEAPON);
	A_SpawnProjectile("DCY_AMissile", 48, 12, frandom(-40.0, 40.0), CMF_OFFSETPITCH, pitch + frandom(-30, 30));
  }
  
  override void Tick()
  {
    super.Tick();
	
	If (Health > 0 && !IsFrozen() && !sv_effectchoker && DCY_EffectDistance(sfxdistance))
	{
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-2.0, -5.0), flags: SXF_NOCHECKPOSITION, 140);
	  
		A_SpawnItemEx("DCY_Explosion_1_Smaller", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(38.0, 41.0), 0, 0, frandom(-1.0, -2.5), flags: SXF_NOCHECKPOSITION, 220);
	}
  }
  
  bool rushing;
  int mmissile;
  
  States
  {
  Spawn:
	FR6T AB 2 DCY_Look();
	Loop;
  Look:
    FR6T ABABAB 2 DCY_LookingForPlayer();
    TNT1 A 0 A_CheckSight("Look");
    Goto Alert;
  Alert:
    "####" "#" 0 DCY_AlertSound();
    Goto See;
  See:
	FR6T AB 2 DCY_Chase();
	Loop;
  Rush:
	FR6T A 0 { return (rushing == 0) ? true : false; }
	Goto See;
  Melee:
	FR6T EF 6 A_FaceTarget();
	FR6T G 5 A_CustomMeleeAttack(10 * random(3, 7), "Baron/Melee");
	FR6T F 6;
	Goto See;
  Missile:
	FR6T C 0
	{
		if (target && distance3D(target) > 400)
			Return A_Jump(120, "Napalm", "Comet", "MiniMissile", "MiniMissile");
		
		Return A_Jump(120, "Comet", "MiniMissile", "MiniMissile");
	}
	Goto Rockets;
  MiniMissile:
	FR6T C 20 { mmissile = 0; A_FaceTarget(); }
	FR6T D 3 A_MM();
	FR6T C 2
	{
		if (mmissile > 15)
			SetStateLabel("See");
		
		mmissile++;
		A_FaceTarget();
	}
	Goto MiniMissile+1;
  Rockets:
	FR6T C 15 A_FaceTarget();
	FR6T D 5 A_AMissile();
	FR6T C 10 A_FaceTarget();
	FR6T D 5 A_AMissile();
	FR6T C 10 A_FaceTarget();
	FR6T D 5 A_AMissile();
	FR6T C 15;
	Goto See;
  Napalm:
	FR6T E 1 A_StartSound("Excoriator/Attack", CHAN_VOICE);
	FR6T CC 15 A_FaceTarget();
	FR6T D 5 Napalm();
	FR6T C 12;
	Goto See;
  Comet:
	FR6T E 1 A_StartSound("Excoriator/Attack", CHAN_VOICE);
	FR6T EEEEEEEEEEEEEEEEEEEE 1
	{
		A_FaceTarget();
		A_SpawnParticleEx(0xff5219, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 55, 0, 5 + frandom(-2.5, 2.5), -35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 66, velz: 3, fadestepf: 0, sizestep: -4.325);
	}
	FR6T EF 6 A_FaceTarget();
	FR6T G 6 AcroComet();
	FR6T F 6 A_FaceTarget();
	Goto See;
  Pain:
	FR6T H 4;
	FR6T H 4 A_Pain();
	Goto See;
  Death:
	FR6T I 2;
	FR6T I 4 A_Scream();
	FR6T JK 5;
	FR6T L 5
	{
		DCY_Gibs(false);
		A_SpawnItemEx("DCY_Explosion_1_Biggie", zofs: 30, flags: SXF_NOCHECKPOSITION);
		A_NoBlocking();
		A_StartSound("Archon/Explode", CHAN_BODY);
	}
	FR6T MNO 5;
	Stop;
  Death.Massacre:
	FR6T I 2;
	FR6T I 4 A_Scream();
	FR6T JK 5;
	FR6T L 5 A_NoBlocking();
	FR6T MNO 5;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_AComet : DCY_GComet
{
  Default
  {
  DamageType "Fire";
  +SEEKERMISSILE
  Translation "112:255=%[0.37,0.00,0.00]:[2.00,1.53,0.54]";
  Speed 15;
  +NEVERFAST
  Scale 0.76;
  SeeSound "TORPMISL";
  DeathSound "DCYComet/Explode";
  }
  
  States
  {
  Spawn: 
    AC_Z AAAABBBBCCCC 1
	{
		A_Tracer2(3);
		A_SpawnItemEx("DCY_ACometTrail", flags: SXF_NOCHECKPOSITION);
	}
    Loop;
  Death:
	TNT1 A 2
	{
		A_Explode(77, 100, XF_NOTMISSILE, 1);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, falloff: 400);
		A_SetTranslucent(1.0, 1);
		
		A_SpawnItemEx("DCY_Explosion_1_Biggie", flags: SXF_NOCHECKPOSITION);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
	}
	TNT1 A 25;
    Stop;
  }
}

Class DCY_ACometTrail : DCY_GCTrail { Default { Translation "0:255=%[0.37,0.00,0.00]:[2.00,1.53,0.54]"; Scale 0.76; } }

Class DCY_AMissile : DCY_CyberMicromissile
{
  Default
  {
  SeeSound "BOSSMISL";
  Speed 5;
  +NEVERFAST
  Damage 6;
  Scale 0.8;
  +SEEKERMISSILE
  Translation "192:207=%[0.50,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  int timer;
  
  States
  {
  Spawn:
	MMIS A 1
	{
		if (timer < 7 || (timer > 60 && timer < 100)) A_ScaleVelocity(1.05);
		vel.y += frandom(-0.2, 0.2);
		vel.x += frandom(-0.2, 0.2);
		vel.z += frandom(-0.2, 0.2);
		if (timer < 60) A_Tracer2(2);
		if (DCY_EffectDistance(sfxdistance)) A_SpawnFlames("ff7e3d", 20, 26, 0, true, frandom(-2.5, 2.5));
		timer++;
	}
	Loop;
  }
}