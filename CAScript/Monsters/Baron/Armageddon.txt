Class DCY_ArmageddonPredominator : DCY_BaronParent
{
  Default
  {
  Health 1800;
  Tag "\c[Sapphire]Armageddon Predominator\c-";
  Obituary "%o felt what it was like to be in a state of frostbite.";
  HitObituary "%o got the cold breath by the Armageddon Predominator.";
  Translation "16:47=%[0.00,0.00,0.00]:[0.31,0.31,0.31]", "48:79=%[0.00,0.00,0.00]:[0.61,0.61,0.61]", "112:127=%[0.00,0.00,0.00]:[0.40,0.69,2.00]", "128:159=%[0.00,0.00,0.00]:[0.47,0.47,0.47]", "5:15=%[0.00,0.00,0.00]:[0.47,0.47,0.47]", "1:2=%[0.00,0.00,0.00]:[0.47,0.47,0.47]";
  Scale 1.1;
  +MISSILEMORE
  +QUICKTORETALIATE
  +BOSS
  +BOSSDEATH
  +NODROPOFF
  +ALWAYSFAST
  +NOTARGET
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +NOICEDEATH
  BloodColor "0F 52 BA";
  SeeSound "ARWLSIT";
  ActiveSound "ARWLACT";
  PainSound "ARWLPAI";
  DeathSound "ARWLDTH";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  DamageType "ArmageddonPredominator";
  DamageFactor "ArmageddonPredominator", 0.000;
  Speed 8;
  FastSpeed 16;
  DCYMonster.WalkFlames "DCY_LightBlueWalkFlames";
  }
  
  int asteroid;
  int asteroidspawn;

  States
  {
  Spawn:
    LHS_ A 10 DCY_Look();
	Loop;
  Look:
    LHS_ AAA 2 DCY_LookingForPlayer();
	LHS_ A 0 A_WalkFlame();
	LHS_ BBB 2 DCY_LookingForPlayer();
	LHS_ B 0 A_WalkFlame();
	LHS_ CCC 2 DCY_LookingForPlayer();
	LHS_ C 0 A_WalkFlame();
	LHS_ DDD 2 DCY_LookingForPlayer();
	LHS_ D 0 A_WalkFlame();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	LHS_ P 4
	{
		A_QuakeEx(3, 3, 1, 60, 0, 800, "", QF_3D|QF_SCALEDOWN);
		DCY_AlertSound();
		A_FaceTarget();
	}
	LHS_ QPRPQPRPQPRP 4 A_FaceTarget();
	Goto See;
  See:
    TNT1 A 0 A_Jump(198, "Faaast");
    LHS_ AAA 2 A_Chase();
	LHS_ A 0 A_WalkFlame();
	LHS_ BBB 2 A_Chase();
	LHS_ B 0 A_WalkFlame();
	LHS_ CCC 2 A_Chase();
	LHS_ C 0 A_WalkFlame();
	LHS_ DDD 2 A_Chase();
	LHS_ D 0 A_WalkFlame();
	Loop;
  Faaast:
    LHS_ AAA 2 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
	LHS_ A 0 A_WalkFlame();
	LHS_ BBB 2 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
	LHS_ B 0 A_WalkFlame();
	LHS_ CCC 2 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
	LHS_ C 0 A_WalkFlame();
	LHS_ DDD 2 A_Chase("Melee", "Missile", CHF_NIGHTMAREFAST);
	LHS_ D 0 A_WalkFlame();
	TNT1 A 0 A_Jump(192, "See");
	Loop;
  Melee:
    LHS_ EF 4 A_FaceTarget();
	LHS_ G 4 A_CustomMeleeAttack(random(15, 25), "baron/melee", 0);
	TNT1 A 0 A_Jump(128, "Melee2");
	Goto See;
  Melee2:
	LHS_ XY 4 A_FaceTarget();
	LHS_ Z 4 A_CustomMeleeAttack(random(15, 25), "baron/melee", 0);
	TNT1 A 0 A_Jump(60, "Missile");
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(128, "Missile2", "Comets");
	TNT1 A 0 A_Jump(90, "Asteroid");
    LHS_ EF 5 A_FaceTarget();
	LHS_ G 5
	{
		A_SpawnProjectile("DCY_APBall", 30, 0);
		A_SpawnProjectile("DCY_APBall", 30, 0, frandom(9.0, -8.5));
		A_SpawnProjectile("DCY_APBall", 30, 0, frandom(-9.0, 8.5));
	}
	LHS_ XY 5 A_FaceTarget();
	LHS_ Z 5
	{
		A_SpawnProjectile("DCY_APBall", 30, 0);
		A_SpawnProjectile("DCY_APBall", 30, 0, frandom(9.0, -8.5));
		A_SpawnProjectile("DCY_APBall", 30, 0, frandom(-9.0, 8.5));
	}
	Goto See;
  Missile2:
    LHS_ EF 8 A_FaceTarget();
	LHS_ GG 0 A_SpawnProjectile("DCY_APBall", 30, 0, frandom(-9.0, 8.5));
	LHS_ GG 0 A_SpawnProjectile("DCY_APBall", 30, 0, frandom(9.0, -8.5));
	LHS_ G 8 A_SpawnProjectile("DCY_APBall", 30, 0);
	LHS_ XY 8 A_FaceTarget();
	LHS_ ZZ 0 A_SpawnProjectile("DCY_APBall", 30, 0, frandom(-9.0, 8.5));
	LHS_ ZZ 0 A_SpawnProjectile("DCY_APBall", 30, 0, frandom(9.0, -8.5));
	LHS_ Z 8 A_SpawnProjectile("DCY_APBall", 30, 0);
	Goto See;
  Comets:
    LHS_ EF 10 A_FaceTarget();
	LHS_ G 10
	{
		for (int i = 10; i > -11; i -= 5)
			A_SpawnProjectile("DCY_APNComet", 30, 0, i);
	}
	TNT1 A 0 A_Jump(100, "ANDANOTHERONE");
	Goto See;
  ANDANOTHERONE:
    LHS_ XY 10 A_FaceTarget();
	LHS_ Z 10
	{
		for (int i = 10; i > -11; i -= 5)
			A_SpawnProjectile("DCY_APNComet", 30, 0, i);
	}
	Goto See;
  Asteroid:
    LHS_ F 8 A_FaceTarget();
	LHS_ E 19
	{
		A_FaceTarget();
		bNoPain = true;
		A_SpawnItemEx("DCY_AsteroidGrowing", 0, 0, 130, flags: SXF_SETMASTER);
	}
	LHS_ E 1 A_SpawnItemEx("DCY_AsteroidReady", 0, 0, 130, flags: SXF_SETMASTER);
	Goto AsteroidCharge;
  AsteroidCharge:
    TNT1 A 0 A_JumpIf(Health < 800, "MiniCometsforfack");
	LHS_ E 3
	{
		if (asteroid >= 4)
			Return ResolveState("Fiyah");
		
		A_FaceTarget();
		asteroid += 1;
		Return ResolveState(null);
	}
	Goto AsteroidCharge+1;
  Fiyah:
	LHS_ EEEEFFFF 3 A_FaceTarget();
	LHS_ G 10
	{
		asteroidspawn = 0;
		asteroid = 0;
		A_RemoveChildren(1, RMVF_EVERYTHING);
		A_SpawnProjectile("DCY_Asteroid", 130);
	}
	LHS_ G 10;
    TNT1 A 0
	{
		bNoPain = true;
	}
	Goto See;
  MiniCometsforfack:
	LHS_ E 3
	{
		If(asteroidspawn >=32)
			SetStateLabel("Fiyah");
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_APComet", 130, 0, random(-360, 360), CMF_AIMDIRECTION|CMF_TRACKOWNER, random(-20, 20));
		asteroidspawn++;
	}
	Loop;
  Pain:
    LHS_ H 1;
	LHS_ H 3 A_RemoveChildren(1, RMVF_EVERYTHING);
	LHS_ H 4 A_Pain();
	Goto See;
  Death:
    TNT1 A 0 { if (damagetype != "Massacre") Spawn("DCY_APFXSpawner", pos + (0, 0, 40)); }
    LHS_ I 1 A_RemoveChildren(1, RMVF_EVERYTHING);
	LHS_ I 1 A_Scream();
	LHS_ I 10 A_NoBlocking();
	TNT1 A 0 { if (damagetype != "Massacre") Spawn("DCY_APFXSpawner", pos + (0, 0, 40)); }
	LHS_ JKLMNO 10;
	LHS_ O -1 A_BossDeath2();
	Stop;
  Raise:
    Stop;
  }
}

Class DCY_APComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.20,2.00]";
  DamageFunction 9;
  Speed 30;
  FastSpeed 35;
  +SEEKERMISSILE
  +DONTREFLECT
  +MTHRUSPECIES
  +THRUACTORS
  DamageType "Ice";
  }
  
  States
  {
  Spawn:
    CGMT A 1 Bright A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    TNT1 A 0 { bThruActors = false; }
    CGMT ABBCC 1 Bright
	{
		A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Seekermissile(1, 2, SMF_LOOK);
	}
    Goto Spawn+2;
  Death:
    CGMT D 0 Bright;
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(25, 50, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN|QF_3D, 0, 0, 0, 400);
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_SetTranslucent(1.0, 1);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_MonochromaticExplosion", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.25, Scale.Y+0.25);
	TNT1 A 25;
    Stop;
  }
}

Class DCY_APNComet : DCY_APComet
{
  Default { -SEEKERMISSILE; }
  
  States
  {
  Spawn:
    CGMT A 1 Bright A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    TNT1 A 0 { bThruActors = false; }
    CGMT ABBCC 1 Bright A_SpawnItemEx("DCY_CometTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    Goto Spawn+2;
  }
}

Class DCY_Asteroid : DCY_APComet
{
  Default
  {
  DamageFunction 64;
  Scale 3.0;
  Speed 40;
  FastSpeed 43;
  SeeSound "BigAssComet/Attack";
  DeathSound "BigAssComet/Death";
  }
  
  States
  {
  Spawn:
    CGMT A 1 Bright A_SpawnItemEx("DCY_CometTrailBIG", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
    TNT1 A 0 { bThruActors = false; }
    CGMT ABBCC 1 Bright A_SpawnItemEx("DCY_CometTrailBIG", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Loop;
  Death:
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(200, 155, XF_NOTMISSILE, 1);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", 0, 0, 0, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1", frandom(-15, 15), frandom(-15, 15), frandom(-15, 15), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SetScale(Scale.X+0.075, Scale.Y+0.075);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN|QF_3D, 0, 0, 0, 400);
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.075, Scale.Y+0.075);
		A_SetTranslucent(1.0, 1);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_MonochromaticExplosion", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.075, Scale.Y+0.075);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	TNT1 A 25;
    Stop;
  }
}

Class DCY_CometTrailBIG : DCY_CometTrail { Default { Scale 3.0; } }

Class DCY_AsteroidReady : DCY_Asteroid
{
  Default
  {
  Speed 0;
  DamageFunction 0;
  -MISSILE
  -SOLID
  +THRUACTORS
  +NOINTERACTION
  +NOGRAVITY
  +NOBLOCKMAP
  +NOTELEPORT
  +NOCLIP
  +NONETID
  +NOCLIP
  }
  
  override void Tick()
  {
	super.tick();
	Warp(Master, 0, 0, 130, 0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
  }
  
  States
  {
  Spawn:
	CGMT ABC 2 Bright;
	Loop;
  Death:
    CGMT A 1 Bright;
	Stop;
  }
}

Class DCY_AsteroidGrowing : DCY_AsteroidReady
{
  Default { Scale 1.0; }
  
  States
  {
  Spawn:
    CGMT AABBCCAABBCCAABBCCAA 1 Bright A_SetScale(Scale.X + 0.1, Scale.Y + 0.1);
	Stop;
  }
}

Class DCY_APFXSpawner : Actor
{
  Default
  {
  +NOINTERACTION
  +MTHRUSPECIES
  +CLIENTSIDEONLY
  +THRUSPECIES
  +THRUACTORS
  Speed 0;
  Radius 1;
  Height 1;
  }
  
  int loop;
  
  States
  {
  Spawn:
    TNT1 A 0 A_JumpIf(loop ==14, "Stahp");
    TNT1 A 1
	{
		A_SpawnItemEx("DCY_APFX", 0, 0, 0, frandom(5, -5), frandom(-5, 5), frandom(-5, 5), frandom(0, 360), SXF_NOCHECKPOSITION);
		loop += 1;
	}
	Loop;
  Stahp:
    TNT1 A 1;
	Stop;
  }
}

Class DCY_APFX : DCY_GunmetalInfiltratorBallTrail
{
  Default
  {
  +NOCLIP;
  Scale 0.85;
  Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.20,2.00]";
  }
  
  States
  {
  Spawn:
    AC_Z JJKKLLMMNN 2 Bright A_FadeOut(0.06, 0);
	Stop;
  }
}

Class DCY_APBall : BaronBall
{
  Default
  {
  DamageType "ArmageddonPredominator";
  SeeSound "ARPDBALL";
  DeathSound "ARPDBALX";
  DamageFunction 7;
  Speed 27.5;
  DamageType "Ice";
  Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.20,2.00]";
  +BRIGHT
  }
  
  States
  {
  Spawn:
	BAL7 AB 1
	{
		A_Weave(2, 2, 1, 1);
		A_SpawnItemEx("DCY_APBallTrail", 0, 0, 0, frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), 0, SXF_NOCHECKPOSITION, 60);
		A_SpawnItemEx("DCY_BaronKnightFlame", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 150);
	}
	Loop;
  Death:
    BAL7 CDE 3;
	Stop;
  }
}

Class DCY_APBallTrail : DCY_GunmetalInfiltratorBallTrail
{
  Default
  {
  Alpha 0.85;
  Translation "0:255=%[0.00,0.00,0.00]:[1.04,1.20,2.00]";
  }
  
  States
  {
  Spawn:
    TNT1 A 1;
	BAL7 ABABAB 1 A_FadeOut(0.2, 0);
	Stop;
  }
}