Class DCY_Repealer : DCY_BaronParent
{
  Default
  {
  Health 1500;
  BloodColor "Yellow";
  SeeSound "Repealer/Sight";
  ActiveSound "Repealer/Active";
  PainSound "Repealer/Pain";
  DeathSound "Repealer/Death";
  BloodColor "Yellow";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  Tag "\c[y4]Repealer\c-";
  DamageFactor "Electric", 0.00;
  DCYMonster.ScientificName "Ambulans exstirpatio";
  +MISSILEEVENMORE
  DCYMonster.FootstepSound "";
  }
  
  void A_RepealerStep()
  {
	A_StartSound("BIGSTEPP", CHAN_BODY, attenuation: 0.8);
	A_QuakeEx(0, 0, 1, 10, 0, 400, "", QF_3D|QF_SCALEDOWN);
  }
  
  int missile;
  bool micromissile;
  
  States
  {
  Spawn:
	3LCY A 10 DCY_Look();
    Loop;
  Look:
	3LCY AAAABBBB 3 DCY_LookingForPlayer();
	3LCY C 0 A_RepealerStep();
	3LCY CCCCDDDD 3 DCY_LookingForPlayer();
	3LCY A 0 A_RepealerStep();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 A_StartSound(SeeSound, CHAN_VOICE);
	Goto See;
  See:
	3LCY AAAABBBB 3 DCY_Chase();
	3LCY C 0 A_RepealerStep();
	3LCY CCCCDDDD 3 DCY_Chase();
	3LCY A 0 A_RepealerStep();
	Loop;
  Melee:
	3LCY GH 4 A_FaceTarget();
	3LCY I 4 A_CustomMeleeAttack(10 * random(1, 7), "baron/melee");
	3LCY H 4;
	Goto See;
  Missile:
	3LCY E 0 A_Jump(80, "BigOrb", "HectorLaserMinigun");
	3LCY E 0 A_Jump(128, "Micromissiles", "HectorLaser");
	Goto Plasma;
  Plasma:
	3LCY E 20 { A_FaceTarget(); missile = random(12, 16); }
	3LCY F 1
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_RepealerPlasma", 34, 10, frandom(-4, 4), CMF_OFFSETPITCH, frandom(-4, 4));
		missile--;
		if (!missile) { frame = 4; SetStateLabel("Cooldown"); }
	}
	3LCY E 2 A_FaceTarget();
	Goto Plasma+1;
  Cooldown:
	3LCY # 15;
	Goto See;
  BigOrb:
	3LCY U 1 A_StartSound("Repealer/Active", CHAN_VOICE, attenuation: 0.75);
	3LCY UUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUUVVVVVVVV 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			if (!(level.time % 2)) A_SpawnItemEx("DCY_RepealerElec", frandom(-2, 2) + ((frame == 19) ? 5 : 2), frandom(-2, 2) - ((frame == 19) ? 30 : 40), frandom(-2, 2) + ((frame == 19) ? 45 : 66), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
			if (!(level.time % 8)) A_SpawnItemEx("DCY_RepealerAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION|SXF_SETMASTER);
		}
		
		A_FaceTarget();
	}
	3LCY W 25
	{
		A_FaceTarget();
		A_StartSound("LightningPulse/MainFire", CHAN_WEAPON, attenuation: 0.5);
		A_SpawnProjectile("DCY_RepealerOrb", 34, 10);
	}
	Goto See;
  Micromissiles:
	3LCY I 0 { missile = 0; }
	3LCY IHGG 5 A_FaceTarget();
	3LCY EE 10 A_FaceTarget();
	3LCY E 10
	{
		for (int i = -40; i < 1; i += 20)
		{
			A_SpawnProjectile("DCY_RepealerMicrorocket", 40, -10, -90, CMF_OFFSETPITCH|CMF_AIMDIRECTION, pitch + i);
			A_SpawnProjectile("DCY_RepealerMicrorocket", 40, -10, 90, CMF_OFFSETPITCH|CMF_AIMDIRECTION, pitch + i);
		}
		
		A_StartSound("DCYTorpedo", CHAN_WEAPON, attenuation: 0.75);
		missile++;
		A_FaceTarget();
	}
	3LCY E 10
	{
		A_FaceTarget();
		if (missile >= 3) SetSTateLabel("Cooldown");
	}
	Goto Micromissiles+7;
  HectorLaser:
	3LCY I 0 { missile = 0; }
	3LCY IHGGGG 6 A_FaceTarget();
	3LCY G 20
	{
		A_StartSound("DCYSeekerLaser", CHAN_WEAPON, attenuation: 0.75);
		for (int i = -90; i < 91; i += 180)
			A_SpawnProjectile("DCY_RepealerLaser", 50, 0, i, CMF_AIMDIRECTION);
		A_FaceTarget();
		missile++;
		
		if (missile >= 2) SetStateLabel("Cooldown");
	}
	Goto Hectorlaser+7;
  HectorLaserMinigun:
	3LCY E 0 { missile = 0; }
	3LCY E 1 A_StartSound("Repealer/Active", CHAN_VOICE, attenuation: 0.75);
	3LCY EEEEEEEEEEEEEEEEEEEEEEEEEEEE 1
	{
		if (DCY_EffectDistance(sfxdistance))
			if (!(level.time % 8)) A_SpawnItemEx("DCY_RepealerAura", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION|SXF_SETMASTER);
		
		A_FaceTarget();
	}
	Goto ML;
  ML:
	3LCY E 5
	{
		A_StartSound("DCYSeekerLaser", CHAN_WEAPON, attenuation: 0.75);
		A_SpawnProjectile("DCY_RepealerLaser", 50, 0, frandom(-180, 180), CMF_AIMDIRECTION|CMF_OFFSETPITCH, frandom(-180, 180));
		A_FaceTarget(2, 2);
		missile++;
		
		if (missile >= 14) SetStateLabel("Cooldown");
	}
	Loop;
  Pain:
	3LCY J 4;
	3LCY J 4 A_Pain();
	Goto See;
  Death:
	3LCY J 4;
	3LCY K 12 A_Scream();
	3LCY LMN 5;
	3LCY O 5
	{
		A_NoBlocking();
		
		A_StartSound("V3RYEXPL", CHAN_BODY, attenuation: 0.5);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_Explosion_1_Repealer", 0, 0, 30, i / 2, 0, i / 2);
	}
	3LCY PQ 5;
	3LCY R -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_RepealerAura : DCY_CuratorExplosion3
{
	override void Tick()
	{
		super.Tick();
		
		if (master)
		{
			angle = master.angle;
			frame = master.frame;
		}
	}
}

class DCY_RepealerElec : DCY_ElecDeathBase { Default { Translation "0:255=%[0.0,0.00,0.00]:[2.00,2.00,1.01]"; Scale 0.75; } }
Class DCY_Explosion_1_RepealerS : DCY_Explosion_1_Small { Default { Translation "0:255=%[0.0,0.00,0.00]:[2.00,2.00,1.01]"; } }
Class DCY_Explosion_1_Repealer : DCY_Explosion_1_Biggie { Default { Scale 0.6; Translation "0:255=%[0.0,0.00,0.00]:[2.00,2.00,1.01]"; } }

Class DCY_RepealerPlasma : DCY_ConsulPlasma
{
  Default
  {
  Translation "0:255=%[0.0,0.00,0.00]:[2.00,2.00,1.01]";
  Speed 23;
  +SEEKERMISSILE
  SeeSound "Rifle/Laser";
  }
  
  States
  {
  Spawn:
	9_AL AAABBBCCC 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), (Vel.x / 3.75), (Vel.y / 3.75), (Vel.z / 3.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.25);
			A_SpawnFlames("917910", 15, 12, 0, false, thrust: 2.25);
		}
	}
	Loop;
  Death:
	TNT1 A 0
	{
		for (int i = 12; i > 0; i--)
			A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 45), 5, frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), fadestepf: 0, sizestep: -0.125);
		A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
	}
	9_AL DEFGH 3;
	Stop;
  }
}

Class DCY_RepealerOrb : DCY_RepealerPlasma
{
  Default
  {
  Speed 5;
  +NEVERFAST
  DamageFunction 2;
  +RIPPER
  +BRIGHT
  Radius 25;
  Height 25;
  DamageType "Electric";
  SeeSound "LightningPulse/Fire";
  DeathSound "LightningPulse/Explode";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  int timer;
  
  States
  {
  Spawn:
	LFX1 STUVW 1
	{
		if (timer < 40)
			vel += Vel.Unit() * 0.65;
		
		timer++;
		
		for(int i = 2; i > 0; i--)
			A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 20, frandom(10, 20), frandom(0, 360), frandom(-35, 35), frandom(-35, 35), frandom(-35, 35), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
		
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-25, 25), frandom(-25, 25), frandom(-25, 25), flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE, failchance: 120);
		A_SpawnFlames("917910", 15, 30, 0, false);
		A_SpawnItemEx("DCY_Arc", flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE, failchance: 120);
	}
	Loop;
  Death:
	SFE1 A 1
	{
		A_QuakeEx(2, 2, 2, 38, 0, 1000, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 45, 0, 1250, "", QF_RELATIVE|QF_SCALEDOWN);
		A_QuakeEx(1, 1, 1, 35, 0, 1250, "", QF_SCALEDOWN, 0, 0, 0, 0, 0, 0.7, frandom(0.7, -0.7));
		A_Explode(180, 200, XF_NOTMISSILE, True);
		
		if (DCY_EffectDistance(2000))
		{
			A_SpawnItemEx("DCY_Explosion_1_Biggie", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
			for (int i = 20; i > 0; i--) A_SpawnItemEx("DCY_ElecDeathBase", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
		}
	}
    SFE1 AAAABBBCCDEF 1 A_SpawnProjectile("DCY_RepealerSmallOrb", 0, 0, frandom(-360, 360), CMF_OFFSETPITCH, frandom(-360, 0));
	Stop;
  }
}

Class DCY_RepealerSmallOrb : DCY_ImpFireball
{
  Default
  {
  SeeSound "";
  Damage 6;
  -RIPPER
  Radius 8;
  Height 16;
  Scale 0.45;
  Speed 25;
  BounceCount 4;
  BounceType "Doom";
  BounceFactor 0.8;
  -NOGRAVITY
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,2.00,1.01]";
  }
  
  States
  {
  Spawn:
	LFX1 STUVW 1
	{
		A_SpawnItemEx("DCY_ElecTrailBase", flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
		A_SpawnParticleEx(0x917910, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 7, frandom(10, 20), frandom(0, 360), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1));
	}
	Loop;
  Death:
	LFX1 STUVW 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_RepealerMicroRocket : DCY_CyberMicromissile
{
  Default
  {
  Translation "0:255=%[0.0,0.00,0.00]:[2.00,2.00,1.01]";
  Speed 5;
  +NEVERFAST
  +SEEKERMISSILE
  Scale 1;
  Damage 4;
  Scale 1.1;
  SeeSound "";
  }
  
  int timer;
  
  States
  {
  Spawn:
	SMOL A 1
	{
		if (timer < 20) A_ScaleVelocity(0.975);
		if (timer == 20) for (int i = 2; i > 0; i--) A_SeekerMissile(99, 99, SMF_PRECISE);
		if (timer >= 20 && timer < 40)
		{
			A_SeekerMissile(4, 4, SMF_PRECISE);
			A_ScaleVelocity(2);
		}
		
		A_SpawnFlames("917910", 15, 15);
		timer++;
	}
	Loop;
  Death:
	SMOL B 1
	{
		A_SetTranslucent(1.0, 1);
		A_SpawnItemEx("DCY_Explosion_1_RepealerS");
		A_QuakeEx(1, 1, 1, 20, 0, 100, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 200);
	}
	SMOL CDEFGHIJKL 1;
	TNT1 A 8;
	Stop;
  }
}

Class DCY_RepealerLaser : DCY_FastProjectile
{
  Default
  {
  Speed 10;
  +RIPPER
  +SEEKERMISSILE
  Height 7;
  Radius 7;
  Damage 3;
  DCY_FastProjectile.CT true;
  }
  
  override void Effect()
  {
	super.Effect();
	string spr;
	
	switch (random(0, 2))
	{
		case 0: spr = "S0FXA1";
		case 1: spr = "S0FXB1";
		case 2: spr = "S0FXC1";
	}
	
	if (DCY_EffectDistance(sfxdistance))
		A_SpawnParticleEx(0xfcdb03, TexMan.CheckForTexture(spr), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 10, 30, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), sizestep: randompick(1, -1) * 1.725, startroll: frandom(0, 360));
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (getAge() < 10) A_SeekerMissile(12, 12, SMF_PRECISE);
		if (getAge() > 10 && getAge() < 20) A_ScaleVelocity(1.22);
	}
	Loop;
  Death:
	TNT1 A 1;
	Stop;
  }
}