Class DCY_Peril : DCY_BaronParent
{
  Default
  {
  Health 1500;
  Speed 10;
  MeleeDamage 8;
  +QUICKTORETALIATE;
  +MISSILEMORE;
  +NOFEAR;
  +DONTHARMCLASS;
  +NEVERRESPAWN;
  BloodColor "Green";
  DropItem "Blursphere", 25;
  SeeSound "Peril/Sight";
  DeathSound "Peril/Death";
  ActiveSound "Peril/Active";
  PainSound "Peril/Pain";
  Obituary "%o got liquidated by a peril.";
  HitObituary "%o was slashed in half by a peril.";
  Tag "\c[w3]Peril\c-";
  StencilColor "004ac9";
  }
  
  int missile;
  
  States
  {
  Spawn:
    GNM8 A 10 DCY_Look();
	Loop;
  Look:
	GNM8 AAAABBBBCCCCDDDD 4 DCY_LookingForPlayer();
    "####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(30, "Rush");
	GNM8 AAAABBBBCCCCDDDD 4 DCY_Chase();
    Loop;
  Rush:
    TNT1 A 0 A_StartSound("AM2DASH", CHAN_WEAPON);
    GNM8 AAAABBBBCCCCDDDDAAAABBBBCCCCDDDD 1 
	{
		DCY_Chase();
		if (DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL);
	}
	Goto See;
  Melee:
    GNM8 EF 9 A_FaceTarget();
	GNM8 G 9 A_CustomMeleeAttack(8 * random(1, 10), "Baron/Melee", "");
	Goto See;
  Missile:
    GNM8 E 0
	{
		missile = random(3, 5);
		if ((target && distance3D(target) < 300) && (!random(0, 3))) SetStateLabel("DashSlash");
		if (!random(0, 6)) SetStateLabel("Comet");
	}
	Goto Triple;
  Triple:
    GNM8 EEEEEEEEEEEEEEEEEEEEEEEEE 1
	{
		A_SpawnParticleEx(0x004ac9, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 35, 0, 5 + frandom(-2.5, 2.5), 35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -2.325);
		A_FaceTarget();
	}
	GNM8 F 5 { if (!random(0, 2)) SetStateLabel("Barrage"); A_FaceTarget(); }
	GNM8 G 7
	{
		A_FaceTarget();
		for (int i = -20; i < 21; i += 20)
			A_SpawnProjectile("DCY_GBall", 35, 0, i);
	}
	Goto See;
  Barrage:
	GNM8 G 3
	{
		A_FaceTarget();
		missile--;
		A_SpawnProjectile("DCY_GBall", 35, 0, frandom(-3, 3), CMF_OFFSETPITCH, frandom(-3, 3));
	}
	GNM8 EF 7
	{
		A_FaceTarget();
		if (missile <= 0) SetStateLabel("See");
	}
	Loop;
  Comet:
	GNM8 F 10
	{
		A_FaceTarget();
		A_StartSound(ActiveSound, CHAN_VOICE);
	}
	GNM8 EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1
	{
		if ((level.time % random(1, 3)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, 40, 0, 5 + frandom(-2.5, 2.5), 35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.3, 1), startalphaf: 0.05, fadestepf: 0.0007, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
	
		A_SpawnParticleEx(0x004ac9, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, 50, 0, 5 + frandom(-2.5, 2.5), 35 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 56, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -2.325);
		A_FaceTarget();
	}
	GNM8 F 10 A_FaceTarget();
	GNM8 G 35
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_GComet", 35);
	}
	GNM8 A 20;
	Goto See;
  DashSlash:
	GNM8 E 1 A_FaceTarget();
	GNM8 EF 9 A_FaceTarget(8, 8);
	GNM8 G 0
	{
		bNoDropOff = true;
		A_Hop(4);
	}
	GNM8 GGGGGGGGGGG 1
	{
		A_Recoil(-2);
		if (DCY_EffectDistance(sfxdistance)) A_SpawnItemEx("DCY_HexodenicanTrail2", flags: SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL);
		A_CustomMeleeAttack(random(1, 5) * 5, "baron/melee");
	}
	NULL A 0 { bNoDropOff = false; }
	Goto See;
  Pain:
	GNM8 H 4;
    GNM8 H 4 A_Pain();
	Goto See;
  Death:
  XDeath:
    GNM8 I 8;
    GNM8 J 6 A_Scream();
	GNM8 K 5;
    GNM8 L 4 A_NoBlocking();
	GNM8 M 5 A_QuakeEx(1, 1, 2, 40, 0, 700, "", QF_SCALEDOWN);
    GNM8 NOPQRSTUV 5;
    GNM8 W -1 A_BossDeath2();
    Stop;
  Raise:
	GNM8 VUTSRQPOMNLKJI 3;
	Goto See;
  }
}

Class DCY_GTrail : DCY_Effect
{
  Default
  {
  Radius 1;
  Height 1;
  Alpha 0;
  +NOINTERACTION
  -SOLID
  }
  States
  {
  Spawn:
    GNM8 "#" 1;
	GNM8 "#" 1 A_FadeIn(0.9);
	Goto SpawnLoop;
  SpawnLoop:
    "####" "#" 1;
    "####" "##########" 1 A_FadeOut();
	Stop;
  }
}

Class DCY_GBall : DCY_BaronFireball
{
  Default
  {
  Speed 14;
  FastSpeed 20;
  Damage 6;
  Projectile;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  SeeSound "Painsend/Fireball";
  DeathSound "Painsend/FireExplode";
  +RANDOMIZE
  +BRIGHT
  }
  States
  {
  Spawn:
    BAL7 AAABBB 1
	{
		if (DCY_EffectDistance(sfxdistance))
		{
			let splosion = Spawn("DCY_ExplosiveBallT", pos);
			if (splosion) { splosion.translation = translation; splosion.scale *= 0.25; }
			A_SpawnItemEx("DCY_BaronKnightFlame", frandom(-5.0, 5.0), frandom(-5.0, 5.0), frandom(-5.0, 5.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
			A_SpawnFlames("004cff", 20, 50);
		}
	}
	Loop;
  Death:
	BAL7 C 7
	{
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Explosion_1_Small", 0, 0, 0, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		for (int i = 7; i > 0; i--)
			A_SpawnParticle(0x004cff, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(3.5, 6.4), accelz: -0.85);
		
		for (int i = random(0, 2); i > 0; i--)
		{
			let ember = Spawn("DCY_GEmber", (pos.x, pos.y, pos.z));
			
			if (ember)
			{
				ember.vel.x = frandom(-10, 10);
				ember.vel.y = frandom(-10, 10);
				ember.vel.z = frandom(1, 10);
			}
		}
	}
    BAL7 DE 7;
	Stop;
  }
}

Class DCY_GEmber : DCY_NapalmEmber
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  Gravity 0.8;
  }
  
  static const string ParticleColors[] = { "87efff", "54c3ff", "005eff" };
  
  States
  {
  Spawn:
	TNT1 A 1 Bright
	{
		if (waterlevel > 0)
			Destroy();
		
		A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], Texman.CheckForTexture("Graphics/Particles/dcyflamep.png"), STYLE_ADD, SPF_FULLBRIGHT, 35, random(9, 13), frandom(0, 360), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), sizestep: -0.325);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		let fire = Spawn("DCY_GFire", (pos.x, pos.y, pos.z));
		
		if (fire)
			fire.scale *= 0.9;
	}
	Stop;
  }
}

Class DCY_GFire : DCY_NapalmFire
{
  Default
  {
  DCY_NapalmFire.FireDuration 55;
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  }
  
  static const string ParticleColors[] = { "004cff", "002d96", "001547" };
  
  States
  {
  Spawn:
	123E "#" 0 NoDelay { fireloop = random(0, 20); A_StartSound("FIR3LOOP", CHAN_BODY, CHANF_LOOPING, vol, pitch: frandom(1.1, 0.9)); A_SetScale(frandom(0.5, 0.8) * randompick(-1, 1), frandom(0.5, 0.8)); }
	123E "##########" 1 A_FadeIn(0.1);
	123E "#" 1
	{
		if (fireloop > ftime)
			SetStateLabel("Gone");
		
		if ((level.time % 3) == 0)
		{
			fireloop++;
			A_Explode(3, 25);
		}
		
		if ((level.time % random(2, 5)) == 0)
			A_SpawnParticleEx(ParticleColors[random(0, ParticleColors.Size() - 1)], TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT, 35, random(9, 12), frandom(0, 360), frandom(-10.0, 10.0), frandom(-10.0, 10.0), frandom(4.0, 13.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.2, fadestepf: 0, sizestep: -0.45);
	}
	Goto Spawn+11;
  }
}

Class DCY_GComet : DCY_Projectile
{
  Default
  {
  Radius 15;
  Height 15;
  Speed 20;
  Damage 10;
  Scale 1.05;
  Projectile;
  +THRUSPECIES;
  SeeSound "TORPMISL";
  DeathSound "DCYComet/Explode";
  DamageType "Gunmetal";
  Translation "112:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  }
  States
  {
  Spawn: 
    AC_Z AAAABBBBCCCC 1 Bright A_SpawnItemEx("DCY_GCTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
    Loop;
  Death:
    CGMT D 0 Bright;
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(100, 100, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, falloff: 400);
		A_SpawnItemEx("DCY_AndroidExplosionBiggie", flags: SXF_NOCHECKPOSITION);
		
		for (int i = random(6, 9); i > 0; i--)
		{
			let ember = Spawn("DCY_GEmber", (pos.x, pos.y, pos.z));
			
			if (ember)
			{
				ember.vel.x = frandom(-10, 10);
				ember.vel.y = frandom(-10, 10);
				ember.vel.z = frandom(1, 15);
			}
		}
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_SetTranslucent(1.0, 1);
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_AndroidExplosion", random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(-2, 2), random(0, 360), SXF_NOCHECKPOSITION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.25, Scale.Y+0.25);
	TNT1 A 25;
    Stop;
  }
}

Class DCY_GCTrail : Actor
{
  Default
  {
  Radius 3;
  Height 3;
  Scale 0.75;
  Speed 0;
  Projectile;
  Translation "0:255=%[0.00,0.00,0.00]:[0.00,1.01,2.00]";
  RenderStyle "Add";
  Alpha 1.0;
  +ROLLSPRITE
  }
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_SetRoll(frandom(0, 360));
    GMCT DDDEEEFFFGGGHHHIII 1 Bright A_FadeOut(0.06, 0);
    Stop;
  }
}