Class DCY_EOD : DCY_HeckBaron
{
  Default
  {
  Health 2000;
  Translation "0:255=%[0.00,0.00,0.00]:[0.54,0.54,0.54]", "16:79=%[0.00,0.00,0.00]:[1.01,2.00,1.01]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "160:255=%[0.00,0.00,0.00]:[1.02,2.00,1.02]", "80:111=[255,255,255]:[0,0,0]";
  BloodColor "52ff4f";
  PainChance 50;
  DamageFactor "EOA", 0.00;
  DCYMonster.WalkFlames "DCY_GreenWalkFlames";
  Tag "\c[n9]The Elucidation of Dilapidator\c-";
  BloodType "DCY_SpiritBlood", "DCY_SpiritBlood", "DCY_SpiritBlood";
  Scale 1.1;
  +BOSS
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NOINFIGHTING
  +NOFEAR
  +DONTHARMCLASS
  +DONTHARMSPECIES
  SeeSound "DCYEOD/Sight";
  ActiveSound "DCYEOD/Active";
  PainSound "DCYEOD/Pain";
  DeathSound "DCYEOD/Death";
  DCYMonster.ScientificName "Dominus superbus";
  DCYMonster.FootstepSound "";
  +DCYMONSTER.DCYNORMALBOSS;
  }
  
  void A_EODChase()
  {
	if (target && distance3D(target) > 511)
	{
		for (int i = 2; i > 0; i--)
			A_Chase();
		
		A_SpawnItemEx("DCY_EODTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		tics = 2;
	}
	
	else if (target && distance3D(target) < 411)
		tics = 2;
	else if (target && distance3D(target) < 311)
		tics = 3;
	
	A_Chase();
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o met fate.",
		"%o could not handle Dilapidation."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  void A_EODFlame()
  {
	if(target)
	{
		A_FaceTarget();
		let flame = SpawnMissileXYZ((pos.x + 5, pos.y, pos.z + 30), target, "DCY_EODLargeFlame");
		
		if (flame)
		{
			flame.Vel3DFromAngle(GetDefaultSpeed("DCY_EODLargeFlame") / 1.35, flame.angle, BulletSlope());
			flame.bouncecount = 0;
		}
	}
  }
  
  void A_EODBall()
  {
	if (target)
	{
		A_FaceTarget();
	
		for (int i = 1; i > -2; i -= 2)
		{
			A_SpawnProjectile("DCY_EODBall", 30, 0, (frandom(20, 5) * i));
			A_SpawnProjectile("DCY_EODBall", 30, 0, (frandom(20, 5) * i));
		}
	}
  }
  
  bool flame;
  int missile;
  
  States
  {
  Spawn:
	DOR_ A 10 DCY_Look();
    Loop;
  Look:
	DOR_ AAA 3 DCY_LookingForPlayer();
	DOR_ B 0 A_WalkFlame();
	DOR_ BBB 3 DCY_LookingForPlayer();
	DOR_ C 0
	{
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY);
		A_WalkFlame();
	}
	DOR_ CCC 3 DCY_LookingForPlayer();
	DOR_ D 0 A_WalkFlame();
	DOR_ DDD 3 DCY_LookingForPlayer();
	DOR_ A 0
	{
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY);
		A_WalkFlame();
	}
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	DOR_ AAA 3 A_EODChase();
	DOR_ B 0 A_WalkFlame();
	DOR_ BBB 3 A_EODChase();
	DOR_ C 0
	{
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY);
		A_WalkFlame();
	}
	DOR_ CCC 3 A_EODChase();
	DOR_ D 0 A_WalkFlame();
	DOR_ DDD 3 A_EODChase();
	DOR_ A 0
	{
		A_StartSound("WastelandInfiltrator/Step", CHAN_BODY);
		A_WalkFlame();
	}
	Loop;
  Melee:
	DOR_ EF 4 Bright A_FaceTarget();
    DOR_ G 3 Bright A_CustomMeleeAttack(7 * frandom(2, 12), "baron/melee", "");
    DOR_ H 2 Bright;
    DOR_ IJ 4 Bright A_FaceTarget();
    DOR_ K 3 Bright A_CustomMeleeAttack(7 * frandom(2, 12), "baron/melee", "");
    DOR_ L 2 Bright;
	Goto Missile;
  Missile:
	DOR_ E 0
	{
		if (target && distance3D(target) > 400)
			Return A_Jump(50, "CometBlast", "FloorShits");
		else
			Return A_Jump(50, "CometBlast");
	}
	Goto MissileChoose;
  MissileChoose:
	DOR_ E 0
	{
		switch(random(0, 5))
		{
			case 0:
			case 1: SetStateLabel("MiniBalls"); break;
			case 2:
			case 3: SetStateLabel("BallBarrage"); break;
			case 4: SetStateLabel("CometsAndFlames"); break;
			case 5: SetStateLabel("Teleporting"); break;
		}
	}
	Goto See;
  Teleporting:
	DOR_ A 1
	{
		for (int i = 0; i < 360; i += 90)
			A_SpawnItemEx("DCY_EODTrail2", angle: self.angle + i + 45, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		
		bSolid = false;
		bShootable = false;
		bNoPain = true;
		bDontThrust = true;
		A_Recoil(-9);
		A_SetAngle(Angle + randompick(45, 135, 225, 315));
	}
	TNT1 AAAAAA 1 A_Recoil(-9);
	DOR_ A 10
	{
		bSolid = true;
		bNoPain = false;
		bShootable = true;
		bDontThrust = false;
		A_Stop();
	}
	Goto See;
  MiniBalls:
	DOR_ L 1
	{
		A_FaceTarget();
		missile = 0;
	}
	DOR_ LKJI 6 A_FaceTarget();
	DOR_ I 15
	{
		for (int i = missile; i > 0; i--)
			A_SpawnProjectile("DCY_EODMiniBall", 60, -35, ((3 * missile) * frandom(-1, 1)), CMF_OFFSETPITCH, pitch + ((3 * missile) * frandom(-1, 1)));
		
		A_FaceTarget();
		missile++;
		if (missile > 5) SetStateLabel("See");
	}
	Goto MiniBalls+5;
  BallBarrage:
	DOR_ H 1
	{
		A_FaceTarget();
		missile = 0;
	}
	DOR_ HGFE 6 A_FaceTarget();
	DOR_ E random(3, 6)
	{
		A_SpawnProjectile("DCY_EODBall", 60, 35, frandom(-10, 10));
		A_FaceTarget();
		missile++;
		
		if (missile > 15)
			SetStateLabel("See");
	}
	Goto BallBarrage+5;
  CometsAndFlames:
	DOR_ E 7
	{
		flame = randompick(0, 0, 1);
		A_FaceTarget();
	}
	DOR_ F 7 A_FaceTarget();
	DOR_ G 5
	{
		A_FaceTarget();
		
		if (flame)
			A_EODFlame();
		else
			A_SpawnProjectile("DCY_EODComet", 30, 0, 0, CMF_OFFSETPITCH|CMF_TRACKOWNER, 35);
	}
	DOR_ H 5 A_FaceTarget();
	DOR_ H 1 A_Jump(100, "See");
	DOR_ IJ 7 A_FaceTarget();
	DOR_ K 5
	{
		A_FaceTarget();
		
		if (flame)
			A_EODFlame();
		else
			A_SpawnProjectile("DCY_EODComet", 30, 0, 0, CMF_OFFSETPITCH|CMF_TRACKOWNER, 35);
	}
	DOR_ L 5 A_FaceTarget();
	Goto See;
  CometBlast:
	DOR_ M 0
	{
		A_SpawnItemEx("DCY_EODTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		A_StartSound("DCYEOD/Tracer", CHAN_VOICE, attenuation: 0.5);
	}
	DOR_ MN 16 A_FaceTarget();
	DOR_ O 8
	{
		for (int i = 4; i > 0; i--)
			A_SpawnProjectile("DCY_EODComet", 30, 0, frandom(-40, 40), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(0, -35));
		
		A_StartSound("SP1DJUMP", CHAN_WEAPON, attenuation: 0.75);
	}
	DOR_ P 8;
	Goto See;
  Floorshits:
	DOR_ M 0
	{
		A_SpawnItemEx("DCY_EODTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
		A_StartSound("DCYEOD/Floor", CHAN_VOICE, attenuation: 0.5);
	}
	DOR_ MN 17 A_FaceTarget();
	DOR_ O 10
	{
		for (int i = 0; i < 360; i += 45)
			A_SpawnProjectile("DCY_EODLargeFlame", 20, 0, i, CMF_AIMDIRECTION);
	}
	DOR_ P 10;
	Goto See;
  Pain:	
	DOR_ Q 3;
	DOR_ Q 3 A_Pain();
	DOR_ Q 2 A_Jump(60, "Teleporting");
	Goto See;
  Death:
	DOR_ Q 1;
	DOR_ Q 20 A_Scream();
	D0R_ E 6 A_NoBlocking();
	D0R_ FFFFGGGGHHHHIIII 1
	{
		if (damagetype != "Massacre")
		{
			for (int i = 2; i > 0; i--)
			{
				A_SpawnItemEx("DCY_ImpBigFlame", frandom(-radius, radius) * 3, frandom(-radius, radius) * 3, frandom(4, height) * 3, zvel: 2, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 130);
				A_SpawnItemEx("DCY_ImpFlame1", frandom(-radius, radius) * 3, frandom(-radius, radius) * 3, frandom(4, height) * 3, zvel: 2, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 130);
			}
		}
	}
	D0R_ J 4 A_SetRenderStyle(1.0, STYLE_ADD);
	D0R_ K 4 { if (damagetype != "Massacre") A_SpawnItemEx("DCY_EODSoul", zofs: 50, zvel: 10, flags: SXF_NOCHECKPOSITION); }
	D0R_ LM 4;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_EODComet : DCY_GeneralComet
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,1.01]";
  -NOGRAVITY
  BounceType "Doom";
  BounceFactor 0.865;
  Speed 20;
  FastSpeed 25;
  BounceSound "EODCOMET";
  BounceCount 8;
  }
}

Class DCY_EODLargeFlameExplosion : DCY_Projectile
{
  Default
  {
  Projectile;
  DamageFunction (0);
  Speed 0;
  FastSpeed 0;
  DamageType "EOA";
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,1.01]";
  +RIPPER
  +THRUACTORS
  Radius 20;
  Height 20;
  }
  
  States
  {
  Spawn:
	TNT1 A 2 NoDelay
	{
		A_QuakeEx(1, 1, 1, 40, 0, 400, "", QF_SCALEDOWN|QF_3D);
		A_Explode(30, 80, XF_NOTMISSILE, 0, 20);
		
		for (int i = 2; i > 0; i--)
		{
			A_SpawnItemEx("DCY_ImpBigFlame", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 100);
			A_SpawnItemEx("DCY_Explosion_1_Small", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 100);
		}
	}
	TNT1 AAAAAAAAAA 1
	{
		A_Explode(3, 55, XF_NOTMISSILE, 0, 10);
		A_SpawnItemEx("DCY_ImpBigFlame", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
		A_SpawnItemEx("DCY_Explosion_1_Small", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(-20.0, 20.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
	}
	TNT1 A 30;
	Stop;
  }
}

Class DCY_EODLargeFlame : DCY_MeatballElecball
{
  Default
  {
  Speed 28;
  +NEVERFAST
  +RIPPER
  DamageFunction (0);
  Radius 20;
  Height 20;
  BounceCount 2;
  BounceFactor 1.0;
  BounceType "Hexen";
  SeeSound "BigFireball/Attack";
  DeathSound "";
  }
  
  int timer;
  
  States
  {
  Spawn:
	TNT1 A 4;
	TNT1 A 2
	{
		A_SpawnProjectile("DCY_EODLargeFlameExplosion", -5);
		
		timer++;
		
		if (timer > 30)
			Destroy();
	}
	Goto Spawn+1;
  Death:
	TNT1 A 0;
	Stop;
  }
}

Class DCY_EODBall : DCY_EspouserSoul
{
  Default
  {
  SeeSound "PhantomRev/ScreamLong";
  Scale 0.5;
  DeathSound "DCYEOD/Tracer";
  Speed 20;
  FastSpeed 25;
  DamageFunction (40);
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,1.01]";
  DamageType "EOA";
  }
  
  States
  {
  Spawn:
	_334 EEEFFF 1
	{
		A_Tracer2(1.25);
		vel.x += frandom(-1, 1);
		vel.y += frandom(-1, 1);
		vel.z += frandom(-1, 1);
		A_SpawnParticleEx(0x52ff4f, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 20, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.05, sizestep: 0.725);
	}
	Loop;
  Death:
	_334 G 2
	{
		actor boom = Spawn("DCY_Explosion_1_Smaller", pos);
		if (boom) boom.translation = translation;
		
		for (int i = 2; i > 0; i--)
		{
			if (random(0, 2) == 0)
			{
				actor spirit = Spawn("DCY_RZSpirit", pos + (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10)));
				if (spirit) { scale *= 0.75; spirit.translation = translation; }
			}
		}
	}
	_334 HIJKLM 2;
	Stop;
  }
}

Class DCY_EODMiniBall : DCY_EODBall
{
  Default
  {
  Scale 1.2;
  Damage 7;
  -RANDOMIZE
  SeeSound "EOD/Miniball";
  DeathSound "Auteur/BallX";
  Speed 15;
  FastSpeed 20;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,1.01]";
  }
  
  States
  {
  Spawn:
	9_AL AAABBBCCC 1
	{
		if (DCY_EffectDistance(sfxdistance))
			for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x52ff4f, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), (Vel.x / 3.75), (Vel.y / 3.75), (Vel.z / 3.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.25);
			
		A_SpawnParticleEx(0x52ff4f, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(90, 95), 25, 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.0325, sizestep: 0.55);
	}
	Loop;
  Death:
	9_AL C 3 A_SpawnItemEx("DCY_AndroidExplosion", random(-1, 1), random(-1, 1), random(-1, 1), 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	9_AL DEFGHIJK 3 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_EODTrail2 : DCY_Effect
{
  Default
  {
  RenderStyle "Translucent";
  Alpha 1.0;
  +THRUACTORS
  +THRUSPECIES
  -SOLID
  Radius 20;
  Height 64;
  }
  
  States
  {
  Spawn:
	"####" "#######" 1
	{
		A_Recoil(-9);
		A_SpawnItemEx("DCY_EODTrail", zofs: 5, flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
	}
	"####" "#" 10 A_Stop();
	"####" "#####" 1 A_FadeOut(0.2, 0);
	Stop;
  }
}

Class DCY_EODSoul : Actor
{
  Default
  {
  Scale 1.1;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,1.01]";
  RenderStyle "Add";
  +DONTSPLASH
  +NOCLIP
  -SOLID
  +THRUACTORS
  }
  
  States
  {
  Spawn:
	D0R_ AAAABBBBCCCDDD 1
	{
		vel.z += 1;
		A_FadeOut(0.05, 0);
		A_SetScale(Scale.X + 0.01);
		A_SpawnItemEx("DCY_MonsoonTrailED", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), zvel: 2, flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION, failchance: 150);
		A_SpawnItemEx("DCY_Smonk", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), zvel: -4, flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION, failchance: 100);
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), zvel: 2, flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION, failchance: 50);
	}
	TNT1 A 1
	{
		for (int i = 15; i > 0; i--)
			A_SpawnItemEx("DCY_BFGThunder", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 100);
	}
	Stop;
  }
}