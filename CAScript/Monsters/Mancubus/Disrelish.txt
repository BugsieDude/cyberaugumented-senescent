Class DCY_Disrelish : DCY_SkinnyFat
{
  Default
  {
  Health 1000;
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  Translation "48:79=%[0.00,0.00,0.00]:[0.41,0.41,0.41]", "5:15=%[0.00,0.00,0.00]:[0.41,0.41,0.41]", "1:3=%[0.00,0.00,0.00]:[0.41,0.41,0.41]", "128:159=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "80:111=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "208:223=%[0.00,0.00,0.00]:[0.67,0.67,0.67]", "236:239=%[0.00,0.00,0.00]:[0.41,0.41,0.41]", "16:22=[255,255,255]:[255,255,128]", "168:175=[255,255,255]:[255,255,128]", "23:31=[255,255,128]:[255,147,0]", "176:184=[255,255,128]:[255,147,0]", "185:191=[255,147,0]:[64,0,0]", "32:47=[255,147,0]:[64,0,0]", "112:127=[255,0,0]:[0,0,0]";
  SeeSound "L4DBoomer/Sight";
  ActiveSound "L4DBoomer/Active";
  PainSound "L4DBoomer/Pain";
  DeathSound "L4DBoomer/Death";
  Tag "Disrelish";
  PainChance 50;
  +QUICKTORETALIATE
  DCYMonster.GibType "DCY_GibletsAdd";
  }
  
  States
  {
  Spawn:
	FOFO A 10 DCY_Look();
	Loop;
  Look:
	FOFO AAABBBCCCDDDEEEFFF 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	FOFO G 20 DCY_AlertSound();
	Goto See;
  See:
	FOFO AAABBBCCCDDDEEEFFF 3 A_Chase();
	Loop;
  Melee:
  Missile:
	FOFO G 20
	{
		A_FaceTarget();
		
		if (target && distance3D(target) < 300)
			SetStateLabel("Vomit");
		else
			A_StartSound("L4DBoomer/Attack", CHAN_VOICE);
	}
	FOFO HI 8
	{
		for (int i = 30; i > -31; i -= 60)
			A_SpawnProjectile("DCY_DisrelishFire", 30, i, frandom(-3, 3));
		
		A_FaceTarget();
		
		Return A_MonsterRefire(60, "See");
	}
	Goto Missile+1;
  Vomit:
	FOFO G 20
	{
		A_FaceTarget();
		A_StartSound("L4DBoomer/SlightVomit", CHAN_VOICE);
	}
	FOFO G 1 A_StartSound("L4DBoomer/Vomit", CHAN_VOICE);
	FOFO IIIIIIIIIIIIIIII 2
	{
		A_SpawnProjectile("DCY_DisrelishLavaVomit", 50, 0, frandom(-2.0, 2.0), CMF_OFFSETPITCH|CMF_TRACKOWNER, -8);
		A_FaceTarget();
	}
	Goto See;
  Pain:
    FOFO J 3;
    FOFO J 4 A_Pain();
    Goto See;
  Death:
    FOFO K 6 DCY_Gibs(false);
    FOFO L 6 A_Scream();
    FOFO M 5 A_NoBlocking();
    FOFO NOPQRS 5;
    FOFO T -1 A_BossDeath2();
    Stop;
  Raise:
    FOFO R 5;
    FOFO QPONMLK 5;
    Goto See;
  }
}

Class DCY_DisrelishFire : DCY_LeFatFireball
{
  Default
  {
  DamageType "Fire";
  DamageFunction 2;
  Speed 24;
  FastSpeed 36;
  Scale 0.65;
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,2.00,1.01]";
  SeeSound "InfernoGallant/Ball";
  }
  
  States
  {
  Spawn:
	BAL1 AB 1 A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 22);
	Loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("DCY_ImpBigFlame", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	TNT1 A 1 A_Explode(5, 50, XF_NOTMISSILE, 0);
	Stop;
  }
}

Class DCY_DisrelishLavaVomit : DCY_FatThrower
{
  Default
  {
  -NOGRAVITY
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.39,0.83]";
  DeathSound "world/lavasizzle";
  }
  
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAA 1 Bright
	{
		A_Explode(3, 35, 0, 1);
		A_SpawnItemEx("DCY_ImpBigFlame", random(-5, 5), random(-5, 5), random(-5, 5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_ThrowerEffect", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
	}
	Goto Death;
  Death:
	TNT1 A 1
	{
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_WaterParticleX", random(-5,5), random(-5,5), random(-5,5), xvel: frandom(1, 5), zvel: frandom(2, 6), angle: frandom(0, 360), flags: SXF_TRANSFERTRANSLATION);
			A_SpawnItemEx("DCY_WaterParticleXFaster", random(-5,5), random(-5,5), random(0,5), xvel: frandom(1, 5), zvel: frandom(2, 6), angle: frandom(0, 360), flags: SXF_TRANSFERTRANSLATION);
		}
	}
	Stop;
  }
}