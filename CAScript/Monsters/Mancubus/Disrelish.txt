Class DCY_Disrelish : DCY_SkinnyFat
{
  Default
  {
  Health 1000;
  BloodColor "ad3002";
  BloodType "AndroidNeonFluid";
  Translation "0:255=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "1:3=%[0.00,0.00,0.00]:[0.81,0.51,0.43]", "5:8=%[0.00,0.00,0.00]:[0.81,0.51,0.43]", "13:15=%[0.00,0.00,0.00]:[0.81,0.51,0.43]", "236:239=%[0.00,0.00,0.00]:[0.38,0.24,0.19]", "160:167=%[0.00,0.00,0.00]:[2.00,1.01,0.00]", "112:127=%[0.00,0.00,0.00]:[2.00,0.70,0.00]", "208:235=%[0.00,0.00,0.00]:[2.00,0.70,0.00]", "248:255=%[0.00,0.00,0.00]:[2.00,0.70,0.00]", "4:4=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "48:79=[175,119,103]:[0,0,0]", "168:191=%[0.22,0.00,0.00]:[1.91,0.50,0.09]", "16:47=%[0.22,0.00,0.00]:[1.92,0.50,0.09]";
  SeeSound "L4DBoomer/Sight";
  ActiveSound "L4DBoomer/Active";
  PainSound "L4DBoomer/Pain";
  DeathSound "L4DBoomer/Death";
  AttackSound "";
  Tag "Disrelish";
  PainChance 50;
  +QUICKTORETALIATE
  DCYMonster.GibType "DCY_GibletsLava";
  DCYMonster.ScientificName "Dipem vomitum";
  }
  
  States
  {
  Spawn:
	FOFO A 10 DCY_Look();
	Loop;
  Look:
	FOFO AAABBBCCCDDDEEEFFF 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	FOFO G 20 DCY_AlertSound();
	Goto See;
  See:
	FOFO AAABBBCCCDDDEEEFFF 3 DCY_Chase();
	Loop;
  Melee:
  Missile:
	FOFO G 20
	{
		A_FaceTarget();
		
		if (target && distance3D(target) < 300)
			SetStateLabel("Vomit");
		else
			A_StartSound("L4DBoomer/Attack", CHAN_VOICE);
	}
	FOFO HI 8
	{
		for (int i = 30; i > -31; i -= 60)
			A_SpawnProjectile("DCY_DisrelishFire", 30, i, frandom(-3, 3));
		
		A_FaceTarget();
		
		Return A_MonsterRefire(60, "See");
	}
	Goto Missile+1;
  Vomit:
	FOFO G 20
	{
		A_FaceTarget();
		A_StartSound("L4DBoomer/SlightVomit", CHAN_VOICE);
	}
	FOFO G 1 A_StartSound("L4DBoomer/Vomit", CHAN_VOICE);
	FOFO IIIIIIIIIIIIIIII 2
	{
		A_SpawnProjectile("DCY_DisrelishLavaVomit", 50, 0, frandom(-2.0, 2.0), CMF_OFFSETPITCH|CMF_TRACKOWNER, -8);
		A_FaceTarget();
	}
	Goto See;
  Pain:
    FOFO J 3;
    FOFO J 4 A_Pain();
    Goto See;
  Death:
    FOFO K 6;
    FOFO L 6 A_Scream();
    FOFO M 5 { DCY_Gibs(false); A_NoBlocking(); }
    FOFO NOPQRS 5;
    FOFO T -1 A_BossDeath2();
    Stop;
  Raise:
    FOFO R 5;
    FOFO QPONMLK 5;
    Goto See;
  }
}

Class DCY_DisrelishFire : DCY_LeFatFireball
{
  Default
  {
  DamageType "Fire";
  Damage 2;
  Speed 20;
  FastSpeed 25;
  Scale 0.65;
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.50,1.01]";
  SeeSound "InfernoGallant/Ball";
  }
  
  States
  {
  Spawn:
	BAL1 AB 1
	{
		if (DCY_EffectDistance(999)) A_SpawnFlames("a84a07", 25, 20, 0, true, frandom(-2.5, 2.5), thrust: 3);
	}
	Loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("DCY_ImpBigFlame", frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Stop;
  }
}

Class DCY_DisrelishLavaVomit : DCY_FatThrower
{
  Default
  {
  -NOGRAVITY
  Translation "0:255=%[0.50,0.00,0.00]:[2.00,1.39,0.83]";
  DeathSound "world/lavasizzle";
  Speed 20;
  +NEVERFAST
  Scale 0.9;
  }
  
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAA 1 Bright
	{
		A_Explode(4, 35, 0, 1);
		if (scale.x < 1.4) scale.x = scale.y += 0.1;
		A_SpawnItemEx("DCY_VomitSprite", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE);	
	}
	Goto Death;
  Death:
	TNT1 A 1
	{
		for (int i = random(2, 4); i > 0; i--)
		{
			A_SpawnItemEx("DCY_BloodMist", xvel: frandom(-1, 1), yvel: frandom(-1, 1), zvel: frandom(-1, 1), angle: frandom(0, 360), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 140);
			A_SpawnItemEx("De_Blood_Splat", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-6, 6), frandom(-6, 6), frandom(1, 3), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE, 150);
			A_SpawnItemEx("De_Blood_BigSplat", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-6, 6), frandom(-6, 6), frandom(1, 3), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE, 15);	
		}
	}
	Stop;
  }
}

Class DCY_VomitSprite : De_Blood_BigSplat
{
  States
  {
  Spawn:
	#### EFGHIJKLMNOPQRS 1
	{
		if (e <= 0) Destroy();
		e--;
		A_FadeOut(0.05);
		
		if (vel.z == 0)
		{
			Destroy();
		}
	}
	Stop;
  }
}