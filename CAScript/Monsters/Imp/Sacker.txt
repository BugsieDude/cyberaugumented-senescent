Class DCY_Sacker : DCY_Impe
{
  Default
  {
  Health 300;
  SeeSound "sacker/sight";
  ActiveSound "sacker/active";
  PainSound "sacker/pain";
  DeathSound "sacker/death";
  Scale 1.05;
  Translation "80:111=%[0.00,0.00,0.00]:[0.63,0.63,0.63]", "1:3=%[0.00,0.00,0.00]:[0.63,0.63,0.63]", "5:12=%[0.00,0.00,0.00]:[0.63,0.63,0.63]", "4:4=%[0.00,0.00,0.00]:[0.63,0.63,0.63]", "16:47=%[0.00,0.00,0.00]:[0.61,0.61,2.00]", "168:191=%[0.00,0.00,0.00]:[0.61,0.61,2.00]";
  BloodColor "Blue";
  DamageType "Electric";
  Tag "\c[j1]Sacker\c-";
  PainChance 60;
  }
  
  void A_Sacker()
  {
	if (target)
	{
		A_StartSound("SCYTHE", CHAN_WEAPON);
		A_SpawnProjectile("DCY_SackerBall", 34, 0, frandom(-12.0, 12.0));
		A_FaceTarget();
	}
  }
  
  void A_Sacker2()
  {
	if (target)
	{
		A_StartSound("SCYTHE2", CHAN_WEAPON);
		
		for (int i = 5; i > -6; i -= 10)
		{
			A_SpawnProjectile("DCY_SackerParticule", 34, 0, i);
			A_SpawnProjectile("DCY_SackerParticule", 34, 0, 0, CMF_OFFSETPITCH|CMF_TRACKOWNER, i);
		}
		
		A_SpawnProjectile("DCY_SackerParticule", 34);
		
		A_FaceTarget();
	}
  }
  
  States
  {
  Spawn:
	TIO4 A 10 A_Look();
	Loop;
  See:
	TIO4 AABBCCDD 2
	{
		A_Chase();
		Return A_Jump(5, "Hop");
	}
	Loop;
  Hop:
	TIO4 A 10
	{
		A_Dodging(-13);
		A_Chase(null, "Missile", CHF_DONTMOVE);
		A_Hop(8);
	}
	TIO4 A 10 A_Chase("Melee", "Missile", CHF_DONTMOVE);
	Goto See;
  Melee:
	TIO4 EF 4 A_FaceTarget();
	TIO4 G 4 A_CustomMeleeAttack(random(1, 7) * 12, "imp/melee");
	TIO4 F 4;
	Goto See;
  Missile:
	TIO4 EF 5 A_FaceTarget();
	TIO4 F 0 A_Jump(100, "Missile2");
	TIO4 GGGG 4 A_Sacker();
	TIO4 F 4 A_FaceTarget();
	Goto See;
  Missile2:
	TIO4 GG 10 A_Sacker2();
	TIO4 F 4 A_FaceTarget();
	Goto See;
  Pain:
	TIO4 H 4;
	TIO4 H 4 A_Pain();
	Goto See;
  Death:
	TIO4 I 3;
	TIO4 I 3 A_Scream();
	TIO4 J 5 A_NoBlocking();
	TIO4 KL 5;
	TIO4 M -1;
	Stop;
  XDeath:
	TIO4 N 3;
	TIO4 N 3 A_XScream();
	TIO4 O 5 A_NoBlocking();
	TIO4 PQRST 4;
	TIO4 U -1;
	Stop;
  Raise:
	TIO4 LKJI 5;
	Goto See;
  }
}

Class DCY_SackerBall : DoomImpBall
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.61,0.61,2.00]";
  Speed 18;
  FastSpeed 25;
  SeeSound "SCYTHE";
  DeathSound "V3RYHURT";
  +BRIGHT
  }
  
  States
  {
  Spawn:
	AV_3 AAABBB 1
	{
		A_SpawnParticle(0xb0b9ff, SPF_FULLBRIGHT, 34, random(10, 15), 0, frandom(-15, 15), frandom(-15, 15), frandom(-15, 15), fadestepf: 0.1, sizestep: -1.5);
		A_SpawnItemEx("DCY_SackerTrailB", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Weave(0, 2, 0, 1);
	}
	Loop;
  Death:
	AV_3 CDEFG 2;
	Stop;
  }
}

Class DCY_SackerParticule : DCY_SackerBall
{
  Default
  {
  Scale 0.5;
  Speed 25;
  FastSpeed 35;
  +RIPPER
  Damage 1;
  SeeSound "SCYTHE2";
  DeathSound "SCYTHE2";
  }
  
  int timer;
  
  States
  {
  Spawn:
    AV_3 AABB 1
	{
		A_SpawnParticle(0xb0b9ff, SPF_FULLBRIGHT, 34, random(5, 10), 0, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), fadestepf: 0.1, sizestep: -1.15);
		A_SpawnItemEx("DCY_SackerTrailB", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
		
		if (timer > 200)
			Destroy();
		
		timer++;
	}
	Loop;
  }
}

Class DCY_SackerTrailB : DCY_Effect
{
  Default
  {
  RenderStyle "Add";
  +NOGRAVITY
  +BRIGHT
  }
  
  States
  {
  Spawn:
	AV_3 HIJK 2;
	Stop;
  }
}