Class DCY_AtramentousFade : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 0.45;
  Scale 0.5;
  +NOCLIP
  +NOINTERACTION
  +BRIGHT
  }
  States
  {
  Spawn:
	EF1_ Q 1
	{
		If (Scale.X <= 0.0000001 || Alpha <= 0.000000001)
			Return ResolveState("Null");
		
		A_FadeOut(0.05, 0);
		A_SetScale(Scale.X-0.05, Scale.Y-0.05);
		Return ResolveState(null);
	}
	Loop;
  }
}

Class DCY_AtramentousImp : DCY_Impe
{
  Default
  {
  Health 100;
  Speed 15;
  +FIRERESIST
  +MISSILEMORE
  +QUICKTORETALIATE
  +NOFEAR
  -CANBLAST
  BloodColor "Cyan";
  Tag "\c[c9]Atramentous Imp\c-";
  SeeSound "atramentousimp/sight";
  ActiveSound "atramentousimp/sight";
  PainSound "atramentousimp/pain";
  DropItem "Blursphere", 40;
  DeathSound "";
  }
  
  int phase;
  
  void A_Atramentous()
  {
	A_SetTranslucent(1);
	A_UnHideThing();
	
	if (phase == 1)
	{
		for (int i = 11; i > 0; i--)
			A_SpawnItemEx("DCY_AtramentousFade", random(-20, 20), random(-20, 20), random(0, 50), 0, 0, random(-3, 3));
	}
	
	phase = 0;
  }
	
  States
  {
  Spawn:
    A0TP A 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    A0TP AAABBBCCCDDD 2 A_Chase();
	TNT1 A 0 A_Jump(192, "Dodging");
	Loop;
  SeeInvincible:
    A0TP AABBCCDDAABBCCDDAABBCCDDAABBCCDD 1 A_Chase(null, null);
	TNT1 A 0
	{
		phase = 0;
		
		for (int i = 11; i > 0; i--)
			A_SpawnItemEx("DCY_AtramentousFade", random(-20, 20), random(-20, 20), random(0, 50), 0, 0, random(-3, 3));
	}
	A0TP E 0 A_StartSound("atramentousimp/teleport");
	TNT1 A 0 A_UnhideThing();
	A0TP E 8;
    Goto See;
  Dodging:
	A0TP A 5
	{
		A_Dodging(40);
		A_FaceTarget();
		A_Chase("Melee", "Missile", CHF_DONTMOVE);
	}
    A0TP AAA 5 A_Chase("Melee", "Missile", CHF_DONTMOVE);
	A0TP A 1 A_Stop();
	Goto See;
  Melee:
	A0TP E 3
	{
		A_UnHideThing();
		A_SetTranslucent(1);
		A_FaceTarget();
	}
	A0TP F 3 A_FaceTarget();
	A0TP G 3 A_CustomMeleeAttack(7 * random(1, 4), "imp/melee");
	Goto See;
  Missile:
	A0TP E 0 A_Jump(64, "Hide", "FastMissile");
	A0TP E 0 A_Jump(256, "NormalM", "ShadowM");
  NormalM:
	A0TP E 4
	{
		A_UnHideThing();
		A_SetTranslucent(1);
		A_FaceTarget();
	}
	A0TP F 4 A_FaceTarget();
	A0TP G 4
	{
		for (int i = 20; i > -21; i -= 20)
			A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, i, 1);
	}
	Goto See;
  FastMissile:
	A0TP E 4
	{
		A_UnHideThing();
		A_SetTranslucent(1);
		A_FaceTarget();
	}
	A0TP F 4 A_FaceTarget();
	A0TP G 4 A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, 0, 1);
	A0TP EF 4 A_FaceTarget();
	A0TP G 4 A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, 0, 1);
	A0TP EF 4 A_FaceTarget();
	A0TP G 4 A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, 0, 1);
	A0TP EF 4 A_FaceTarget();
	A0TP G 4 A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, 0, 1);
	A0TP EF 4 A_FaceTarget();
	A0TP G 4 A_SpawnProjectile("DCY_AtramentousImpBall", 30, 0, 0, 1);
    Goto See;
  ShadowM:
	A0TP E 7
	{
		A_UnHideThing();
		A_SetTranslucent(1);
		A_FaceTarget();
	}
	A0TP F 7 A_FaceTarget();
	A0TP G 7
	{
		for (int i = -20; i < 21; i += 10)
			A_SpawnProjectile("DCY_AtramentousImpShadowBall", 30, 0, i, 0);
	}
	Goto See;
  Hide:
	A0TP E 8 Bright
	{
		A_StartSound("atramentousimp/teleport");
		A_FaceTarget();
	}
	TNT1 A 0
	{
		phase = 1;
		
		for (int i = 11; i > 0; i--)
			A_SpawnItemEx("DCY_AtramentousFade", random(-20, 20), random(-20, 20), random(0, 50), 0, 0, random(-3, 3));
		
		A_HideThing();
	}
    Goto SeeInvincible;
  Pain:
	A0TP H 2 A_Atramentous();
	A0TP H 2 A_Pain();
    Goto See;
  Death:
	A0TP I 1;
	A0TP I 8 A_Atramentous();
	A0TP J 8 A_StartSound("atramentousimp/death", CHAN_VOICE, 0, 1.0, ATTN_NORM);
	A0TP K 6;
	A0TP L 6 A_Fall();
	A0TP M -1;
	Stop;
  XDeath:
	A0TP N 1 A_UnHideThing();
	A0TP N 5 A_Atramentous();
	A0TP O 5 A_StartSound("misc/gibbed", CHAN_VOICE, 0, 1.0, ATTN_NORM);
	A0TP P 5 A_Fall();
	A0TP RST 5;
	A0TP U -1;
	Stop;
  Ice:
    A0TP N 0
	{
		phase = 0;
		A_UnHideThing();
		A_SetTranslucent(1);
	}
    A0TP H 5 A_GenericFreezeDeath();
	A0TP H 1 A_FreezeDeathchunks();
	Wait;
  Raise:
	A0TP LK 8;
	A0TP JI 6;
	Goto See;
  }
}

Class DCY_AtramentousImpBall : DoomImpBall
{
  Default
  {
  Speed 10;
  Damage 5;
  SeeSound "atramentousimp/ball";
  DeathSound "atramentousimp/explode";
  +SEEKERMISSILE;
  }
  States
  {
  Spawn:
	A0TB AABB 1 Bright
	{
		A_SeekerMissile(6, 0);
		
		for (int i = 2; i > 0; i--)
			A_SpawnItemEx("DCY_AtramentousImpBallTrail", 0, 0, 0, random(-1, 1), random(-1, 1), random(-1, 1));
		
		A_Weave(2, 2, 0.3, 0.3);
	}
    Loop;
  Death:
	A0TB CDE 2 Bright;
	Stop;
  }
}

Class DCY_AtramentousImpBallTrail : DCY_ViolationBallTrail
{
  States
  {
  Spawn:
	A0TB AABBAABBAA 1 Bright A_FadeOut(0.15, 0);
	Stop;
  }
}

Class DCY_AtramentousImpShadowBall : DCY_ViolationBall
{
  Default
  {
  Speed 15;
  Damage 3;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,2.00]";
  }
  
  States
  {
  Spawn: 
	A0SB ABC 2 BRIGHT A_SpawnItemEx("DCY_AVTrail", flags: SXF_NOCHECKPOSITION);
	Loop;
  Death:
	A0SB C 5 Bright;
	A0SB DEFGH 4 BRIGHT;
	Stop;
  }
}

Class DCY_AVTrail : DCY_ViolationBallTrail
{
  States
  {
  Spawn:
	A1SB ABCDEF 2 Bright;
	Stop;
  }
}