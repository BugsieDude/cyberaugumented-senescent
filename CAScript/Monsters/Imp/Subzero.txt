Class DCY_SubzeroUmbrage : DCY_Impe
{
  Default
  {
  Health 300;
  Speed 9;
  FastSpeed 13;
  RenderStyle "Add";
  Alpha 3.0;
  Mass 100;
  PainChance 200;
  +NOBLOOD
  +BRIGHT
  +THRUSPECIES
  DamageType "Ice";
  Tag "\c[j0]Sub-Zero \c[j1]Umbrage\c-";
  Obituary "%o felt the frostbite of an Subzero Umbrage.";
  HitObituary "%o felt the frostbite of an Subzero Umbrage.";
  SeeSound "SuperShadow/Sight";
  ActiveSound "SuperShadow/Active";
  PainSound "SuperShadow/Pain";
  DeathSound "SuperShadow/Death";
  +DONTHARMCLASS
  DCYMonster.ScientificName "NO DATA";
  DCYMonster.FootStepSound "";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o felt the frostbite of an Subzero Umbrage.",
		"%o got the cold breath."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  override void Tick()
  {
	super.tick();
	
	if (!level.isFrozen() && DCY_EffectDistance(sfxdistance))
	{
		let trail = Spawn("DCY_ShadowTrailBase", (pos.x, pos.y, pos.z + 6));
		
		if (trail)
		{
			trail.vel.x = frandom(-1.0, 1.0);
			trail.vel.y = frandom(-1.0, 1.0);
			trail.vel.z = frandom(-1.0, 1.0);
			trail.frame = frame;
			trail.scale = scale;
			trail.angle = angle;
			trail.sprite = GetSpriteIndex("SZ84");
		}
	}
  }
  
  States
  {
  Spawn:
	SZ83 A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    SZ83 BBCCDDEE 2 DCY_Chase();
	Loop;
  Melee:
    SZ83 EF 4 A_FaceTarget();
	SZ83 G 4 A_CustomMeleeAttack(5 * random(1, 10), "SuperShadow/Attack", damagetype: "Ice");
	TNT1 A 0 A_Jump(150, "Missile");
	Goto See;
  Missile:
    SZ83 A 5
	{
		If (!random(0, 1) && (target && Distance3D(target) < 355)) SetStateLabel("Jump");
		A_FaceTarget();
	}
    SZ83 A 10
	{
		A_StartSound("SuperShadow/Attack", CHAN_VOICE);
		A_FaceTarget();
	}
	SZ83 F 10 A_FaceTarget();
	SZ83 GGGGGGGGG 3
	{
		A_Facetarget();
		A_SpawnProjectile("DCY_UmbrageBreath", 30, 0, frandom(3, -3));
	}
	SZ83 F 10 A_FaceTarget();
	Goto See;
  Jump:
	SZ83 A 7 A_FaceTarget();
	SZ83 F 7 A_FaceTarget();
	SZ83 G 5
	{
		A_Recoil(-26);
		A_Hop(5);
		A_StartSound("SuperShadow/Attack", CHAN_VOICE);
		A_FaceTarget();
	}
	Goto Missile+1;
  Pain:
    SZ83 H 2;
    SZ83 H 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
  XDeath:
	SZ83 H 1;
    SZ83 H 19 A_ScreamAndUnblock();
	SZ83 HHHHHHHHHHH 1 A_SpawnParticleEx(0x222222, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_SUBTRACT, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.2, fadestepf: 0.0035, sizestep: 1.525);
	SZ83 HHHHHHHHHH 2
	{
		A_FadeOut(0.045, 0);
		A_SpawnParticleEx(0x222222, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_SUBTRACT, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, 0, frandom(-20, 20), frandom(-20, 20), 36 + frandom(-20, 20), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), startalphaf: 0.2, fadestepf: 0.0035, sizestep: 1.525);
	}
	Stop;
  Death.Massacre:
	SZ83 H 1;
    SZ83 H 19 A_ScreamAndUnblock();
	Stop;
  }
}

Class DCY_UmbrageBreath : DCY_RegulusThrower
{
  States
  {
  Spawn:
	TNT1 A 5;
	Goto Looping;
  Looping:
	TNT1 A 1
	{
		A_FadeOut(0.0415);
		if ((GetAge() % 2) == 0) A_Explode(6, 30);
		vel.x += frandom(-0.1, 0.1);
		vel.y += frandom(-0.1, 0.1);
		vel.z += frandom(-0.1, 0.1);
		A_SpawnParticleEx(0x666666, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_SUBTRACT, SPF_FULLBRIGHT|SPF_RELATIVE, 128, 40, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0045, sizestep: 1.525);
	}
	Wait;
  Death:
	Stop;
  }
}