Class DCY_SubzeroUmbrage : DCY_Impe
{
  Default
  {
  Health 300;
  Speed 13;
  FastSpeed 15;
  RenderStyle "Add";
  Alpha 3.0;
  Mass 100;
  PainChance 200;
  +NOBLOOD
  +BRIGHT
  +THRUSPECIES
  DamageType "Ice";
  Tag "\c[j0]Sub-Zero \c[j1]Umbrage\c-";
  Obituary "%o felt the frostbite of an Subzero Umbrage.";
  HitObituary "%o felt the frostbite of an Subzero Umbrage.";
  SeeSound "SuperShadow/Sight";
  ActiveSound "SuperShadow/Active";
  PainSound "SuperShadow/Pain";
  DeathSound "SuperShadow/Death";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o felt the frostbite of an Subzero Umbrage.",
		"%o got the cold breath.",
		"The weather temperature is quite cold today thanks to that bitchass Subzero Umbrage."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  override void Tick()
  {
	super.tick();
	
	if (!level.isFrozen() && DCY_EffectDistance(sfxdistance))
	{
		let trail = Spawn("DCY_ShadowTrailBase", (pos.x, pos.y, pos.z + 6));
		
		if (trail)
		{
			trail.vel.x = frandom(-1.0, 1.0);
			trail.vel.y = frandom(-1.0, 1.0);
			trail.vel.z = frandom(-1.0, 1.0);
			trail.frame = frame;
			trail.scale = scale;
			trail.angle = angle;
			trail.sprite = GetSpriteIndex("SZ84");
		}
	}
  }
  
  States
  {
  Spawn:
	SZ83 A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    SZ83 BBCCDDEE 2 A_Chase();
	Loop;
  Melee:
    SZ83 EF 4 A_FaceTarget();
	SZ83 G 4 A_CustomMeleeAttack(5 * random(1, 10), "SuperShadow/Attack", damagetype: "Ice");
	TNT1 A 0 A_Jump(150, "Missile");
	Goto See;
  Missile:
    TNT1 A 0
	{
		If(target && Distance3D(target) < 355)
			Return ResolveState("Jump");
		
		Return ResolveState(null);
	}
	Goto MissileContinue;
  MissileContinue:
    TNT1 A 0 A_Jump(128, "Spread");
    SZ83 E 4 A_FaceTarget();
    SZ83 F 4 A_FaceTarget();
	SZ83 G 4 A_SpawnProjectile("DCY_SubzeroUmbrageBall", 30, 0);
	SZ83 E 4 A_FaceTarget();
    SZ83 F 4 A_FaceTarget();
	SZ83 G 4 A_SpawnProjectile("DCY_SubzeroUmbrageBall", 30, 0);
	Goto See;
  Spread:
    SZ83 A 10
	{
		A_StartSound("SuperShadow/Attack", CHAN_VOICE);
		A_FaceTarget();
	}
    SZ83 F 10 A_FaceTarget();
	SZ83 G 10
	{
		for (float i = -8.5; i < 9; i += 8.5)
			A_SpawnProjectile("DCY_SubzeroUmbrageBall", 30, 0, i);
	}
	Goto See;
  Jump:
	SZ83 A 7
	{
		A_FaceTarget();
		Return A_Jump(100, "Missile");
	}
	SZ83 F 7 A_FaceTarget();
	SZ83 G 5
	{
		A_Recoil(-26);
		A_Hop(5);
		A_StartSound("SuperShadow/Attack", CHAN_VOICE);
		A_FaceTarget();
	}
	Goto MissileContinue;
  Pain:
    SZ83 H 2;
    SZ83 H 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
  XDeath:
	SZ83 H 1;
    SZ83 H 19 A_ScreamAndUnblock();
	SZ83 HHHHHHHHHHH 1 A_SpawnItemEx("DCY_MaelstromSmonk", frandom(-10, 10), frandom(-10, 10), frandom(10, 35), 0, 0, frandom(0, 6), 0, SXF_NOCHECKPOSITION, 100);
	SZ83 HHHHHHHHHH 2
	{
		A_FadeOut(0.045, 0);
		A_SpawnItemEx("DCY_MaelstromSmonk", frandom(-10, 10), frandom(-10, 10), frandom(10, 35), 0, 0, frandom(0, 6), 0, SXF_NOCHECKPOSITION, 100);
		A_SpawnItemEx("DCY_MaelstromSmonk", frandom(-10, 10), frandom(-10, 10), frandom(10, 35), 0, 0, frandom(0, 6), 0, SXF_NOCHECKPOSITION, 100);
	}
	Stop;
  Death.Massacre:
	SZ83 H 1;
    SZ83 H 19 A_ScreamAndUnblock();
	Stop;
  }
}

Class DCY_SubzeroUmbrageBall : DCY_ViolationBall
{
  Default
  {
  Speed 25;
  Translation "0:255=%[0.00,0.00,0.00]:[1.01,1.50,2.00]";
  SeeSound "DCYViolator/attack";
  DamageType "Ice";
  }
  States
  {
  Spawn: 
	SBAL A 1 Bright
	{
		A_SpawnItemEx("DCY_ViolationBallTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_Weave(3, 3, 0.35, 0.35);
	}
	Loop;
  }
}