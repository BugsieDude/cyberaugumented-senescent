Class DCY_Pryer : DCY_Impe
{
  Default
  {
  Health 80;
  DamageType "Fire";
  Obituary "A pryer fried %o to pieces.";
  HitObituary "%o was deep fried by a pryer.";
  Tag "Pryer";
  Translation "16:111=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "128:255=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "13:15=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "112:116=[255,255,255]:[255,255,128]", "117:121=[255,255,108]:[255,128,0]", "122:127=[244,122,0]:[64,0,0]";
  Scale 0.875;
  SeeSound "Pryer/Sight";
  ActiveSound "";
  PainSound "Pryer/Pain";
  DeathSound "Pryer/Death";
  BloodType "AndroidNeonFluid";
  BloodColor "ad3002";
  DCYMonster.GibType "DCY_GibletsLava";
  DCYMonster.ScientificName "Ignauus surculus";
  DCYMonster.FootstepSound "dcyrocky/walk";
  }
  States
  {
  Spawn:
	XX5_ A 10 DCY_Look();
	Loop;
  Look:
	XX5_ AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	XX5_ AABBCCDD 4 DCY_Chase("Missile", "Missile");
	Loop;
  Missile:
	XX5_ Q 0 A_Jump(90, "Dash", "Jump");
	XX5_ Q 15 A_FaceTarget();
	XX5_ Q 5
	{
		A_SpawnProjectile("DCY_PFire", 37, -12);
		A_SpawnProjectile("DCY_PFire2", 37, 12);
		A_FaceTarget();
	}
	XX5_ Q 5;
	Goto See;
  Jump:
	XX5_ E 0;
	XX5_ E 1 A_FaceTarget();
	XX5_ E 7
	{
		vel.z = 12;
		A_Recoil(-12);
	}
	XX5_ FG 7;
	Goto Missile+2;
  Dash:
	XX5_ E 0;
	XX5_ EF 8 A_FaceTarget();
	XX5_ GGGGGGGG 2
	{
		A_FaceTarget();
		A_Recoil(-3);
	}
	Goto Missile+2;
  Pain:
	XX5_ H 4;
	XX5_ H 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
	XX5_ R 1 A_Flip();
	XX5_ R 6
	{
		A_NoBlocking();
		A_Scream();
	}
	XX5_ STUV 6;
	XX5_ W -1;
	Stop;
  XDeath:
	XX5_ I 1;
	XX5_ I 6
	{
		A_NoBlocking();
		DCY_Gibs();
		A_Scream();
	}
	XX5_ JKLMNO 5;
	XX5_ P -1;
	Stop;
  Raise:
	XX5_ PO 4;
	XX5_ NMLKJIH 3;
	XX5_ H 0 A_Restore();
	Goto See;
  }
}

Class DCY_PFire : DCY_ImpFireball
{
  Default
  {
  Damage 3;
  Speed 11;
  FastSpeed 22;
  DamageType "Fire";
  Scale 0.765;
  Radius 6;
  Height 6;
  Translation "0:255=%[0.47,0.00,0.00]:[1.50,1.00,0.01]";
  Obituary "A pryer fried %o to pieces.";
  +ROLLSPRITE
  }
  
  void A_PryerTrail(int weavenum)
  {
	if (!DCY_EffectDistance(sfxdistance)) return;
	A_SetRoll(random(0, 360), SPF_INTERPOLATE);
	A_SpawnFlames("613a00", 25, 32, 0, allsmoke: true);
	if ((level.time % random(1, 9)) == 0) A_SpawnParticleEx(0x613a00, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 80, random(3, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.2, 0.2), frandom(-0.2, 0.2), frandom(-0.2, 0.2), fadestepf: 0, sizestep: -0.125);			
	A_SpawnItemEx("DCY_ImpFlame1", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE, failchance: 200);
	A_Weave(2, 0, weavenum, 0);
  }
  
  States
  {
  Spawn:
	BAL1 AB 1 A_PryerTrail(2);
	Loop;
  Death:
	BAL1 CCCDDDEEE 1
	{
		A_SetScale(Scale.X+0.1, Scale.Y+0.1);
		A_FadeOut(0.125, 0);
	}
	Stop;
  }
}

Class DCY_PFire2 : DCY_PFire
{
  States
  {
  Spawn:
	BAL1 AB 1 A_PryerTrail(-2);
	Loop;
  }
}