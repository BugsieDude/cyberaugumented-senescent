Class DCY_Anathema : DCY_Impe
{
  Default
  {
  Translation "16:47=%[0.00,0.00,0.00]:[0.63,0.63,0.63]", "128:159=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "13:15=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "236:239=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "160:167=%[0.00,0.00,0.00]:[1.17,0.00,0.00]", "208:231=%[0.00,0.00,0.00]:[2.00,0.00,0.00]", "248:249=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  Health 333;
  Tag "\c[Red]Anathema\c-";
  SeeSound "sacker/sight";
  ActiveSound "sacker/active";
  PainSound "sacker/pain";
  DeathSound "sacker/death";
  +MISSILEEVENMORE
  DamageType "Anathemic";
  Obituary "The anathema's cluster ripped %o apart.";
  }
  
  States
  {
  Spawn:
	SAIM A 10 DCY_Look();
	Loop;
  Look:
	"####" AAAABBBBCCCCDDDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	SAIM AAAABBBBCCCCDDDD 2
	{
		A_Chase("Missile", "Missile");
		Return A_Jump(10, "Dodge");
	}
	Loop;
  Dodge:
	SAIM A 10
	{
		bNoDropOff = true;
		A_Chase("Missile", "Missile", CHF_DONTMOVE);
		A_Dodging(24);
		A_Hop(5);
	}
	SAIM A 2
	{
		A_Stop();
		bNoDropOff = false;
	}
	Goto See;
  Missile:
	SAIM AEF 7 A_FaceTarget();
	SAIM G 7
	{
		A_SpawnProjectile("DCY_AnathemaProjectile", 34, 0, -12);
		A_SpawnProjectile("DCY_AnathemaProjectile", 34, 0, 0);
		A_SpawnProjectile("DCY_AnathemaProjectile", 34, 0, 12);
		A_FaceTarget();
	}
	SAIM F 6;
	Goto See;
  Pain:
	SAIM H 4 { bNoDropOff = false; }
	SAIM H 4 A_Pain();
	Goto See;
  Death:
  XDeath:
	SAIM H 4;
	SAIM I 6 A_ScreamAndUnblock();
	SAIM J 5;
	SAIM KLM 4;
	SAIM M -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_AnathemaProjectile : DoomImpBall
{
  Default
  {
  Damage 1;
  Speed 8;
  FastSpeed 18;
  SeeSound "MagicProjectile/Shot";
  DeathSound "PlasmaOrb";
  Scale 1.1;
  +ROLLSPRITE
  +NOEXTREMEDEATH
  +SEEKERMISSILE
  +BRIGHT
  DamageType "Anathemic";
  }
  States
  {
  Spawn:
	SUE2 A 0 NoDelay A_SetRoll(Roll+frandom(0.00, 360.00));
	SUE2 AAAABBBB 1
	{
		A_SeekerMissile(1, 1, SMF_PRECISE);
		A_SetRoll(Roll+10, SPF_INTERPOLATE);
		A_SpawnItemEx("DCY_AnathemaAura", xvel: -1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL|SXF_TRANSFERSPRITEFRAME);
		A_Weave(2, 3, 4, 1);
	}
	Goto Spawn+1;
  Death:
	SUE2 FGHIJKLM 3;
	Stop;
  }
}

Class DCY_AnathemaAura : Actor
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 0.85;
  Scale 1.05;
  +NOCLIP
  +BRIGHT
  +NOGRAVITY
  +ROLLSPRITE
  +NOINTERACTION
  }
  States
  {
  Spawn:
	SUE2 "#" 1
	{
		If(Alpha <= 0.000000001 || Scale.X <= 0.000000001)
		{
			Return ResolveState("Null");
		}

		A_SetRoll(Roll+10, SPF_INTERPOLATE);
		A_SetScale(Scale.X-0.02, Scale.Y-0.02);
		A_FadeOut(0.075, 0);
		Return ResolveState(null);
	}
	Loop;
  }
}