Class DCY_LiquidatorTypeImp : DCY_Impe
{
  Default
  {
  Health 120;
  Speed 10;
  FastSpeed 13;
  Tag "\c[Orange]Liquidator\c- ''Type - Imp''";
  Obituary "The Liquidator brought another soul to hell's army.";
  HitObituary "The Liquidator brought another soul to hell's army.";
  Translation "80:111=[255,255,255]:[0,0,0]", "59:79=[164,118,94]:[0,0,0]", "13:15=%[0.00,0.00,0.00]:[0.94,0.68,0.53]", "236:239=%[0.00,0.00,0.00]:[0.76,0.54,0.42]", "48:58=[164,118,94]:[164,118,94]", "208:235=[255,4,4]:[255,121,121]", "248:249=[0,0,0]:[255,255,255]", "160:167=[64,0,0]:[255,0,0]", "168:191=%[0.00,0.00,0.00]:[2.00,0.00,0.00]";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +DONTHARMCLASS
  BloodColor "DarkRed";
  SeeSound "L4DSmoker/Sight";
  ActiveSound "L4DSmoker/Active";
  PainSound "L4DSmoker/Pain";
  DeathSound "L4DSmoker/Death";
  DCYMonster.BurnSound "L4DSmoker/Burn";
  Mass 200;
  }
  
  void A_LiquidatingPlasma(double angle)
  {
	A_StartSound("PlasmaLaser2", CHAN_WEAPON);
	A_SpawnProjectile("DCY_ArachPlasma", 32, angle, random(-5, 5), CMF_OFFSETPITCH, pitch + random(-5, 5));
	A_FaceTarget();
	missile--;
  }
  
  int missile;
  
  States
  {
  Spawn:
    RIMP A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    RIMP AABBCCDD 3 DCY_Chase();
	Loop;
  Melee:
  Missile:
  Normal1:
	TNT1 A 0
	{
		A_StartSound("L4DSmoker/Attack", CHAN_VOICE);
		if (!random(0, 1)) SetStateLabel("Normal2");
		missile = 10;
	}
	RIMP EFG 10 A_FaceTarget();
	RIMP V 2 A_LiquidatingPlasma(5);
	RIMP G 2 {
		if (missile <= 0) SetStateLabel("Cooldown1"); A_FaceTarget();
	}
	Goto Normal1+4;
  Cooldown1:
	RIMP VGGG 2 A_FaceTarget();
	RIMP F 5;
	Goto See;
  Normal2:
	RIMP GFE 10 A_FaceTarget();
	RIMP W 2 A_LiquidatingPlasma(-5);
	RIMP E 2 {
		if (missile <= 0) SetStateLabel("Cooldown2"); A_FaceTarget();
	}
	Goto Normal2+3;
  Cooldown2:
	RIMP WEEE 2 A_FaceTarget();
	RIMP F 5;
	Goto See;
  Pain:
    RIMP H 3;
	RIMP H 3 A_Pain();
	Goto See;
  Death:
	RIMP I 8;
    RIMP J 8 A_Scream();
    RIMP K 6;
    RIMP L 6 A_NoBlocking();
    RIMP M -1;
    Stop;
  XDeath:
	RIMP N 1;
	RIMP N 5
	{
		A_SpawnItemEx("DCY_Explosion_1", zofs: 30, flags: SXF_NOCHECKPOSITION);
		DCY_Gibs();
	}
	//RIMP O 0 A_custommissile("DCY_LiquidatorArm2",30,-10,270)
	//RIMP O 0 A_custommissile("DCY_LiquidatorArm",30,10,93)
    RIMP O 3 A_Explode(80,100);
	RIMP P 3;
    RIMP Q 3 A_NoBlocking();
    RIMP RST 3;
    RIMP U -1;
    Stop;
  Raise:
    RIMP ML 8;
    RIMP KJI 6;
    Goto See;
  }
}