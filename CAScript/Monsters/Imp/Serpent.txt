Class DCY_Serpent : DCY_Impe
{
  Default
  {
  Health 88;
  Tag "Serpent";
  Obituary "%o got liquified by a Serpent.";
  HitObituary "%o got slashed by a Serpent.";
  DamageType "Acid";
  SeeSound "Serpent/Sight";
  ActiveSound "Serpent/Active";
  PainSound "Serpent/Pain";
  DeathSound "Serpent/Death";
  DCYMonster.ScientificName "Acidum serpentis";
  DCYMonster.FootStepSound "";
  }
  
  States
  {
  Spawn:
    SERP A 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    SERP AAABBBCCCDDD 3 DCY_Chase();
	Loop;
  Melee:
	SERP EF 8 A_FaceTarget();
	SERP G 5 A_CustomMeleeAttack(3 * random(1, 8), "imp/melee");
	SERP F 5;
	Goto See;
  Missile:
    SERP E 0
	{
		if (target && Distance3D(target) < 257)
			Return ResolveState("Lunge");
		
		Return A_Jump(67, "Double", "Dash");
	}
    SERP EF 7 A_FaceTarget();
	SERP G 0 A_SpawnProjectile("DCY_SerpentBall", 33, 0, random(-2, 2));
	SERP GF 6;
	Goto See;
  Lunge:
	SERP EEFF 5 A_FaceTarget();
	SERP G 1
	{
		A_Hop(1);
		A_FaceTarget();
	}
	SERP G 0 A_Stop();
	SERP G 0 A_Recoil(-20);
	SERP GGGGGGFFFFFFF 2;
	Goto Missile+1;
  Double:
	SERP EF 7 A_FaceTarget();
	SERP G 0 A_SpawnProjectile("DCY_SerpentBall", 33, 0, random(-2, 2));
	SERP GF 4 A_FaceTarget();
	SERP G 0 A_SpawnProjectile("DCY_SerpentBall", 33, 0, random(-2, 2));
	SERP GF 8;
	Goto See;
  Dash:
    SERP AAAAABBBBBCCCCCDDDDDAAAAABBBBBCCCCCDDDDD 1
	{
		A_Recoil(-2);
		A_FaceTarget();
	}
	SERP EF 7 A_FaceTarget();
	SERP G 0 A_SpawnProjectile("DCY_SerpentBall", 33, 0, random(-2, 2));
	SERP GF 4 A_FaceTarget();
	SERP G 0 A_SpawnProjectile("DCY_SerpentBall", 33, 0, random(-2, 2));
	SERP GF 8;
	Goto See;
  Pain:
    SERP H 3;
	SERP H 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
    SERP I 1;
	SERP I 7 A_Scream();
	SERP JK 8;
	SERP J 0 A_NoBlocking();
	SERP LM 6;
	SERP M -1;
	Stop;
  XDeath:
    SERP N 1;
	SERP N 6 DCY_Gibs();
	SERP O 7 A_NoBlocking();
	SERP PQRST 5;
	SERP U -1;
	Stop;
  Raise:
    SERP LJ 8;
    SERP KIH 6;
    Goto See;
  }
}

Class DCY_SerpentBall : DoomImpBall
{
  Default
  {
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[1.46,2.00,0.64]";
  Scale 1.1;
  DamageType "Poison";
  Speed 13;
  FastSpeed 20;
  Alpha 0.67;
  +BRIGHT
  SeeSound "AcidBall/Shoot";
  DeathSound "AcidBall/Death";
  }
  
  States
  {
  Spawn:
	FVUL AABB 1
	{
		A_SpawnParticleEx(0x328700, TexMan.CheckForTexture("graphics/particles/dcygas.png"), 0, SPF_ROLL|SPF_FULLBRIGHT|SPF_RELATIVE, 60, frandom(15, 25), xoff: frandom(-2, 2), yoff: frandom(-2, 2), zoff: frandom(-2, 2), startalphaf: 0.3, fadestepf: 0.01, sizestep: 0.04, startroll: frandom(0, 360));
		
		if ((level.time % 2) == 0)
		for (int i = 2; i > 0; i--)
			A_SpawnParticle(0xc5ffa3, SPF_FULLBRIGHT, 40, frandom(9, 13), 0, frandom(-8, 8), frandom(-8, 8), frandom(-8, 8), fadestepf: 0, sizestep: -0.45);
	}
	Loop;
  Death:
	FVUL C 1 Bright;
	FVUL C 1 Bright
	{
		for (int i = 8; i > 0; i--)
			A_SpawnItemEx("AndroidNeonFluid", random(-3, 3), random(-3, 3), random(-3, 3), random(-5, 5), random(-5, 5), random(3, 7), Angle - 180 + frandom(-45, 45), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, 135);
    }
	FVUL CCCDDDEEFF 1 Bright A_FadeOut(0.065, 0);
	Stop;
  }
}