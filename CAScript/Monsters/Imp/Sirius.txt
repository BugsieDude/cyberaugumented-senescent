Class DCY_SiriusImp : DCY_Impe
{
  Default
  {
  Tag "\c[Sapphire]Castrator\c-";
  Obituary "%o was in blue ash by a Castrator.";
  HitObituary "%o was lacerated by a Castrator.";
  Speed 8;
  FastSpeed 10;
  Mass 200;
  Health 100;
  Translation "48:79=%[0.00,0.00,0.00]:[0.57,0.57,0.77]", "168:191=%[0.00,0.00,0.00]:[0.55,1.46,2.00]", "16:47=%[0.00,0.00,0.00]:[0.55,1.46,2.00]", "128:159=%[0.00,0.00,0.00]:[0.57,0.57,0.77]", "5:15=%[0.00,0.00,0.00]:[0.44,0.44,0.44]", "236:239=%[0.00,0.00,0.00]:[0.57,0.57,0.77]", "160:167=%[0.00,0.00,0.00]:[0.57,0.57,0.77]", "112:127=%[0.00,0.00,0.00]:[0.57,0.57,0.77]";
  SeeSound "Unwilling/Sight";
  ActiveSound "Unwilling/Active";
  PainSound "Unwilling/Pain";
  HowlSound "Unwilling/Pain";
  DeathSound "Unwilling/Death";
  BloodColor "1B 81 E0";
  +NOTARGET
  +DONTHARMCLASS
  +MISSILEEVENMORE
  +NODROPOFF
  }
  States
  {
  Spawn:
    TROO A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(128, "Avoid");
    TROO AABBCCDD 3 A_Chase();
	Loop;
  Avoid:
    TROO A 1 A_Dodging(10);
	TROO AAAAAA 1;
	Goto See;
  Melee:
	TNT1 A 0 A_StartSound("Unwilling/Attack", CHAN_7);
    TROO EF 3 A_FaceTarget();
	TROO G 3 A_CustomMeleeAttack(5 * random(1, 10), "imp/melee");
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(80, "Triple");
	Goto NormalMissile;
  NormalMissile:
    TROO EF 3 A_FaceTarget();
	TROO GG 0 A_SpawnProjectile("DCY_SiriusImpBall", 30, 0, random(-10, 10));
	TROO G 3;
	TROO EF 3 A_FaceTarget();
	TROO GG 0 A_SpawnProjectile("DCY_SiriusImpBall", 30, 0, random(-10, 10));
	TROO G 3;
	Goto See;
  Triple:
    TROO EF 3 A_FaceTarget();
	TROO GGG 0 A_SpawnProjectile("DCY_SiriusImpBall", 30, 0, random(-17, 17));
	TROO G 3;
	Goto See;
  Pain:
    TROO H 6 A_Pain();
	TNT1 A 0 A_Jump(80, "Avoid");
	Goto See;
  Death:
    TNT1 A 0 A_Jump(128, "Death2");
	Goto NormalDeath;
  NormalDeath:
    TROO I 1;
	TROO I 1 A_Scream();
	TROO I 5 A_NoBlocking();
	TROO JKL 7;
	TROO M -1;
	Stop;
  Death2:
    1D_8 A 1;
	1D_8 A 6 A_ScreamAndUnblock();
	1D_8 CD 7;
	1D_8 E -1;
	Stop;
  XDeath:
    TNT1 A 0 A_Jump(128, "XDeath1");
	Goto XDeathNormal;
  XDeathNormal:
    TROO N 6 A_Scream();
	TROO O 6 A_XScream();
	TROO P 4 A_NoBlocking();
	TROO QRST 6;
	TROO U -1;
	Stop;
  XDeath1:
    1D_1 A 6 A_Scream();
	1D_1 B 6 A_XScream();
	1D_1 C 6 A_NoBlocking();
	1D_1 DEFGH 6;
	1D_1 I -1;
	Stop;
  }
}

Class DCY_SiriusImpBall : DoomImpBall
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[1.06,1.73,1.95]";
  Speed 20;
  Damage 2;
  +NEVERFAST
  +BRIGHT
  +ROLLSPRITE
  SeeSound "Freedoom/Imp/Attack";
  DeathSound "Freedoom/Imp/ShotX";
  }
  States
  {
  Spawn:
    BAL1 AAABBB 1
	{
		A_SetRoll(frandom(0.00, 360.00));
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 80);
	}
	Loop;
  Death:
    BAL1 CDE 5;
	Stop;
  }
}