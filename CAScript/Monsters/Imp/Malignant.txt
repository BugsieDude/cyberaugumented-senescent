Class DCY_Malignant : DCY_Impe
{
  Default
  {
  Health 200;
  DamageType "Poison";
  Obituary "$o felt the poison of a Malignant.";
  HitObituary "%o mutated under the eyes of the Malignant.";
  Tag "Malignant";
  SeeSound "Malignant/Sight";
  ActiveSound "Malignant/Idle";
  PainSound "Malignant/Pain";
  DeathSound "Malignant/Death";
  BloodType "AndroidNeonFluid";
  BloodColor "Green";
  DCYMonster.GibType "DCY_GibletsAdd";
  DCYMonster.ScientificName "Toxicus fugi";
  }
  
  States
  {
  Spawn:
	JAAS A 10 DCY_Look();
	Loop;
  Look:
	JAAS AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	JAAS AABBCCDD 4 DCY_Chase("Missile", "Missile");
	Loop;
  Jump:
	JAAS EF 5 A_FaceTarget();
	JAAS G 14
	{
		vel.z = 11;
		A_Recoil(-15);
	}
	Goto Missile+1;
  Missile:
	JAAS A 0
	{
		if (random(0, 2) == 1)
			SetStateLabel("Jump");
	}
	JAAS A 10 A_FaceTarget();
	JAAS X 1
	{
		bNOPAIN = true;
		
		A_FaceTarget();
		A_StartSound("Malignant/Charge", CHAN_BODY);
		let aurafx = Spawn("DCY_MalignantFlash", pos);
		
		if (aurafx)
		{
			aurafx.master = self;
			aurafx.sprite = sprite;
			aurafx.frame = frame;
			aurafx.angle = angle;
		}
	}
	JAAS XXXXXXXXXXXXXXXXXX 1 A_FaceTarget();
	JAAS Y 15
	{
		A_FaceTarget();
		A_StartSound("Malignant/Fire", CHAN_BODY);
		
		for (int k = 29; k > -30; k -= 58)
		{
			for (int i = 5; i > 0; i--)
			{
				let poisonfx = Spawn("DCY_SerpentBall", pos + (0, k, 36));
				
				if (poisonfx)
				{
					poisonfx.target = self;
					poisonfx.angle = angle;
					poisonfx.Vel3DFromAngle(GetDefaultSpeed("DCY_CWFX"), poisonfx.angle + frandom(-13, 13), BulletSlope() + frandom(-13, 13));
				}
			}
		}
	}
	JAAS X 10 { bNOPAIN = false; }
	Goto See;
  Pain:
	JAAS H 4;
	JAAS H 4 A_Pain();
	Goto See;
  Death:
	JASS A 6 A_SetScale(Scale.X * randompick(-1, 1), Scale.Y);
	JASS B 6
	{
		A_StartSound("unwilling/death", CHAN_VOICE);
		A_NoBlocking();
	}
	JASS CDE 6;
	JASS F -1;
	Stop;
  XDeath:
	JAAS H 6;
	JAAS I 6 { for (int i = 2; i > 0; i--) DCY_Gibs(false); A_Scream(); }
	JAAS J 5 A_NoBlocking();
	JAAS KLMNO 5;
	JAAS P -1;
	Stop;
  Raise:
	JAAS O 0 A_Restore();
	JAAS ONMLKJIH 5;
	Goto See;
  }
}

Class DCY_MalignantFlash : DCY_HexodenicanTrail
{
  Default
  {
  Alpha 0;
  RenderStyle "AddStencil";
  StencilColor "3cff19";
  Scale 1.0;
  }
  
  bool scalin;
  
  override void Tick()
  {
	super.Tick();
		
	if (master)
		angle = master.angle;
  }
	
  States
  {
  Spawn:
	"####" "#" 1
	{
		if (master)
			frame = master.frame;
		
		if (scalin)
		{
			A_SetScale(Scale.X+0.05);
			A_FadeOut(0.0895);
		}
		else
			A_FadeIn(0.05);
		
		if (alpha > 1 && !scalin)
			scalin = true;
		
		Warp(master, 0, 0, 2, flags: WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION);
	}
	Loop;
  }
}