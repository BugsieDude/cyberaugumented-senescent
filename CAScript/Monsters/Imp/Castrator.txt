Class DCY_Castrator : DCY_Impe
{
  Default
  {
  Tag "\c[Fire]Castrator\c-";
  Obituary "%o got struck by the bolts of the Castrator.";
  HitObituary "%o got lacerated by a Castrator.";
  Speed 8;
  FastSpeed 10;
  Mass 200;
  Health 200;
  Scale 1.05;
  Translation "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.43,0.43,0.43]", "5:12=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "112:127=%[0.00,0.00,0.00]:[2.00,0.65,0.00]", "160:167=%[0.00,0.00,0.00]:[2.00,1.01,0.00]";
  SeeSound "Unwilling/Sight";
  ActiveSound "Unwilling/Active";
  PainSound "Unwilling/Pain";
  HowlSound "Unwilling/Pain";
  DeathSound "Unwilling/Death";
  BloodColor "66 22 00";
  BloodType "AndroidNeonFluid";
  DCYMonster.GibType "DCY_GibletsLava";
  +NOTARGET
  +DONTHARMCLASS
  +MISSILEEVENMORE
  +NODROPOFF
  DCYMonster.ScientificName "Delirare electricae";
  }
  States
  {
  Spawn:
    DRKF A 10 DCY_Look();
	Loop;
  Look:
	"####" AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	DRKF [ 15 { A_FaceTarget(); DCY_AlertSound(); }
	DRKF YZYZYZYZYZYZYZYZ 2 A_FaceTarget();
	Goto See;
  See:
    DRKF AABBCCDD 3
	{
		if (random(0, 3) == 0)
		{
			A_Dodging(10);
			tics = 10;
		}
		
		DCY_Chase();
	}
	Loop;
  Melee:
	TNT1 A 0 A_StartSound("Unwilling/Attack", CHAN_7);
    DRKF EF 8 A_FaceTarget();
	DRKF GGGGGGGG 1
	{
		if ((level.time % 10) == 0)
			A_CustomMeleeAttack(5 * random(1, 10), "imp/melee", damagetype: "Lightning");
		
		A_SpawnItemEx("DCY_CastratorSpark", frandom(-5, 5), frandom(-5, 5), 36 + frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION, 100);
	}
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(80, "Alert");
	Goto NormalMissile;
  NormalMissile:
	DRKF E 5 A_FaceTarget();
    DRKF FFFFFFFFFFF 3
	{
		A_FaceTarget();

		for (int i = -20; i < 21; i += 40)
			A_SpawnItemEx("DCY_ElecTrailBaseCast", frandom(-5, 5), i + frandom(-5, 5), 36 + frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE);
	}
	DRKF G 5
	{
		A_FaceTarget();
		
		for (int i = 6; i > -7; i -= 12)
			A_SpawnProjectile("DCY_SiriusImpBall", 30, i, random(-1, 1));
	}
	Goto See;
  Pain:
    DRKF H 6 A_Pain();
	DRKF A 0
	{
		if (random(0, 2) == 0)
		{
			A_Dodging(10);
			tics = 10;
		}
	}
	Goto See;
  Death:
    DRKF L 6;
    DRKF M 6 A_Scream();
    DRKF N 5;
    DRKF O 5 A_NoBlocking();
	DRKF PPPPPPPPPPPPPPPP 3 A_SpawnItemEx("DCY_CastratorSpark", frandom(-5, 5), frandom(-5, 5), 16 + frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION, 100);
    DRKF P -1;
    Stop;
  XDeath:
    DRKF Q 5;
    DRKF R 5 DCY_Gibs();
    DRKF S 5 A_NoBlocking();
    DRKF TUVW 5;
    DRKF X -1;
    Stop;
  }
}

Class DCY_SiriusImpBall : CacodemonBall
{
  Default
  {
  Speed 20;
  FastSpeed 30;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.01,0.00]";
  DamageType "Electric";
  SeeSound "ElectricBall/Sight";
  DeathSound "ElectricBall/Death";
  Damage 7;
  +ROLLSPRITE
  Scale 0.65;
  }
  
  States
  {
  Spawn:
	DLIT DEFGH 1
	{
		A_SetRoll(Roll+20);
		vel.y += frandom(-0.1, 0.1);
		vel.z += frandom(-0.1, 0.1);
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecTrailBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_CastratorSpark", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 200);
	}
	Loop;
  Death:
	TSAR A 1
	{
		A_SetRoll(frandom(0, 360));
		vel.x = 0;
		vel.y = 0;
		vel.z = 0;
		
		for (int i = 4; i > 0; i--)
		{
			A_SpawnParticle(0x662200, SPF_FULLBRIGHT, random(10, 20), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0));
			A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
		}
	}
	TSAR EFGHIJKLMNO 1
	{
		A_Explode(4, 100, damagetype: "lightning");
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	TNT1 AAAA 3
	{
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	Stop;
  }
}

Class DCY_CastratorSpark : DCY_Effect
{
  int timer;
  
  Default
  {
	Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.01,0.00]";
	Scale 0.2;
	+NOGRAVITY
	+NOCLIP
	+NOINTERACTION
  }
  
  States
  {
  Spawn:
    TNT1 A 10 { tics = frandom(2, 6); }
	TNT1 A 1
	{
		A_SpawnItemEx("DCY_Arc", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, failchance: 100);
		A_SpawnItemEx("DCY_ElecDeathBase", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION, 100);
	}
	Stop;
  }
}

Class DCY_ElecTrailBaseCast : DCY_ElecTrailBase { Default { Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.01,0.00]"; } }