Class DCY_Manes : DCY_Impe
{
  Default
  {
  SeeSound "Manes/Chatter";
  ActiveSound "Manes/Chatter";
  PainSound "Manes/Pain";
  DeathSound "Manes/Death";
  Alpha 1.0;
  Speed 8;
  Health 400;
  FloatSpeed 4;
  FastSpeed 12;
  Translation "16:239=%[0.00,0.00,0.35]:[0.00,0.00,2.00]";
  +FLOAT
  +NOGRAVITY
  +VISIBILITYPULSE
  +NOICEDEATH
  +THRUSPECIES
  +NOBLOOD
  +BRIGHT
  +GHOST
  Tag "\c[w9]manes\c-";
  RenderStyle "Add";
  Obituary "%o succumbed awaiting death.";
  }
  
  override void Tick()
  {
	if (!level.isfrozen())
	{
		A_SpawnItemEx("DCY_MaelstromSmonk", frandom(-30, 30), frandom(-30, 30), frandom(10, 60), zvel: frandom(0.5, 1.5), flags: SXF_NOCHECKPOSITION, failchance: 128);
		A_SpawnItemEx("DCY_ManesTrail", xvel: frandom(-1, 1), yvel: frandom(-1, 1), zvel: frandom(-1, 1), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
	
	Super.Tick();
  }
  
  States
  {
  Spawn:
	NSH1 AAABBBCCCDDD 10 DCY_Look();
	Loop;
  Look:
	NSH1 AAABBBCCCDDD 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0
	{
		bNOCLIP = true;
		DCY_AlertSound();
	}
	Goto See;
  See:
	NSH1 AAABBBCCCDDD 3
	{
		if (target && distance3D(target) > 2000)
			SetStateLabel("Teleporting");
		
		A_Chase();
	}
	Loop;
  Teleporting:
	TNT1 A 1 A_Stop();
	TNT1 A 1
	{
		for (int i = 20; i > 0; i--)
			A_Chase(flags: CHF_NOPLAYACTIVE);
		
		if (target && distance3D(target) < 200)
			SetStateLabel("Breath");
	}
	Goto Teleporting+1;
  Missile:
	NSH1 E 10
	{
		A_FaceTarget();
		Return A_Jump(80, "Stalk");
	}
	NSH1 E 1 A_StartSound("Manes/Chatter", CHAN_VOICE);
	NSH1 FFFFFFFFFFFFFFFFFFFF 2
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ManesBall", 30, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-10, 10));
	}
	NSH1 E 8 A_Stop();
	Goto See;
  Stalk:
	NSH1 AAABBBCCCDDD 3
	{
		A_FaceTarget();
	
		A_Recoil(-0.75);
		
		if (target && distance3D(target) < 200)
			SetStateLabel("Breath");
	}
	Loop;
  Melee:
  Breath:
	NSH1 E 10 A_FaceTarget();
	NSH1 E 1 A_StartSound("Manes/Pain");
	NSH1 FFFFF 2
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_ManesSoul", 30, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-10, 10));
	}
	NSH1 F 0 A_Stop();
	Goto See;
  Pain:
	NSH1 G 4 A_Stop();
	NSH1 G 4 A_Pain();
	Goto See;
  XDeath:
  Death.Fire:
  Death:
	NSH1 I 6;
	NSH1 J 6 A_Scream();
	NSH1 K 6 A_NoBlocking();
	NSH1 LMNOP 5;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_ManesBall : DCY_MeatballElecball
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[0.10,0.10,0.50]";
  DamageType "Ghost";
  SeeSound "";
  }
  
  States
  {
  Spawn:
	SUE1 BBBBCCCCCDDDDDDCCCCC 1
	{
		roll += 3;
		A_SpawnItemEx("DCY_CWFXTrail", flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERROLL);
		vel.x += frandom(-1, 1);
		vel.y += frandom(-1, 1);
		vel.z += frandom(-1, 1);
		A_SpawnParticleEx(0x000066, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), random(25, 35), 0, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), fadestepf: 0.035, sizestep: 0.825);
	}
	Loop;
  Death:
	SUE1 BCDEF 5;
	Stop;
  }
}

Class DCY_ManesSpirit : DCY_RZSpirit { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.50,0.50,0.50]"; RenderStyle "Subtract"; Scale 1.0; } }

Class DCY_ManesSoul : DCY_ChimeraSoul
{
  Default
  {
  RenderStyle "Subtract";
  Speed 14;
  +NEVERFAST
  DamageType "Occult";
  SeeSound "";
  }
  
  int timer;
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		vel.x += frandom(-1, 1);
		vel.y += frandom(-1, 1);
		vel.z += frandom(-1, 1);
		
		if (timer <= 20)
			A_Tracer2(2);
		
		timer++;
		
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_ManesSpirit", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 240);
			A_SpawnParticleEx(0x666666, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_SUBTRACT, SPF_FULLBRIGHT|SPF_RELATIVE, random(50, 75), random(25, 35), 0, velx: frandom(-1.5, 1.5), vely: frandom(-1.5, 1.5), velz: frandom(-0.5, 0.5), fadestepf: 0.035, sizestep: 0.825);
		}
	}
	Loop;
  }
}