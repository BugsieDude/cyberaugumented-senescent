Class DCY_Painsend : DCY_Impe
{
  Default
  {
  Health 135;
  Tag "Painsend";
  BloodType "AndroidNeonFluid";
  BloodColor "Orange";
  Obituary "%o was castrated by a Painsend.";
  HitObituary "%o was torn apart by a Painsend.";
  MeleeSound "Imp/Melee";
  SeeSound "Doom4Imp/Sight";
  PainSound "Doom4Imp/Pain";
  DeathSound "Doom4Imp/Death";
  ActiveSound "Doom4Imp/Active";
  Translation "48:111=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "128:159=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "1:2=%[0.00,0.00,0.00]:[1.51,0.44,0.44]", "5:12=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "0:0=%[0.00,0.00,0.00]:[2.00,2.00,2.00]", "16:47=%[0.54,0.00,0.00]:[2.00,1.01,0.00]", "168:191=%[0.54,0.00,0.00]:[2.00,1.01,0.00]", "236:239=%[0.54,0.00,0.00]:[0.38,0.38,0.38]";
  MeleeDamage 5;
  +DONTHARMSPECIES
  }
  
  States
  {
  Spawn:
    1A1_ E 10 DCY_Look();
	Loop;
  Look:
	"####" AAABBBCCCDDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    1A1_ AAABBBCCCDDD 2
	{
		A_Recoil(-0.25);
		A_Chase();
	}
	Loop;
  Melee:
    1A1_ E 6 A_FaceTarget();
    1A1_ F 5
	{
		A_FaceTarget();
		A_StartSound("Painsend/Swing");
	}
    1A1_ G 5 A_CustomMeleeAttack(5 * random(5, 15), "painsend/melee");
    Goto See;
  Missile:
	1A1_ E 0
	{
		If (Distance3D(target) < 256)
			if (random(0, 2) == 0)
				SetStateLabel("Lunge");
	}
	1A1_ E 3
	{
		bool hopping = random(0, 1);
		if (hopping && (vel.z == 0))
		{
			A_Dodging(15);
			A_Hop(10);
		}
		
		A_FaceTarget();
	}
	1A1_ FFFFFFFFFFFFFFFFF 1
	{
		A_FaceTarget();
		
		for (int i = -20; i < 21; i += 40)
			A_SpawnParticleEx(0xfc8003, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 27, 0, frandom(-2.5, 2.5), i + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 31, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -1.325);
	}
	1A1_ G 5
	{
		A_StartSound("Painsend/Fire", CHAN_WEAPON);
		
		for (int i = -10; i < 11; i += 20)
			A_SpawnProjectile("DCY_PainsendFireball", 32, i, random(-1, 1));
	}
	1A1_ F 6;
	Goto See;
  Lunge:
	1A1_ E 5 A_FaceTarget();
	1A1_ F 5
	{
		A_StartSound("Painsend/Swing", CHAN_BODY);
		A_Recoil(-18);
		A_Hop(12);
		A_FaceTarget();
	}
	1A1_ GGF 4;
	Goto Missile+13;
  Pain:
    1A1_ H 3;
	1A1_ H 3 A_Pain();
	Goto See;
  Death:
    1A1_ I 8;
    1A1_ J 8 A_Scream();
    1A1_ KL 6;
    1A1_ M 6 A_NoBlocking();
    1A1_ M -1;
    Stop;
  XDeath:
    1A1_ N 5;
    1A1_ O 5 A_XScream();
    1A1_ P 5;
    1A1_ Q 5 A_NoBlocking();
    1A1_ R 5;
    1A1_ S -1;
    Stop;
  Raise:
    1A1_ ML 8;
    1A1_ KJI 6;
    Goto See;
  }
}

Class DCY_PainsendFireball : DCY_ImpFireball
{
  Default
  {
  Speed 15;
  FastSpeed 25;
  Damage 6;
  DamageType "Fire";
  +BRIGHT
  SeeSound "Painsend/Fireball";
  DeathSound "Painsend/FireExplode";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.01,0.50]";
  }
  
  States
  {
  Spawn:
    BAL1 AABB 1
	{
		A_SpawnItemEx("DCY_MastermindJetFlareSmall", zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 30);
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), zvel: 0.25, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE, failchance: 70);
	}
	Loop;
  }
}

Class DCY_PainsendFireballFast : DCY_PainsendFireball { Default { Speed 15; FastSpeed 25; } }

Class DCY_PainsendWeavingball : DCY_PainsendFireball
{
  Default
  {
  Speed 10;
  Fastspeed 15;
  Damage 2;
  +SEEKERMISSILE;
  }
  
  States
  {
  Spawn:
	BAL3 AABBCCDD 1
	{
		A_SeekerMissile(1, 2, SMF_LOOK);
		A_Weave(2, 2, 1, 1);
		A_SpawnItemEx("DCY_ImpFlame1", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), zvel: 1, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE, failchance: 150);
	}
	Loop;
  Death:
	BAL3 EEFFGG 1 A_FadeOut(0.2, 0);
	Stop;
  }
}