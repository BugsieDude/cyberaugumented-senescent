Class DCY_Terminalist : DCY_Violator
{
  Default
  {
  Translation "48:79=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "5:15=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "1:2=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "128:159=%[0.00,0.00,0.00]:[0.35,0.35,0.35]", "192:207=%[0.00,0.00,0.00]:[2.00,0.37,0.37]", "240:247=%[0.00,0.00,0.00]:[2.00,0.37,0.37]";
  Tag "\c[DarkRed]Terminalist\c-";
  Obituary "%o got atomized by the Terminalist's hatred.";
  HitObituary "%o's ribcage got ripped apart by the Terminalist.";
  Health 100;
  SeeSound "TerminalImp/Sight";
  ActiveSound "TerminalImp/Active";
  PainSound "TerminalImp/Pain";
  DeathSound "TerminalImp/Death";
  AttackSound "imp/melee";
  MeleeDamage 8;
  +MISSILEMORE
  DamageFactor "Plasma", 0.9;
  }
  
  int missile;
  
  States
  {
  Spawn:
    CIMP A 10 DCY_Look();
	Loop;
  Look:
	CIMP AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0 A_Jump(12, "Avoid");
    CIMP AABBCCDD 3 DCY_Chase("Melee", "Missile");
	Goto See+1;
  Melee:
    C1MP AB 5 A_FaceTarget();
	C1MP C 5
	{
		A_FaceTarget();
		A_CustomMeleeAttack(8 * random(1, 5), "");
		Return A_Jump(45, "Missile");
	}
	Goto See;
  Lunge:
    CIMP E 9 A_FaceTarget();
	TNT1 A 0 A_Jump(192, "Missile");
	CIMP F 1
	{
		A_FaceTarget();
		A_Recoil(-15);
		A_Hop(5);
		bSlidesOnWalls = true;
	}
	CIMP FFFFFFFFFFF 1 A_CustomMeleeAttack(8, "imp/melee");
	CIMP F 0 { bSlidesOnWalls = false; if (target) SetStateLabel("Missile"); }
	Goto See;
  Avoid:
    CIMP A 2
	{
		bSlidesOnWalls = true;
		A_Hop(5);
		A_Dodging(6);
	}
	CIMP AAAAA 2 DCY_Chase();
	TNT1 A 0 { bSlidesOnWalls = false; }
	Goto See;
  Missile:
	CIMP E 0
	{
		bSlidesOnWalls = false;
		A_FaceTarget();
		missile = random(8, 14);
		
		switch (random(0, 2))
		{
			case 0: Return ResolveState("Laser"); break;
			case 1: Return ResolveState("Plasma"); break;
			case 2: Return ResolveState("Fireball"); break;
		}
		Return ResolveState(null);
	}
	Goto Plasma;
  Plasma:
	CIMP E 10 A_FaceTarget();
	CIMP F 10 A_FaceTarget();
	CIMP G random(2, 4)
	{
		A_SpawnProjectile("DCY_ConsulPlasma", 32, 0, frandom(-3, 3), CMF_OFFSETPITCH, frandom(-3, 3));
		A_FaceTarget();
		missile--;
	}
	CIMP F 2
	{
		A_Facetarget();
		if (missile <= 0) SetStateLabel("See");
	}
	Goto Plasma+2;
  Laser:
    CIMP E 10 A_FaceTarget();
	CIMP F 10 A_FaceTarget();
	CIMP G 10
	{
		If (target && Distance3D(target) < 200)
			Return ResolveState("Lunge");
		
		A_SpawnProjectile("DCY_TLaser", 32, 0);
		Return A_Jump(50, "Avoid");
	}
    Goto See;
  Fireball:
	C1MP A 5 A_StartSound("dcyimp/charging", CHAN_WEAPON);
	C1MP AAAAAAAAAAAAAA 1
	{
		A_SpawnParticleEx(0xaa2222, particlefire, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, random(20, 45), 18, 0, frandom(-2.5, 2.5), -25 + frandom(-2.5, 2.5), frandom(-2.5, 2.5) + 36, velz: 1.5, accelz: 0.1175, fadestepf: 0, sizestep: -1.325);
		A_FaceTarget();
	}
	C1MP B 5 A_FaceTarget();
	C1MP C 5 Bright A_SpawnProjectile("DCY_ViolationFire", 30, 0, 0);
	C1MP B 5 A_FaceTarget();
	TNT1 A 0
	{
		If(target && Distance3D(target) < 365)
			SetStateLabel("Lunge");
		
		Return A_Jump(50, "Avoid");
	}
	Goto See;
  Pain:
    CIMP H 4;
	CIMP H 3 A_Pain();
	Goto See;
  Death:
    CIMP I 7 {
		if (random(0, 1)) SetStateLabel("Death2");
	}
    CIMP J 6 A_Scream();
    CIMP K 5;
    CIMP L 5 A_NoBlocking();
    CIMP M -1;
    Stop;
  Death2:
	C2MP A 7;
	C2MP B 6 A_Scream();
	C2MP C 6 A_NoBlocking();
	C2MP D 6;
	C2MP E -1;
	Stop;
  XDeath:
    CIMP N 5;
    CIMP O 4 DCY_Gibs();
    CIMP P 4;
    CIMP Q 4 A_NoBlocking();
    CIMP RST 4;
    CIMP U -1;
    Stop;
  Raise:
    CIMP MLKJI 8;
    Goto See;
  }
}

Class DCY_TLaser : DCY_FastProjectile
{
  Default
  {
  Obituary "That laser disintegrated %o molecule by molecule.";
  DamageType "Disintegration";
  Speed 45;
  Damage 10;
  SeeSound "R3LASR3";
  DeathSound "R3MINIEX";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.37,0.37]";
  MissileType "DCY_TLTrail";
  MissileHeight 6;
  RenderStyle "Add";
  DCY_FastProjectile.CSX true;
  DCY_FastProjectile.CSY true;
  Scale 1.0;
  Alpha 1.0;
  Radius 14;
  Height 6;
  }
  States
  {
  Spawn:
    TML_ A 1 Bright;
    Loop;
  Death:
	TNT1 A 1;
    TNT1 A 5 A_SpawnItemEx("DCY_AndroidExplosionSmall", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	Stop;
  }
}

Class DCY_TLTrail : DCY_GunmetalInfiltratorBallTrail
{
  Default
  {
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.37,0.37]";
  }
  
  States
  {
  Spawn:
    TML_ AAAAA 1 Bright A_FadeOut(0.2, 0);
	Stop;
  }
}