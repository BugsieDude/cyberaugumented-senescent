Class DCY_RegulusImp : DCY_Impe
{
  Default
  {
  Translation "48:79=[128,128,128]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "5:15=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "128:167=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "236:239=%[0.00,0.00,0.00]:[0.45,0.45,0.45]", "112:127=%[0.00,0.00,0.00]:[1.12,1.30,1.56]", "16:47=%[0.00,0.00,0.15]:[0.77,0.91,1.28]", "168:191=%[0.00,0.00,0.00]:[0.69,0.76,1.51]";
  Health 128;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 10;
  PainChance 50;
  Damage 40;
  SeeSound "EternalGargoyle/Sight";
  ActiveSound "EternalGargoyle/Sight";
  PainSound "EternalGargoyle/Pain";
  DeathSound "EternalGargoyle/Death";
  MeleeSound "imp/melee";
  DamageType "Ice";
  Obituary "A varmint entangled %o in cold blood.";
  HitObituary "A varmint entangled %o in cold blood.";
  BloodColor "7377c9";
  Tag "\c[y9]Varmint\c-";
  DCYMonster.ScientificName "Torpore ruptae";
  +FLOAT
  +NOGRAVITY
  +QUICKTORETALIATE
  +DONTHARMCLASS
  }
  
  float chill;
  float chillangle;
  
  States
  {
  Spawn:
    FAEM A 10 DCY_Look();
	Loop;
  Look:
	FAEM AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    FAEM AABBCCDD 3 DCY_Chase();
    Loop;
  Melee:
    FAEM EF 4 { A_Recoil(-4); A_FaceTarget(); }
	FAEM OOO 4 A_CustomMeleeAttack(5 * random(1, 5), "imp/melee");
	FAEM F 4 A_Stop();
	Goto See;
  Missile:
	FAEM E 0
	{
		if (target && distance3D(target) < 240) return ResolveState("Frostthrower");
		
		switch (random(0, 1))
		{
			case 0: return ResolveState("Normal"); break;
			case 1: return ResolveState("Sway"); break;
		}
		
		return ResolveState(null);
	}
	Goto Normal;
  Frostthrower:
	FAEM O 10 A_FaceTarget();
	FAEM F 10 { A_StartSound("DCYDeadlyBreath"); A_Facetarget(); }
	FAEM EEEEEEEEEEE 4
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_RegulusThrower", 40, 0, frandom(-2, 2));
	}
	FAEM F 10 A_FaceTarget();
	Goto See;
  Normal:
	FAEM E 8 A_FaceTarget();
	FAEM F 8 A_FaceTarget();
	FAEM OOO 12
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_RegulusImpBall", 31);
	}
    Goto See;
  Sway:
    FAEM E 10 { chillangle = randompick(-20, 20); A_FaceTarget(); chill = -chillangle; }
	FAEM F 10 A_FaceTarget();
    FAEM OOOOO 7
	{
		A_FaceTarget();
		chill += chillangle;
		A_SpawnProjectile("DCY_RegulusImpBall", 31, 0, chill - (chillangle * 2));
	}
    Goto See;
  Pain:
    FAEM G 2;
    FAEM G 2 A_Pain();
    Goto See;
  Death:
  XDeath:
    FAEM H 8 A_Flip();
    FAEM I 8 A_Scream();
    FAEM J 6;
    FAEM K 6 A_NoBlocking();
    FAEM L 6;
    FAEM M 6;
    FAEM N -1;
    Stop;
  Raise:
    FAEM NMLKJIH 6;
	FAEM H 0 A_Restore();
	Goto See;
  }
}

Class DCY_RegulusImpBall : DCY_ImpFireball
{
  Default
  {
  Radius 6;
  Height 8;
  Speed 15;
  FastSpeed 30;
  Damage 6;
  Renderstyle "Add";
  Projectile;
  +RANDOMIZE
  DamageType "Ice";
  SeeSound "Freedoom/Imp/Attack";
  DeathSound "Freedoom/Imp/ShotX";
  Translation "0:255=%[0.00,0.00,0.00]:[1.03,1.04,1.39]";
  Decal "DoomImpScorch";
  }
  States
  {
  Spawn:
    RGBL AAAABBBB 1 Bright
	{
		A_SpawnItemEx("DCY_RegulusImpBallTrail", 0, 0, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), 0, SXF_NOCHECKPOSITION);
		A_SpawnParticleEx(0x7377c9, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 40, random(3, 6), angle, frandom(-8, 8), frandom(-8, 8), frandom(-8, 8), fadestepf: 0, sizestep: -0.115);	
	}
    Loop;
  Death:
    RGBL C 3 Bright A_SpawnParticleEx(0x7377c9, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 200, 120, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0025, sizestep: 1.525);
	RGBL DEFG 3 Bright;
	Stop;
  }
}

Class DCY_RegulusImpBallTrail : DCY_Effect
{
  Default
  {
  Scale 1.0;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[0.52,0.52,0.94]";
  +NOINTERACTION;
  -SOLID;
  }
  States
  {
  Spawn:
    RGBL AAAABBBBAA 1 A_FadeOut(0.1);
	Stop;
  }
}

Class DCY_RegulusThrower : DCY_RegulusImpBall
{
  Default
  {
  Damage (0);
  SeeSound "";
  DeathSound "";
  +RIPPER
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		A_FadeOut(0.0415);
		if ((GetAge() % 2) == 0) A_Explode(5, 50);
		vel.x += frandom(-0.1, 0.1);
		vel.y += frandom(-0.1, 0.1);
		vel.z += frandom(-0.1, 0.1);
		A_SpawnParticleEx(0x7377c9, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 128, 60, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0045, sizestep: 1.525);
	}
	Wait;
  Death:
	Stop;
  }
}