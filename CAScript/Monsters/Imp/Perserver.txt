Class DCY_Perserver : DCY_Impe
{
  Default
  {
  SeeSound "Zorg/Sight";
  ActiveSound "Zorg/Active";
  PainSound "Zorg/Pain";
  DeathSound "Zorg/Death";
  AttackSound "";
  Health 90;
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  Tag "Perserver";
  Speed 13.2555555;
  FastSpeed 13.5555555;
  DropItem "DCY_UltramodernEnergy", 25;
  Obituary "%o got outsmarted by a Perserver demon, that just kicked ass.";
  }
  
  States
  {
  Spawn:
	SDDM A 10 A_Look();
	Loop;
  See:
	SDDM AABBCCDD 2 A_Chase();
	Loop;
  Melee:
  Missile:
	SDDM E 0 A_Jump(100, "Dodge");
	Goto MissileContinue;
  Dodge:
	SDDM A 5
	{
		A_Dodging(12);
		A_FaceTarget();
	}
    SDDM AAA 5;
	Goto See;
  MissileContinue:
	SDDM EEEEEEEEE 1 A_FaceTarget();
	SDDM F 3 Bright
	{
		A_SpawnProjectile("DCY_PerserverMissile", 30, 8, frandom(-1.65, 1.65), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(0.655, -0.655));
		A_FaceTarget();
	}
	SDDM EEEE 3 A_FaceTarget();
	SDDM E 0 A_MonsterRefire(128, "See");
	Goto MissileContinue+9;
  Pain:
	SDDM G 4;
	SDDM G 4 A_Pain();
	Goto See;
  Death:
    SDDM H 5;
    SDDM I 5 A_Scream();
    SDDM J 4 A_NoBlocking();
    SDDM KLM 4;
    SDDM N -1;
    Stop;
  XDeath:
    SDDM O 5;
    SDDM P 5 A_StartSound("Android/Gib", CHAN_VOICE);
    SDDM Q 4 A_NoBlocking();
    SDDM RSTUV 3;
    SDDM W -1;
    Stop;
  Raise:
    SDDM NM 4;
	SDDM LKJIH 5;
    Goto See;
  }
}

Class DCY_PerserverMissile : FastProjectile
{
  Default
  {
  Radius 11;
  Height 7;
  Damage 5;
  Speed 15.5;
  FastSpeed 22.5;
  RenderStyle "Add";
  Scale 0.8785;
  SeeSound "drone/fire";
  DeathSound "drone/fire";
  Translation "0:255=%[0.00,0.00,0.00]:[0.34,1.17,2.00]";
  Decal "ImpScorch";
  }
  States
  {
  Spawn:
    THZ4 A 1 Bright A_SpawnItemEx("DCY_ProjectileBaseTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME);
    Loop;
  Death:
    THZ4 DEFGHIJKLM 1 Bright;
    Stop;
  }
}