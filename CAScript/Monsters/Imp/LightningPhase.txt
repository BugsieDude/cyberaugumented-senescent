Class DCY_LightningPhase : DCY_Impe
{
  Default
  {
  Health 115;
  Speed 9;
  +MISSILEEVENMORE;
  +QUICKTORETALIATE;
  +DONTHARMCLASS;
  +DONTHARMSPECIES;
  +NODROPOFF;
  BloodColor "Red";
  Translation "80:111=[208,208,208]:[14,14,14]", "1:3=[0,0,0]:[59,59,59]", "5:15=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "192:207=[64,255,64]:[0,43,0]", "240:247=[0,43,0]:[0,0,0]", "16:79=%[0.00,0.00,0.00]:[1.79,0.22,0.22]", "160:191=%[0.00,0.00,0.00]:[1.79,0.22,0.22]", "208:255=%[0.00,0.00,0.00]:[1.79,0.22,0.22]";
  SeeSound "LightningPhase/Sight";
  ActiveSound "LightningPhase/Active";
  PainSound "LightningPhase/Pain";
  DeathSound "LightningPhase/Death";
  Obituary "%o was all safe until a Lightning Phase sneaked upon %h.";
  HitObituary "%o was ripped and zapped by the Lightning Phase.";
  Tag "\c[l7]Lightning Phase\c-";
  MeleeSound "Doom4Imp/Slash";
  Scale 1.07;
  MeleeDamage 6;
  }
  
  int phased;
  
  States
  {
  Spawn:
    LMEI A 10 DCY_Look();
	Loop;
  Look:
	LMEI AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" E 15 DCY_AlertSound();
	Goto See;
  See:
    LMEI A 0
	{ 
		if (phased)
			SetStateLabel("SeePhased");
		
		phased == 0;
	}
    LMEI AABBCCDD 3 A_Chase();
	Goto See+1;
  Lunge:
	LMEI E 7 
	{
		A_FaceTarget();
		phased = 0;
		return A_Jump(150, "Missile");
	}
    LMEI F 7 A_FaceTarget();
	LMEI G 0 A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
	LMEI GGGG 7
	{
		A_Recoil(random(-10, -13));
		A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
	}
	LMEI G 0 A_Stop();
	TNT1 A 0 A_JumpIfCloser(85, "Melee");
	TNT1 A 0 A_Jump(150, "Missile");
	Goto Missile;
  Melee:
	LMEI E 6
	{
		A_SetTranslucent(1.0, 0);
		A_UnHideThing();
		phased = 0;
		A_FaceTarget();
	}
    LMEI F 6 A_FaceTarget();
	LMEI G 6
	{
		A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
		A_FaceTarget();
	}
	LMEI F 6;
	Goto See;
  Missile:
    LMEI E 0;
	LMEI E 0
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LMEI E 0
	{
		If(target && Distance3D(target) < 200)
			Return ResolveState("Lunge");
		
		A_FaceTarget();
		Return A_Jump(100, "Phase");
	}
	Goto NormalMissile;
  Phase:
    LMEI E 0;
	LMEI E 1 A_StartSound("dune/active", CHAN_5);
	LMEI E 5 A_FaceTarget();
	LMEI EEEEEEEEE 1 A_FadeOut(0.1, 0);
	TNT1 A 0 { phased = 1; }
	Goto SeePhased;
  SeePhased:
    LMEI E 0;
	LMEI AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	LMEI AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	TNT1 A 0 A_Jump(50, "Unphase");
	LMEI AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	LMEI AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	Goto Unphase;
  Unphase:
    LMEI E 0;
	LMEI E 1 A_StartSound("neophaseimp/teleport", CHAN_5);
	LMEI E 1 A_FaceTarget();
	LMEI EEEEEEEEE 1 A_FadeIn(0.1, 0);
	TNT1 A 0 { phased = 0; }
	Goto See;
  NormalMissile:
	LMEI E 0 A_FaceTarget();
	LMEI EF 7 A_FaceTarget();
	LMEI GGG 7
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_LightningPhaseBall", 32, 0, random(-1, 1));
	}
	LMEI F 7 A_FaceTarget();
	Goto See;
  Pain:
    LMEI H 4
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LMEI H 2 A_Pain();
	Goto See;
  Death:
    LMEI I 0;
    LMEI I 1
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LMEI I 6
	{
		A_UnSetSolid();
		A_Scream();
		A_NoBlocking();
	}
	LMEI JKLMNOPQR 6;
	LMEI S -1;
	Stop;
  XDeath:
	LMEG A 5;
	LMEG B 5 A_XScream();
	LMEG C 5 A_NoBlocking();
	LMEG DEFG 5;
	LMEG H -1;
	Stop;
  Raise:
	LMEI RQPONMLKJIH 5;
	Goto See;
  }
}

Class DCY_LightningPhaseBall : DCY_ImpFireball
{
  Default
  {
  Damage 7;
  Speed 15.5;
  FastSpeed 18.5;
  Scale 1.3;
  +SEEKERMISSILE
  +ROLLSPRITE
  +NOEXTREMEDEATH
  Translation "0:255=%[0.00,0.00,0.00]:[0.45,2.00,0.45]";
  Obituary "%o was all safe until a Lightning Phase sneaked upon %h.";
  SeeSound "BALL002";
  DeathSound "FIR4IMPC";
  }
  States
  {
  Spawn:
	LMBB AAAABBBB 1 Bright
	{
		A_Weave(3, 3, 1, 1);
		A_SetRoll(frandom(0.00, 360.00));
		if (bSEEKERMISSILE) A_SeekerMissile(1, 5, SMF_PRECISE);
		if (random(0, 30) == 0) bSEEKERMISSILE = 0;
		A_SpawnItemEx("DCY_LightningPhaseBallTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnItemEx("DCY_Arc", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 230);
		if (DCY_EffectDistance(sfxdistance))
		{
			for (int i = 2; i > 0; i--) A_SpawnParticleEx(0x008028, TexMan.CheckForTexture("graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 70, frandom(1.00, 4.00), angle, frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), (Vel.x / 4.75), (Vel.y / 4.75), (Vel.z / 4.75), startalphaf: 1.0, fadestepf: 0, sizestep: -0.15);
			A_SpawnFlames("008028", 15, 15, 0, false, thrust: 2.25, ember: false);
		}
	}
	Loop;
  Death:
	LMBB C 3 Bright A_SpawnParticleEx(0x008028, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT, 40, 40, frandom(0, 360), fadestepf: 0.124, sizestep: 3.25);
	LMBB DEFG 3 Bright;
	Stop;
  }
}

Class DCY_LightningPhaseBallTrail : DCY_Effect
{
  Default
  {
  Alpha 0.755;
  Scale 1.0;
  RenderStyle "Add";
  Radius 1;
  Height 1;
  +NOCLIP
  +CLIENTSIDEONLY
  +NOINTERACTION
  +DONTSPLASH
  +ROLLSPRITE
  -SOLID
  }
  States
  {
  Spawn:
	LMBB AAAABBBBAA 1 Bright 
	{
		A_SetScale(Scale.X-0.1, Scale.Y-0.1);
		A_FadeOut(0.1);
	}
	Stop;
  }
}