Class DCY_LightningPhase : DCY_Impe
{
  Default
  {
  Health 115;
  Speed 15;
  +MISSILEEVENMORE;
  +QUICKTORETALIATE;
  +DONTHARMCLASS;
  +DONTHARMSPECIES;
  +NODROPOFF;
  BloodColor "Blue";
  SeeSound "LightningPhase/Sight";
  ActiveSound "LightningPhase/Active";
  PainSound "LightningPhase/Pain";
  DeathSound "LightningPhase/Death";
  Obituary "%o was struck by THAT Lightning Phase.";
  HitObituary "%o was ripped and zapped by the Lightning Phase.";
  Tag "\c[l7]Lightning Phase\c-";
  MeleeSound "Doom4Imp/Slash";
  MeleeDamage 6;
  }
  
  int phased;
  
  States
  {
  Spawn:
    LTR3 A 10 DCY_Look();
	Loop;
  Look:
	LTR3 AABBCCDD 3 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" E 15 DCY_AlertSound();
	Goto See;
  See:
    LTR3 A 0
	{ 
		if (phased)
			SetStateLabel("SeePhased");
		
		phased == 0;
	}
    LTR3 AABBCCDD 3 A_Chase();
	Goto See+1;
  Lunge:
	LTR3 E 7 
	{
		A_FaceTarget();
		phased = 0;
		return A_Jump(150, "Missile");
	}
    LTR3 F 7 A_FaceTarget();
	LTR3 G 0 A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
	LTR3 GGGG 7
	{
		A_Recoil(random(-10, -13));
		A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
	}
	LTR3 G 0 A_Stop();
	TNT1 A 0 A_JumpIfCloser(85, "Melee");
	TNT1 A 0 A_Jump(150, "Missile");
	Goto Missile;
  Melee:
	LTR3 E 6
	{
		A_SetTranslucent(1.0, 0);
		A_UnHideThing();
		phased = 0;
		A_FaceTarget();
	}
    LTR3 F 6 A_FaceTarget();
	LTR3 G 6
	{
		A_CustomMeleeAttack(random(10, 15), "Doom4Imp/Slash", "");
		A_FaceTarget();
	}
	LTR3 F 6;
	Goto See;
  Dodge:
    LTR3 A 0;
    LTR3 A 0 A_FaceTarget();
	LTR3 A 0 A_Dodging(40);
	LTR3 E 10;
	LTR3 E 0 A_Stop();
	Goto Missile+4;
  Missile:
    LTR3 E 0;
	LTR3 E 0
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LTR3 E 0
	{
		If(target && Distance3D(target) < 200)
			Return ResolveState("Lunge");
		
		A_FaceTarget();
		Return A_Jump(128, "Dodge", "Phase");
	}
	Goto NormalMissile;
  Phase:
    LTR3 E 0;
	LTR3 E 1 A_StartSound("neophaseimp/teleport", CHAN_5);
	LTR3 E 5 A_FaceTarget();
	LTR3 EEEEEEEEE 1 A_FadeOut(0.1, 0);
	TNT1 A 0 { phased = 1; }
	Goto SeePhased;
  SeePhased:
    LTR3 E 0;
	LTR3 AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	LTR3 AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	TNT1 A 0 A_Jump(50, "Unphase");
	LTR3 AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	LTR3 AAAABBBBCCCCDDDD 1 A_Chase(null, null, CHF_NOPLAYACTIVE);
	Goto Unphase;
  Unphase:
    LTR3 E 0;
	LTR3 E 1 A_StartSound("neophaseimp/teleport", CHAN_5);
	LTR3 E 1 A_FaceTarget();
	LTR3 EEEEEEEEE 1 A_FadeIn(0.1, 0);
	TNT1 A 0 { phased = 0; }
	Goto See;
  NormalMissile:
	LTR3 E 0 A_FaceTarget();
	LTR3 EF 7 A_FaceTarget();
	LTR3 G 7
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_LightningPhaseBall", 32, 0, random(-1, 1));
	}
	LTR3 F 7 A_FaceTarget();
	Goto See;
  Pain:
    LTR3 H 4
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LTR3 H 2 A_Pain();
	TNT1 A 0 A_Jump(30, "Dodge");
	Goto See;
  Death:
  XDeath:
    LTR3 I 0;
    LTR3 I 1
	{
		phased = 0;
		A_UnHideThing();
		A_SetTranslucent(1.0, 0);
	}
	LTR3 I 6
	{
		A_UnSetSolid();
		A_Scream();
		A_NoBlocking();
	}
	LTR3 JKL 8;
	LTR3 M -1;
	Stop;
  }
}

Class DCY_LightningPhaseBall : DoomImpBall
{
  Default
  {
  Damage 10;
  Speed 15.5;
  FastSpeed 18.5;
  Alpha 0.825;
  Scale 1.1;
  +SEEKERMISSILE
  +ROLLSPRITE
  +NOEXTREMEDEATH
  Obituary "%o was struck THAT Lightning Phase.";
  }
  States
  {
  Spawn:
	P_FX AAAABBBB 1 Bright Light("DCY_LightningPhaseBall1")
	{
		A_Weave(3, 3, 1, 1);
		A_SetRoll(frandom(0.00, 360.00));
		if (bSEEKERMISSILE) A_SeekerMissile(1, 5, SMF_PRECISE);
		A_SpawnItemEx("DCY_LightningPhaseBallTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERROLL);

		if (random(0, 30) == 0)
			bSEEKERMISSILE = 0;
	}
	Loop;
  Death:
    P_FX C 3 Bright Light("DCY_LightningPhaseBall2");
	P_FX D 3 Bright Light("DCY_LightningPhaseBall3");
	P_FX E 3 Bright Light("DCY_LightningPhaseBall4");
	P_FX F 3 Bright Light("DCY_LightningPhaseBall5");
	P_FX G 3 Bright Light("DCY_LightningPhaseBall6");
	P_FX H 3 Bright;
	Stop;
  }
}

Class DCY_LightningPhaseBallTrail : DCY_Effect
{
  Default
  {
  Alpha 0.755;
  Scale 1.0;
  RenderStyle "Add";
  Radius 1;
  Height 1;
  +NOCLIP
  +CLIENTSIDEONLY
  +NOINTERACTION
  +DONTSPLASH
  +ROLLSPRITE
  -SOLID
  }
  States
  {
  Spawn:
    TNT1 A 1;
	P_FX AAAABBBBAA 1 Bright 
	{
		A_SetScale(Scale.X-0.1, Scale.Y-0.1);
		A_FadeOut(0.1);
	}
	Stop;
  }
}