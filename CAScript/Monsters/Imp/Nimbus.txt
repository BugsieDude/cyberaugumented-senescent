Class DCY_NimbusImp : DCY_Impe
{
  Default
  {
  Health 115;
  Tag "\c[Purple]Nimbus Conductor\c-";
  +MISSILEEVENMORE
  +QUICKTORETALIATE
  +NOTARGET
  +NODROPOFF
  SeeSound "roughimp/sight";
  ActiveSound "roughimp/active";
  PainSound "roughimp/pain";
  DeathSound "roughimp/death";
  DamageType "NimbusImpDMG";
  Translation "168:191=%[0.00,0.00,0.00]:[1.50,0.51,0.88]", "16:47=%[0.00,0.00,0.00]:[1.50,0.51,0.88]", "208:239=%[0.00,0.00,0.00]:[1.51,0.51,0.88]", "192:207=%[0.00,0.00,0.00]:[1.51,0.51,0.88]", "160:167=%[0.00,0.00,0.00]:[1.51,0.51,0.88]", "122:122=%[0.00,0.00,0.00]:[2.00,2.00,2.00]";
  DamageFactor "NimbusImpDMG", 0.00000;
  BloodColor "8F 00 61";
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o got the bloody heart disease.",
		"An nimbus conductor got the best of %o.",
		"Chaos and destruction"
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
    XX4_ A 10 DCY_Look();
	Loop;
  Look:
	XX4_ AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
    TNT1 A 0
	{
		If (target && Distance3D(target) < 128)
			Return ResolveState("Jump");
			
		Return A_Jump(128, "FastChase", "Avoid");
	}
    XX4_ AABBCCDD 2 A_Chase();
	Loop;
  Jump:
    TNT1 A 0 A_Jump(100, "See");
    XX4_ EF 3 A_FaceTarget();
	XX4_ G 10
	{
		A_Recoil(-13);
		A_Hop(5);
	}
	TNT1 A 0 A_Jump(128, "FastChase");
	Goto See;
  FastChase:
    TNT1 A 0 A_Jump(128, "See", "Jump");
    XX4_ AABBCCDD 2 A_Chase(flags: CHF_FASTCHASE);
	Loop;
  Avoid:
	TNT1 A 0
	{
		A_Dodging(20);
		A_FaceTarget();
	}
	XX4_ AAAAA 1 A_Chase();
	TNT1 A 0 A_Jump(128, "FastChase");
	Goto See;
  Melee:
    XX4_ EF 6 A_FaceTarget();
	XX4_ G 6 A_CustomMeleeAttack(random(5, 15), "imp/melee", 0, "NimbusImpDMG");
	TNT1 A 0 A_Jump(128, "FastChase");
	Goto See;
  Missile:
    TNT1 A 0 A_Jump(128, "Spread");
	TNT1 A 0 A_Jump(60, "BigBall");
    XX4_ EF 6 A_FaceTarget();
	XX4_ GG 0 A_SpawnProjectile("DCY_NimbusImpBall", 32, 0, random(-10, 10));
	XX4_ G 6 A_SpawnProjectile("DCY_NimbusImpBall", 32, 0, random(-1, 1));
	TNT1 A 0 A_Jump(128, "See");
	Goto See;
  Spread:
    XX4_ F 10 A_FaceTarget();
	XX4_ GGG 0 A_SpawnProjectile("DCY_NimbusImpBall", 32, 8, random(-10, 10));
	XX4_ GGG 0 A_SpawnProjectile("DCY_NimbusImpBall", 32, -8, random(-20, 20));
	XX4_ E 8;
	TNT1 A 0 A_Jump(128, "See");
	Goto See;
  BigBall:
    TNT1 A 0 A_StartSound("B3LLCHRG", CHAN_WEAPON, 0, 1.0, 0.45);
    XX4_ EF 20 A_FaceTarget();
	TNT1 A 0 A_StartSound("freedoom/brain/spit", CHAN_WEAPON, 0, 1.0, 0.4);
	XX4_ G 20 A_SpawnProjectile("DCY_NimbusImpBigBall", 32);
	TNT1 A 0 A_Jump(128, "See");
	Goto See;
  Pain:
    XX4_ H 6 A_Pain();
	TNT1 A 0 A_Jump(100, "Avoid");
	Goto See;
  Death:
	XX4_ I 0;
    XX4_ I 1 A_Jump(130, "Death2");
    XX4_ I 7;
    XX4_ J 8 A_Scream();
    XX4_ K 6;
    XX4_ L 6 A_NoBlocking();
    XX4_ M -1;
    Stop;
  Death2:
    XX4_ N 5;
    XX4_ O 5 A_Scream();
    XX4_ P 5 A_NoBlocking();
    XX4_ Q 5;
    XX4_ R -1;
    Stop;
  XDeath:
    XX4_ S 5;
    XX4_ T 5 A_XScream();
    XX4_ U 5;
    XX4_ V 5 A_NoBlocking();
    XX4_ WXY 5;
    XX4_ Z -1;
    Stop;
  Raise:
    XX4_ ML 8;
    XX4_ KJI 6;
    Goto See;
  }
}

Class DCY_NimbusImpBall : DCY_SiriusImpBall 
{
  Default
  {
  DamageType "NimbusImpDMG";
  Speed 22;
  FastSpeed 24;
  Translation "0:255=%[0.00,0.00,0.00]:[1.43,0.85,2.00]";
  Obituary "%o was in total shit by a Nimbus Imp, with it's Nimbus spamming balls power.";
  +BRIGHT
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o got the bloody heart disease.",
		"An nimbus imp got the best of %o.",
		"Chaos and destruction"
	};

	return messages[Random(0, messages.Size() - 1)];
  }
}

Class DCY_NimbusImpBigBall : DCY_NimbusImpBall
{
  Default
  {
  Scale 0.95;
  DamageFunction (random(15, 25));
  SeeSound "freedoom/brain/spit";
  Obituary "%o was inferior to the Nimbus Imp's power.";
  DeathSound "CYBRSMS2";
  +BRIGHT
  +SEEKERMISSILE
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o was inferior to the Nimbus Imp's power.",
		"Damaged as hell."
	};

	return messages[Random(0, messages.Size() - 1)];
  }
  
  States
  {
  Spawn:
    TNT1 A 0 A_SeekerMissile(2,2);
    VVE_ ABCD 2
	{
		A_Weave(1, 1, 1, 1);
		A_SpawnItemEx("DCY_NimbusImpBigBallTrail", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Loop;
  Death:
    VVE_ A 0
	{
		A_SpawnItemEx("DCY_Explosion_1", flags: SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_NimbusImpBigBallTrail", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	VVE_ ABCD 2 A_FadeOut(0.27, 0);
	Stop;
  }
}

Class DCY_NimbusImpBigBallTrail : DCY_GeneralImpBallTrail
{
  Default
  {
  Scale 0.95;
  }
  States
  {
  Spawn:
    VVE_ ABCDA 2 A_FadeOut(0.2, 0);
	Stop;
  }
}