Class DCY_Vamchnorm : DCY_Cybie
{
  Default
  {
  Health 5000;
  Tag "\c[t2]Vamchnorm\c-";
  +NOGRAVITY
  +FLOAT
  +DONTFALL
  Translation "112:207=%[0.00,0.00,0.00]:[0.57,0.65,1.43]";
  BloodColor "DarkBlue";
  SeeSound "Vamchnorm/Sight";
  ActiveSound "VamchNorm/Active";
  PainSound "Vamchnorm/Pain";
  DeathSound "Vamchnorm/Death";
  Speed 12;
  FastSpeed 15;
  }
  
  int timer;
  
  override void Tick()
  {
    super.Tick();
	
	If (!IsFrozen() && !sv_effectchoker && DCY_EffectDistance(sfxdistance))
	{
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("DCY_BlueVoidJetFlare", frandom(-17.0, 17.0), frandom(-17.0, 17.0), frandom(48.0, 51.0), 0, 0, frandom(-2.0, -5.0), flags: SXF_NOCHECKPOSITION, 150);
	  
		A_SpawnItemEx("DCY_VamchnormFire", frandom(-17.0, 17.0), frandom(-17.0, 17.0), frandom(48.0, 51.0), 0, 0, frandom(-1.0, -2.5), flags: SXF_NOCHECKPOSITION, 150);
	}
  }
  
  States
  {
  Spawn:
	CARD AB 2 DCY_Look();
	Loop;
  Look:
	CARD AB 3 DCY_LookingForPlayer();
	"####" A 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	CARD AB 2 A_Chase();
	Loop;
  Missile:
	CARD C 20
	{
		if (target && distance3D(target) < 500)
			Return A_Jump(100, "Railgunsinyourface");
		
		missile = random(0, 1);
		A_FaceTarget();
		
		Return ResolveState(null);
	}
	CARD D 5
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_VamchnormRocket", 40, -16, frandom(-3.0, 3.0));
	}
	CARD C 5
	{
		A_FaceTarget();
		A_Dodging(frandompick(-15.5, 0, 15.5));
		
		missile += 1;
		if (missile > 3)
		{
			A_Stop();
			SetStateLabel("See");
		}
	}
	Goto Missile+1;
  Railgunsinyourface:
	CARD C 20
	{
		missile = 0;
		A_FaceTarget();
	}
	CARD C 1
	{
		A_StartSound("Cyberdemon/RailgunCharge", CHAN_WEAPON, attenuation: 0.35);
		A_FaceTarget();
	}
	CARD CCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1
	{
		A_FaceTarget();
		A_CustomRailgun(0, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, 2, sparsity: 70, spawnclass: "DCY_VamchnormTrack");
	}
	CARD D 10
	{
		A_StartSound("Cyberdemon/RailgunFire", CHAN_WEAPON, attenuation: 0.5);
		A_CustomRailgun(145, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, 2, sparsity: 70, spawnclass: "DCY_VamchnormLazer");
		A_FaceTarget();
		missile++;
	}
	CARD C 11
	{
		A_FaceTarget();
		
		if (missile > 2)
		{
			A_Stop();
			SetStateLabel("See");
		}
	}
	Goto Railgunsinyourface+1;
  Pain:
	CARD AB 2;
	CARD AB 2 A_Pain();
	Goto See;
  Death:
	CARD E 5;
	CARD E 20 A_Pain();
	CARD E 1
	{
		if (timer > 15)
			SetStateLabel("Explode");
		
		if (timer > 2)
		{
			if (vel.z == 0)
				SetStateLabel("Explode");
		}
		
		timer++;
		
		A_ChangeVelocity(0, 0, 1);
	}
	Goto Death+2;
  Explode:
	CARD E 6 A_Scream();
	CARD F 6
	{
		A_Stop();
		A_NoBlocking();
	}
	CARD G 6
	{
		if (damagetype != "Massacre")
			A_SpawnItemEx("DCY_VamchnormExplode", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), flags: SXF_NOCHECKPOSITION);
		
		A_StartSound("Cyber/Death", CHAN_BODY, attenuation: ATTN_NONE, startTime: 0.2);
	}
	CARD H 6 { if (damagetype != "Massacre") A_SpawnItemEx("DCY_VamchnormExplode", frandom(-20, 20), frandom(-20, 20), frandom(-20, 20), flags: SXF_NOCHECKPOSITION); }
	CARD IJKL 6;
	TNT1 A 10;
	TNT1 A 1 A_BossDeath2();
	Stop;
  }
}

Class DCY_VamchnormFire : DCY_Explosion_1 { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.58,0.69,1.47]"; Scale 0.15; } }
Class DCY_VamchnormExplode : DCY_AndroidExplosionBiggie { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.58,0.69,1.47]"; } }

Class DCY_VamchnormFlame : DCY_PlasmaTrail { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.58,0.69,1.47]"; Scale 1.15; } }
Class DCY_VamchnormFlare : DCY_RocketFlare { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.58,0.69,1.47]"; } }
Class DCY_VamchnormRing : DCY_NeonicRing { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.58,0.69,1.47]"; } }

Class DCY_VamchnormRocket : DCY_OverhauledRocket
{
  Default
  {
  Speed 25.0;
  SeeSound "DNROCKET";
  Translation "160:249=%[0.00,0.00,0.00]:[0.54,0.66,1.46]", "16:79=%[0.00,0.00,0.00]:[0.54,0.66,1.46]";
  Scale 1.1;
  }
  
  States
  {
  Spawn:
	NMSL A 1
	{
		A_SpawnItemEx("DCY_VamchnormFlame", frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), zvel: 1, flags: SXF_NOCHECKPOSITION, failchance: 120);
		A_SpawnItemEx("DCY_VamchnormFlare", frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), flags: SXF_NOCHECKPOSITION, failchance: 60);
	}
	Loop;
  Death:
	TNT1 A 1
	{
		A_Explode(128, 128);
		for (int i = 2; i > 0; i--)
		{
			A_SpawnItemEx("DCY_VamchnormRing", frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), flags: SXF_NOCHECKPOSITION);
			A_SpawnItemEx("DCY_Explosion_1_Nightmare", frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-3.0, 3.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), frandom(-1.0, 1.0), flags: SXF_NOCHECKPOSITION);
		}
	}
	Stop;
  }
}
