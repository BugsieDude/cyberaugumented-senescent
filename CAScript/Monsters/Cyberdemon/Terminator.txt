Class DCY_NavyTerminator : DCY_Cybie
{
  Default
  {
  Scale 1.25;
  Health 5600;
  Tag "\c[v6]Extirpater ''Codeword - Navy''\c-";
  Obituary "%o is now in sapphire ashes by the Extirpater.";
  SeeSound "TerminatorBase/Sight";
  ActiveSound "TerminatorBase/Active";
  PainSound "TerminatorBase/Pain";
  DeathSound "TerminatorBase/Explode";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  DamageFactor "SiriusPower", 0.5;
  DamageType "SiriusPower";
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  +MISSILEMORE
  +QUICKTORETALIATE
  +NEVERFAST
  +NOBLOODDECALS
  Speed 12;
  }
  
  int nmissile;
  int voiceline;
  
  States
  {
  Spawn:
	NVRM A 10 DCY_Look();
	Loop;
  Look:
	"####" A 1;
	"####" A 1 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	NVRM AAAABBBB 2 A_Chase();
	NVRM A 0 A_StartSound("TerminatorBase/Footstep", CHAN_BODY);
	NVRM CCCCDDDD 2 A_Chase();
	NVRM A 0 A_StartSound("TerminatorBase/Footstep", CHAN_BODY);
	Loop;
  Missile:
	NVRM G 0 A_Jump(128, "Rawkets", "Rawkets2");
	NVRM G 0 A_Jump(55, "BallofStar");
	NVRM G 30
	{
		if (voiceline == 0)
			A_StartSound("TerminatorBase/Attack1", CHAN_VOICE);
		if (voiceline == 3)
			A_StartSound("TerminatorBase/Attack2", CHAN_VOICE);
			
		voiceline = random(0, 7);
		A_FaceTarget();
	}
	NVRM HI 4
	{
		A_FaceTarget();
		A_StartSound("Android/Laser", CHAN_WEAPON, 0, 1.0, 0.75);
		A_SpawnProjectile("DCY_MechaZombiePlasma", 52, -20, frandom(-2.5, 2.5), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-1.25, 1.25));
		Return A_MonsterRefire(10, "See");
	}
	Goto Missile+3;
  Rawkets:
	NVRM E 30 A_FaceTarget();
	NVRM F 5
	{
		for (int i = 10; i > -11; i -= 5)
			A_SpawnProjectile("DCY_TransmuterTorpedo", 52, 20, i);
	}
	NVRM EEE 5 A_FaceTarget();
	NVRM F 5
	{
		for (int i = 10; i > -11; i -= 5)
			A_SpawnProjectile("DCY_TransmuterTorpedo", 52, 20, i);
	}
	NVRM EEEAAAA 5;
	Goto See;
  Rawkets2:
	NVRM E 30
	{
		nmissile = 0;
		A_FaceTarget();
		
		if (voiceline == 0)
			A_StartSound("TerminatorBase/Attack1", CHAN_VOICE);
		if (voiceline == 3)
			A_StartSound("TerminatorBase/Attack2", CHAN_VOICE);
			
		voiceline = random(0, 18);
	}
	NVRM F 2
	{
		if (nmissile > 9)
			Return ResolveState("Cooldown");
		
		A_FaceTarget();
		A_SpawnProjectile("DCY_TransmuterTorpedo", 52, 20, frandom(-1.2, 1.2), CMF_OFFSETPITCH|CMF_TRACKOWNER, frandom(-1.2, 1.2));
		nmissile++;
		Return ResolveState(null);
	}
	NVRM E 2 A_FaceTarget();
	Goto Rawkets2+1;
  Cooldown:
	NVRM EEEAAAA 5;
	Goto See;
  BallofStar:
	NVRM J 30
	{
		if (voiceline == 0)
			A_StartSound("TerminatorBase/Attack1", CHAN_VOICE);
		if (voiceline == 3)
			A_StartSound("TerminatorBase/Attack2", CHAN_VOICE);
			
		voiceline = random(0, 10);
		
		A_FaceTarget();
	}
	NVRM K 3
	{
		A_StartSound("StarProjectile/See", attenuation: 0.45);
		A_SpawnProjectile("DCY_NavyBallofStar", 52, -20);
		A_FaceTarget();
	}
	NVRM JJJJJJJJJJJJ 3;
	Goto See;
  Pain:
	NVRM L 4;
	NVRM L 3 A_Pain();
	Goto See;
  Death:
	NVRM M 1;
	NVRM M 6 A_Scream();
	NVRM MMMNNNOOO 3 { if (damagetype != "Massacre") A_SpawnItemEx("DCY_AndroidExplosionSmaller", random(-20, 20), random(-20, 20), random(15, 60), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), random(0, 360), SXF_NOCHECKPOSITION); }
	NVRM P 3
	{
		A_FaceTarget();
		A_Fall();
		
		if (damagetype != "Massacre")
		{
			for (int i = 4; i > 0; i--)
				A_SpawnItemEx("DCY_AndroidExplosionBiggie", random(-15, 15), random(-15, 15), random(20, 50), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), random(0, 360), SXF_NOCHECKPOSITION);
			
			A_SpawnItemEx("DCY_AndroidExplosion", random(-20, 20), random(-20, 20), random(15, 60), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), random(0, 360), SXF_NOCHECKPOSITION);
		
			let arm = Spawn("DCY_TerminatorArm", pos + (60, 0, 0));
			let head = Spawn("DCY_TerminatorHead", pos + (15, 0, 90));
			let shoulder = Spawn("DCY_TerminatorShoulder", pos + (60, 0, 90));
			
			if (arm && head && shoulder)
			{	
				arm.Vel3DFromAngle(5, self.angle - 90, self.pitch - 50);
				head.Vel3DFromAngle(10, self.angle - 170, self.pitch - 100);
				shoulder.Vel3DFromAngle(8, self.angle - 70, self.pitch - 50);
				
				head.translation = translation;
				shoulder.translation = translation;
				arm.translation = translation;
			}
		}
	}
	NVRM QRSTUVWXY 5;
	NVRM Z 5
	{
		A_QuakeEx(0, 0, 3, 40, 0, 900, "", QF_SCALEDOWN|QF_3D);
		A_StartSound("Trounce/BodyPound", CHAN_BODY, attenuation: 0.5);
	}
	NVRM "[" 45;
	NVRM "[" -1 A_BossDeath2();
	Stop;
  }
}

Class DCY_NavyBallofStar : CacodemonBall
{
  Default
  {
  DamageFunction (5);
  Alpha 1.0;
  Scale 1.5;
  Radius 33;
  Height 33;
  SeeSound "StarProjectile/See";
  DeathSound "StarProjectile/Explode";
  Translation "16:22=[255,255,255]:[128,255,255]", "23:31=[102,255,255]:[0,128,255]", "32:38=[0,121,242]:[0,64,128]", "39:47=[0,55,111]:[0,0,64]", "168:175=[255,255,255]:[128,255,255]", "176:182=[102,255,255]:[0,128,255]", "183:188=[0,121,242]:[0,64,128]", "189:191=[0,55,111]:[0,0,64]";
  Speed 10;
  FastSpeed 13;
  +SEEKERMISSILE
  +RIPPER
  +BLOODLESSIMPACT
  +BRIGHT
  }
  
  int timer;
  
  States
  {
  Spawn:
	TSAR AABBCCDD 1
	{
		A_SpawnItemEx("DCY_FluorescentSparkle", frandom(-20.5, 20.5), frandom(-20.5, 20.5), frandom(-20.5, 20.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), flags: SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_NavyBallTrail", flags: SXF_NOCHECKPOSITION);
		
		if (timer > 30 && timer < 50)
			A_ScaleVelocity(1.15);
		
		if (timer < 30)
			A_SeekerMissile(random(5, 10), random(10, 16));
		
		timer++;
	}
	Loop;
  Death:
	TSAR E 0 A_Explode(450, 250, XF_NOTMISSILE, True);
	TSAR EEE 0 A_SpawnItemEx("DCY_AndroidExplosionBiggie", random(-35, 35), random(-35, 35), random(-35, 35), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), random(0, 360), SXF_NOCHECKPOSITION);
	TSAR EFGHIJKLMNO 2;
	Stop;
  }
}

Class DCY_NavyBallTrail : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Scale 1.5;
  Translation "0:255=%[0.00,0.00,0.00]:[0.48,0.48,1.58]";
  Alpha 0.25;
  +NOCLIP
  -SOLID
  +BRIGHT
  +NOGRAVITY
  +ROLLSPRITE
  +NOINTERACTION
  }
  States
  {
  Spawn:
    TSAR "#################" 1
	{
		A_FadeOut(0.005, 0);
		A_SetRoll(Roll+10, SPF_INTERPOLATE);
		A_SetScale(Scale.X+0.01, Scale.Y+0.01);
	}
	Stop;
  }
}

Class DCY_TerminatorArm : DCY_GoreBase
{
  Default
  {
  -ROLLSPRITE
  }
  
  int timer;
  
  override void Tick()
  {
	super.tick();
	
	if (timer < 200)
	{
		if ((level.time % 2) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 120, 50, 0, frandom(-8, 8), frandom(-8, 8), 20 + frandom(-8, 8), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.3, 1), startalphaf: 0.04, fadestepf: 0.0001, sizestep: 0.2, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
		timer++;
	}
  }
  
  States
  {
  Spawn:
	N1__ AAABCDEF 3 Bright;
	Goto Death;
  Death:
	N1__ F -1 { vel.x = 0; vel.y = 0; }
	Stop;
  }
}

Class DCY_TerminatorHead : DCY_TerminatorArm
{  
  States
  {
  Spawn:
	N2__ AABBCCDDEEFFGGHH 1 { roll += xroll; }
	Loop;
  Death:
	N2__ I -1 { vel.x = 0; vel.y = 0; roll = 0; }
	Stop;
  }
}

Class DCY_TerminatorShoulder : DCY_TerminatorHead
{
  States
  {
  Spawn:
	N3__ AABBCCDDEEFFGGHH 1 { roll += xroll; }
	Loop;
  Death:
	N3__ I -1 { vel.x = 0; vel.y = 0; roll = 0; }
	Stop;
  }
}