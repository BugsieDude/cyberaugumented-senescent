Class DCY_Dasher : DCY_PinkyParent
{
  Default
  {
  Health 125;
  Obituary "%o was a sustenance for the Dasher.";
  HitObituary "%o was a sustenance for the Dasher.";
  SeeSound "Dasher/Sight";
  ActiveSound "Dasher/Active";
  AttackSound "Dasher/Melee";
  MeleeSound "Dasher/Melee";
  PainSound "Dasher/Pain";
  DeathSound "Dasher/Death";
  CrushPainSound "Dasher/Howl";
  HowlSound "Dasher/Howl";
  Speed 15;
  FastSpeed 21;
  +ALWAYSFAST
  +NOTARGET
  +NODROPOFF
  MeleeDamage 10;
  Tag "Dasher";
  DCYMonster.ScientificName "Tenebris devorantem";
  }
  
  int meleeloop;
  States
  {
  Spawn:
    U1X_ A 10 DCY_Look();
	Loop;
  Look:
	U1X_ AABB 2 DCY_LookingForPlayer();
	U1X_ A 0 A_StartSound("Dasher/Walk", CHAN_BODY);
	U1X_ CCDD 2 DCY_LookingForPlayer();
	U1X_ A 0 A_StartSound("Dasher/Walk", CHAN_BODY);
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	U1X_ E 1 DCY_AlertSound();
	U1X_ EFFFFFFE 4 A_FaceTarget();
	Goto See;
  See:
	U1X_ AABB 2 DCY_Chase();
	U1X_ A 0 A_StartSound("Dasher/Walk", CHAN_BODY);
	U1X_ CCDD 2 DCY_Chase();
	U1X_ A 0 A_StartSound("Dasher/Walk", CHAN_BODY);
	Loop;
  Missile:
	TNT1 A 0
	{
		if (target && distance3D(target) > 256)
			Return A_Jump(100, "Scream");
		
		Return ResolveState(null);
	}
	Goto Charge;
  Scream:
	U1X_ E 1 A_StartSound("dasher/charge", CHAN_VOICE);
	U1X_ EFFFFFFFFE 4 A_FaceTarget();
	Goto Charge;
  Charge:
	TNT1 A 0
	{
		bSLIDESONWALLS = true;
		meleeloop = 0;
		
		if (target && Distance3D(target) < 256)
			SetStateLabel("Lunge");
		
		Return A_Jump(128, "See");
	}
	TNT1 A 0
	{
		If(meleeloop >= 9)
		{
			SetStateLabel("See");
			bSLIDESONWALLS = false;
		}
		
		if (target && Distance3D(target) < 256)
			SetStateLabel("Lunge");
			
		A_FaceTarget();
	}
    U1X_ AA 1 A_Recoil(-1);
	U1X_ BB 1 A_Recoil(-2);
	U1X_ A 0
	{
		A_StartSound("Dasher/Walk", CHAN_BODY);
		A_FaceTarget();
		
		if (target && Distance3D(target) < 256)
			SetStateLabel("Lunge");
	}
    U1X_ CC 1 A_Recoil(-1);
	U1X_ DD 1 A_Recoil(-2);
	U1X_ A 0
	{
		A_StartSound("Dasher/Walk", CHAN_BODY);
		meleeloop += 1;
	}
	Goto Charge+1;
  Lunge:
    U1X_ E 1;
    U1X_ E 9
	{
		bSLIDESONWALLS = false;
		A_Stop();
		A_StartSound("Dasher/Melee", CHAN_VOICE);
		A_FaceTarget();
		bNoDropOff = true;
		A_Recoil(-25);
		ThrustThingZ(0, 12, 0, 0);
	}
	U1X_ F 1;
	U1X_ F 1 A_SpawnProjectile("DCY_DasherLunge", flags: CMF_AIMDIRECTION|CMF_OFFSETPITCH|CMF_SAVEPITCH);
	U1X_ F 8;
	U1X_ G 14;
	U1X_ G 1
	{
		bNoDropOff = false;
		A_Stop();
	}
	Goto See;
  Melee:
    U1X_ EF 5 A_FaceTarget();
	U1X_ G 5 A_CustomMeleeAttack(10 * random(1, 3), "dsaher/melee");
	Goto See;
  Pain:
	U1X_ H 3;
    U1X_ H 4 A_Pain();
	Goto See;
  Pain.Fire:
  Pain.Acid:
  Pain.Electric:
  Pain.Holy:
    U1X_ H 3;
	U1X_ H 4 A_StartSound("Dasher/Howl", CHAN_VOICE);
	Goto See;
  Death:
    U1X_ I 1;
	U1X_ I 9 A_Scream();
	U1X_ J 7
	{
		A_FaceTarget();
		let arm = Spawn("DCY_DasherArm", self.pos + (0, 0, 20));
		if (arm) arm.Vel3DFromAngle(random(5, 7), self.angle - 90, self.pitch - 50);
		A_NoBlocking();
	}
	U1X_ KLM 7;
	U1X_ N -1;
	Stop;
  Death.Holy:
  Death.Fire:
  XDeath:
    U1X_ I 1;
	U1X_ I 9 A_StartSound("Dasher/Howl", CHAN_VOICE);
	U1X_ J 7
	{
		A_FaceTarget();
		let arm = Spawn("DCY_DasherArm", self.pos + (0, 0, 20));
		if (arm) arm.Vel3DFromAngle(random(5, 7), self.angle - 90, self.pitch - 50);
		A_NoBlocking();
	}
	U1X_ KLM 7;
	U1X_ N -1;
	Stop;
  Raise:
    U1X_ MLKJI 7;
	Goto See;
  }
}

Class DCY_DasherLunge : FastProjectile
{
  Default
  {
  Damage 5;
  Speed 18.65;
  FastSpeed 18.65;
  Radius 30;
  Height 56;
  +MTHRUSPECIES
  +THRUSPECIES
  +DONTREFLECT
  }
  States
  {
  Spawn:
    NULL A 20;
	Stop;
  Death:
    NULL A 1;
	Stop;
  }
}

Class DCY_DasherArm : DCY_GoreBase
{  
  Default
  {
  +ROLLCENTER
  }
  
  States
  {
  Spawn:
	U1X_ O 1 { roll += xroll; }
	Loop;
  Death:
	U1X_ W -1 { vel.x = 0; vel.y = 0; roll = 0; }
	Stop;
  }
}