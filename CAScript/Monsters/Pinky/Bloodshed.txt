Class DCY_BloodshedAssailant : DCY_Pinke
{
  Default
  {
  Health 400;
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,0.25,0.00]", "16:47=%[0.00,0.00,0.00]:[0.00,0.00,0.00]", "168:191=%[0.00,0.00,0.00]:[0.00,0.00,0.00]";
  +NOBLOOD
  +NEVERFAST
  +DONTHARMSPECIES
  +THRUSPECIES
  +VISIBILITYPULSE
  Speed 12;
  RenderStyle "Translucent";
  Species "Bloodshed";
  Tag "\c[o5]Bloodshed Assailant\c-";
  SeeSound "DEM2SIT";
  ActiveSound "DEM2PAI";
  PainSound "DEM2PAI";
  DeathSound "DEM2SIT";
  MeleeThreshold 150;
  DCYMonster.ScientificName "Sanguis abactor";
  }
  
  int hologram;
  
  override void Tick()
  {
	super.tick();
	
	if (Health > 0 && !isFrozen())
	{
		A_SpawnItemEx("DCY_AssailantTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, failchance: 150);
	}
  }
  
  States
  {
  Spawn:
	UU9_ A 10 DCY_Look();
	Loop;
  Look:
    UU9_ AAABBBCCCDDD 2 DCY_LookingForPlayer();
    Loop;
  Alert:
    "####" "#" 0 DCY_AlertSound();
  See:
	UU9_ AAABBBCCCDDD 2 A_Chase();
	Loop;
  Missile:
	UU9_ A 0 A_JumpIf(target && Distance3D(target) < 256, "Lunge");
	UU9_ A 0 A_JumpIf(target && Distance3D(target) < 512, "Hologram");
	UU9_ AAABBBCCCDDD 2 A_Chase("Melee", null);
	Goto See;
  Hologram:
	UU9_ A 0 A_JumpIf(hologram >= 3, "See");
	UU9_ A 0 A_Jump(150, "See");
	UU9_ AAABBBCCCDDD 2 A_Chase("Melee", null);
	UU9_ A 0
	{
		A_SpawnItemEx("DCY_Hallucination", 0, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 90);
		A_Chase("Melee", null);
	}
	UU9_ AAABBBCCCDDD 2 A_Chase("Melee", null);
	UU9_ A 0
	{
		A_SpawnItemEx("DCY_Hallucination", 0, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 90);
		A_Chase("Melee", null);
	}
	UU9_ AAABBBCCCDDD 2 A_Chase("Melee", null);
	UU9_ A 0
	{
		A_SpawnItemEx("DCY_Hallucination", 0, 0, 5, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 90);
		A_Chase("Melee", null);
		hologram = 1;
	}
	Goto See;
  Lunge:
	UU9_ E 1;
    UU9_ E 9
	{
		A_Stop();
		A_StartSound("DEM2PAI", CHAN_VOICE);
		A_StartSound("demon/melee", CHAN_BODY, pitch: 0.85);
		A_FaceTarget();
		bNoDropOff = false;
		A_Recoil(-30);
		A_Hop(8);
	}
	UU9_ FFFFFF 2 A_Explode(5 * random(1, 3), 65, XF_NOTMISSILE, 0, damagetype: "Bloodshed");
	UU9_ G 14;
	UU9_ G 1
	{
		bNoDropOff = true;
		A_Stop();
	}
	Goto See;
  Melee:
	UU9_ E 1;
	UU9_ E 10
	{
		A_FaceTarget();
		A_StartSound("DEM2PAI", CHAN_VOICE);
		A_StartSound("demon/melee", CHAN_BODY, pitch: 0.85);
	}
	UU9_ F 10;
	UU9_ G 10 A_CustomMeleeAttack(10 * random(1, 8), "", "", "Bloodshed");
	Goto See;
  Pain:
	UU9_ H 4;
	UU9_ H 5 A_Pain();
	Goto See;
  Death:
	UU9_ H 1;
	UU9_ H 35
	{
		A_KillChildren("Normal", RMVF_EVERYTHING, "DCY_Hallucination", "Bloodshed");
		A_KillSiblings("Normal", RMVF_EVERYTHING, "DCY_Hallucination", "Bloodshed");
		A_Scream();
		A_NoBlocking();
		bStealth = true;
		bVisibilityPulse = false;
	}
	UU9_ O 6 A_SpawnItemEx("DCY_AssailantTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, failchance: 150);
	UU9_ PQRSTUV 5
	{
		A_SpawnItemEx("DCY_AssailantTrail", zofs: 5, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_NOCHECKPOSITION|SXF_TRANSFERALPHA, failchance: 30);
		A_FadeOut(0.175, 0);
	}
	UU9_ W -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_Hallucination : DCY_BloodshedAssailant
{
  Default
  {
  -COUNTKILL
  +STEALTH
  +THRUSPECIES
  -VISIBILITYPULSE
  Species "Bloodshed";
  SeeSound "";
  Alpha 0.5;
  }
  
  int fade;
  
  States
  {
  Spawn:
	UU9_ AAABBBCCCDDD 2
	{
		If(fade >= 150)
		{
		Return ResolveState("Fade");
		}
		Else
		{
		A_Chase();
		fade++;
		Return ResolveState(null);
		}
	}
	Loop;
  Missile:
	UU9_ A 0 A_JumpIf(Distance3D(target) < 256, "Lunge");
	UU9_ AAABBBCCCDDD 2
	{
		If(fade >= 150)
		{
		Return ResolveState("Fade");
		}
		Else
		{
		A_Chase();
		fade++;
		Return ResolveState(null);
		}
	}
	Goto See;
  Lunge:
	UU9_ E 1;
    UU9_ E 9
	{
		A_Stop();
		A_StartSound("DEM2PAI", CHAN_VOICE);
		A_StartSound("demon/melee", CHAN_BODY, pitch: 0.85);
		A_FaceTarget();
		bNoDropOff = false;
		A_Recoil(-30);
		A_Hop(8);
	}
	UU9_ FFFFFF 2 A_Explode(5 * random(1, 3), 65, XF_NOTMISSILE, 0, damagetype: "Bloodshed");
	UU9_ G 14;
	UU9_ G 1
	{
		If(fade >= 150)
		{
		Return ResolveState("Fade");
		}
		Else
		{
		bNoDropOff = true;
		A_Stop();
		fade += 12;
		Return ResolveState(null);
		}
	}
	Goto See;
  Melee:
	UU9_ E 1;
	UU9_ E 10
	{
		A_FaceTarget();
		A_StartSound("DEM2PAI", CHAN_VOICE, 1.0, 0, 1.0, 0, 1.0);
		A_StartSound("demon/melee", CHAN_BODY, 1.0, 0, 1.0, 0, 0.85);
	}
	UU9_ F 10;
	UU9_ G 10 A_CustomMeleeAttack(10 * random(1, 8), "", "", "Bloodshed");
	Goto Fade;
  Pain:
	UU9_ H 4;
	UU9_ H 4 A_Pain();
	UU9_ H 1 A_Jump(36, "Fade");
	Goto See;
  Fade:
	UU9_ "#" 0
	{
		A_Die();
		bStealth = false;
	}
	Goto Death;
  Death:
	UU9_ "##########" 1 A_FadeOut(0.1, 0);
	Stop;
  }
}