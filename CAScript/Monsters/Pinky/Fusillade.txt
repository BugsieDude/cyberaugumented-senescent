Class DCY_Fusillade : DCY_Pinke
{
  Default
  {
  DamageType "Fire";
  SeeSound "Fusillade/Roar";
  AttackSound "Dasher/Melee";
  PainSound "Fusillade/Melee";
  DeathSound "Fusillade/Death";
  ActiveSound "Fusillade/Active";
  DamageFactor "Ice", 2;
  DamageFactor "Lava", 0;
  DamageFactor "Fire", 0;
  Obituary "%o could not stand the Fusillade's mania.";
  HitObituary "%o became charred meat.";
  Translation "48:74=%[0.15,0.00,0.00]:[2.00,0.95,0.00]", "75:79=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "128:159=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "13:15=%[0.00,0.00,0.00]:[0.72,0.72,0.72]", "80:111=[255,255,255]:[0,0,0]", "1:3=%[0.00,0.00,0.00]:[0.81,0.81,0.81]", "5:8=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "16:47=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "168:191=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "236:239=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "160:167=%[0.20,0.00,0.00]:[2.00,0.70,0.00]", "232:235=%[0.20,0.00,0.00]:[2.00,0.70,0.00]";
  BloodColor "ad3002";
  BloodType "AndroidNeonFluid";
  Speed 15;
  FastSpeed 17;
  Health 250;
  DCYMonster.WalkFlames "DCY_WalkFlames";
  Tag "\c[x1]Fusillade\c-";
  +MISSILEMORE
  MeleeRange 100;
  DCYMonster.ScientificName "Flamma devorans";
  }
  
  void A_FusilladeStep()
  {
	A_WalkFlame();
	A_StartSound("Dasher/Walk", CHAN_BODY);
  }
  
  void A_FusilladeCheck(int w = -1)
  {
	if (health > 0)
	{
		If(meleeloop >= 14)
			SetStateLabel("See");
		
		A_FV();
		A_StartSound("Dasher/Walk", CHAN_BODY);
		A_SpawnItemEx("DCY_NapalmFire", 1, w * 10, flags: SXF_NOCHECKPOSITION);
		meleeloop++;
		A_WalkFlame();
	}
  }
  
  void A_FV()
  {
	if (vel.xy ~== (0, 0))
	{
		A_Recoil(10);
		vel.z += 5;
		A_StartSound("Fist/PunchEntity", 9);
		A_CustomMeleeAttack(12 * random(1, 10), "", "", DamageType: "Fire");
		SetStateLabel("PainLong");
	}
	
	A_FaceTarget(4, 4);
  }
  
  int meleeloop;
  
  States
  {
  Spawn:
    CYF3 A 10 DCY_Look();
	Loop;
  Look:
	CYF3 AABB 3 DCY_LookingForPlayer();
	CYF3 A 0 A_FusilladeStep();
	CYF3 CCDD 3 DCY_LookingForPlayer();
	CYF3 A 0 A_FusilladeStep();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	CYF3 E 1 DCY_AlertSound();
	CYF3 EFPFPFPFPFPO 4
	{
		A_QuakeEx(3, 3, 1, 30, 0, 250, "", QF_SCALEDOWN|QF_3D, falloff: 150);
		A_FaceTarget();
	}
	Goto See;
  See:
	CYF3 AABB 2 DCY_Chase();
	CYF3 C 0 A_FusilladeStep();
	CYF3 CCDD 2 DCY_Chase();
	CYF3 A 0 A_FusilladeStep();
	Loop;
  Missile:
	CYF3 A 0
	{
		if (target && Distance3D(target) < 242)
			SetStateLabel("Lunge");
		if (!random(0, 1) && target && distance2D(target) < 320)
			SetStateLabel("Scream");
	}
	CYF3 EOFP 3 A_FaceTarget();
	CYF3 P 10
	{
		A_StartSound("BLERGH", CHAN_VOICE);
		
		for (int i = 3; i > 0; i--)
			A_SpawnProjectile("DCY_FusilladeBall", 32, angle: frandom(-5, 5), flags: CMF_OFFSETPITCH, pitch: pitch + frandom(-5, -10));
		
		A_FaceTarget();
	}
	CYF3 O 6;
	Goto See;
  Melee:
	CYF3 EF 6 A_FaceTarget();
	CYF3 G 4
	{
		A_FaceTarget();
		A_CustomMeleeAttack(8 * random(1, 10), "", "", DamageType: "Fire");
	}
	Goto See;
  Scream:
	CYF3 E 1 A_StartSound(SeeSound, CHAN_VOICE);
	CYF3 EFFFFFFFFE 3
	{
		A_WalkFlame();
		A_FaceTarget();
	}
	Goto Charge;
  Charge:
	CYF3 A 0
	{
		meleeloop = 0;
	}
	CYF3 A 0 A_Recoil(-5);
    CYF3 AAA 1 { A_FV(); A_Recoil(-2); }
	CYF3 BBB 1 { A_FV(); A_Recoil(-2); }
	CYF3 A 0 A_FusilladeCheck(1);
    CYF3 CCC 1 { A_FV(); A_Recoil(-2); }
	CYF3 DDD 1 { A_FV(); A_Recoil(-2); }
	CYF3 A 0 A_FusilladeCheck();
	Goto Charge+2;
  Lunge:
	CYF3 E 0 { bGhost = false; }
	CYF3 EEEEEEEEE 1 A_FaceTarget();
	CYF3 E 0
	{
		A_Hop(4);
		A_StartSound("Fusillade/Melee", CHAN_VOICE);
	}
	CYF3 FFFFFFFF 1 A_Recoil(-4);
	CYF3 G 0 A_CustomMeleeAttack(8 * random(1, 10), "", "", DamageType: "Fire");
	CYF3 G 8;
	Goto See;
  Pain:
	CYF3 H 4;
	CYF3 H 4 A_Pain();
	Goto See;
  PainLong:
	CYF3 H 4;
	CYF3 H 20 A_Pain();
	Goto See;
  Death:
	CYF3 I 1;
	CYF3 I 6 A_ScreamAndUnblock();
	CYF3 JKLM 5;
	CYF3 N -1;
	Stop;
  Raise:
	CYF3 MLKJI 5;
	Goto See;
  }
}

Class DCY_FusilladeBall : DCY_ImpFireball
{
  Default
  {
  -NOGRAVITY
  Gravity 0.25;
  Speed 25;
  FastSpeed 30;
  DamageType "Fire";
  DeathSound "DCYLavaImpact";
  SeeSound "BLERGH";
  Scale 1.2;
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	BAL3 AAAABBBBCCCCDDDD 1
	{
		A_SetRoll(Roll+10);
		A_SpawnParticleEx(0xad3002, TexMan.CheckForTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	C3AL A 1
	{
		for (int i = 20; i > 0; i--)
			A_SpawnParticleEx(0xad3002, TexMan.CheckForTexture("Graphics/Particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT, random(20, 40), random(1, 3), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		
		A_Explode(30, 150, damagetype: "Lava");
		A_SpawnItemEx("DCY_Explosion_1");
		
		for (int i = 5; i > 0; i--)
			A_SpawnItemEx("De_Blood_BigSplat", frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), frandom(0, 5), flags: SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE);
	}
	C3AL BCDE 1;
	Stop;
  }
}