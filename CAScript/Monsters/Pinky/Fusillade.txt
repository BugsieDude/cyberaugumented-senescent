Class DCY_Fusillade : DCY_Pinke
{
  Default
  {
  DamageType "Fire";
  SeeSound "Fusillade/Roar";
  AttackSound "Dasher/Melee";
  PainSound "Fusillade/Melee";
  DeathSound "Fusillade/Death";
  ActiveSound "Fusillade/Active";
  DamageFactor "Ice", 2;
  DamageFactor "Lava", 0;
  DamageFactor "Fire", 0;
  Obituary "%o could not stand the Fusillade's mania.";
  HitObituary "%o became charred meat.";
  Translation "48:79=%[0.00,0.00,0.00]:[0.61,0.61,0.61]", "48:74=%[0.34,0.00,0.00]:[1.91,1.23,0.10]", "75:79=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "128:159=%[0.00,0.00,0.00]:[0.83,0.83,0.83]", "13:15=%[0.00,0.00,0.00]:[0.72,0.72,0.72]", "16:47=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "168:191=%[0.00,0.00,0.00]:[0.38,0.38,0.38]", "236:239=%[0.00,0.00,0.00]:[0.46,0.46,0.46]", "160:167=%[0.34,0.00,0.00]:[1.91,1.23,0.10]", "232:235=%[0.30,0.00,0.00]:[1.91,1.23,0.10]", "1:2=%[0.00,0.00,0.00]:[0.23,0.23,0.23]";
  BloodColor "Orange";
  BloodType "AndroidNeonFluid";
  Speed 15;
  FastSpeed 17;
  Health 250;
  DCYMonster.WalkFlames "DCY_WalkFlames";
  Tag "\c[x1]Fusillade\c-";
  +BRIGHT
  +MISSILEMORE
  }
  
  void A_FusilladeStep()
  {
	A_WalkFlame();
	A_StartSound("Dasher/Walk", CHAN_BODY);
  }
	
  States
  {
  Spawn:
    SAR3 A 10 DCY_Look();
	Loop;
  Look:
	SAR3 AABB 3 DCY_LookingForPlayer();
	SAR3 A 0 A_FusilladeStep();
	SAR3 CCDD 3 DCY_LookingForPlayer();
	SAR3 A 0 A_FusilladeStep();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	SAR3 E 1 DCY_AlertSound();
	SAR3 EFFFFFFFFFFE 4
	{
		A_QuakeEx(3, 3, 1, 30, 0, 250, "", QF_SCALEDOWN|QF_3D, falloff: 150);
		A_FaceTarget();
	}
	Goto See;
  See:
	SAR3 AABB 2 A_Chase();
	SAR3 C 0 A_FusilladeStep();
	SAR3 CCDD 2 A_Chase();
	SAR3 A 0 A_FusilladeStep();
	Loop;
  Missile:
	SAR3 A 0
	{
		if (target && Distance3D(target) < 242)
			SetStateLabel("Lunge");
	}
	SAR3 EF 6 A_FaceTarget();
	SAR3 F 10
	{
		A_StartSound("BLERGH", CHAN_VOICE);
		
		for (int i = 3; i > 0; i--)
			A_SpawnProjectile("DCY_FusilladeBall", 32, angle: frandom(-5, 5), flags: CMF_OFFSETPITCH, pitch: pitch + frandom(-5, -10));
		
		A_FaceTarget();
	}
	SAR3 E 6;
	Goto See;
  Melee:
	SAR3 EF 6 A_FaceTarget();
	SAR3 G 4
	{
		A_FaceTarget();
		A_CustomMeleeAttack(8 * random(1, 10), "", "", DamageType: "Fire");
	}
	Goto See;
  Lunge:
	SAR3 E 0 { bGhost = false; }
	SAR3 EEEEEEEEE 1 A_FaceTarget();
	SAR3 E 0
	{
		A_Hop(4);
		A_StartSound("Fusillade/Melee", CHAN_VOICE);
	}
	SAR3 FFFFFFFF 1 A_Recoil(-4);
	SAR3 G 0 A_CustomMeleeAttack(8 * random(1, 10), "", "", DamageType: "Fire");
	SAR3 G 8;
	Goto See;
  Pain:
	SAR3 H 4;
	SAR3 H 4 A_Pain();
	Goto See;
  Death:
	SAR3 I 1;
	SAR3 I 6 A_ScreamAndUnblock();
	SAR3 JKLM 5;
	SAR3 N -1;
	Stop;
  Raise:
	SAR3 MLKJI 5;
	Goto See;
  }
}

Class DCY_FusilladeBall : DCY_ImpFireball
{
  Default
  {
  -NOGRAVITY
  Gravity 0.25;
  Speed 25;
  FastSpeed 30;
  DamageType "Fire";
  DeathSound "DCYLavaImpact";
  SeeSound "BLERGH";
  Scale 1.2;
  Translation "0:255=%[0.00,0.00,0.00]:[1.71,0.83,0.30]";
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	BAL3 AAAABBBBCCCCDDDD 1
	{
		A_SetRoll(Roll+10);
		A_SpawnParticle(0xffdc9c, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		A_SpawnItemEx("DCY_MastermindJetFlare", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), flags: SXF_NOCHECKPOSITION);
	}
	Loop;
  Death:
	C3AL A 1
	{
		for (int i = 20; i > 0; i--)
			A_SpawnParticle(0xffdc9c, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-9.5, 9.5), frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.5, 7.4), accelz: -0.55);
		
		A_Explode(30, 150, damagetype: "Lava");
		A_SpawnItemEx("DCY_Explosion_1");
	}
	C3AL BCDE 1;
	Stop;
  }
}