Class DCY_ExplodingScreamingDood : DCYMonster
{
  Default
  {
  Obituary "%o was exploded by a Infernal-powered Bomber.";
  Tag "Beheaded Infernal-powered Bomber";
  Health 20;
  Radius 16;
  Height 56;
  Mass 100;
  Speed 12;
  Translation "48:79=%[0.00,0.00,0.00]:[1.01,1.01,1.01]";
  +QUICKTORETALIATE
  }
  
  override string GetObituary (Actor victim, Actor inflictor, Name mod, bool playerattack)
  {
	static const string messages[] =
	{
		"%o got exploded by an Infernal-powered Bomber.",
		"bruh they look so familiar to the kamikaze guys from serious sam",
		"AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA!!"
	};

	return messages[Random(0, messages.Size() - 1)];
  }

  float soundpitch;
  
  States
  {
  Spawn:
    BKAM A 0
	{
		A_StopSound(CHAN_5);
		soundpitch = (frandom(0.96, 1.03));
	}
    BKAM A 10 A_LookEx(0, 0, 0, 1024);
    Loop;
  Idle:
    BKAM A 0
	{
		A_StopSound(CHAN_5);
		soundpitch = (frandom(0.96, 1.03));
	}
    BKAM A 10 A_LookEx(0, 0, 0, 1024);
    BKAM A 0 A_Jump(8, "IdleNoise");
    Loop;
  IdleNoise:
    BKAM A 0 A_StartSound("kamikazi/idle");
    Goto Idle;
  See:
    BKAM A 0 A_StartSound("kamikazi/scream", CHAN_5, CHANF_DEFAULT, 1.0, ATTN_NORM, soundpitch);
    BKAM AABBCCDD 2
	{
		If (target && Distance3D(target) < 56)
		{
			Return ResolveState("Melee");
		}
		
		A_FaceTarget(12.5, 12.5);
		A_Recoil(-4);
		Return ResolveState(null);
	}
	Goto CheckSee;
  CheckSee:
    BKAM D 0 A_CheckSight("Navigate");
    Goto See;
  Navigate:
    BKAM AABBCCDDAABBCCDDAABBCCDD 2 A_Chase();
    Goto CheckSee;
  Melee:
    BKAM A 0 A_Die();
    Goto Death;
  Death:
	BMBE M 5 Bright
	{
		A_StartSound("weapons/rocklx", CHAN_BODY);
		A_StopSound(CHAN_5);
		A_Stop();
		A_XScream();
		A_Explode(50, 96);
		A_SpawnItemEx("DCY_Explosion_1", 0, 0, 20, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DCY_Explosion_1", 0, 0, 20, random(-3, 3), random(-3, 3), random(-3, 3), random(0, 360), SXF_NOCHECKPOSITION);
	}
    BMBE N 5 Bright;
    BMBE O 5 A_NoBlocking();
    BMBE PQRST 5;
    BMBE U -1;
    Stop;
  Death.Massacre:
    BMBE M 5 Bright
	{
		A_StartSound("weapons/rocklx", CHAN_BODY);
		A_StopSound(CHAN_5);
		A_Stop();
		A_XScream();
		A_Explode(50, 96);
	}
    BMBE N 5 Bright;
    BMBE O 5 A_NoBlocking();
    BMBE PQRST 5;
    BMBE U -1;
    Stop;
  }
}