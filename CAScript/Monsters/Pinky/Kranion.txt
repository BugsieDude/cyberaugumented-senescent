Class DCY_Kranion : DCY_Pinke //iron demon but awesome
{
  Default
  {
  Tag "\c[Red]Kranion\c-";
  Health 566;
  Translation "192:195=[255,255,255]:[128,255,255]", "196:201=[106,255,255]:[0,128,255]", "202:207=[0,116,232]:[0,0,64]", "80:111=%[0.00,0.00,0.00]:[0.46,0.46,0.46]";
  BloodType "AndroidNeonFluid", "AndroidNeonFluid", "AndroidNeonFluid";
  BloodColor "2ba0ff";
  Obituary "%o got swiped out by a Kranion.";
  HitObituary "%o got swiped out by a Kranion.";
  +MISSILEMORE
  Speed 9;
  FastSpeed 11;
  DropItem "DCY_UltramodernEnergy", 128;
  DropItem "DCY_UltramodernEnergy", 128;
  DamageType "AndroidRed";
  SeeSound "Kranion/Sight";
  ActiveSound "Kranion/Active";
  PainSound "Kranion/Pain";
  DeathSound "Kranion/Death";
  AttackSound "Kranion/Attack";
  }
  
  int selfdestruct;
  
  States
  {
  Spawn:
	IFN2 A 4 DCY_Look();
	IFIN A 4 DCY_Look();
	IFN2 B 4 DCY_Look();
	IFIN A 4 DCY_Look();
	Loop;
  Look:
	IFIN AABBCCDD 2 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	IFIN E 0 DCY_AlertSound();
	Goto See;
  See:
	IFIN AABBCCDD 2
	{
		If(Health <= 25)
		{
			bInvulnerable = true;
			Return ResolveState("SuicideBomb");
		}
		Else
		{
			DCY_Chase("Melee", null);
			Return ResolveState(null);
		}
	}
	Loop;
  Melee:
	IFIN EEEE 4 A_FaceTarget();
	IFIN E 10
	{
		A_SpawnProjectile("DCY_KranionBall", 30, 0, -10);
		A_SpawnProjectile("DCY_KranionBall", 30, 0, 10);
		A_SpawnProjectile("DCY_KranionBall", 30, 0, 0);
		A_FaceTarget();
	}
	IFIN EEA 4;
	Goto See;
  Pain:
	IFIN G 3;
	IFIN G 4 A_Pain();
	Goto See;
  SuicideBomb:
	IFIN AAABBBCCCDDD 1
	{
		If(selfdestruct >= 80)
		{
			Return ResolveState("Explode");
		}
		Else
		{
			DCY_Chase(null, null, CHF_NIGHTMAREFAST);
			DCY_Chase(null, null, CHF_NIGHTMAREFAST);
			selfdestruct++;
			Return A_JumpIfCloser(80, "Explode");
		}
	}
	IFIN C 0 A_StartSound("Kranion/Beep", CHAN_BODY);
	Loop;
  Death:
  Explode:
	IFIN G 1;
	IFIN G 10 A_ScreamAndUnblock();
	IFIN H 5 A_Die();
	IFIN I 3 Bright
	{
		A_SpawnItemEx("DCY_Explosion_1", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(20.0, 30.0));
		A_SpawnItemEx("DCY_Explosion_1", frandom(-20.0, 20.0), frandom(-20.0, 20.0), frandom(20.0, 30.0));
		A_QuakeEx(1, 1, 1, 20, 0, 256, "none", QF_SCALEDOWN, 0, 0, 0, 512);
		A_Explode(130, 200, XF_NOTMISSILE, 1, 85);
		A_StartSound("weapons/rocklx", CHAN_BODY);
	}
	IFIN JK 3 Bright;
	IFIN LMN 3;
	IFIN N -1;
	Stop;
  Death.Massacre:
	IFIN G 1;
	IFIN G 10 A_ScreamAndUnblock();
	IFIN H 5 A_Die();
	IFIN I 3 Bright;
	IFIN JK 3 Bright;
	IFIN LMN 3;
	IFIN N -1;
	Stop;
  Raise:
	Stop;
  }
}

Class DCY_KranionBall : DCY_MeatballElecball
{
  Default
  {
  DamageFunction (2);
  DamageType "AndroidRed";
  Translation "0:255=%[0.00,0.00,0.00]:[2.00,1.07,1.07]";
  Speed 15;
  FastSpeed 15;
  Alpha 0.75;
  +RIPPER
  +BRIGHT
  }
  States
  {
  Spawn:
	SMOL EEFFGGHHIIJJKKLL 1
	{
		A_Weave(1, 1, 1, 1);
		A_Weave(3, 0, frandom(1, 3), 0);
		for (int i = 2; i > 0; i--) A_SpawnItemEx("DCY_KranionBallTrail", frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		A_SpawnParticleEx(0xd14141, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 128, 25, velx: frandom(-0.5, 0.5), vely: frandom(-0.5, 0.5), velz: frandom(-0.5, 0.5), startalphaf: 0.35, fadestepf: 0.0065, sizestep: 1.525);
	
	}
	Stop;
  Death:
	SMOL LLKKJJII 1
	{
		A_SetScale(Scale.X+0.5, Scale.Y+0.5);
		A_FadeOut(0.095, 0);
	}
	Stop;
  }
}

Class DCY_KranionBallTrail : DCY_Effect
{
  Default
  {
  Scale 1.15;
  Alpha 0.65;
  RenderStyle "Add";
  Radius 0;
  Height 0;
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	SMOL EEFFGGGGIIJJKKLL 1 A_FadeOut(0.05, 0);
	Stop;
  }
}
