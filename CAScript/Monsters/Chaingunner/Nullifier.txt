Class DCY_Nullifier : DCY_Plutonia
{
  Default
  {
  Health 120;
  SeeSound "possessed5/sight";
  ActiveSound "possessed5/active";
  PainSound "possessed5/pain";
  DeathSound "possessed5/death";
  Tag "\c[r4]Nullifier\c-";
  DCYMonster.ExtraPrefix "\c[r4]";
  DCYMonster.BurnSound "Possessed5/Burn";
  DropItem "Chaingun";
  Translation "224:231=[255,255,255]:[255,255,128]", "208:216=[255,255,255]:[255,255,128]", "217:223=[255,255,128]:[255,121,0]", "232:235=%[0.00,0.00,0.00]:[2.00,0.95,0.00]", "236:239=%[0.00,0.00,0.00]:[0.48,0.48,0.48]", "48:79=%[0.00,0.00,0.00]:[0.91,0.69,0.62]", "128:151=%[0.00,0.00,0.00]:[0.91,0.69,0.62]", "152:159=%[0.00,0.00,0.00]:[0.57,0.57,0.57]", "13:15=%[0.00,0.00,0.00]:[0.73,0.73,0.73]", "160:167=[255,255,128]:[64,9,0]", "192:195=[255,255,255]:[255,255,128]", "196:201=[255,255,128]:[255,121,0]", "202:207=[255,121,0]:[64,9,0]", "9:12=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "112:127=%[0.00,0.00,0.00]:[0.49,0.49,0.49]", "248:255=%[0.50,0.07,0.00]:[2.00,2.00,1.01]";
  }
  
  int bufftimer;
  
  override void Tick()
  {
	super.Tick();
	
	if (!isFrozen() && Health > 0 && bufftimer > 0)
	{
		bufftimer--;
		A_SpawnItemEx("DCY_NullifierLine", frandom(-20, 20), frandom(-20, 20), frandom(5, 35), zvel: frandom(1, 2), flags: SXF_NOCHECKPOSITION);
	}
  }
  
  States
  {
  Spawn:
	RPZ2 A 10 DCY_Look();
	Loop;
  Look:
	RPZ2 AABBCCDD 4 DCY_LookingForPlayer();
	"####" "#" 0 A_CheckSight("Look");
	Goto Alert;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	RPZ2 AABBCCDD 4
	{
		if (bufftimer > 0)
		{
			A_SpawnItemEx("DCY_NullifierRush", zofs: 5, flags: SXF_TRANSFERSCALE|SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
			
			for(int i = random(1, 2); i > 0; i--)
				DCY_Chase();
		}
		
		tics = (bufftimer > 0) ? 1 : 4;
		
		DCY_Chase();
	}
	Loop;
  Missile:
	RPZ2 E 10
	{
		tics = (bufftimer > 0) ? 2 : 5;
		A_FaceTarget();
	}
	RPZ2 FFG 2
	{
		A_FaceTarget();
		A_SpawnProjectile("DCY_NullifierRing", 32);
	}
	RPZ2 EA 5;
	Goto See;
  Pain:
	RPZ2 I 3
	{
		if (Health < 90 && bufftimer <= 0)
			Return A_Jump(50, "Pissed");
		
		Return ResolveState(null);
	}
	RPZ2 I 4 A_Pain();
	Goto See;
  Pissed:
	RPZ2 I 3 A_FaceTarget();
	RPZ2 I 20
	{
		A_StartSound("possessed5/mad", CHAN_VOICE, attenuation: 0.75);
		A_QuakeEx(2, 2, 2, 50, 0, 300, "", QF_SCALEDOWN|QF_3D);
		A_FaceTarget();
		bufftimer = 200;
	}
	Goto See;
  Death:
	RPZ2 J 3 A_SetScale(frandompick(Scale.X, -Scale.X), Scale.Y);
	RPZ2 J 3 A_Scream();
	RPZ2 K 5 A_NoBlocking();
	RPZ2 LMN 5;
	RPZ2 N -1;
	Stop;
  Raise:
	RPZ2 NMLKJ 5;
	RPZ2 J 2 A_SetScale(1, Scale.Y);
	Goto See;
  XDeath:
	RPZ2 O 1;
	RPZ2 O 5 DCY_Gibs();
	RPZ2 P 4 A_NoBlocking();
	RPZ2 QRSTUV 3;
	RPZ2 W -1;
	Stop;
  }
}

Class DCY_NullifierRing : DCY_RehauledPlasma
{
  Default
  {
  Damage 2;
  Speed 20;
  FastSpeed 25;
  Radius 7;
  Height 7;
  Scale 0.7;
  Damage 5;
  SeeSound "CARCATK";
  DeathSound "CARCATKX";
  Translation "0:255=%[0.37,0.00,0.00]:[2.00,1.63,1.01]";
  }
  
  States
  {
  Spawn:
	R1N6 A 1 A_SpawnItemEx("DCY_NullifierRingTrail", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	Loop;
  Death:
	MPB_ H 3
	{
		A_SetScale(1.0, 1.0);
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_SetRoll(frandom(0, 360));
		for (int i = 4; i > 0; i--)
		{
			A_SpawnParticle(0xffe4a6, SPF_FULLBRIGHT, random(20, 40), random(6, 9), frandom(0, 360), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-6.5, 6.5), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0));
			A_SpawnItemEx("DCY_NullifierThunder", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(5, 10), zvel: frandom(-7, 7), angle: frandom(0, 360), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
		}
	}
	MPB_ IJKL 2;
	Stop;
  }
}

Class DCY_NullifierRingTrail : DCY_ProjectileBaseTrail { Default { Scale 0.7; Translation "0:255=%[0.00,0.00,0.00]:[0.96,0.23,0.04]"; } }

Class DCY_NullifierThunder : DCY_BFGThunder
{
  Default { Translation "0:255=%[0.37,0.00,0.00]:[2.00,1.63,1.01]"; }
  
  States
  {
  Spawn:
	TNT1 AAAAAAAAAAAAAAAA 1
	{
		tics = random(0, 1);
		A_ChangeVelocity(frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5));
		A_SpawnParticle(0xffe4a6, SPF_FULLBRIGHT, random(10, 20), random(12, 18), frandom(0, 360), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5));
	}
	Goto Death;
  Death:
	C3AL ABCDE 2 A_Stop();
	Stop;
  }
}

Class DCY_NullifierLine : DCY_VLine { Default { Translation "0:255=%[0.37,0.00,0.00]:[2.00,1.63,1.01]"; } }