Class DCY_HellPrototypeMech : DCY_Plutonia
{
  Default
  {
  Health 125;
  Speed 5;
  FastSpeed 8;
  Radius 20;
  Height 56;
  Mass 450;
  PainChance 40;
  Tag "\c[d4]Preventive\c-";
  SeeSound "preventive/sight";
  ActiveSound "preventive/active";
  PainSound "preventive/pain";
  DeathSound "preventive/death";
  Obituary "%o got vaporized.";
  DropItem "Cell";
  DropItem "DCY_Cyberrifle";
  GibHealth 100;
  }
  
  States
  {
  Spawn:
	LS20 H 1 DCY_Look();
	Loop;
  Look:
	LS20 AABBCCDD 3 DCY_LookingForPlayer();
	LS20 A 10 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	LS20 AABBCCDD 2 A_Chase();
	Loop;
  Melee:
  Missile:
	LS20 E 8 A_FaceTarget();
	LS20 F 8
	{
		A_StartSound("Underline/Fire", CHAN_WEAPON);
		
		for (int i = 4; i > 0; i--)
			A_SpawnProjectile("DCY_Zombie2Tracer", 45, -10, frandom(-1.1, 1.1), CMF_TRACKOWNER|CMF_OFFSETPITCH, random(-1, 1));
		
		A_FaceTarget(3, 3);
	}
	LS20 E 8 A_FaceTarget();
	LS20 E 0
	{
		if (target && target.health <= 0) SetStateLabel("See");
		if (target && !CheckSight(target)) SetStateLabel("See");
		return resolvestate(null);
	}
	Goto Missile+1;
  Pain:
	LS20 G 4 A_Pain();
	Goto See;
  Death:
  Death.Fire:
	LS20 I 6 A_Scream();
	LS20 J 6 A_Fall();
	LS20 K 6;
	LS20 L 12;
	LS20 MNO 5;
	LS20 L -1;
	Stop;
  XDeath:
	LS20 Q 5
	{
		A_SpawnItemEx("DCY_Explosion_1", zofs: 30, flags: SXF_NOCHECKPOSITION);
		A_Scream();
	}
	LS20 Q 0 A_Fall;
	LS20 RS 4;
	LS20 T 4 A_StartSound("weapons/rocklx", CHAN_BODY);
	LS20 UVW 5;
	LS20 X -1;
	Stop;
  Raise:
	LS20 WVUTSRQ 4;
	Goto See;
  }
}