Class DCY_ShadowCrusader : DCY_Plutonia
{
  Default
  {
  Health 400;
  BloodColor "Purple";
  DCYMonster.ExtraPrefix "\c[t7]";
  Speed 12;
  FastSpeed 19;
  Tag "\c[t7]Shadow Crusader\c-";
  SeeSound "ShadowCrusader/Sight";
  ActiveSound "ShadowCrusader/Active";
  PainSound "ShadowCrusader/Pain";
  DeathSound "ShadowCrusader/Death";
  PainChance 60;
  DamageFactor "SC", 0;
  DropItem "Chaingun";
  }
  
  int xangle;
  protected bool cloaked;
  
  void A_FireP(int um)
  {
	A_FaceTarget();
	
	A_StartSound("RBGSH0T", CHAN_WEAPON, attenuation: 0.85);
	A_SpawnProjectile("DCY_SCFire", 30, 10, xangle);
	
	xangle += 10 * um;
	
	If (Health < 50)
		tics = 1;
	If (Health < 190)
		tics = 2;
  }
  
  void A_Reappear()
  {
	if (cloaked)
	{
		A_StartSound("ElectricBall/Death", CHAN_BODY);
		for (int i = 1; i < 6; i++)
			A_SpawnItemEx("DCY_PhasicRazerTrail", zofs: 5, zvel: i, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
	}
	
	cloaked = false;
	A_SetTranslucent(1.0);
	A_UnHideThing();
  }
  
  States
  {
  Spawn:
	PSGC A 10 DCY_Look();
	Loop;
  Look:
	PSGC AABBCCDD 3 DCY_LookingForPlayer();
	Loop;
  Alert:
	"####" "#" 0 DCY_AlertSound();
	Goto See;
  See:
	PSGC AABBCCDD 2
	{
		if (cloaked)
			SetStateLabel("Cloaked");
		
		A_Jump(10, "Dodge");
		DCY_Chase();
	}
	Loop;
  Dodge:
	PSGC A 10 A_Dodging(20);
	PSGC A 10 A_Stop();
	Goto See;
  Cloaked:
	PSGC AABBCCDD 1
	{
		if (!cloaked)
			SetStateLabel("See");
		
		A_SpawnItemEx("DCY_PhasicRazerTrail", zofs: 5, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME);
		DCY_Chase(null, "Grenades", CHF_NOPLAYACTIVE);
	}
	Loop;
  Grenades:
	PSGC E 10 A_FaceTarget();
	PSGC EEEEEEE 5
	{
		A_FaceTarget();
		tics = random(3, 5);
		A_SpawnProjectile("DCY_SCNade", 30, -12, frandom(-30, 30), CMF_OFFSETPITCH, pitch - frandom(8, 20));
	}
	Goto Cloaked;
  Missile:
	PSGC E 0
	{
		If (Health < 200)
			Return A_Jump(80, "CloakYourself");
		
		Return ResolveState(null);
	}
	Goto RaidenDXAttack;
  RaidenDXAttack:
	PSGC E 30
	{
		A_StartSound("ShadowCrusader/Attack", CHAN_VOICE);
		A_FaceTarget();
		xangle = 50;
	}
	PSGC FEFEFEF 3 A_FireP(-1);
	PSGC E 3 { xangle = -50; A_FireP(1); }
	PSGC FEFEFEF 3 A_FireP(1);
	PSGC E 3 { xangle = 50; A_FireP(-1); }
	PSGC FEFEFEF 3 A_FireP(-1);
	PSGC E 3 { xangle = -50; A_FireP(1); }
	PSGC FEFEFEF 3 A_FireP(1);
	Goto See;
  CloakYourself:
	PSGC A 0 A_StartSound("ST3ALTH", CHAN_BODY);
	PSGC AAAAAAAAAAAAAAAA 1 A_FadeOut(0.05);
	PSGC A 1
	{
		cloaked = true;
		A_HideThing();
	}
	Goto Cloaked;
  Pain:
	PSGC G 4;
	PSGC G 4
	{
		A_Reappear();
		A_Pain();
	}
	Goto See;
  Death:
	PSGC G 4 A_Reappear();
	PSGC H 5 A_Scream();
	PSGC IJ 5;
	PSGC K 4 A_NoBlocking();
	PSGC LMNO 4;
	PSGC P -1;
	Stop;
  }
}

Class DCY_SCNade : DCY_OverhauledRocket
{
  Default
  {
  -NOGRAVITY
  +BRIGHT
  +NEVERFAST
  Gravity 0.75;
  Damage 0;
  BounceFactor 0.4;
  Translation "160:248=%[0.00,0.00,0.00]:[0.85,0.67,1.34]";
  Scale 0.7;
  Speed 19;
  BounceType "Doom";
  SeeSound "";
  DeathSound "";
  BounceSound "";
  DamageType "SC";
  +ROLLSPRITE
  -ZDOOMTRANS
  }
  
  int loop;
  
  States
  {
  Spawn:
	MMIS A 1 A_SpawnParticle(0xc9b8ff, SPF_FULLBRIGHT, random(20, 40), random(9, 13), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.05, sizestep: -1.25);
	Loop;
  XDeath:
	MMIS A 0 A_Recoil(random(1, 4));
	Goto Death;
  Death:
	MMIS A 1
	{		
		if (loop > 40)
			SetStateLabel("Explode");
		
		A_SetTranslucent(1);
		
		loop++;
		A_SpawnParticle(0xc9b8ff, SPF_FULLBRIGHT, random(20, 40), random(9, 13), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), accelz: 0.05, sizestep: -1.25);
	}
	Loop;
  Explode:
	TNT1 A 1
	{
		A_SetRenderStyle(1, STYLE_ADD);
		A_SetRoll(frandom(0, 360));
		A_Explode(10, 80);
		A_StartSound("EXPLD001", pitch: frandom(1.6, 1.5));
		A_SpawnItemEx("DCY_Explosion_1_SCNade", flags: SXF_NOCHECKPOSITION);
		
		for (int i = 12; i > 0; i--)
			A_SpawnParticle(0xc9b8ff, SPF_FULLBRIGHT, random(20, 45), 5, frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(3.5, 6.4), accelz: -0.85, fadestepf: 0, sizestep: -0.125);
	}
	Stop;
  }
}

Class DCY_Explosion_1_SCNade : DCY_Explosion_1_Small { Default { Translation "0:255=%[0.00,0.00,0.00]:[0.85,0.67,1.34]"; } }

Class DCY_SCFire : CacodemonBall
{
  Default
  {
  SeeSound "";
  DeathSound "";
  Speed 30;
  +NEVERFAST
  Damage 5;
  Radius 4;
  Height 8;
  +ROLLSPRITE
  Translation "0:255=%[0.13,0.05,0.34]:[1.15,0.61,1.87]";
  Scale 1.2;
  +BRIGHT
  }
  
  States
  {
  Spawn:
	G7TR B 1
	{
		A_SetRoll(Roll+30);
		A_SpawnItemEx("DCY_DFX1", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERROLL);
		A_SpawnParticle(0xc9b8ff, SPF_FULLBRIGHT, random(20, 40), random(9, 13), frandom(0, 360), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.35, 0.35), frandom(-0.35, 0.35), frandom(-0.35, 0.35), sizestep: -1.25);
	}
	Loop;
  Death:
	G7BL F 3
	{
		for (int i = 7; i > 0; i--)
			A_SpawnParticle(0xc9b8ff, SPF_FULLBRIGHT, random(20, 45), 5, frandom(0, 360), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-3.5, 3.5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(3.5, 6.4), accelz: -0.85, fadestepf: 0, sizestep: -0.125);
	}
	G7BL GH 3;
	Stop;
  }
}