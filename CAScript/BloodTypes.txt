Class De_Blood : Actor Replaces Blood
{
  Mixin DCY_Effects;
  
  string bloodtype;
  class<Actor> bloodtype2, bloodtype3;
  property Type : bloodtype;
  property Type2 : bloodtype2;
  property Type3 : bloodtype3;
  
  Default
  {
	Alpha 0.8;
	RenderStyle "Translucent";
	Scale 0.5;
	Mass 5;
	+NOBLOCKMAP
	+NOTELEPORT
	+ALLOWPARTICLES
	Gravity 0.5;
	De_Blood.Type "De_Blood_Trail";
	De_Blood.Type2 "De_Blood_BigSplat";
	De_Blood.Type3 "De_Blood_Splat";
  }
  
  void A_SpawnBlood(string blood, bool brightness)
  {
    if (!DCY_EffectDistance(1000)) return;
	
	let trail = Spawn(blood, pos);
	
	if (trail)
	{
		trail.vel = vel / 4;
		trail.gravity = gravity / 4;
		trail.scale = scale;
		trail.translation = translation;
		
		if (brightness) trail.bBRIGHT = true;
	}
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	A_SetScale(frandom(0.7, 0.8));
	bSPRITEFLIP = randompick(false, true);
	vel.x += frandom(-3, 3);
	vel.y += frandom(-3, 3);
	vel.z += frandom(-1.5, 2.5);
	
	A_SpawnItemEx(bloodtype3, frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-4, 4), frandom(-4, 4), frandom(-1, 4), frandom(0, 360), SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_NOCHECKPOSITION, 150);
	
	if (random(0, 2) > 0) {
		A_SpawnItemEx(bloodtype2, frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), frandom(-1, 3), frandom(0, 360), SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE|SXF_NOCHECKPOSITION);
	}
  }
  
  States
  {
  Spawn:
	BL0D AAABBCC 2 A_SpawnBlood(bloodtype, false);
	Stop;
  Crush:
	"####" A 1;
	Stop;
  Spray:
    SPRY ABCDEF 2;
    SPRY G 2;
    Stop;
  }
}

Class De_Blood_Trail : Actor
{
  Default
  {
  +NOINTERACTION
  }
  
  States
  {
  Spawn:
	BL0D ABC 4;
	Stop;
  }
}

Class De_Blood_Splat : DCY_Effect
{
  Default
  {
	Gravity 0.325;
	Scale 1;
  }
  
  override void PostBeginPlay()
  {
	Super.PostBeginPlay();
	scale *= frandom(0.8, 1.2);
	scale.x *= randompick(1, -1);
	scale.y *= randompick(1, -1);
  }
  
  States
  {
  Spawn:
	BL2D ABCDEFGH random(1, 2);
	Stop;
  }
}

Class De_Blood_BigSplat : DCY_Effect
{
  int e;
  Default { +ROLLSPRITE }
  override void PostBeginPlay()
  {
	DCY_Effect.PostBeginPlay();
	scale *= frandom(0.5, 0.8);
	scale.x *= randompick(1, -1);
	scale.y *= randompick(1, -1);
	roll = frandom(0, 360);
	gravity *= 0.2;
	switch (random(0, 2))
	{
		case 0: sprite = GetSpriteIndex("Z_67"); e = 19; break;
		case 1: sprite = GetSpriteIndex("Z_68"); e = 18; break;
		case 2: sprite = GetSpriteIndex("Z_69"); e = 13; break;
	}
  }
  
  States
  {
  Spawn:
	#### ABCDEFGHIJKLMNOPQRS random(1, 2)
	{
		if (e <= 0) Destroy();
		e--;
		A_FadeOut(0.05);
		
		if (vel.z == 0)
		{
			Destroy();
		}
	}
	Stop;
  LoadSprites:
	Z_67 ABCDEFGHIJKLMNOPQRS 0;
	Z_68 ABCDEFGHIJKLMNOPQR 0;
	Z_69 ABCDEFGHIJKLM 0;
  }
}

Class Add_Blood_Trail : De_Blood_Trail { Default { RenderStyle "Add"; } }
Class Sub_Blood_Trail : De_Blood_Trail { Default { RenderStyle "Subtract"; } }

Class De_Plat_Blood : De_Blood Replaces BloodSplatter {}

Class AndroidNeonFluid : De_Blood
{
  Default
  {
  +BRIGHT
  +NODECAL
  RenderStyle "Add";
  De_Blood.Type "Add_Blood_Trail";
  }
}

Class DCY_SpectreFluid : De_Blood
{
  Default
  {
  +BRIGHT
  +NODECAL
  RenderStyle "Subtract";
  De_Blood.Type "Sub_Blood_Trail";
  }
}

Class DCY_SpiritBlood : DE_BLOOD
{
  Mixin DCY_Projectiles;
  Mixin DCY_Effects;
  
  Default
  {
  RenderStyle "Add";
  +BRIGHT
  +NODECAL
  +NOGRAVITY
  Scale 0.425;
  Alpha 0.5;
  }
  
  int loop;
  
  States
  {
  Spawn:
	TNT1 A 0 NoDelay
	{
		loop = 6;
		A_SetAngle(frandom(-40, 40));
		A_ChangeVelocity(frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(2.5, 4.5), CVF_REPLACE);
	}
	TNT1 A 1
	{
		loop--;
		A_ChangeVelocity(frandom(-2, 2), frandom(-2, 2), frandom(-2, 2));
		A_SpawnItemEx("DCY_Smonk", frandom(-2, 2), frandom(-2, 2), frandom(-2, 2), flags: SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA|SXF_TRANSFERTRANSLATION);
		A_FadeOut(0.075, 0);
		
		if (loop <= 0)
			destroy();
	}
	Goto Spawn+1;
  }
}