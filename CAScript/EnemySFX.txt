Class DCY_GeneralComet : DCY_Projectile
{
  Default
  {
  Radius 12;
  Height 12;
  Speed 15;
  Damage 2;
  Scale 0.85;
  RenderStyle "Normal";
  Alpha 1.0;
  +ROLLSPRITE
  +NEVERFAST
  +EXTREMEDEATH
  Projectile;
  SeeSound "DCYComet/Spawn";
  DeathSound "DCYComet/Explode";
  Decal "Scorch";
  }
  
  float r;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	r = frandom(0.8, 4.0) * randompick(1, -1);
  }
  
  States
  {
  Spawn:
    CGMT AABBCC 1 Bright
	{
		if ((level.time % random(1, 5)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, 50, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 1), startalphaf: 0.25, fadestepf: 0.0014, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
		
		roll += r;
		A_SpawnItemEx("DCY_CometTrail", frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), vel.x / 4, vel.y / 4, vel.z / 4, flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
	}
    Loop;
  Death:
	CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_Explode(100, 100, XF_NOTMISSILE, 1);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_QuakeEx(3, 3, 3, 40, 0, 200, "none", QF_SCALEDOWN, falloff: 400);
	}
    CGMT D 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
		A_SetTranslucent(1.0, 1);
		for (int i = 3; i > 0; i--) A_SpawnItemEx("DCY_MonochromaticExplosion", 0, 0, 0, random(-5, 5), random(-5, 5), random(-5, 5), random(0, 360), SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
    CGMT EEFF 1 Bright
	{
		A_FadeOut(0.15, 0);
		A_SetScale(Scale.X+0.15, Scale.Y+0.15);
	}
	CGMT GGHHII 1 Bright A_SetScale(Scale.X+0.25, Scale.Y+0.25);
	TNT1 A 25;
    Stop;
  }
}

Class DCY_CometTrail : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  RenderStyle "Add";
  Alpha 0.5;
  Scale 1.0;
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +ROLLSPRITE
  }
  
  States
  {
  Spawn:
	FRTG A 0 NoDelay A_SetRoll(frandom(0, 360));
	NBKL AABBCC 1
	{
		A_SetScale(Scale.X-0.01, Scale.Y-0.01);
		A_FadeOut(0.0175, 0);
		
		if (alpha < 0) Destroy();
	}
	Loop;
  }
}

Class DCY_GunmetalInfiltratorBallTrail : DCY_Effect
{
  Default
  {
  Speed 0;
  Damage 0;
  Speed 0;
  Radius 1;
  Height 1;
  RenderStyle "Add";
  Projectile;
  +NOINTERACTION
  +CLIENTSIDEONLY
  -SOLID
  +BRIGHT
  }
  
  States
  {
  Spawn:
    GBL8 AAAAABBBBB 1 Bright A_FadeOut();
	Stop;
  }
}

Class DCY_FatThrower : DCY_Projectile
{
  Default
  {
  Speed 32;
  FastSpeed 35;
  Damage (0);
  Radius 18;
  Height 18;
  Scale 0.7;
  Alpha 0.1;
  DamageType "Fire";
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.00]:[1.50,0.31,0.00]";
  Projectile;
  +NEVERFAST;
  +RIPPER;
  +NOEXTREMEDEATH;
  }
  States
  {
  Spawn:
	EXP_ AABCDEFGHIJKLM 1 Bright
	{
		A_Explode(4, 30, 0, 1);
		if (DCY_EffectDistance(sfxdistance))
		{
			A_SpawnFlames("c24d0e", 25, 80, 0, true, frandom(-2.5, 2.5));
			A_SpawnItemEx("DCY_ImpBigFlame", random(-5, 5), random(-5, 5), random(-5, 5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			for (int i = 3; i > 0; i--) A_SpawnItemEx("DCY_ThrowerEffect", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
	}
	Goto Death;
  Death:
    TNT1 A 1 A_Die;
	Stop;
  }
}

Class DCY_RattailFire : DCY_FatThrower
{
  Default { Translation "0:255=%[0.00,0.00,0.00]:[0.18,0.87,2.00]"; }
  
  States
  {
  Spawn:
	EXP_ ABCDEGHJM 1 Bright
	{
		A_Explode(4, 35, 0, 1);
		A_SpawnItemEx("DCY_AndroidExplosionSmaller", random(-5, 5), random(-5, 5), random(-5, 5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_WretchedthrowerEffect", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
	}
	Goto Death;
  }
}

Class DCY_ThrowerEffect : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  Scale 0.25;
  Alpha 0.035;
  RenderStyle "Add";
  +NOCLIP
  +NOGRAVITY
  +THRUACTORS
  +BRIGHT
  }
  States
  {
  Spawn:
    EXP_ ABCDEFGHIJKLM 1 Bright;
	Stop;
  }
}

Class DCY_WretchedthrowerEffect : DCY_ThrowerEffect { Default { Alpha 0.1; Scale 0.65; -NOGRAVITY; } }

Class DCY_WretchedFlame : DCY_FatThrower
{
  Default
  {
  Translation "0:255=%[0.20,0.00,0.00]:[2.00,0.03,0.03]";
  DamageType "WretchedFire";
  Alpha 0.1;
  }
  States
  {
  Spawn:
	EXP_ ABCDEFGHIJKLM 1 Bright
	{
		A_Explode(5, 35, 0, 1);
		A_SpawnItemEx("DCY_AndroidExplosionSmall", random(-5, 5), random(-5, 5), random(-5, 5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		for (int i = 3; i > 0; i--)
		{
			A_SpawnItemEx("DCY_ElecDeathBase", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			A_SpawnItemEx("DCY_WretchedthrowerEffect", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		}
	}
	Goto Death;
  }
}

Class DCY_WastelandFlame : DCY_FatThrower
{
  Default
  {
  DamageType "Fire";
  Alpha 0.1;
  }
  States
  {
  Spawn:
	EXP_ A 0;
	EXP_ ABCDEFGHIJKLM 1 Bright
	{
		A_Explode(4, 30, 0, 1);
		A_SpawnItemEx("DCY_Explosion_1_Small", random(-5, 5), random(-5, 5), random(-5, 5), frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(-2.0, 2.0), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		
		if ((level.time % random(2, 10)) == 0)
			A_SpawnParticleEx(0x000000, TexMan.CheckForTexture("graphics/Particles/dcygas.png"), STYLE_TRANSLUCENT, SPF_RELATIVE|SPF_ROLL, 90, 90, 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1) + 2, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(1, 2), startalphaf: 0.5, fadestepf: 0.015, sizestep: 0.1, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
		
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("DCY_WretchedthrowerEffect", random(-2, 2), random(-2, 2), random(-2, 2), frandom(-1.1, 1.1), frandom(-1.1, 1.1), frandom(-1.1, 1.1), 0, SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
	}
	Stop;
  Death:
	TNT1 A 1 { if (!random(0, 1)) A_SpawnItemEx((!random(0, 3)) ? "DCY_NapalmBigFire" : "DCY_NapalmFire", frandom(-20, 20), frandom(-20, 20)); }
	Stop;
  }
}

Class DCY_OverdemoniacExplosion : Actor
{
  Default
  {
  Scale 1.0;
  +ROLLSPRITE
  +BRIGHT
  +NOCLIP
  +NOINTERACTION
  +NOBLOCKMAP
  Radius 0;
  Height 0;
  Alpha 1.0;
  RenderStyle "Add";
  Translation "0:255=%[0.00,0.00,0.50]:[1.01,2.00,2.00]";
  }
  States
  {
  Spawn:
	SUPH A 0 NoDelay A_SetRoll(Roll+random(0, 360));
	SUPH ABCDEFGHIJKLMNNOOPPQQRR 1;
	Stop;
  }
}

Class DCY_SorcererOrbit : LostSoul
{
  Default
  {
  Health 0x7FFFFFFF;
  Radius 6;
  Height 6;
  SeeSound "";
  ActiveSound "";
  PainSound "";
  DeathSound "";
  AttackSound "";
  PainChance 0;
  RenderStyle "Normal";
  Alpha 1.0;
  -ZDOOMTRANS
  +PAINLESS
  +NOPAIN
  +NOGRAVITY
  +DROPOFF
  +FLOAT
  +NEVERRESPAWN
  +NOBLOOD
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  +THRUSPECIES
  +THRUGHOST
  -SOLID
  +NOINFIGHTING
  DamageFactor 0;
  Tag "\c[Purple]Sorcerer Orbit\c-";
  Obituary "bruh";
  HitObituary "bruh";
  }
  States
  {
  Spawn:
  See:
    SBMP A 0 NoDelay
	{
		angle = 720;
	}
    SBMP ABCDEFGHIJKLMNOP 2
	{
		A_Warp(AAPTR_MASTER, 0, 70, 70, Angle, WARPF_ABSOLUTEANGLE|WARPF_INTERPOLATE);
		Angle += 5;
		A_SpawnItemEx("DCY_SorcererAura", zofs: 5, flags: SXF_NOCHECKPOSITION);
	}
    Goto Spawn+1;
  Pain:
    Goto See;
  Death:
    TNT1 A 0 A_Stop();
    SBS3 D 3 A_Explode(255, 255);
    SBS3 E 3;
    SBS3 FGH 2;
    Stop;
  }
}

Class DCY_SorcererAura : DCY_Effect
{
  Default
  {
  Radius 0;
  Height 0;
  Alpha 0.45;
  Scale 1;
  RenderStyle "Add";
  +NOCLIP
  +NOINTERACTION
  +THRUACTORS
  +BRIGHT
  }
  
  States
  {
  Spawn:
	EF1_ L 1
	{
		if (Alpha < 0.000000001)
			Destroy();
		
		A_FadeOut(0.025, 0);
		A_SetScale(Scale.X-0.075, Scale.Y-0.075);
		A_ChangeVelocity(frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
	}
	Loop;
  }
}

Class DCY_ElecTrailBase : DCY_Effect
{
  Default
  {
  +NOGRAVITY
  +BRIGHT
  +THRUACTORS
  +NOCLIP
  +ROLLSPRITE
  +NOINTERACTION
  -SOLID
  RenderStyle "Add";
  Alpha 1.0;
  Scale 1.0;
  }
  
  States
  {
  Spawn:
	ASZ0 ABCABCABC 1
	{
		A_SetScale(Scale.X - 0.01);
		A_FadeOut(0.1);
		A_SetRoll(frandom(0, 360));
	}
	Stop;
  }
}

Class DCY_ElecDeathBase : DCY_ElecTrailBase
{
  States
  {
  Spawn:
	4L4C A 0 NoDelay { roll = frandom(0, 360); }
	4L4C ABCDEFG 2;
	Stop;
  }
}

Class DCY_FallingSpark : DCY_Effect
{
  Mixin DCY_Effects;
  
  Default
  {
	Radius 2;
	Height 4;
	BounceFactor 0.55;
	WallBounceFactor 0.55;
	Gravity 0.8;
	Speed 9;
	Mass 2;
	BounceType "Doom";
	+NOBLOCKMAP
	+MISSILE
	+DONTSPLASH
	+NOTELEPORT
	DCY_FallingSpark.SColor "8a3919";
  }
  
  string col;
  property SColor : col;
  protected textureID t;
  double f;
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	f = random(2, 100);
  }
  
  States
  {
  Spawn:
	TNT1 A random(1, 2)
	{
		if (f < 0) Destroy();
		f--;
		
		if (DCY_EffectDistance(900))
			A_SpawnParticleEx(col, t, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 90, 7, frandom(0, 360), velx: frandom(-1, 1) + (vel.x / 5), vely: frandom(-1, 1)+ (vel.y / 5), velz: frandom(-0.1, 0.1) + (vel.z / 5), accelz: -0.05, startalphaf: 1, fadestepf: 0, sizestep: -0.2);
	}
	Loop;
  Death:
	TNT1 A 0;
	Stop;
  }
}

Class DCY_BigSpark : DCY_FallingSpark
{
  Default
  {
  Speed 8;
  Gravity 0.5;
  }
  
  override void PostBeginPlay()
  {
	DCY_Effect.PostBeginPlay();
	f = random(2, 10);
  }
  
  States
  {
  Spawn:
	TNT1 A 1
	{
		if (f < 0) Destroy();
		f--;
		
		if (DCY_EffectDistance(900))
			for (int i = random(1, 6); i > 0; i--) A_SpawnParticleEx(col, t, STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 90, 10, frandom(0, 360), velx: frandom(-2.5, 2.5) + (vel.x / 2), vely: frandom(-2.5, 2.5) + (vel.y / 2), velz: frandom(-2.1, 2.1) + (vel.z / 2), accelz: -0.1, startalphaf: 1, fadestepf: 0, sizestep: -0.3);
	}
	Loop;
  }
}

Class DCY_LightningExplode : DCY_Explosion_1
{
  Mixin DCY_Effects;
  
  Default
  {
	Scale 0.5;
	Translation "0:255=%[0.00,0.00,0.00]:[1.39,1.50,1.72]";
	+ALWAYSPUFF
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	
	if (DCY_EffectDistance(900))
	{
		for (int i = random(2, 5); i > 0; i -= random(1, 2))
		{
			DCY_BigSpark s = DCY_BigSpark(Spawn("DCY_BigSpark", pos + (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10))));
			if (s) s.vel = (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10));
		}
	}
  }
  
  override void Tick()
  {
	if (level.isFrozen()) return;
	super.Tick();
	
	if (DCY_EffectDistance(900) && (getAge() < 15))
	{
		if (!random(0, 1))
		{
			for (int i = random(1, 2); i > 0; i--)
			{
				DCY_FallingSpark s = DCY_FallingSpark(Spawn("DCY_FallingSpark", pos + (frandom(-10, 10), frandom(-10, 10), frandom(-10, 10))));
				if (s) s.vel = (frandom(-14, 14), frandom(-14, 14), frandom(-14, 14));
			}
		}
		
		A_SpawnItemEx("DCY_SiriusArc", frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-5, 5), frandom(-5, 5), frandom(-5, 5), flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION, failchance: 120);
	}
  }
}