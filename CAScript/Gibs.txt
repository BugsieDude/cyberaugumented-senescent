Class DCY_GoreBase : Actor
{
  Default
  {
  Scale 1.0;
  BounceType "Doom";
  Speed 8;
  BounceFactor 0.5;
  Radius 8;
  Height 8;
  Gravity 0.65;
  Projectile;
  -NOGRAVITY
  +THRUACTORS
  +BOUNCEONFLOORS
  +BOUNCEONWALLS
  +ROLLSPRITE
  +CANNOTPUSH
  +MOVEWITHSECTOR
  }
  
  int xroll;
  
  override void PostBeginPlay()
  {
	xroll = random(3, 10) * randompick(-1, 1);
  }
}

Class DCY_Gib : DCY_GoreBase
{
  Default
  {
	BounceFactor 0.1;
	Mass 2;
  }
  
  Mixin DCY_Effects;
  
  override void Tick()
  {
	super.Tick();
	
	if (DCY_EffectDistance(2000) && vel.x != 0 && !(getAge() % random(1, 6)))
	{
		let b = spawn("De_Blood_BigSplat", pos);
		if (b)
		{
			b.scale *= 0.55;
			b.translation = translation;
		}
	}
	
	if (vel.z == 0 && (GetAge() > dcy_gibtime))
	{
		A_FadeOut(0.005);
		if (alpha <= 0) Destroy();
	}
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	frame = random(0, 8);
	scale.x *= randompick(-1, 1);
  }
  
  States
  {
  Spawn:
	G0_E # 1 { roll += xroll; }
	Loop;
  Death:
  Crash:
	G1_E # -1 { roll = 0; }
	Stop;
  }
}

Class DCY_LavaGib : DCY_Gib
{ 
  override void Tick()
  {
	DCY_GoreBase.Tick();
	
	if (DCY_EffectDistance(2000) && vel.x != 0 && !(getAge() % random(1, 3)))
	{
		let b = spawn("De_Blood_BigSplat", pos);
		if (b)
		{
			b.scale *= 0.55;
			b.A_SetRenderStyle(1.0, STYLE_ADD);
			b.bBRIGHT = true;
			b.translation = translation;
		}
	}
	
	if (vel.z == 0 && (GetAge() > dcy_gibtime))
	{
		A_FadeOut(0.005);
		if (alpha <= 0) Destroy();
	}
  }
}

Class DCY_AndroidGib : DCY_Gib
{
  Default
  {
	BounceFactor 0.3;
  }
  
  override void Tick()
  {
	DCY_GoreBase.Tick();
	
	if (DCY_EffectDistance(2000) && vel.x != 0 && !(getAge() % random(1, 3)))
	{
		let b = spawn("De_Blood_BigSplat", pos);
		if (b)
		{
			b.scale *= 0.55;
			b.A_SetRenderStyle(1.0, STYLE_ADD);
			b.bBRIGHT = true;
			b.translation = translation;
		}
	}
	
	if (vel.z == 0 && (GetAge() > dcy_gibtime))
	{
		A_FadeOut(0.005);
		if (alpha <= 0) Destroy();
	}
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	frame = random(0, 19);
	scale.x *= randompick(-1, 1);
  }
  
  States
  {
  Spawn:
	Z_71 # 1 { roll += xroll; }
	Loop;
  Death:
  Crash:
	Z_71 # -1 { roll = 0; }
	Stop;
  }
}

Class DCY_MetallicJunk : DCY_GoreBase
{
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	frame = random(0, 19);
	scale.x *= randompick(-1, 1);
  }
  
  override void Tick()
  {
	super.Tick();
	if (vel.z == 0 && (GetAge() > dcy_gibtime))
	{
		A_FadeOut(0.005);
		if (alpha <= 0) Destroy();
	}
  }
  
  States
  {
  Spawn:
	Z_71 # 1 { roll += xroll; }
	Loop;
  Death:
  Crash:
	Z_71 # -1 { roll = 0; }
	Stop;
  }
}

Class DCY_BloodMist : DCY_Effect
{
  Default
  {
	Alpha 0.01;
	Scale 1;
	RenderStyle "Translucent";
	+NOGRAVITY
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	frame = random(0, 6);
  }
  
  States
  {
  Spawn:
	Z_70 ################### 1 { A_SetScale(Scale.X + frandom(0.01, 0.1)); A_FadeIn(0.01); }
	Goto Fade;
  Fade:
	Z_70 # 1 { A_SetScale(Scale.X + 0.01); A_FadeOut(0.0075); }
	Wait;
  }
}

Class DCY_Giblets : DCY_Effect
{
	int mn, mx, gtime, tick;
	double rg;
	class<Actor> gibtype;
	
	override void PostBeginPlay()
	{
		mn = 6;
		mx = 14;
		gtime = 10;
		tick = 1;
		rg = 10;
		gibtype = "DCY_Gib";
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay;
		TNT1 A 0
		{
			for (int i = random(mn, mx); i > 0; i--)
			{
				A_SpawnItemEx("DCY_BloodMist", xvel: (vel.x / 7) + frandom(-1, 1), yvel: (vel.y / 7) + frandom(-1, 1), zvel: (vel.z / 7) + frandom(-1, 1), angle: frandom(0, 360), flags: SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, failchance: 140);
				A_SpawnItemEx("De_Blood_Splat", frandom(-10, 10), frandom(-10, 10), frandom(5, 60), (vel.x / 4) + frandom(-rg, rg), (vel.y / 4) + frandom(-rg, rg), (vel.z / 4) + frandom(1, 12), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, 150);
				A_SpawnItemEx("De_Blood_BigSplat", frandom(-10, 10), frandom(-10, 10), frandom(5, 60), (vel.x / 4) + frandom(-rg, rg), (vel.y / 4) + frandom(-rg, rg), (vel.z / 4) + frandom(1, 12), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE, 15);
				A_SpawnItemEx(gibtype, frandom(-10, 10), frandom(-10, 10), frandom(5, 60), (vel.x / 4) + frandom(-rg, rg), (vel.y / 4) + frandom(-rg, rg), (vel.z / 4) + frandom(1, 12), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION, 50);
			}
		}
		TNT1 A 1
		{
			tics = tick;
			gtime--;
			if (gtime <= 0) Destroy();
			A_SpawnItemEx(random(0, 1) ? "De_Blood_Splat" : "De_Blood_BigSplat", frandom(-10, 10), frandom(-10, 10), frandom(5, 60), vel.x * 1.5 + frandom(-5, 5), vel.y * 1.5 + frandom(-5, 5), vel.z * 1.5 + frandom(1, 5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_TRANSFERRENDERSTYLE, 20);
			A_SpawnItemEx(gibtype, frandom(-10, 10), frandom(-10, 10), frandom(5, 60), vel.x * 1.5 + frandom(-5, 5), vel.y * 1.5 + frandom(-5, 5), vel.z * 1.5 + frandom(1, 5), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION, 20);
		}
		Goto Spawn+2;
	}
}

Class DCY_GibletsAdd : DCY_Giblets
{
	Default { RenderStyle "Add"; }
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		gibtype = "DCY_AndroidGib";
	}
}

Class DCY_GibletsLava : DCY_GibletsAdd
{
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		gibtype = "DCY_LavaGib";
	}
}

Class DCY_ShorterGiblets : DCY_Giblets
{
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		mn = 5;
		mx = 8;
		gtime = 4;
		rg = 5;
		tick = 2;
	}
}

Class DCY_FatGiblets : DCY_Giblets
{
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		mn = 8;
		mx = 17;
		gtime = 8;
		rg = 12;
		tick = 2;
	}
}

Class DCY_MechaGiblets : DCY_Giblets
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			for (int i = random(8, 11); i > 0; i--)
				A_SpawnItemEx("DCY_MetallicJunk", frandom(-40, 40), frandom(-40, 40), frandom(5, 60), (vel.x / 4) + frandom(-12, 12), (vel.y / 4) + frandom(-12, 12), (vel.z / 4) + frandom(1, 12), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY, 50);
		}
		TNT1 AAAAAAAA 2 A_SpawnItemEx("DCY_MetallicJunk", frandom(-25, 25), frandom(-25, 25), frandom(5, 60), vel.x * 1.5 + frandom(-5, 5), vel.y * 1.5 + frandom(-5, 5), vel.z * 1.5 + frandom(1, 15), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY, 20);
		Stop;
	}
}